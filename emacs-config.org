#+title: Emacs configuration
#+author: matheuristic
#+options: h:4 num:t toc:t
#+property: header-args:emacs-lisp :exports code

* Notes

** Using this Emacs config

Clone this repository and initialize and update the submodules (see
"Cloning a Project with Submodules" under [[https://git-scm.com/book/en/v2/Git-Tools-Submodules][Submodules]] in [[https://git-scm.com/book/en/v2/Git-Tools-Submodules][Pro Git]]).

#+begin_example
$ git clone https://github.com/matheuristic/emacs-config.git
$ cd emacs-config
$ git submodule init
$ git submodule update
#+end_example

Make sure [[https://www.gnu.org/software/stow/][GNU Stow]] is installed. Run the following to create the
required directory structure in the user home directory and symlink
the individual files.

#+begin_example
$ stow -t $HOME --no-folding emacs
#+end_example

This configuration uses several external tools, which need to be
compiled, installed or configured. Search for headings with an
~external~ tag in this Org file, and follow the instructions there.

Note that Emacs versions ~27~ and newer also support [[https://specifications.freedesktop.org/basedir-spec/basedir-spec-latest.html][XDG Base
Directory Specification]], so it is possible to use =~/.config/emacs= as
an alternative emacs directory. If desired, use the follow GNU Stow
command instead of the one above.

#+begin_example
$ mkdir -p $HOME/.config/emacs
$ stow -d ./emacs -t $HOME/.config/emacs --no-folding .emacs.d
#+end_example

** Generating the Emacs Lisp config files

The [[https://www.gnu.org/software/emacs/manual/html_node/elisp/][Emacs Lisp]] (Elisp) config files for Emacs can be generated with
~M-x org-babel-tangle~ or ~C-c C-v C-t~ while in this file's buffer.
Two config files will be created in the =emacs/.emacs.d= directory:
- ~early-init.el~: Early initialization file (Emacs 27+).
- ~init.el~: Main configuration file.

** Measuring startup times

Startup times can be measured in Linux using
#+begin_example
$ emacs -q --eval='(message "%s" (emacs-init-time))'
#+end_example
or in Mac OS X using
#+begin_example
$ open -n /Applications/Emacs.app \
      --args -q --eval='(message "%s" (emacs-init-time))'
#+end_example

** Optimizing startup times

1. Install [[https://github.com/jschaf/esup][ESUP]] and use it to profile load times (~M-x esup~).
2. Defer loading of packages when possible, for example use-package's
   ~:defer N~ with ~N~ set to ~1~ (second) for higher priority
   packages and ~2~ for lower priority ones.
3. Avoid helper functions that can cause eager loads.

** Trying packages before installing them

[[https://github.com/larstvei/Try][Try]] allows trying of Emacs packages without installing them.

It can try packages that are singular Elisp files or packages on an
ELPA-compatible repository.

** Recompiling Elisp bytecode after upgrading Emacs to a new major version

When upgrading versions, rebuild Elisp bytecode in installed packages
via ~M-: (byte-recompile-directory package-user-dir nil 'force)~ as
the newer versions may include new features that speed up performance.

** What to do when Emacs hangs and C-g does not work

Copied from this StackOverflow [[https://emacs.stackexchange.com/questions/21643/what-do-i-do-when-emacs-is-frozen][question]].

When Emacs stalls, usually ~C-g~ will quit the current activity and
make Emacs responsive again.

If that does not work, one solution is to try sending Emacs a
~SIGUSR2~ signal which tells Emacs to turn on ~debug-on-quit~.
See the Emacs [[https://www.gnu.org/software/emacs/manual/html_node/elisp/Error-Debugging.html][documentation]] on ~debug-on-event~.

After Emacs recovers, it can be turned back off using
~M-x toggle-debug-on-quit~.

#+begin_example
$ pkill -SIGUSR2 -i emacs
#+end_example

If using ~emacsclient~, an additional command can be sent right
after the signal is sent to turn off ~debug-on-quit~.

#+begin_example
$ pkill -SIGUSR2 -i emacs ; emacsclient -e '(setq debug-on-quit nil)'
#+end_example

For more information, see the [[https://www.gnu.org/software/emacs/manual/html_node/elisp/Misc-Events.html#Misc-Events][official]] [[https://www.gnu.org/software/emacs/manual/html_node/elisp/Error-Debugging.html#Error-Debugging][documentation]] (specifically on
the ~debug-on-event~ user option).

** Making TRAMP sessions more robust to SSH session hang events

When SSH sessions hang while a TRAMP connection is open, it can cause
Emacs to freeze (even if a ~SIGUSR2~ signal is sent to the process).

The [[https://www.gnu.org/software/emacs/manual/html_node/tramp/Frequently-Asked-Questions.html][TRAMP FAQ]] indicates to configure SSH to kill hangs due to network
connections by editing =~/.ssh/config= to include the following, which
works sometimes.

#+begin_example
Host *
     ServerAliveInterval 5
#+end_example
** Useful Emacs default bindings

*** Moving to beginning of line text (M-m)

There is a function ~back-to-indentation~ which moves the point to the
beginning of the line /text/, globally bound to ~M-m~ by default.

This is a useful complement to the standard ~C-a~ binding that moves
the point to the beginning of the line.

*** Jumping to function definitions (M-.)

Jumping to the definition of a function under point can be done by
calling ~xref-find-definitions~ interactively, which is bound to ~M-.~
by default.

*** Backtracking through a chain of jumps (M-,)

Backtracking through a chain of jumps can be done by calling
~xref-pop-marker-stack~ which is bound to ~M-,~ by default (this only
works for jumps that push the jump origin onto the marker stack, which
~xref-find-definitions~ does).

To modify some function ~name-of-some-function~ so that it pushes the
current location onto marker stack when run, ~advice-add~ can be
utilized as follows.

#+begin_example
(advice-add #'name-of-some-function :before #'xref-push-marker-stack))
#+end_example

** Random useful Emacs links

Literate configs, package lists, and so on and so forth.

- Nice [[https://leanpub.com/lit-config][intro]] to literate configuration files.
- [[https://github.com/caisah/emacs.dz][Awesome emacs config files]].
- [[https://so.nwalsh.com/2020/02/29/dot-emacs][My .emacs | SO… A WEBLOG BY NORM]] and [[https://so.nwalsh.com/2020/02/29-dotfiles][Dotfiles | SO… A WEBLOG BY NORM]].
- [[https://uwabami.github.io/cc-env/Emacs.html][Emacs の設定 | Youhei SASAKI’s official site]].
- [[https://github.com/grettke/lolsmacs][GitHub - grettke/lolsmacs: The Law Of Least Surprise Lattice For Emacs.]]
- [[https://github.com/m-cat/init.el][GitHub - m-cat/init.el: My emacs config file]]
- [[https://www.bytedude.com/useful-emacs-shortcuts/][Useful Emacs Keybindings | Bytedude]]
- [[https://github.com/emacs-tw/awesome-emacs][GitHub - emacs-tw/awesome-emacs]]
- [[https://www.gnu.org/software/emacs/manual/html_node/emacs/Change-Log-Commands.html][Change Log Commands - GNU Emacs Manual]]
- [[https://two-wrongs.com/why-you-should-buy-into-the-emacs-platform][Why You Should Buy Into the Emacs Platform]]
- [[https://emacs.stackexchange.com/questions/12212/how-to-type-the-password-of-a-gpg-file-only-when-opening-it/12213#12213][encryption - How to type the password of a .gpg file only when opening it]]
- [[https://colinxy.github.io/software-installation/2016/09/24/emacs25-easypg-issue.html][Emacs 25 EasyPG Issue]]

** Useful Emacs Lisp references

- [[https://www.gnu.org/software/emacs/manual/html_node/elisp/index.html][GNU Emacs Lisp Reference Manual]]
- [[http://www.wilfred.me.uk/blog/2017/03/19/pattern-matching-in-emacs-lisp/][Pattern Matching in Emacs Lisp]]
- [[https://github.com/alphapapa/emacs-package-dev-handbook][GitHub - alphapapa/emacs-package-dev-handbook: An Emacs package development handbook]]

** Useful Org-mode references

- [[https://karl-voit.at/2019/10/26/all-things-org/][All Things Org Mode: PIM, Scientific Writing, Presentation, Programming]]
- [[https://rgoswami.me/posts/org-note-workflow/][An Orgmode Note Workflow]]

** Tags

- ~early~: Configuration code (not boilerplate comments) that should
  be loaded in ~early-init.el~ to optimize startup time.
- ~elpa~: Uses a package from GNU ELPA.
- ~external~: Configuration code in the section uses external tools
  that are not typically packaged with the default userland on Linux
  and BSD systems (including Mac OS X), and which need to be installed
  at the system-level outside of the Emacs ~package.el~ mechanisms.
  Also includes Elisp scripts that need to be downloaded manually.
- ~hydra~: Configuration code that enables, creates or modifies
  [[Hydra][hydras]].
- ~melpa~: Uses a package from MELPA.
- ~semiearly~: Configuration that should be loaded early in ~init.el~
  because other configuration code depend on them.
- ~workaround~: Section contains a workaround for Emacs or package
  bug, which can be removed if and when the issues are fixed upstream.

* Front matter

** Header and lexical binding

File header comment indicating the filename, along with declaring any file-specific variables.
One file-specific variable that should generally be set is enabling ~lexical-binding~ ([[https://nullprogram.com/blog/2016/12/22/][link]]), which has the following benefits:
- Closures.
- Better performance.
- Less bugs.

#+name: generate-header
#+begin_src emacs-lisp
(concat ";;; " feature ".el --- " summary " -*- lexical-binding: t; -*-")
#+end_src

** File generation timestamp

Tangled initialization files are timestamped to track when they were last generated.

#+name: generate-timestamp
#+begin_src emacs-lisp
(concat ";; Generated: " (current-time-string))
#+end_src

** Author info

Author information and where to get the newest version of this configuration.

#+name: author-info
#+begin_src emacs-lisp
;; Author: matheuristic
;; URL: https://github.com/matheuristic/emacs-config
#+end_src

** File commentary

File descriptions.

*** early-init

#+name: file-commentary-early-init
#+begin_src emacs-lisp
;; Emacas early initialization configuration file, symlink or copy to
;; ~/.emacs.d/early-init.el or $XDG_CONFIG_HOME/.emacs.d/early-init.el

;; In Emacs 27+, the sequence of initialization is
;; 1. early-init.el
;; 2. package.el
;; 3. init.el

;; early-init.el is run before UI elements are rendered,
;; so it is best to configure UI elements here rather than init.el
#+end_src

*** init

#+name: file-commentary-init
#+begin_src emacs-lisp
;; Emacs initialization configuration file, symlink or copy to
;; ~/.emacs.d/init.el or $XDG_CONFIG_HOME/.emacs.d/init.el

;; In Emacs 27+, the sequence of initialization is
;; 1. early-init.el
;; 2. package.el
;; 3. init.el
#+end_src

* Backward compatibility

In Emacs versions before 27, we should load the ~early-init.el~ file explicitly in ~init.el~.

#+name: early-init-pre-27
#+begin_src emacs-lisp
;; backwards-compatibility code for Emacs versions <27
(when (version< emacs-version "27")
  ;; load early-initialization file ~/.emacs.d/early-init.el
  ;; Emacs 27+ automatically loads this file before rendering UI elements
  (let ((local-f (expand-file-name "early-init.el" user-emacs-directory)))
    (when (file-exists-p local-f) (load-file local-f))))
#+end_src

* Optimizations

** Startup optimizations                                              :early:

Optimizations for improving startup time:
- Increase garbage collection threshold from the default (~800~ kb) to
  ~128~ MB and revert it after initialization.
- Set ~file-name-handler-alist~ to ~nil~ as it is always scanned
  whenever files are loaded and revert it after initialization.
  This specifies special I/O handlers for files based on file name.
  Startup files are always local ~*.el~ files, so no special handlers
  are needed for them.
- Don't load installed packages automatically.
  Load them manually in the config.

#+name: startup-optimizations
#+begin_src emacs-lisp
;; optimizations for reducing startup time (reverted later)
;; * file-name-handler-alist -> nil as it is scanned when files are loaded
;; * increase garbage collection threshold
;; * increase max bytes read from a sub-process in a single op (Emacs 27+)
(setq file-name-handler-alist-orig file-name-handler-alist
      gc-cons-threshold-orig gc-cons-threshold
      file-name-handler-alist nil ;; no special file handling during init
      gc-cons-threshold 134217728) ;; 128MB in bytes, default is 800k

;; revert optimizations after initialization
(add-hook 'after-init-hook
          (lambda ()
            (setq file-name-handler-alist file-name-handler-alist-orig)
            (setq gc-cons-threshold gc-cons-threshold-orig))
          t)

;; disable automatic activation of installed packages
(setq package-enable-at-startup nil)
#+end_src

** I/O optimizations                                                  :early:

Increase the maximum bytes read from a sub-process in a single file
operation from the default (~4096~ bytes) to ~1~ MB ([[https://github.com/emacs-mirror/emacs/blob/master/etc/NEWS.27#L3212-L3217][Emacs 27+]]).

#+name: io-optimizations
#+begin_src emacs-lisp
;; optimizations for improving I/O performance
;; * increase max bytes read from a sub-process in a single op (Emacs 27+)
(when (boundp 'read-process-output-max)
  (setq read-process-output-max 1048576)) ;; 1MB in bytes, default 4096 bytes
#+end_src

* Customize file

Emacs has a text GUI interface for customizing the editor, and
settings configured with this interface are saved in ~custom-file~.
To avoid the ~M-x customize~ settings clobbering the tangled
initialization files (which it does by default), set ~custom-file~ to
to something that is not the Emacs init file.

#+name: custom-file
#+begin_src emacs-lisp
;; store Customize settings in a separate file, custom.el
(setq custom-file (expand-file-name "custom.el" user-emacs-directory))
(add-hook 'after-init-hook
          (load custom-file 'noerror))
#+end_src

* Package management

** Prefer newer Elisp files                                           :early:

When multiple versions of an Elisp file exist (compiled and
uncompiled), load the newest.

#+name: load-prefer-newer
#+begin_src emacs-lisp
;; when multiple versions of a package are installed, load the newest
(setq load-prefer-newer t)
#+end_src

** Local packages                                                     :early:

Add the ~lisp/~ and ~site-lisp/~ directories in the user Emacs
directory to the load path to facilitate loading of user maintained
and local copies of third-party packages.

This can be done in ~early-init.el~ so that locally maintained
decoration and theming code can be loaded there.

#+name: add-dirs-to-load-path
#+begin_src emacs-lisp
;; add user packages in lisp/ to load path
(defvar lisp-dir (expand-file-name "lisp" user-emacs-directory))
(unless (file-exists-p lisp-dir) (make-directory lisp-dir))
(add-to-list 'load-path lisp-dir)
(dolist (project (directory-files lisp-dir t "\\w+"))
  (when (file-directory-p project) (add-to-list 'load-path project)))

;; add third-party packages in site-lisp/ and its subdirs to load path
(defvar site-lisp-dir (expand-file-name "site-lisp" user-emacs-directory))
(unless (file-exists-p site-lisp-dir) (make-directory site-lisp-dir))
(add-to-list 'load-path site-lisp-dir)
(dolist (project (directory-files site-lisp-dir t "\\w+"))
  (when (file-directory-p project) (add-to-list 'load-path project)))
#+end_src

** ELPA-compatible package repositories

Set ELPA-compatible repositories to fetch and install packages from,
and their priorities.
When the packages with the same name exist on multiple repositories,
the version on the repository with the highest priority is preferred.

The following package repositories are the most well-known:
- [[https://elpa.gnu.org/][GNU Emacs Lisp Package Archive]] (ELPA).
  This is the default package repository for Emacs.
- [[https://melpa.org/][Milkypostman’s Emacs Lisp Package Archive]] (MELPA).
  This is an unofficial package repository containing a large
  selection of packages.
  Packages in this repository are vetted at time of initial inclusion,
  which are automatically rebuilt on source updates.
  Does not contain some packages from [[https://www.emacswiki.org/][EmacsWiki]] due to [[https://github.com/melpa/melpa/pull/5008][security risks]]
  (some are in MELPA because they were mirrored on Github)
- [[https://stable.melpa.org/][Milkypostman’s Emacs Lisp Package Archive Stable]] (MELPA Stable).
  This is a version of MELPA that only builds tagged releases.
  Has less packages compared to MELPA.
- [[https://orgmode.org/elpa.html][Org Emacs Lisp Package Archive]] (Org).
  This is the official [[https://orgmode.org/][Org]] package repository that contains the newest
  version of ~org~ (also in ELPA), along with ~org-plus-contrib~ that
  contains all contributed files (not in ELPA).

Only ELPA and MELPA are used here so the latest package versions are
installed, and because there isn't generally a need for all the
contributed files for Org.

#+name: elpa-repositories
#+begin_src emacs-lisp
;; set ELPA-compatible package repositories and their priorities
(setq package-archives '(("GNU"   . "https://elpa.gnu.org/packages/")
                         ("MELPA" . "https://melpa.org/packages/"))
      package-archive-priorities '(("GNU"   . 1)
                                   ("MELPA" . 2)))
#+end_src

** Package initialization

Initialize package loading support.
Disable auto-package loading and load packages explicitly for faster initialization times.

#+name: package-init
#+begin_src emacs-lisp
;; initialize package.el
(require 'package)
(package-initialize)
#+end_src

** use-package                                                        :melpa:

Download the [[https://github.com/jwiegley/use-package][use-package]] if not already on the system.
Load it, which will provide configuration macros for installing,
loading and configuring packages.
Also load its subpackage [[https://github.com/jwiegley/use-package/blob/master/bind-key.el][bind-key]], which provides macros for key
bindings.

#+name: use-package
#+begin_src emacs-lisp
;; bootstrap use-package, provides configuration macros
;; for info, see https://github.com/jwiegley/use-package
(unless (package-installed-p 'use-package)
  (package-refresh-contents)
  (package-install 'use-package))

;; preload use-package and bind-key packages
;; configure imenu support for the `require' and `use-package' keywords
(eval-when-compile
  (setq use-package-enable-imenu-support t)
  (require 'use-package)
  (require 'bind-key)
  (setq use-package-always-ensure t)) ;; default to ":ensure t"
#+end_src

* Environment variables                                     :semiearly:melpa:

Copy the environment variables over from the login shell.

#+name: environment-variables
#+begin_src emacs-lisp
;; copy environment variables from shell, OS X GUI mode-only
(use-package exec-path-from-shell
  :init (exec-path-from-shell-initialize))
#+end_src

* Backend and frontend frameworks for building user interfaces

** Flexible minibuffer completion style

Emacs 27+ added a [[https://github.com/emacs-mirror/emacs/commit/fabfb54d1f60cf90e72b1efaabfbefbe877e076a][flexible completion]] similar to [[https://github.com/lewang/flx][flx]] for ~ido~.

#+name: flex-minibuffer-completion-style
#+begin_src emacs-lisp
;; enable flex completion on Emacs 27+
(when (not (version< emacs-version "27"))
  (with-eval-after-load 'minibuffer
    (add-to-list 'completion-styles 'flex t)))
#+end_src

** Helm                                                               :melpa:

[[https://github.com/emacs-helm/helm][Helm]] is a framework for incremental completions and narrowing
selections, in other words an alternative for Icomplete.

It is much heavier but more featureful.

In most instances, use only this or Icomplete but not both.

*Configuration*:
- Rebind standard Emacs commands that use completion like ~find-files~
  to helm equivalents.
- ~<tab>~ key triggers completion rather than action, which is closer
  to standard Emacs minibuffer behavior and that of most shells.

#+name: helm
#+begin_src emacs-lisp
(use-package helm
  :init
  (setq helm-allow-mouse t
        helm-command-prefix-key "C-c C-M-h"
        helm-prevent-escaping-from-minibuffer nil
        ;; show helm completion buffer using default display function
        ;; instead of always opening a new frame for it
        helm-show-completion-display-function #'helm-show-completion-default-display-function
        ;; show helm buffers by splitting current window instead of
        ;; taking over another window in multi-window layout
        helm-split-window-inside-p t)
  (when (version< emacs-version "27")
    (add-to-list 'completion-styles 'helm-flex t))
  :config
  (require 'helm-config)
  (helm-mode 1)
  (helm-autoresize-mode 1)
  ;; bind over the standard Emacs commands
  (define-key global-map [remap find-file] 'helm-find-files)
  (define-key global-map [remap occur] 'helm-occur)
  ;; (define-key global-map [remap list-buffers] 'helm-buffers-list)
  (define-key global-map [remap switch-to-buffer] 'helm-mini)
  (define-key global-map [remap dabbrev-expand] 'helm-dabbrev)
  (define-key global-map [remap execute-extended-command] 'helm-M-x)
  (define-key global-map [remap apropos-command] 'helm-apropos)
  ;; make <tab> only complete names during helm completion, instead of
  ;; default behavior that creates new buffer on the second press
  ;; after which a third press kills the newly created buffer
  (setq helm-ff-kill-or-find-buffer-fname-fn #'ignore)
  (define-key helm-map (kbd "TAB") #'helm-execute-persistent-action)
  (define-key helm-map (kbd "<tab>") #'helm-execute-persistent-action)
  (define-key helm-map (kbd "C-i") #'helm-execute-persistent-action)
  (define-key helm-map (kbd "C-z") #'helm-select-action))
#+end_src

*** Use icons in Helm                                                 :melpa:

[[https://github.com/yyoncho/helm-icons][helm-icons]] shows file icons using those from Treemacs when using
file-related Helm commands.

#+name: helm-icons
#+begin_src emacs-lisp
(use-package helm-icons
  :after helm
  :config (helm-icons-enable))
#+end_src

** Hydra                                              :semiearly:melpa:hydra:

[[https://github.com/abo-abo/hydra][Hydra]] is a framework for surfacing temporary bindings with visual
help.

Temporary bindings are defined using ~defhydra~.
They are not bound by default on creation, and need to be explicitly
given a binding (for example, using ~define-key~) for easy invocation.

Hydra bindings and definitions in this configuration adhere to the
following conventions:
- Globally-accessible hydras are always bound to a key sequence
  prefixed by ~C-c C-M-~ (like ~C-c C-M-b~ for the basic buffer
  management hydra).
- Major mode-specific hydras are always bound to ~C-c C-M-m~.
- A hydra always has a ~q~ head that quits the hydra, except when the
  user is expected to type normally while the hydra is active
  (for example, the [[Multiple cursors hydra][multiple cursors hydra]]).

For ease of reference, hydra colors are mapped to the following
behavior (copied from [[https://github.com/abo-abo/hydra/wiki/Hydra-Colors][here]]):

| Body color | Head color | Executing NON-HEADS   | Executing HEADS |
|------------+------------+-----------------------+-----------------|
| amaranth   | red        | Disallow and Continue | Continue        |
| teal       | blue       | Disallow and Continue | Quit            |
| pink       | red        | Allow and Continue    | Continue        |
| red        | red        | Allow and Quit        | Continue        |
| blue       | blue       | Allow and Quit        | Quit            |

The ~:demand t~ keyword is used to make sure the package is loaded and
the ~defhydra~ macro immediately available when the block is
processed.

#+name: hydra
#+begin_src emacs-lisp
;; framework for defining temporary, repeatable bindings
;; see https://github.com/abo-abo/hydra
(use-package hydra
  :demand t)
#+end_src

** Text completion with Company                                       :melpa:

[[https://company-mode.github.io/][Company]] is a text completion framework for Emacs that supports
pluggable back-ends and front-ends for retrieving and displaying
completion candidates.
Many other Emacs packages support this.

This can get in the way for non-programming modes, so it is
enabled by default only in programming modes.

#+name: company
#+begin_src emacs-lisp
;; text completion framework
(use-package company
  :defer t
  :init (with-eval-after-load 'prog-mode
          (add-hook 'prog-mode-hook 'company-mode))
  :config
  (setq company-dabbrev-downcase nil
        company-idle-delay 0.5
        company-minimum-prefix-length 2
        company-selection-wrap-around t
        company-show-numbers t ;; use M-<num> to directly choose completion
        company-tooltip-align-annotations t))
#+end_src

** Edit-indirect                                                      :melpa:

Backend package that allows editing regions in a separate buffer, much like
how ~C-c '~ works in Org source blocks.
This is used by other packages, like ~markdown-mode~.

#+name: edit-indirect
#+begin_src emacs-lisp
;; edit regions in separate buffers, used by other packages like markdown-mode
(use-package edit-indirect)
#+end_src

* Backups

Backup files to the =~/.backup/= directory, keeping only the newest three versions.

#+name: backup-files-directory
#+begin_src emacs-lisp
;; backup files to ~/.backup/
(let ((backup-dir (expand-file-name "~/.backup/")))
  (when (not (file-directory-p backup-dir))
    (make-directory backup-dir t))
  (setq backup-directory-alist `(("." . ,backup-dir))
        version-control t ;; use version numbers for backups
        kept-new-versions 3 ;; number of newest versions to keep
        kept-old-versions 0 ;; number of oldest versions to keep
        delete-old-versions t ;; don't ask before deleting old backups
        backup-by-copying t)) ;; backup by copying instead of renaming
#+end_src

* Bookmarks and history

** Bookmarks hydra                                                    :hydra:

Hydra for easier [[https://www.gnu.org/software/emacs/manual/html_node/emacs/Bookmarks.html][bookmark]] manipulation and usage.

#+name: bookmarks-hydra
#+begin_src emacs-lisp
(defhydra my-hydra/bookmarks (:color teal :columns 3)
  "
Bookmarks (_q_: quit)"
  ("q" nil nil)
  ("s" bookmark-set "set")
  ("d" bookmark-delete "delete")
  ("l" list-bookmarks "list")
  ("j" bookmark-jump "jump")
  ("i" bookmark-insert "insert")
  ("I" bookmark-insert-location "insert-loc")
  ("L" bookmark-load "load")
  ("W" bookmark-write "write"))
(global-set-key (kbd "C-c C-M-j") 'my-hydra/bookmarks/body)
#+end_src

** Recent files

The built in [[https://github.com/emacs-mirror/emacs/blob/master/lisp/recentf.el][recentf]] provides functionality to track and list recently
opened files.

#+name: recentf
#+begin_src emacs-lisp
;; recently opened files
(setq recentf-max-menu-items 10
      recentf-max-saved-items 100
      recentf-auto-cleanup 'mode) ;; clean up recent list when turning on mode
(recentf-mode 1)
;; exclude source code files in installed packages from ELPA-compatible repos
(add-to-list 'recentf-exclude
             (concat "^" (expand-file-name "elpa/" user-emacs-directory)))
;; exclude files opened with SSH so TRAMP is not spammed with stat calls
;; exclude files opened as the superuser with su or sudo
(add-to-list 'recentf-exclude "^/\\(?:ssh\\|su\\|sudo\\)?:")
;; exclude files from /var/folder as these are temp files
(add-to-list 'recentf-exclude "^/var/folders")
;; exclude files in `org-agenda-files' and `notdeft-directories'
;; these files are quickly accessible from their respective tooling
(add-hook 'after-init-hook
          (lambda ()
            (dolist (file-list (list org-agenda-files
                                     notdeft-directories))
              (dolist (exclude-file file-list)
                (add-to-list 'recentf-exclude (concat "^" exclude-file))))))

;; binding for recentf, use Helm version if available
(global-set-key (kbd "C-c C-M-r") #'recentf-open-files)
#+end_src

*** Helm recentf

Prefer ~helm-recentf~ to ~recentf-open-files~ for the main binding.

#+name: helm-recentf
#+begin_src emacs-lisp
;; prefer helm-recentf to recentf-open-files
(add-hook 'after-init-hook
          (lambda ()
            (when (featurep 'helm)
              (define-key global-map [remap recentf-open-files]
                'helm-recentf))))
#+end_src

** Save location in file

Enable [[https://www.emacswiki.org/emacs/SavePlace][saveplace]] to automatically save location in file,
so that the next time the file is visited the point will
automatically go to the last place it was at during the
previous visit.

#+name: saveplace
#+begin_src emacs-lisp
(save-place-mode 1)
#+end_src

** Save minibuffer and other history

Enable [[https://github.com/emacs-mirror/emacs/blob/master/lisp/savehist.el][savehist]] to automatically save minibuffer command history,
which can be leverage by different completion packages.
Other history (like search history, registers, the kill ring, and the
macro ring) can also be saved.
The default history file location is ~history~ in
the ~user-emacs-directory~ directory, and can be changed by setting
the ~savehist-file~ variable. The number of items saved is determined
by the ~history-length~ variable.

#+name: savehist
#+begin_src emacs-lisp
;; save minibuffer and other history across sessions
;; don't persist kill-ring if in the habit of copy-pasting passwords
(setq history-delete-duplicates t
      history-length 100
      savehist-additional-variables '(Info-history-list
                                      ;; kill-ring
                                      kmacro-ring
                                      regexp-search-ring
                                      register-alist
                                      last-kbd-macro
                                      search-ring
                                      shell-command-history))

;; enable save history mode
(savehist-mode 1)
#+end_src

* Buffers, windows, frames, workspaces

** Buffer management

*** Protect scratch and message buffers

Protect the ~*scratch*~ and ~*Message*~ buffers, locking them to make
them unkillable.

#+name: protect-buffers
#+begin_src emacs-lisp
;; protect these buffers, locking them to make them unkillable
(dolist (buf '("*scratch*" "*Messages*"))
  (with-current-buffer buf
    (emacs-lock-mode 'kill)))
#+end_src

*** Buffer manipulation hydra                                         :hydra:

Hydra for basic [[https://www.gnu.org/software/emacs/manual/html_node/emacs/Buffers.html][buffer]] manipulation.

#+name: buffer-hydra
#+begin_src emacs-lisp
;; hydra for basic buffer management
(defhydra my-hydra/buffer (:color amaranth :columns 4)
  "
Buffer (_q_: quit)"
  ("q" nil nil :exit t)
  ("b" switch-to-buffer "switch" :exit t)
  ("p" previous-buffer "previous")
  ("n" next-buffer "next")
  ("R" revert-buffer "revert")
  ("B" bury-buffer "bury")
  ("U" unbury-buffer "unbury")
  ("s" save-buffer "save")
  ("S" save-some-buffers "save-all") ;; call with "1 S" to save all
  ("k" kill-this-buffer "kill")
  ("K" kill-matching-buffers "kill-match")
  ("t" (lambda ()
         (interactive)
         (when (y-or-n-p "Cleanup all TRAMP buffers and connections? ")
           (tramp-cleanup-all-buffers)))
   "tramp-cleanup" :exit t))
(global-set-key (kbd "C-c C-M-b") 'my-hydra/buffer/body)
#+end_src

*** Advanced buffer management with Ibuffer

Use [[https://www.emacswiki.org/emacs/IbufferMode][Ibuffer]] to manage buffers.

#+name: ibuffer
#+begin_src emacs-lisp
;; advanced buffer management with Ibuffer
(add-hook 'ibuffer-mode-hook
          (lambda ()
            ;; refresh buffer after interactive commands
            ;; default to first saved group
            (progn (ibuffer-auto-mode 1)
                   (when ibuffer-saved-filter-groups
                     (ibuffer-switch-to-saved-filter-groups
                      (car (car ibuffer-saved-filter-groups)))))))
(setq ibuffer-expert t ;; skip extraneous confirm messages
      ibuffer-show-empty-filter-groups nil)
(global-set-key (kbd "C-x C-b") #'ibuffer)
#+end_src

**** Ibuffer filter groups

Set default rules for grouping files in Ibuffer.

#+name: ibuffer-filter-groups
#+begin_src emacs-lisp
;; configure Ibuffer filter groups
(with-eval-after-load 'ibuffer
  (setq ibuffer-saved-filter-groups
        ;; files are grouped by the first matching filter group in the list
        '(("default"
           ("Emacs" (or (name . "^\\*scratch\\*$")
                        (name . "^\\*Messages\\*$")))
           ("Programming" (derived-mode . prog-mode))
           ("Shell" (or (mode . eshell-mode)
                        (mode . shell-mode)
                        (mode . term-mode)
                        (name . "^vterm .*")))
           ("Org" (or (derived-mode . org-mode)
                      (mode . org-agenda-mode)))
           ("Text" (derived-mode . text-mode))
           ("Dired" (mode . dired-mode))
           ("Web" (or (mode . eww-mode)
                      (mode . eww-bookmark-mode)))
           ("Magit" (or (name . "\*magit.*\\*")
                        (mode . magit-mode)))
           ("Help" (or (derived-mode . apropos-mode)
                       (derived-mode . help-mode)
                       (derived-mode . Info-mode)))))))
#+end_src

**** Group by version-controlled project in Ibuffer                   :melpa:

Add support for grouping files by version-controlled project ([[https://github.com/purcell/ibuffer-vc][link]]).

#+name: ibuffer-vc
#+begin_src emacs-lisp
;; build VC project ibuffer filter groups
(use-package ibuffer-vc
  :after ibuffer
  :bind (:map ibuffer-mode-map
         ("/ V" . ibuffer-vc-set-filter-groups-by-vc-root)))
#+end_src

**** Ibuffer hydra                                                    :hydra:

Major mode-specific hydra for Ibuffer.

#+name: ibuffer-hydra
#+begin_src emacs-lisp
;; hydras for Ibuffer commands
;; adapted from https://github.com/abo-abo/hydra/wiki/Ibuffer
(defhydra my-hydra/ibuffer-mode (:color amaranth :columns 3)
  "
Ibuffer (_q_: quit)"
  ("q" nil nil :exit t)
  ;; navigation
  ("n" ibuffer-forward-line "next")
  ("p" ibuffer-backward-line "prev")
  ("RET" (condition-case nil
             (progn (ibuffer-toggle-filter-group)
                    (my-hydra/ibuffer-mode/body))
           (error (ibuffer-visit-buffer))) "open" :exit t)
  ;; mark
  ("m" ibuffer-mark-forward "mark")
  ("u" ibuffer-unmark-forward "unmark")
  ("*" my-hydra/ibuffer-mode/mark/body "→ Mark" :exit t)
  ;; actions
  ("S" ibuffer-do-save "save")
  ("D" ibuffer-do-delete "delete")
  ("a" my-hydra/ibuffer-mode/action/body "→ Action" :exit t)
  ;; view
  ("`" ibuffer-switch-format "format")
  ("g" ibuffer-update "refresh")
  ("s" my-hydra/ibuffer-mode/sort/body "→ Sort" :exit t)
  ("/" my-hydra/ibuffer-mode/filter/body "→ Filter" :exit t)
  ;; other
  ("o" ibuffer-visit-buffer-other-window "open-other" :exit t))
(defhydra my-hydra/ibuffer-mode/mark (:color amaranth :columns 5
                                      :after-exit (my-hydra/ibuffer-mode/body))
  "
Ibuffer → Mark (_q_: ←)"
  ("q" nil nil :exit t)
  ("*" ibuffer-unmark-all "unmark all")
  ("M" ibuffer-mark-by-mode "mode")
  ("m" ibuffer-mark-modified-buffers "modified")
  ("u" ibuffer-mark-unsaved-buffers "unsaved")
  ("s" ibuffer-mark-special-buffers "special")
  ("r" ibuffer-mark-read-only-buffers "read-only")
  ("/" ibuffer-mark-dired-buffers "dired")
  ("e" ibuffer-mark-dissociated-buffers "dissociated")
  ("h" ibuffer-mark-help-buffers "help")
  ("z" ibuffer-mark-compressed-file-buffers "compressed"))
(defhydra my-hydra/ibuffer-mode/action (:color teal :columns 3
                                        :after-exit (if (eq major-mode 'ibuffer-mode)
                                                        (my-hydra/ibuffer-mode/body)))
  "
Ibuffer → Action (_q_: ←)"
  ("q" nil nil)
  ("A" ibuffer-do-view "view")
  ("E" ibuffer-do-eval "eval")
  ("F" ibuffer-do-shell-command-file "shell-command-file")
  ("I" ibuffer-do-query-replace-regexp "query-replace-regexp")
  ("H" ibuffer-do-view-other-frame "view-other-frame")
  ("N" ibuffer-do-shell-command-pipe-replace "shell-cmd-pipe-replace")
  ("M" ibuffer-do-toggle-modified "toggle-modified")
  ("O" ibuffer-do-occur "occur")
  ("P" ibuffer-do-print "print")
  ("Q" ibuffer-do-query-replace "query-replace")
  ("R" ibuffer-do-rename-uniquely "rename-uniquely")
  ("T" ibuffer-do-toggle-read-only "toggle-read-only")
  ("U" ibuffer-do-replace-regexp "replace-regexp")
  ("V" ibuffer-do-revert "revert")
  ("W" ibuffer-do-view-and-eval "view-and-eval")
  ("X" ibuffer-do-shell-command-pipe "shell-command-pipe"))
(defhydra my-hydra/ibuffer-mode/sort (:color amaranth :columns 5)
  "
Ibuffer → Sort (_q_: ←)"
  ("q" my-hydra/ibuffer-mode/body nil :exit t)
  ("a" ibuffer-do-sort-by-alphabetic "alphabetic")
  ("f" ibuffer-do-sort-by-filename/process "filename")
  ("m" ibuffer-do-sort-by-major-mode "mode")
  ("s" ibuffer-do-sort-by-size "size")
  ("v" ibuffer-do-sort-by-recency "recency")
  ("i" ibuffer-invert-sorting "invert"))
(defhydra my-hydra/ibuffer-mode/filter (:color amaranth :columns 5
                                        :pre (require 'ibuffer-vc))
  "
Ibuffer → Filter (_q_: ←)"
  ("q" my-hydra/ibuffer-mode/body nil :exit t)
  ("a" ibuffer-add-saved-filters "add-saved")
  ("c" ibuffer-filter-by-content "content")
  ("e" ibuffer-filter-by-predicate "predicate")
  ("f" ibuffer-filter-by-filename "filename")
  ("m" ibuffer-filter-by-used-mode "mode")
  ("M" ibuffer-filter-by-derived-mode "derived mode")
  ("n" ibuffer-filter-by-name "name")
  ("p" ibuffer-pop-filter "pop")
  (">" ibuffer-filter-by-size-gt "size-gt")
  ("<" ibuffer-filter-by-size-lt "size-lt")
  ("&" ibuffer-and-filter "and")
  ("|" ibuffer-or-filter "or")
  ("V" ibuffer-vc-set-filter-groups-by-vc-root "vc-groups")
  ("R" ibuffer-switch-to-saved-filter-groups "saved-groups")
  ("\\" ibuffer-clear-filter-groups "clear-groups")
  ("/" ibuffer-filter-disable "disable"))

;; bind Ibuffer hydra
(with-eval-after-load 'ibuffer
  (define-key ibuffer-mode-map (kbd "C-c C-M-m") #'my-hydra/ibuffer-mode/body))
#+end_src

**** Ibuffer icons                                                    :melpa:

Add file icons next to the file names.
Uses file icon API exposed by the ~all-the-icons~ package.

#+name: all-the-icons-ibuffer
#+begin_src emacs-lisp
;; use font icons in Ibuffer
(when (display-graphic-p)
  (use-package all-the-icons-ibuffer
    :after (all-the-icons ibuffer)
    :config (all-the-icons-ibuffer-mode 1)))
#+end_src

*** Fast buffer switching with nswbuff                                :melpa:

[[https://github.com/joostkremers/nswbuff][nswbuff]] allows for quick switching between open buffers.

It can be configured to integrate with Projectile to only switch
between between those buffers associated with the current project.

#+name: nswbuff
#+begin_src emacs-lisp
;; quick buffer switching (configured to be within a project)
(use-package nswbuff
  :after projectile
  :bind (("<C-tab>" . nswbuff-switch-to-next-buffer)
         ("<C-S-tab>" . nswbuff-switch-to-previous-buffer))
  :init
  (setq nswbuff-buffer-list-function #'nswbuff-projectile-buffer-list
        nswbuff-clear-delay 2
        nswbuff-display-intermediate-buffers t
        ;; exclude all internal buffers from the nswbuff switch list
        nswbuff-exclude-buffer-regexps '("^ "
                                         "^\\*.*\\*"
                                         "org-src-fontification")
        nswbuff-exclude-mode-regexp (mapconcat
                                     'identity
                                     '("dired-mode"
                                       "gnus-mode")
                                     "\\|")
        nswbuff-start-with-current-centered nil)
  :config
  ;; unbind C-tab in org-mode to not conflict with nswbuff global binding
  (with-eval-after-load 'org
    (unbind-key "<C-tab>" org-mode-map)))
#+end_src

*** Kill all other buffers

Kill buffers other than the current one.

#+name: my-kill-other-buffers
#+begin_src emacs-lisp
(defun my-kill-other-buffers ()
  "Kill all file buffers except the current one."
  (interactive)
  (when (y-or-n-p "Kill all file buffers except the current one? ")
    (seq-each
     #'kill-buffer
     (delete (current-buffer)
             (seq-filter #'buffer-file-name (buffer-list))))))
#+end_src

**** Add my-kill-other-buffers to buffer hydra                        :hydra:

#+name: add-my-kill-other-buffers-to-buffer-hydra
#+begin_src emacs-lisp
;; add my-kill-other-buffers to buffer hydra
(defhydra+ my-hydra/buffer nil
  ("o" my-kill-other-buffers "only" :exit t))
#+end_src

*** Buffer cleanup hydra                                              :hydra:

Buffer cleanup hydra, configured as a subhydra for the buffer hydra.

#+name: buffer-cleanup-hydra
#+begin_src emacs-lisp
;; cleanup hydra
(defhydra my-hydra/buffer/cleanup (:color amaranth :columns 3)
  "
Buffer → Cleanup (_q_: ←)"
  ("q" my-hydra/buffer/body nil :exit t)
  ("r" whitespace-report "whitespace-report" :exit t)
  ("w" whitespace-cleanup "whitespace-cleanup")
  ("i" (lambda ()
         "Indent a selected region, or the buffer otherwise."
         (interactive)
         (cond
          ((use-region-p) (indent-region (region-beginning) (region-end)))
          (t (indent-region (point-min) (point-max)))))
   "indent")
  ("t" (lambda ()
         "Indent a selected region, or the buffer otherwise."
         (interactive)
         (cond
          ((use-region-p) (untabify (region-beginning) (region-end)))
          (t (untabify (point-min) (point-max)))))
   "untabify"))

;; add entry point to cleanup hydra to buffer hydra
(defhydra+ my-hydra/buffer nil
  ("C" my-hydra/buffer/cleanup/body "→ Cleanup" :exit t))
#+end_src

** Window management

*** Traverse window configuration history using Winner mode

[[https://www.gnu.org/software/emacs/manual/html_node/emacs/Window-Convenience.html][Winner mode]] allows the traversal of window configuration history using
~C-c <left>~ (undo) and ~C-c <right>~ (redo).

#+name: winner-mode
#+begin_src emacs-lisp
;; traverse window config changes, C-c left/right to undo/redo
;; uncomment to not bind C-c left/right keys by default
;; (setq winner-dont-bind-my-keys t)
;; enable winner-mode at end of initialization
(add-hook 'after-init-hook #'winner-mode)
#+end_src

*** Window management hydra                                           :hydra:

Hydra for [[https://www.gnu.org/software/emacs/manual/html_node/emacs/Windows.html][window]] (panes) management.

*Note*: In Emacs 27+, ~next-multiframe-window~ and
~previous-multiframe-window~ were renamed to ~next-window-any-frame~
and ~previous-window-any-frame~, and the new names are used here.

#+name: window-hydra
#+begin_src emacs-lisp
(defhydra my-hydra/window (:color amaranth :columns 3)
  "
Window (_q_: quit)"
  ("q" nil nil :exit t)
  ("u" winner-undo "winner-undo")
  ("r" winner-redo "winner-redo")
  ("n" (condition-case nil
           (next-window-any-frame)
         (error (next-multiframe-window)))
   "next")
  ("p" (condition-case nil
           (previous-window-any-frame)
         (error (previous-multiframe-window)))
   "previous")
  ("v" split-window-right "split-v")
  ("s" split-window-below "split-h")
  ("<left>" windmove-left "left")
  ("<down>" windmove-down "down")
  ("<up>" windmove-up "up")
  ("<right>" windmove-right "right")
  ("S-<left>" (my-transpose-windows 'windmove-left) "transpose-l")
  ("S-<down>" (my-transpose-windows 'windmove-down) "transpose-d")
  ("S-<up>" (my-transpose-windows 'windmove-up) "transpose-u")
  ("S-<right>" (my-transpose-windows 'windmove-right) "transpose-r")
  ("-" shrink-window "shrink-v")
  ("+" enlarge-window "enlarge-v")
  ("<" shrink-window-horizontally "shrink-h")
  (">" enlarge-window-horizontally "enlarge-h")
  ("M" minimize-window "minimize")
  ("m" maximize-window "maximize")
  ("=" balance-windows "balance")
  ("_" balance-windows-area "balance-area")
  ("o" delete-other-windows "only")
  ("d" delete-window "delete")
  ("D" kill-buffer-and-window "delete-buf"))
(global-set-key (kbd "C-c C-M-w") 'my-hydra/window/body)
#+end_src

**** Window management helper functions

Helper functions for the frame management hydra.

#+name: window-hydra-helper-functions
#+begin_src emacs-lisp
(defun my-transpose-windows (selector)
  "Call SELECTOR and transpose buffers between current and selected windows."
  (let ((from-win (selected-window))
        (from-buf (window-buffer)))
    (funcall selector)
    (set-window-buffer from-win (window-buffer))
    (set-window-buffer (selected-window) from-buf)))
#+end_src

*** Add buffer rotation across windows to window management hydra     :hydra:

Define a function to rotate buffers across the windows of a frame, and
add an entrypoint to the function in the window management hydra.

#+name: rotate-window-buffers-hydra
#+begin_src emacs-lisp
(defun rotate-window-buffers (rotations)
  "Rotate buffers in the windows of the current frame ROTATIONS times.
ROTATIONS can be negative, which rotates in the opposite direction."
  (interactive "P")
  (let ((num-windows (count-windows)))
    (if (not (> num-windows 1))
        (message "Only one window in the frame. Nothing to rotate.")
      (let* ((windows (window-list))
             ;; original window order properties
             (window-props (mapcar (lambda (w)
                                     `(:buffer ,(window-buffer w)
                                       :start ,(window-start w)
                                       :point ,(window-point w)))
                                   windows))
             ;; new window order after rotation
             (window-moves (mapcar
                            (lambda (k)
                              (elt windows (mod (+ k rotations) num-windows)))
                            (number-sequence 0 (1- num-windows))))
             ;; create alist for easier looping later
             (wins-props (mapcar* #'cons window-moves window-props)))
        ;; iteratively assign orig window props in new window order
        (dolist (w-p wins-props)
          (let ((win (car w-p))
                (prop (cdr w-p)))
            (set-window-buffer-start-and-point
             win
             (plist-get prop :buffer)
             (plist-get prop :start)
             (plist-get prop :point))))))))

;; add entrypoint for `rotate-window-buffers' to window management hydra
(defhydra+ my-hydra/window nil
  ("," (lambda (n) (interactive "p") (rotate-window-buffers (- n))) "rotate-l")
  ("." (lambda (n) (interactive "p") (rotate-window-buffers n)) "rotate-r"))
#+end_src

*** popwin for automatic management of special buffers                :melpa:

[[https://github.com/emacsorphanage/popwin][popwin]] configures Emacs to show special buffers like ~*Help*~,
~*Completions*~ and so on in a window that automatically gets closed
on ~C-g~ or selection of another window.

It can also be used to popup temporary buffers, and that functionality
can be accessed through ~popwin:keymap~ which is configured to
the ~C-z~ prefix (use ~C-x C-z~ to call ~suspend-frame~ instead).

#+name: popwin
#+begin_src emacs-lisp
;; popup window manager, also auto-closes special buffers like
;; *compilation* and *Completions*
(use-package popwin
  :config
  (popwin-mode 1)
  (global-set-key (kbd "C-z") popwin:keymap))
#+end_src

*** ace-window

[[https://github.com/abo-abo/ace-window][ace-window]] provides window navigation and manipulation functions.

#+name: ace-window
#+begin_src emacs-lisp
;; window navigation and management
(use-package ace-window
  :config
  (setq aw-background t
        aw-char-position 'left
        aw-ignore-current nil
        aw-scope 'frame)
  (global-set-key (kbd "M-o") #'ace-window))
#+end_src

**** Add ace-window entry point to window hydra

Add entry point for calling ~ace-window~ to the window hydra.

#+name: add-ace-window-to-window-hydra
#+begin_src emacs-lisp
;; add `ace-window' entry point to window hydra
(defhydra+ my-hydra/window nil
  ("a" ace-window "ace-window"))
#+end_src

** Frame management

*** Frame management hydra                                            :hydra:

Hydra for easier [[https://www.gnu.org/software/emacs/manual/html_node/emacs/Frames.html][frame]] (application window) management.

#+name: frame-hydra
#+begin_src emacs-lisp
(defhydra my-hydra/frame (:color amaranth :columns 4)
  "
Frame (_q_: quit)"
  ("q" nil nil :exit t)
  ("<up>" (lambda (n) (interactive "p") (my-move-frame-pct 0 (- n))) "move-u")
  ("<down>" (lambda (n) (interactive "p") (my-move-frame-pct 0 n)) "move-d")
  ("<left>" (lambda (n) (interactive "p") (my-move-frame-pct (- n) 0)) "move-l")
  ("<right>" (lambda (n) (interactive "p") (my-move-frame-pct n 0)) "move-r")
  ("+" (lambda (n) (interactive "p") (my-enlarge-frame 0 n)) "enlarge-v")
  ("-" (lambda (n) (interactive "p") (my-enlarge-frame 0 (- n))) "shrink-v")
  (">" (lambda (n) (interactive "p") (my-enlarge-frame n 0)) "enlarge-h")
  ("<" (lambda (n) (interactive "p") (my-enlarge-frame (- n) 0)) "shrink-h")
  ("M" toggle-frame-maximized "maximize")
  ("f" toggle-frame-fullscreen "fullscreen")
  ("p" (other-frame -1) "previous")
  ("n" other-frame "next")
  ("s" select-frame-by-name "select")
  ("m" (lambda () (interactive) (my-make-frame 15 20)) "make")
  ("d" delete-frame "delete")
  ("o" delete-other-frames "only"))
(global-set-key (kbd "C-c C-M-f") 'my-hydra/frame/body)
#+end_src

**** Frame management helper functions

Helper functions for the frame management hydra.

#+name: frame-hydra-helper-functions
#+begin_src emacs-lisp
(defun my-enlarge-frame (w h)
  "Enlarge width, height of selected frame by W, H lines (shrink if negative)."
  (let ((this-frame (selected-frame)))
    (set-frame-width this-frame (+ (frame-width this-frame) w))
    (set-frame-height this-frame (+ (frame-height this-frame) h))))

(defun my-move-frame (x y)
  "Move selected frame by X pixels horizontally and Y pixels vertically."
  (let* ((this-frame (selected-frame))
         (fpos (frame-position this-frame)))
    (set-frame-position this-frame (+ (car fpos) x) (+ (cdr fpos) y))))

(defun my-move-frame-pct (x y)
  "Move selected frame within display by X% horizontally and Y% vertically."
  (my-move-frame (* x (/ (x-display-pixel-width) 100))
                 (* y (/ (x-display-pixel-height) 100))))

(defun my-make-frame (x y)
  "Make new frame, offset by X pixels horizontally and Y pixels vertically."
  (let ((cur-pos (frame-position)))
    (select-frame (make-frame (list (cons 'left (+ x (car cur-pos)))
                                    (cons 'top (+ y (cdr cur-pos))))))))
#+end_src

*** transpose-frame for rotating frames                               :melpa:

[[https://github.com/emacsorphanage/transpose-frame][transpose-frame]] allows for the rotation of the frame to get a new
window layout that is rotated from the original.

See the package Elisp code for more details.

#+name: transpose-frame
#+begin_src emacs-lisp
(use-package transpose-frame)
#+end_src

**** Add transpose-frame rotation functions to frame management hydra :hydra:

Add entrypoints to ~transpose-frame~ frame rotation functions to the
frame maangement hydra.

#+name: transpose-frame-hydra
#+begin_src emacs-lisp
;; add entrypoint for transpose-frame to frame management hydra
(defhydra+ my-hydra/frame nil
  ("," (lambda (n) (interactive "p") (dotimes (_ n) (rotate-frame-anticlockwise))) "rotate-l")
  ("." (lambda (n) (interactive "p") (dotimes (_ n) (rotate-frame-clockwise))) "rotate-r"))
#+end_src

** Workspace management

*** Desktop.el for saving and restoring sessions

[[https://www.gnu.org/software/emacs/manual/html_node/emacs/Saving-Emacs-Sessions.html][desktop.el]] provides capabilities for saving and restoring sessions
manually and automatically.

*Configuration*:
- Enable ~desktop-save-mode~ which automatically saves on exit and
  loads on entry, but set ~desktop-auto-save-timeout~ to disable
  default behavior of auto-saving on a timer.

#+name: desktop
#+begin_src emacs-lisp
;; settings for desktop.el
;; desktops are saved to ~/.emacs.d/.emacs.desktop
;; and locks are saved to ~/.emacs.d/.emacs.desktop.lock
;; - enable desktop-save-mode to save on exit and load on entry;
;;   this is added to `after-init-hook' to avoid a prompt on startup
;;   warning about the desktop file being in use that occurs when
;;   `desktop-save-mode' is enabled before initialization is done,
;;   even though the Emacs process PID is the owner of the lock file;
;;   might be specific to emacs-mac port
;; - set `desktop-autosave-timeout' to nil to disable timer auto-saves
;; - restore frames to their original displays
;; - don't re-use frames
(setq desktop-auto-save-timeout nil
      desktop-restore-in-current-display nil
      desktop-restore-reuses-frames t)
(add-hook 'after-init-hook (lambda () (desktop-save-mode 1)))
#+end_src

*** Workspace hydra                                                   :hydra:

Hydra for workspace manipulation and usage.

#+name: workspace-hydra
#+begin_src emacs-lisp
;; hydra for workspace management
(defhydra my-hydra/workspace (:color teal :columns 3)
  "
Emacs workspace (_q_: quit)"
  ("q" nil nil)
  ("dc" desktop-clear "desktop-clear")
  ("ds" desktop-save "desktop-save")
  ("dr" desktop-read "desktop-read")
  ("dR" desktop-revert "desktop-revert")
  ("dd" desktop-change-dir "desktop-dir"))

;; binding for workspace management hydra
(global-set-key (kbd "C-c C-M-e") 'my-hydra/workspace/body)
#+end_src

* Command-line interaction

** Eshell

[[https://www.gnu.org/software/emacs/manual/html_mono/eshell.html][Eshell]] is an Elisp shell-like command interpreter that can be used in place of ~term-mode~ and ~bash~.
[[https://www.masteringemacs.org/article/complete-guide-mastering-eshell][More information]] on Eshell usage.

*Customizations*:
- Increase the size of the history input ring from ~128~ to ~1024~.
- Don't review quick commands (those that have no output and returns a
  ~0~ exit code indicating success).
- Have space go to the end of the buffer when it is visible.
- Have point jump to the beginning of the last command after each
  command.
- Load [[https://github.com/emacs-mirror/emacs/blob/master/lisp/eshell/em-smart.el][em-smart]] which adds some quality of life improvements.

*Usage note*:
- When searching history using the beginning of a command,
  ~eshell-previous-matching-input-from-input~ (~UP~), ~M-p~ or ~C-c
  M-r~ is much friendlier than ~eshell-previous-matching-input~
  (~M-r~).
  Type the first few characters of the command, and press
  the ~UP~ or ~M-p~ key repeatedly to cycle only through the matching
  commands in the history.
  Copied from StackOverflow answer [[https://stackoverflow.com/questions/13009908/eshell-search-history][here]].

#+name: eshell
#+begin_src emacs-lisp
(setq eshell-history-size 1024
      eshell-review-quick-commands nil
      eshell-smart-space-goes-to-end t
      eshell-where-to-jump 'begin)
(require 'em-smart)
#+end_src

*** Run visual commands in a separate term buffers

Some "visual" commands present and update a full-screen interface
instead of streaming output to stdout.
Run these commands inside a separate term buffer instead.

#+name: eshell-visual-commands
#+begin_src emacs-lisp
;; enable Eshell to spawn visual commands inside
(require 'em-term)
;; run visual commands and subcommands in term sessions
(dolist (cmd '("htop" "lftp" "ssh" "vi" "vim" "watch"))
  (add-to-list 'eshell-visual-commands cmd))
(dolist (subcmd '(("tail" "-f" "-F")
                  ("sudo" "vi" "vim")
                  ("vagrant" "ssh")))
  (add-to-list 'eshell-visual-subcommands subcmd))
#+end_src

*** Disabling Git pagers so Git can be used in Eshell

#+name: eshell-disable-git-pager
#+begin_src emacs-lisp
;; ensure Git does not launch a pager for easier usage with eshell
(setenv "GIT_PAGER" "")
#+end_src

*** Named Eshell buffers for easier management of multiple Eshell buffers

Provide a binding to a wrapper function that spawns or switches to a
named Eshell buffer.
This allows for easier access to and management of multiple Eshell
buffers.

#+name: eshell-named-buffers
#+begin_src emacs-lisp
;; adapted from https://arte.ebrahimi.org/blog/named-eshell-buffers
(defun my-eshell-with-name ()
  "Prompts for the name of a eshell buffer to open or switch to.
If the NAME given at the prompt is not an existing eshell buffer,
a new one named *eshell*<NAME> will be opened. If no name is
provided, the default interactive `eshell' command is run."
  (interactive)
  (let* ((my-es-bufs (seq-filter
                      (lambda (buf)
                        (string-match-p "*eshell*" (buffer-name buf)))
                      (buffer-list)))
         (my-es-buf-name-list (mapcar #'buffer-name my-es-bufs))
         (my-es-buf-name (completing-read
                          "Eshell Buffer : " my-es-buf-name-list)))
    (if (member my-es-buf-name (mapcar #'buffer-name (buffer-list)))
        (switch-to-buffer my-es-buf-name)
      (if (string= "" my-es-buf-name)
          (eshell)
        (progn
          (eshell 42)
          (rename-buffer (concat "*eshell*<" my-es-buf-name ">")))))))
#+end_src

*** Eshell fish-like history autosuggestions                          :melpa:

[[https://github.com/dieggsy/esh-autosuggest][esh-autosuggest]] provides [[https://fishshell.com/][fish]]-like history autosuggestions in Eshell.

When an autosuggestion is displayed, press ~<right>~ or ~C-f~ to fully
autocomplete, or ~M-<right>~ or ~M-f~ to autocomplete just the next word.

#+name: esh-autosuggest
#+begin_src emacs-lisp
;; history autosuggestions
;; <right> or C-f completes fully, <M-right> or M-f completes partially
(use-package esh-autosuggest
  :after eshell
  :hook (eshell-mode . esh-autosuggest-mode))
#+end_src

*** Helm completion in Eshell

Extend [[https://github.com/emacs-mirror/emacs/blob/master/lisp/pcomplete.el][pcomplete]] ([[https://www.masteringemacs.org/article/pcomplete-context-sensitive-completion-emacs][more info]]) to complete commands and files in Eshell
using Helm, adapted from the [[https://github.com/emacs-helm/helm/wiki/Eshell][Helm wiki]].

#+name: helm-eshell-completion
#+begin_src emacs-lisp
(when (featurep 'helm)
  (add-hook 'eshell-mode-hook
            (lambda ()
              (eshell-cmpl-initialize)
              (define-key eshell-mode-map [remap eshell-pcomplete] 'helm-esh-pcomplete)
              (define-key eshell-mode-map (kbd "M-r") 'helm-eshell-history))))
#+end_src

*** Helm Fish Completion of CLI options in Eshell            :external:melpa:

[[https://github.com/emacs-helm/helm-fish-completion][Helm Fish Completion]] provides a Helm-based extension of [[https://github.com/emacs-mirror/emacs/blob/master/lisp/pcomplete.el][pcomplete]] to
complete CLI options using [[https://fishshell.com/][fish]] in Eshell and other shells.

If using this, ~fish-completion-mode~ should be disabled.

#+name: helm-fish-completion
#+begin_src emacs-lisp
(when (and (executable-find "fish") (featurep 'helm))
  (use-package helm-fish-completion
    :config
    (setq helm-esh-pcomplete-build-source-fn
          #'helm-fish-completion-make-eshell-source)
    (with-eval-after-load 'shell
      (define-key shell-mode-map (kbd "<tab>") #'helm-fish-completion))
    (add-hook 'eshell-mode-hook
              (lambda ()
                (define-key eshell-mode-map (kbd "<tab>")
                  #'helm-fish-completion)))))
#+end_src

*** eshell-z for jumping to frecent directories                       :melpa:

[[https://github.com/xuchunyang/eshell-z][eshell-z]] is a port of [[https://github.com/rupa/z][z]] to Eshell, providing a shell facility for
jumping to frecent directories.

#+name: eshell-z
#+begin_src emacs-lisp
(use-package eshell-z
  :after eshell)
#+end_src

** Command interpreters for other shells

*** Make command interpreter prompts read-only

Make the command interpreter (comint) prompts read-only.

#+name: comint-prompt-read-only
#+begin_src emacs-lisp
;; make shell prompts read-only
(setq comint-prompt-read-only t)
#+end_src

*** Kill term buffers using "q" after session end

Kill term buffers after session end on a "q" keypress.

#+name: kill-term-buffers-with-q-after-end
#+begin_src emacs-lisp
;; kill term buffers with 'q' after session end
(defun term-handle-exit--close-buffer-on-cmd (&rest args)
  "Kill term buffer with 'q' after session exit."
  (when (null (get-buffer-process (current-buffer)))
    (use-local-map (let ((map (make-sparse-keymap)))
                     (define-key map (kbd "q")
                       (lambda ()
                         (interactive)
                         (kill-buffer (current-buffer))))
                     map))))
(advice-add 'term-handle-exit :after #'term-handle-exit--close-buffer-on-cmd)
#+end_src

*** Term hydra                                                        :hydra:

Major mode-specific hydra for [[https://www.gnu.org/software/emacs/manual/html_node/emacs/Terminal-emulator.html][term-mode]] for toggling between char mode
(mostly similar to a regular terminal emulator) and line mode (which
acts like [[https://www.gnu.org/software/emacs/manual/html_node/emacs/Shell-Mode.html#Shell-Mode][Shell mode]]).

#+name: term-mode-hydra
#+begin_src emacs-lisp
;; hydra for term-mode for toggling between char and line modes
(defhydra my-hydra/term-mode (:color amaranth :columns 4)
  "
Term (_q_: quit)"
  ("q" nil nil :exit t)
  ("m" (lambda () (interactive)
         (if (term-in-line-mode)
             (progn (term-char-mode) (message "line → char"))
           (progn (term-line-mode) (message "char → line")))) "toggle-mode"))
;; bindings
(with-eval-after-load 'term
  (define-key term-mode-map (kbd "C-c C-M-m") #'my-hydra/term-mode/body)
  (define-key term-raw-map (kbd "C-c C-M-m") #'my-hydra/term-mode/body))
#+end_src

** vterm                                                              :melpa:

[[https://github.com/akermu/emacs-libvterm][emacs-libvterm]] (vterm) is full terminal emulator run inside Emacs
using [[https://launchpad.net/libvterm][libvterm]].

Note this requires Emacs be compiled with modules support enabled
(using the ~--with-modules~ option). If so, ~module-fix-suffix~ will
be non-nil.

The default behavior of emacs-libvterm is to compile libvterm from
source on the first run.

The following instructions show steps needed on Mac OS to set up the
compile dependencies using MacPorts.

#+begin_example
$ sudo port install cmake libtool
#+end_example

*Configuration*:
- Use shell title to populate buffer name.
- Clearing the screen also clears scrollback history.
- Limit the automatically eval'ed vterm commands in shell output to
  further limit attack vectors for arbitrary code execution.
- Kill vterm buffer automatically on exit.

#+name: vterm
#+begin_src emacs-lisp
(use-package vterm
  :if (and module-file-suffix
           (executable-find "cmake")
           (executable-find "libtool"))
  :init
  (setq vterm-buffer-name-string "vterm %s"
        vterm-clear-scrollback-when-clearing t
        vterm-eval-cmds '(("vterm-clear-scrollback" vterm-clear-scrollback))
        vterm-kill-buffer-on-exit t
        vterm-shell (or (executable-find "fish") shell-file-name)))
#+end_src

*Updating compiled modules*: ~M-x vterm-module-compile~ recompiles
vterm-module, so run it to update to newer versions of libvterm.

*** Shell-side configuration required for specific vterm features

If desired, vterm also has a number of Emacs and shell integration
features (directory tracking, prompt tracking, message parsing, etc)
that require some [[https://github.com/akermu/emacs-libvterm#shell-side-configuration][shell-side configuration]].

Most of these shell-side configurations require a ~vterm_printf~
helper function. For fish, put the following in a
=~/.config/fish/functions/vterm_printf= file.

#+begin_example
# Helper function for sending info from shell to vterm using escape sequences.
# https://github.com/akermu/emacs-libvterm#shell-side-configuration
if [ "$INSIDE_EMACS" = "vterm" ]
    function vterm_printf
        if [ -n "$TMUX" ]
            # tell tmux to pass the escape sequences through
            # (Source: http://permalink.gmane.org/gmane.comp.terminal-emulators.tmux.user/1324)
            printf "\ePtmux;\e\e]%s\007\e\\" "$argv"
        else if string match -q -- "screen*" "$TERM"
            # GNU screen (screen, screen-256color, screen-256color-bce)
            printf "\eP\e]%s\007\e\\" "$argv"
        else
            printf "\e]%s\e\\" "$argv"
        end
    end
end
#+end_example

**** Directory and prompt tracking

Enables using ~C-c C-n~ and ~C-c C-p~ to go the next and previous
prompts respectively while in vterm.

Also allows ~vterm-beginning-of-line~ and ~vterm-at-prompt-p~
functions to better detect prompts.

For fish, create a =~/.config/fish/functions/vterm_prompt_end.fish=
file with the following.

#+begin_example
# Helper function for redefining prompt in config.fish to enable directory
# and prompt tracking in emacs-libvterm
# https://github.com/akermu/emacs-libvterm#directory-tracking-and-prompt-tracking
if [ "$INSIDE_EMACS" = "vterm" ]
    function vterm_prompt_end
        vterm_printf '51;A'(whoami)'@'(hostname)':'(pwd)
    end
end
#+end_example

Next, add the following near the end of the
=~/.config/fish/config.fish= file.

#+begin_example
# Redefine the prompt for directory and prompt tracking in emacs-libvterm
# https://github.com/akermu/emacs-libvterm#directory-tracking-and-prompt-tracking
if [ "$INSIDE_EMACS" = "vterm" ]
    functions -c fish_prompt vterm_old_fish_prompt
    function fish_prompt --description 'Write out the prompt; do not replace this. Instead, put this at end of your file.'
        # Remove the trailing newline from the original prompt. This is done
        # using the string builtin from fish, but to make sure any escape codes
        # are correctly interpreted, use %b for printf.
        printf "%b" (string join "\n" (vterm_old_fish_prompt))
        vterm_prompt_end
    end
end
#+end_example

**** vterm-clear-scrollback-when-clearing support

Required for ~vterm-clear-scrollback-when-clearing~ to work properly
when it is set to non-nil. This automatically clears the scrollback as well
on ~vterm-clear~, which bound to ~C-l~ by default.

Create a =~/.config/fish/functions/clear.fish= file with the following
contents.

#+begin_example
# Redefine clear function to also clear scrollback history in emacs-libvterm
# https://github.com/akermu/emacs-libvterm#vterm-clear-scrollback
if [ "$INSIDE_EMACS" = "vterm" ]
    function clear
        vterm_printf "51;Evterm-clear-scrollback"
        tput clear
    end
end
#+end_example

*** vterm-specific switch-to-buffer

Define a ~switch-to-buffer~ variant restricted to vterm buffers and
the most recently selected buffer besides the current one (this is the
buffer return by ~other-buffer~, and allows for toggling between the
code and vterm), and bind it to ~C-c C-b~ when in ~vterm-mode~.

#+name: vterm-switchb
#+begin_src emacs-lisp
(defun vterm--switch-to-buffer ()
  "Call `switch-to-buffer' but only for vterm buffers."
  (interactive)
  (let ((completion-regexp-list '("\\`vterm .*")))
    (call-interactively #'switch-to-buffer)))

(with-eval-after-load 'vterm
  (define-key vterm-mode-map (kbd "C-c C-b") #'vterm--switch-to-buffer))
#+end_src

** tmux interaction convenenience functions

Define some convenience functions for interaction with the currently active tmux session.
- ~tmux-send~ prompts for a command to send and sends it.
- ~tmux-resend~ resends the previously sent command from the current buffer.

#+name: tmux-send
#+begin_src emacs-lisp
;; convenience functions for sent commands to an active tmux session
;; adapted from https://explog.in/notes/tmux.html

;; track previously sent tmux commands on per-buffer basis
(setq tmux-send--last-command nil)
(make-variable-buffer-local 'tmux-send--last-command)

(defun tmux-send (command)
  "Sends the specified COMMAND to the currently active tmux pane."
  (interactive "sCommand: ")
  (setq tmux-send--last-command command)
  (call-process "tmux" nil nil nil "send-keys" command "Enter"))

(defun tmux-resend ()
  "Resends previously sent command to currently active tmux pane."
  (interactive)
  (if tmux-send--last-command
      (call-process "tmux" nil nil nil "send-keys" tmux-send--last-command "Enter")
    (message "No previously sent command from the current buffer!")))
#+end_src

** Shell entry point hydra

Hydra providing entry points into Eshell and vterm.

#+name: shell-hydra
#+begin_src emacs-lisp
;; hydra providing entry points into shell tools
(defhydra my-hydra/shell (:color teal :columns 3)
  "
Shell tools (_q_: quit)"
  ("q" nil nil)
  ("v" vterm "vterm")
  ("V" vterm-other-window "vterm-other")
  ("C-v" vterm--switch-to-buffer "vterm-switchb")
  ("e" my-eshell-with-name "eshell")
  ("t" tmux-send "tmux-send")
  ("T" tmux-resend "tmux-resend"))

;; binding for spawning or switching to a named Eshell buffer
(global-set-key (kbd "C-c C-M-t") #'my-hydra/shell/body)
#+end_src

* Comparison tools

** Ediff

[[https://www.gnu.org/software/emacs/manual/html_mono/ediff.html][Ediff]] is a built-in tool that visualizes the standard Unix [[https://en.wikipedia.org/wiki/Diff][diff]] and
[[https://en.wikipedia.org/wiki/Patch_(Unix)][patch]] programs.

*Configuration*:
- Always set control window in the same frame as the diff'ed files.

#+name: ediff
#+begin_src emacs-lisp
;; always set up Ediff control window in the same frame as the diff,
;; open with horizontal window split instead of the default vertical
(setq ediff-split-window-function 'split-window-horizontally
      ediff-window-setup-function 'ediff-setup-windows-plain)
#+end_src

*** Ediff copy A and B diff regions to C in a 3-way diff job

Add an Ediff command for copying diff regions for a hunk from both
buffers A and B to C when in a 3-way diff job, for example when
resolving Git merge conflicts.

Adapted from [[https://stackoverflow.com/questions/9656311/conflict-resolution-with-emacs-ediff-how-can-i-take-the-changes-of-both-version/29757750#29757750][here]].

#+name: ediff-copy-a-and-b-to-c
#+begin_src emacs-lisp
;; copy diff hunk from buffers A and B to C in 3-way Ediff
;; adapted from https://stackoverflow.com/a/29757750
(defun ediff-copy-A-and-B-to-C (arg)
  "Copies ARGth diff region from both buffers A and B to C.
ARG is a prefix argument.  If nil, copy the current difference region."
  (interactive "P")
  (ediff-barf-if-not-control-buffer)
  (if (eq arg '-) (setq arg -1)) ;; translate neg arg to -1
  (if (numberp arg) (ediff-jump-to-difference arg))
  (ediff-copy-diff ediff-current-difference nil 'C nil
                   (concat
                    (ediff-get-region-contents ediff-current-difference
                                               'A
                                               ediff-control-buffer)
                    (ediff-get-region-contents ediff-current-difference
                                               'B
                                               ediff-control-buffer)))
  ;; recenter with rehighlighting, but no messages
  (ediff-recenter))
(add-hook 'ediff-keymap-setup-hook
          (lambda ()
            (when ediff-3way-job
              (define-key ediff-mode-map "d" 'ediff-copy-A-and-B-to-C))))
(with-eval-after-load 'ediff-help
  (setq ediff-long-help-message-compare3
        (concat ediff-long-help-message-compare3
                "                                                 |"
                "  d -copy A + B regions to C
"
)))
#+end_src

*** Ediff hydra                                                       :hydra:

Hydra for Ediff.

#+name: ediff-hydra
#+begin_src emacs-lisp
;; hydra for Ediff
(defhydra my-hydra/ediff (:color teal :hint nil)
  "
Ediff (_q_: quit)
Buffer   _b_ : 2-way       _B_ : 3-way
Files    _f_ : 2-way       _F_ : 3-way       _c_ : current
Region   _l_ : line-wise   _w_ : word-wise
Windows  _L_ : line-wise   _W_ : word-wise
"
  ("q" nil nil :exit t)
  ("b" ediff-buffers)
  ("B" ediff-buffers3)
  ("f" ediff-files)
  ("F" ediff-files3)
  ("c" ediff-current-file)
  ("l" ediff-regions-linewise)
  ("w" ediff-regions-wordwise)
  ("L" ediff-windows-linewise)
  ("W" ediff-windows-wordwise))

;; binding for Ediff hydra
(global-set-key (kbd "C-c C-M-=") #'my-hydra/ediff/body)
#+end_src

** Smerge

[[https://github.com/emacs-mirror/emacs/blob/master/lisp/vc/smerge-mode.el][Smerge]] is a lightweight alternative to Ediff, and is the default merge
tool called by [[Magit][Magit]] to resolve merge conflicts.

*** Smerge hydra                                                      :hydra:

Major mode-specific hydra for Smerge.
Adapted from [[https://github.com/alphapapa/unpackaged.el#hydra][here]].

#+name: smerge-hydra
#+begin_src emacs-lisp
;; hydra for smerge-mode
(defhydra my-hydra/smerge-mode (:color pink :hint nil)
  "
Smerge (_q_: quit)
Move   _n_   : next          _p_   : prev
Keep   _b_   : base          _u_   : upper         _l_   : lower
       _a_   : all           _RET_ : current
Diff   _<_   : upper/base    _=_   : upper/lower   _>_   : base/lower
       _R_   : refine        _E_   : ediff
Other  _C_   : combine       _r_   : resolve       _k_   : kill current
"
  ("q" nil nil :exit t)
  ("n" smerge-next)
  ("p" smerge-prev)
  ("b" smerge-keep-base)
  ("u" smerge-keep-upper)
  ("l" smerge-keep-lower)
  ("a" smerge-keep-all)
  ("RET" smerge-keep-current)
  ("<" smerge-diff-base-upper)
  ("=" smerge-diff-upper-lower)
  (">" smerge-diff-base-lower)
  ("R" smerge-refine)
  ("E" smerge-ediff)
  ("C" smerge-combine-with-next)
  ("r" smerge-resolve)
  ("k" smerge-kill-current)
  ;; emulate Vim's "ZZ" command to save and close current file
  ("ZZ" (lambda ()
          (interactive)
          (save-buffer)
          (bury-buffer))
   "Save and bury buffer" :exit t))
;; binding
(with-eval-after-load 'smerge-mode
  (define-key smerge-mode-map (kbd "C-c C-M-m") #'my-hydra/smerge-mode/body))
#+end_src

** View and compare directory trees using Ztree                       :melpa:

The [[https://github.com/fourier/ztree][Ztree]] package provides a directory tree viewer and directory tree
comparison functionality similar to [[https://www.scootersoftware.com/][Beyond Compare]] or [[https://www.araxis.com/merge/index.en][Araxis Merge]].

#+name: ztree
#+begin_src emacs-lisp
;; view and compare directory trees, like Beyond Compare
(use-package ztree
  :bind ("C-c C-M--" . ztree-diff)
  :config
  (setq ztree-dir-move-focus t ;; RET in ztree-dir also moves focus
        ztree-draw-unicode-lines t ;; unicode lines
        ztree-show-number-of-children t)) ;; show number of files in subdir tree

;; convenience navigation bindings for `ztreedir-mode' and `ztreediff-mode'
(with-eval-after-load 'ztree-view
  (define-key ztree-mode-map (kbd "n") #'ztree-next-line)
  (define-key ztree-mode-map (kbd "p") #'ztree-previous-line))
#+end_src

*** Ztree directory tree hydra                                        :hydra:

Major mode-specific hydra for ~ztreedir-mode~.

#+name: ztree-dir-hydra
#+begin_src emacs-lisp
;; mode-specific hydra for ztreedir-mode
(defhydra my-hydra/ztreedir-mode (:color pink :columns 3)
  "
ztree-dir (_q_: quit)"
  ("q" nil nil)
  ("RET" ztree-perform-action "toggle/open-other" :exit t)
  ("SPC" ztree-perform-soft-action "toggle/open" :exit t)
  ("x" ztree-toggle-expand-subtree "toggle" :exit t)
  ("g" ztree-refresh-buffer "refresh" :exit t)
  ("DEL" ztree-move-up-in-tree "goto-parent" :exit t)
  ("H" ztree-dir-toggle-show-filtered-files "show-filtered" :exit t)
  (">" ztree-dir-narrow-to-dir "narrow" :exit t)
  ("<" ztree-dir-widen-to-parent "widen" :exit t)
  ("d" ztree-dir-open-dired-at-point "dired" :exit t))
(with-eval-after-load 'ztree-dir
  (define-key ztreedir-mode-map (kbd "C-c C-M-m") #'my-hydra/ztreedir-mode/body))
#+end_src

*** Ztree directory tree comparison hydra                             :hydra:

Major mode-specific hydra for ~ztreediff-mode~.

#+name: ztree-diff-hydra
#+begin_src emacs-lisp
;; mode-specific hydra for ztreediff-mode
(defhydra my-hydra/ztreediff-mode (:color pink :columns 3)
  "
ztree-diff (_q_: quit)"
  ("q" nil nil)
  ("RET" ztree-perform-action "toggle/ediff" :exit t)
  ("SPC" ztree-perform-soft-action "toggle/diff" :exit t)
  ("TAB" ztree-jump-side "jump-side" :exit t)
  ("x" ztree-toggle-expand-subtree "toggle" :exit t)
  ("g" ztree-refresh-buffer "refresh" :exit t)
  ("DEL" ztree-move-up-in-tree "goto-parent" :exit t)
  ("h" ztree-diff-toggle-show-equal-files "show-equal" :exit t)
  ("H" ztree-diff-toggle-show-filtered-files "show-filtered" :exit t)
  ("d" ztree-diff-simple-diff-files "diff-files" :exit t)
  ("v" ztree-diff-view-file "view" :exit t)
  ("C" ztree-diff-copy "copy" :exit t)
  ("D" ztree-diff-delete-file "delete" :exit t)
  ("r" ztree-diff-partial-rescan "rescan-part" :exit t)
  ("R" ztree-diff-full-rescan "rescan-full" :exit t))
(with-eval-after-load 'ztree-diff
  (define-key ztreediff-mode-map (kbd "C-c C-M-m") #'my-hydra/ztreediff-mode/body))
#+end_src

* DevOps

** docker.el for managing Docker from Emacs                           :melpa:

[[https://github.com/Silex/docker.el][docker.el]] provides integration with Docker, allowing the management of
Docker containers, images and volumes.

#+name: docker
#+begin_src emacs-lisp
(when (executable-find "docker")
  (use-package docker
    :bind ("C-c C-M-d" . docker)))
#+end_src

* Dired

[[https://www.gnu.org/software/emacs/manual/html_node/emacs/Dired.html][Dired]] is a built-in directory editor for Emacs.

Additionally load some built-in extra Dired features, including a
global binding ~C-x C-j~ to directly jump to a Dired buffer for the
directory containing the current buffer.

#+name: dired
#+begin_src emacs-lisp
(require 'dired-x) ;; extra features
(require 'dired-aux) ;; even more extra features
(setq dired-dwim-target t ;; use neighboring dired buffer as default target dir
      dired-listing-switches "-alhvFG" ;; more readable file listings
      dired-omit-files (concat dired-omit-files "\\|^\\..+$") ;; omit dot files in dired-omit-mode
      dired-recursive-copies 'always ;; always copy recursively
      dired-recursive-deletes 'always) ;; always delete recursively
(add-hook 'dired-mode-hook #'auto-revert-mode) ;; auto-refresh on file change
(add-hook 'dired-mode-hook #'dired-hide-details-mode) ;; hide details initially
#+end_src

** Dired hydra                                                        :hydra:

Hydra for Dired, one of the heads uses [[dired-filter extension to add Ibuffer-like filters to Dired][dired-filter]].

#+name: dired-hydra
#+begin_src emacs-lisp
;; hydras for Dired
(defhydra my-hydra/dired-mode (:color pink :columns 4)
  "
Dired (_q_: quit)"
  ("q" nil nil :exit t)
  ("RET" (progn
           (dired-find-file)
           (when (eq major-mode 'dired-mode)
             (my-hydra/dired-mode/body)))
   "open" :exit t)
  ("{" find-name-dired "find-name" :exit t)
  ("}" find-grep-dired "find-grep" :exit t)
  ("(" dired-hide-details-mode "toggle-details")
  (")" dired-omit-mode "toggle-omit")
  ("+" dired-create-directory "mkdir")
  ("=" dired-diff "diff" :exit t)
  ("_" dired-show-file-type "show-file-type")
  ("?" dired-summary "help")
  ("A" dired-do-find-regexp "find-regex" :exit t)
  ("C" dired-do-copy "copy")
  ("c" dired-do-compress-to "compress-to")
  ("D" dired-do-delete "delete")
  ("E" dired-mark-extension "mark-ext")
  ("F" dired-do-find-marked-files "find-marked" :exit t)
  ("G" dired-do-chgrp "chgrp")
  ("g" revert-buffer "refresh")
  ("i" dired-maybe-insert-subdir "insert-subdir")
  ("K" my-dired-kill-and-next-subdir "kill-subdir")
  ("l" dired-do-redisplay "redisplay")
  ("M" dired-do-chmod "chmod")
  ("m" dired-mark "mark")
  ("O" dired-display-file "display")
  ("o" dired-find-file-other-window "find-file-o" :exit t)
  ("Q" dired-do-find-regexp-and-replace "find-regex-sub" :exit t)
  ("R" dired-do-rename "rename")
  ("S" dired-do-symlink "symlink")
  ("s" dired-sort-toggle-or-edit "date-sort")
  ("T" dired-do-touch "touch")
  ("t" dired-toggle-marks "toggle-marks")
  ("U" dired-unmark-all-marks "unmark-all")
  ("u" dired-unmark "unmark")
  ("v" dired-view-file "view-file" :exit t) ;; open file in view-mode
  ("Y" dired-do-relsymlink "symlink-to-dir")
  ("Z" dired-do-compress "compress"))
;; binding for dired hydra
(with-eval-after-load 'dired
  (define-key dired-mode-map (kbd "C-c C-M-m") #'my-hydra/dired-mode/body))
#+end_src

*** Helper functions for Dired hydra

Helper functions for Dired hydra:
- Kill a Dired directory buffer and jump to its parent.

#+name: dired-hydra-helper-functions
#+begin_src emacs-lisp
;; adapted from https://www.reddit.com/r/emacs/comments/jh1me/keeping_large_dired_buffers_tidy/
(defun my-dired-kill-and-next-subdir ()
  "Kill current subdir in dired, and jump back to its parent dir."
  (interactive)
  (let* ((subdir-name (directory-file-name (dired-current-directory)))
         (parent-dir  (file-name-directory subdir-name))
         (search-term (concat " "
                              (file-name-base subdir-name)
                              (file-name-extension subdir-name t))))
    (dired-kill-subdir)
    (dired-goto-subdir parent-dir)
    (search-forward search-term)))
#+end_src

** Have recentf also track dired buffers

Have recentf track dired buffers as well. From [[https://www.emacswiki.org/emacs/RecentFiles#toc21][here]].

Alternative is [[https://github.com/Vifon/dired-recent.el][dired-recent]].

#+name: recentf-track-dired-buffers
#+begin_src emacs-lisp
;; have recentf track dired buffers as well
;; from https://www.emacswiki.org/emacs/RecentFiles#toc21

(defun recentd-track-opened-file ()
  "Insert the name of the directory just opened into the recent list."
  (and (derived-mode-p 'dired-mode) default-directory
       (recentf-add-file default-directory))
  ;; Must return nil because it is run from `write-file-functions'.
  nil)

(defun recentd-track-closed-file ()
  "Update the recent list when a dired buffer is killed.
That is, remove a non kept dired from the recent list."
  (and (derived-mode-p 'dired-mode) default-directory
       (recentf-remove-if-non-kept default-directory)))

(add-hook 'dired-after-readin-hook 'recentd-track-opened-file)
(add-hook 'kill-buffer-hook 'recentd-track-closed-file)
#+end_src

** dired-filter extension to add Ibuffer-like filters to Dired        :melpa:

~dired-filter~ from the [[https://github.com/Fuco1/dired-hacks][dired-hacks]] collection adds filtering capability like that of [[Advanced buffer management with Ibuffer][Ibuffer]] to Dired.

#+name: dired-filter
#+begin_src emacs-lisp
(use-package dired-filter
  :bind (:map dired-mode-map
         ("/" . dired-filter-map))
  :hook (dired-mode . dired-filter-mode)
  :init (setq-default dired-filter-stack nil))
#+end_src

*** dired-filter hydra                                                :hydra:

Hydra for ~dired-filter~.
Entrypoint is through the [[Dired hydra]].

#+name: dired-filter-hydra
#+begin_src emacs-lisp
;; add dired-filter hydra
(defhydra my-hydra/dired-mode/filter (:color pink :columns 4)
  "
Dired → Filter (_q_: ←)"
  ("q" my-hydra/dired-mode/body nil :exit t)
  ("n" dired-filter-by-name "by-name")
  ("r" dired-filter-by-regex "by-regex")
  ("." dired-filter-by-extension "by-ext")
  ("h" dired-filter-by-dot-files "by-hidden")
  ("o" dired-filter-by-omit "by-omit")
  ("g" dired-filter-by-garbage "by-garbage")
  ("e" dired-filter-by-predicate "by-pred")
  ("f" dired-filter-by-file "by-file")
  ("d" dired-filter-by-directory "by-dir")
  ("m" dired-filter-by-mode "by-mode")
  ("s" dired-filter-by-symlink "by-symlink")
  ("x" dired-filter-by-executable "by-exe")
  ("ig" dired-filter-by-git-ignored "by-git-ign")
  ("|" dired-filter-or "or")
  ("!" dired-filter-negate "negate")
  ("*" dired-filter-decompose "decompose")
  ("TAB" dired-filter-transpose "transpose")
  ("p" dired-filter-pop "pop")
  ("/" dired-filter-pop-all "reset")
  ("S" dired-filter-save-filters "save")
  ("D" dired-filter-delete-saved-filters "del")
  ("A" dired-filter-add-saved-filters "add")
  ("L" dired-filter-load-saved-filters "load"))
;; add entrypoint for dired-filter hydra in my-hydra/dired-mode
(defhydra+ my-hydra/dired-mode nil
  ("/" my-hydra/dired-mode/filter/body "→ Filter" :exit t))
#+end_src

** Dired icons using all-the-icons-dired                   :workaround:melpa:

Add font icons from ~all-the-icons~ to Dired using [[https://github.com/jtbm37/all-the-icons-dired][all-the-icons-dired]].
Requires the ~all-the-icons~ package be loaded.

#+name: all-the-icons-dired
#+begin_src emacs-lisp
;; use font icons in Dired
(use-package all-the-icons-dired
  :after (all-the-icons dired)
  :hook (dired-mode . all-the-icons-dired-mode)
  :config
  (set-face-attribute 'all-the-icons-dired-dir-face nil
                      :weight 'normal)
  ;; extra workaround to avoid misalignment in filenames due to with
  ;; varying icon widths
  ;; https://github.com/jtbm37/all-the-icons-dired/issues/10
  (advice-add 'all-the-icons-dired--setup :after
              (lambda () (setq-local tab-width 2))))
#+end_src

* Editing text

** Indent with soft tabs

Use spaces (soft tabs) to indent by default instead of actual tab
characters (hard tabs).

Use ~C-q TAB~ to input hard tabs if necessary.

#+name: indent-with-soft-tabs
#+begin_src emacs-lisp
;; indent with soft tabs; use C-q <TAB> for real tabs
(setq-default indent-tabs-mode nil)
#+end_src

** Completion-enabled yanking from kill-ring

Add a convenience function for yanking (pasting) from the kill-ring
with completion.
Completion support is provided through ~completing-read~, which is
shadowed by completion frameworks like Icomplete, Ido, Ivy, etc.

#+name: completing-yank
#+begin_src emacs-lisp
(defun my-yank-from-kill-ring ()
  "Yank from the kill ring into buffer at point or region.
Uses `completing-read' for selection, which is set by Ido, Ivy, etc."
  (interactive)
  (let ((to-insert (completing-read
                    "Yank : " (cl-delete-duplicates kill-ring :test #'equal))))
    ;; delete selected buffer region if any
    (if (and to-insert (region-active-p))
        (delete-region (region-beginning) (region-end)))
    ;; insert the selected entry from the kill ring
    (insert to-insert)))

;; yank with completion key binding
(global-set-key (kbd "C-c C-M-y") #'my-yank-from-kill-ring)
#+end_src

** Delete selected region on delete or character input

Use the built-in [[https://github.com/emacs-mirror/emacs/blob/master/lisp/delsel.el][delsel]] package to support deleting the selected
region on delete or some character input, which is the behavior in
line with typical user interface conventions.

#+name: delsel
#+begin_src emacs-lisp
;; typing text replaces the active (i.e. selected) region, if any is selected
(delete-selection-mode)
#+end_src

** Single spacing after sentences.

Single spacing after sentences.
For abbreviations, use non-breaking spaces that can be input with
~\\{}nbsp~ in Org documents, or with ~C-x 8 SPC~ for the UTF-8
non-breaking space character.

#+name: sentence-end-single-space
#+begin_src emacs-lisp
;; use single spaces after sentences
(setq sentence-end-double-space nil)
#+end_src

** Transparent editing of GPG files

[[https://www.gnu.org/software/emacs/manual/html_mono/epa.html][EasyPG Assistant]] is a [[https://gnupg.org/][GnuPG]] interface for Emacs.

#+name: epa-file
#+begin_src emacs-lisp
;; enable transparent editing of GPG files
(require 'epa-file)
(epa-file-enable)
#+end_src

** Keyboard macros hydra                                              :hydra:

Hydra for manipulating and using Emacs [[https://www.gnu.org/software/emacs/manual/html_node/emacs/Keyboard-Macros.html][keyboard macros]].
For an example, see the writeup on [[https://www.emacswiki.org/emacs/KeyboardMacros][EmacsWiki]].

#+name: kmacros-hydra
#+begin_src emacs-lisp
(defhydra my-hydra/kmacros (:color teal :columns 3)
  "
Keyboard Macros (_q_: quit)"
  ("q" nil nil)
  ;; start, end and execute macros
  ("(" kmacro-start-macro "start")
  (")" kmacro-end-or-call-macro "end-or-call-last")
  ("r" apply-macro-to-region-lines "call-last-region")
  ;; macro ring
  ("C-n" kmacro-cycle-ring-next "cycle-ring-next" :exit nil)
  ("C-p" kmacro-cycle-ring-previous "cycle-ring-prev" :exit nil)
  ("C-v" kmacro-view-macro "view-last" :exit nil)
  ("C-d" kmacro-delete-ring-head "delete-ring-head" :exit nil)
  ;; macro editing
  ("e" edit-kbd-macro "edit")
  ("RET" kmacro-edit-macro "edit-last")
  ("l" kmacro-edit-lossage "edit-lossage")
  ("SPC" kmacro-step-edit-macro "step-edit")
  ;; naming and binding
  ("b" kmacro-bind-to-key "bind-to-key")
  ("n" kmacro-name-last-macro "name-last")
  ("x" kmacro-to-register "to-register")
  ;; other
  ("i" insert-kbd-macro "insert-named"))
(global-set-key (kbd "C-c C-M-k") 'my-hydra/kmacros/body)
#+end_src

** Registers hydra                                                    :hydra:

Hydra for easier [[https://www.gnu.org/software/emacs/manual/html_node/emacs/Registers.html][register]] manipulation and usage.

#+name: registers-hydra
#+begin_src emacs-lisp
(defhydra my-hydra/registers (:color teal :columns 4)
  "
Registers (_q_: quit)"
  ("q" nil nil)
  ("SPC" point-to-register "save-point")
  ("w" window-configuration-to-register "save-windows")
  ("f" frameset-to-register "save-frames")
  ("j" jump-to-register "jump")
  ("s" copy-to-register "copy-region")
  ("a" append-to-register "append-region")
  ("p" prepend-to-register "prepend-region")
  ("r" copy-rectangle-to-register "copy-rect")
  ("i" insert-register "insert")
  ("l" list-registers "list")
  ("v" view-register "view"))
(global-set-key (kbd "C-c C-M-\"") 'my-hydra/registers/body)
#+end_src

** Display available bindings in a popup                              :melpa:

[[https://github.com/justbur/emacs-which-key][which-key]] shows the available bindings in the minibuffer.
Modify the configuration from the default to only manually using ~C-h~
in the middle of a key sequence.

#+name: which-key
#+begin_src emacs-lisp
;; display available bindings in popup
(use-package which-key
  :bind ("C-c C-M-?" . which-key-show-top-level)
  :init
  (setq which-key-allow-multiple-replacements t
        which-key-compute-remaps t
        ;; configure for manual activation using C-h in the middle of a key seq
        ;; see https://github.com/justbur/emacs-which-key#manual-activation
        which-key-idle-delay 10000
        which-key-idle-secondary-delay 0.05
        which-key-show-early-on-C-h t)
  (which-key-mode 1))
#+end_src

** Expanding selected regions by semantic units                       :melpa:

Use the [[https://github.com/magnars/expand-region.el][expand-region]] package to support expanding selected regions by semantic units.
Examples include character to word, word to sentence, etc.

#+name: expand-region
#+begin_src emacs-lisp
;; expand selected region by semantic units
(use-package expand-region
  :commands er/expand-region
  :bind ("C-=" . er/expand-region))
#+end_src

** Iedit mode for editing occurances of the same word simultaneously  :melpa:

[[https://github.com/victorhge/iedit][Iedit mode]] enables editing multiple occurances of the same word in the
buffer simultaneously.

*Usage notes*:
- ~C-;~ to edit occurances of the word under point within the buffer,
  or ~C-u 0 C-;~ to edit occurances only within the current function.
- Fine control of the search area for occurances is done by using
  ~M-I~ when in ~iedit-mode~ to restrict to current line and ~M-{~ and
  ~M-}~ to expand search region one-line at a time upwards and
  downwards (add a prefix argument to reverse instead).
- ~C-'~ while editing to toggle narrowing to occurance lines.
- ~C-;~ when editing is done to apply changes.

#+name: iedit
#+begin_src emacs-lisp
(use-package iedit
  :init (setq iedit-toggle-key-default (kbd "C-;")))
#+end_src

** Multiple cursors                                                   :melpa:

[[https://github.com/magnars/multiple-cursors.el][multiple-cursors.el]] is package that enables the creation of multiple cursors in Emacs that all do the same thing simultaneously.

#+name: multiple-cursors
#+begin_src emacs-lisp
;; multiple cursors
(use-package multiple-cursors
  :init (setq mc/always-run-for-all nil
              mc/always-repeat-command nil
              mc/insert-numbers-default 1)
  :config
  ;; decrease width of the multiple-cursors bar
  ;; setting a height of 1 ends up rendering a thick bar
  ;; probably because it is too small a value
  (set-face-attribute 'mc/cursor-bar-face nil :height 10))
#+end_src

*** Multiple cursors hydra                                            :hydra:

Hydra providing usage hints for ~multiple-cursor-mode~.
It enables ~multiple-cursors-mode~ automatically on entrance and
disables ~multiple-cursors-mode~ on exit.

#+name: multiple-cursors-hydra
#+begin_src emacs-lisp
;; hydra helper for multiple-cursors-mode
;; disable prefix interpretation when multiple-cursors-mode is active
;; see https://stackoverflow.com/questions/53798055
(defhydra my-hydra/multiple-cursors (:color pink :hint nil
                                     :base-map (make-sparse-keymap))
  "
Multiple-cursors (_C-g_: quit)
Mark    _C-<_: add-prev _C->_: add-next _C-%_: add-all  _C-s_: search
        _C-,_: skp-prev _C-._: skp-next _M-<_: rm-prev  _M->_: rm-next
        _C-|_: edit-lns _<mouse-1>_: add/remove
Misc    _C-{_: number   _C-}_: letter
"
  ("C-g" (lambda ()
           (interactive)
           (mc/keyboard-quit)
           (when multiple-cursors-mode
             (my-hydra/multiple-cursors/body))) :exit t)
  ("C-<" mc/mark-previous-like-this)
  ("C-," mc/skip-to-previous-like-this)
  ("M-<" mc/unmark-previous-like-this)
  ("C->" mc/mark-next-like-this)
  ("C-." mc/skip-to-next-like-this)
  ("M->" mc/unmark-next-like-this)
  ("C-%" mc/mark-all-like-this)
  ("C-s" mc/mark-all-in-region-regexp)
  ("<mouse-1>" mc/add-cursor-on-click)
  ("<down-mouse-1>" ignore)
  ("<drag-mouse-1>" ignore)
  ("<wheel-up>" scroll-down-line)
  ("<wheel-down" scroll-up-line)
  ("C-{" mc/insert-numbers)
  ("C-}" mc/insert-letters)
  ("C-|" mc/edit-lines))

;; bind multiple-cursors hydra
(global-set-key (kbd "C-c C-M-c") #'my-hydra/multiple-cursors/body)
#+end_src

** Snippet expansion using YASnippet                                  :melpa:

[[https://github.com/joaotavora/yasnippet/tree/5b1217ab085fab4abeb1118dccb260691b446703][YASnippet]] is a snippet expansion package for Emacs, which supports
expanding abbreviations into templates. Snippets can defined and
stored in the =snippets= folder within ~user-emacs-directory~.

Two additional supporting packages can also be loaded:
- The YASnippet official snippet collections package containing
  definitions for multiple file types and languages. (Not enabled.)
- The [[https://github.com/abo-abo/auto-yasnippet][Auto-YASnippet]] package, which enables temporary snippet
  definitions that can optionally be persisted. Persisted snippets are
  saved to ~aya-persist-snippet-dir~ (defaults to ~snippets~ in
  ~user-emacs-directory~) in a subfolder corresponding to the major
  mode. (Enabled.)

#+name: yasnippet
#+begin_src emacs-lisp
;; expandable snippet template system
(use-package yasnippet
  :defer 1 ;; load asynchronously after startup
  :config
  ;; (use-package yasnippet-snippets) ;; official snippets
  (use-package auto-yasnippet) ;; enable creation of temporary snippets
  ;; remove default bindings to avoid conflicts with other packages
  ;; removing prefix bindings also removes bindings that use them
  (unbind-key "\C-c&" yas-minor-mode-map)
  (unbind-key "\C-c" yas-minor-mode-map)
  (yas-global-mode 1))
#+end_src

*** YASnippet hydra                                                   :hydra:

Hydra for ~yas-minor-mode~ YASnippet commands and YASnippet supporting
packages.

#+name: yasnippet-hydra
#+begin_src emacs-lisp
;; hydra for YASnippet commands
(defhydra my-hydra/yas-minor-mode (:color teal :columns 4)
  "
YASnippet (_q_: quit)"
  ("q" nil nil)
  ("SPC" yas-expand "expand") ;; expand snippet
  ("d" yas-describe-tables "describe") ;; snippets for current mode
  ("s" yas-insert-snippet "insert") ;; insert snippet
  ("n" yas-new-snippet "new") ;; create new snippet
  ("v" yas-visit-snippet-file "visit-snippet") ;; visit snippet file
  ("w" aya-create "create-auto") ;; store temp snippet
  ("y" aya-expand "expand-auto") ;; paste temp snippet
  ("?" (message "Current auto-yasnippet:\n%s" aya-current)
   "current-auto")) ;; show temp snippet
(with-eval-after-load 'yasnippet
  (define-key yas-minor-mode-map (kbd "C-c C-M-,") #'my-hydra/yas-minor-mode/body))
#+end_src

** Structured editing using Paredit                                   :melpa:

[[https://www.emacswiki.org/emacs/ParEdit][Paredit]] provides a minor mode for structured editing S-expression data.
Enable it for editing Emacs Lisp buffers and the minibuffer.
Also configure it so its commands integrate appropriately with
~delete-selection-mode~.

#+name: paredit
#+begin_src emacs-lisp
;; structured editing of S-expressions with Paredit
(use-package paredit
  :commands paredit-mode
  :hook ((emacs-lisp-mode . paredit-mode)
         ;; when in minibuffer via `eval-expression`
         (eval-expression-minibuffer-setup . paredit-mode)
         ;; *scratch* default mode
         (lisp-interaction-mode . paredit-mode))
  :config
  (with-eval-after-load 'minions
    (add-to-list 'minions-direct 'paredit-mode))
  ;; make delete-selection-mode work within paredit-mode
  (with-eval-after-load 'delsel
    (put 'paredit-forward-delete 'delete-selection 'supersede)
    (put 'paredit-backward-delete 'delete-selection 'supersede)
    (put 'paredit-open-round 'delete-selection t)
    (put 'paredit-open-square 'delete-selection t)
    (put 'paredit-doublequote 'delete-selection t)
    (put 'paredit-newline 'delete-selection t)))
#+end_src

** Traverse undo history as a tree                                     :elpa:

The [[http://www.dr-qubit.org/undo-tree.html][undo-tree]] package allows the traversal of the undo history as a
tree, which makes utilizing Emacs rather flexible undo/redo
capabilities much easier.
Default bindings are ~C-/~ to undo, ~C-S-/~ to redo, and ~C-x u~ to
open a new window whose buffer where the undo history is presented as
a tree and can be navigated using the regular movement keys.

#+name: undo-tree
#+begin_src emacs-lisp
;; traverse undo history as a tree, default binding is "C-x u"
(use-package undo-tree
  :init (setq undo-tree-visualizer-relative-timestamps nil)
  :config (global-undo-tree-mode))
#+end_src

** Zap up to character

~M-z~ is bound by default to ~zap-to-char~ that deletes from the point
to (including) the next occurrence of a given character, which is like
~d f <char>~ in Vim.

There is also a more useful variant ~zap-up-to-char~ which deletes up
to /but not including/ the next occurrence of a given character, which
is like ~d t <char>~ in Vim.

*Configuration*:
- Rebind ~M-z~ to ~zap-up-to-char~. Use ~C-u ARG M-z~ to delete up to
  the ~ARG~-th next occurrence of a character.

#+name: zap-up-to-char
#+begin_src emacs-lisp
;; bind over `zap-to-char' (defaults to "M-x") with `zap-up-to-char'
(global-set-key [remap zap-to-char] #'zap-up-to-char)
#+end_src

** cycle-spacing

Bind ~cycle-spacing~ in place of ~just-one-space~ as it is more
versatile, utilizing a single command to cycle between one space
around point, no spaces around point and original spacing by calling
it consecutively.

#+name: cycle-spacing
#+begin_src emacs-lisp
(global-set-key [remap just-one-space] #'cycle-spacing)
#+end_src

** Join current and next line like "J" in Vim

Join current and next line, like ~J~ in Vim.

#+name: my-join-next-line
#+begin_src emacs-lisp
;; Join next line to end of current line, like "J" in Vim
(defun my-join-next-line ()
  "Join the next line to the end of the current line."
  (interactive)
  (let ((col (current-column)))
    (join-line -1)
    (move-to-column col)))

(global-set-key (kbd "C-S-j") #'my-join-next-line)
#+end_src

** Open new line below/above like "o"/"O" in Vim

Create a new line below/above the current one and move pointer
there, like ~o~/~O~ in Vim.

#+name: my-open-line-below-and-above
#+begin_src emacs-lisp
(defun my-open-line-below (n)
  "Open a new line below and go to it.
With arg N, insert N newlines."
  (interactive "*p")
  (end-of-line)
  (newline n)
  (indent-according-to-mode))

(defun my-open-line-above (n)
  "Open a new line above and go to it.
With arg N, insert N newlines."
  (interactive "*p")
  (beginning-of-line)
  (newline n)
  (forward-line (- n))
  (indent-according-to-mode))

;; bind over `open-line' ("C-o") with `my-open-line-below'
(global-set-key [remap open-line] #'my-open-line-below)
;; binding for `my-open-line-above
(global-set-key (kbd "C-S-o") #'my-open-line-above)
#+end_src

* Emacs as an edit server

** Server mode

Use ~server-mode~ (toggle) or ~server-start~ to start a server from
the current Emacs session.

Clients for the server can be created using ~emacsclient~ command.

*Note*: Quitting the main Emacs session that initiated the server mode
also quits the server and closes the attached clients. If the goal is
to have a headless Emacs server always running, start it with one of
the following.

#+begin_example
# run headless as a daemon in the background
$ /Applications/Emacs.app/Contents/MacOS/bin/emacsclient --daemon
# run headless as a daemon in the foreground
$ /Applications/Emacs.app/Contents/MacOS/bin/emacsclient --fg-daemon
#+end_example

One option is also to have a headless Emacs server spawn on login,
see [[https://www.emacswiki.org/emacs/EmacsAsDaemon][link]].

** SIGUSR1 as a safety valve to restart server when its process is isolated

Send a ~SIGUSR1~ signal to the Emacs process to start or restart the
server process.

See [[https://www.emacswiki.org/emacs/EmacsAsDaemon#toc15][link]] for more info.

#+name: sigusr1-restart-emacs-server
#+begin_src emacs-lisp
;; server mode restart safety valve
(defun restart-emacs-server ()
  "Restarts an Emacs server."
  (interactive)
  (server-force-delete)
  (server-mode 1)
  (message "Restarted Emacs server."))

;; bind SIGUSR1 signal to call `server-restart'
(define-key special-event-map [sigusr1] #'restart-emacs-server)
#+end_src

To test the signal handler, have Emacs send a signal to itself:

#+begin_example
(signal-process (emacs-pid) 'sigusr1)
#+end_example

To call the signal handler from the command line, run:

#+begin_example
$ pkill -SIGUSR1 -i emacs
#+end_example

** Emacs client-server hydra                                          :hydra:

Hydra for Emacs client-server interaction.

#+name: emacs-client-server-hydra
#+begin_src emacs-lisp
;; hydra for Emacs server interaction
(defhydra my-hydra/emacs-client-server (:color teal :hint nil
                                        :pre (require 'server))
  "
Emacs client-server interaction (_q_: quit)
Server  [% 3`server-mode]   _s_ : toggle  _r_ : restart"
  ("q" nil)
  ("s" server-mode :exit nil)
  ("r" restart-emacs-server))

;; binding for Emacs server hydra
(global-set-key (kbd "C-c C-M-S-s") #'my-hydra/emacs-client-server/body)
#+end_src

* Email

This section describes the following email setup to sync and read mail:
- [[https://github.com/gauteh/lieer][Lieer]] is used for syncing with a Gmail source (which requires XOAUTH2).
  - ~gmi pull~ pulls changes from source like new email or tag changes.
  - ~gmi push~ pushes changes to the source like tag changes.
  - ~gmi send~ sends an email via the Gmail API.
- Index emails using [[https://notmuchmail.org/][notmuch]].
- Read and tag email via notmuch using Emacs.
- Send email via lieer using Emacs.

#+begin_example
  ------- push tags with lieer      (gmi push) <------------
  |                                                        |
  v                                                        |
Gmail --> pull mail/tags with lieer (gmi pull) --> notmuch (index) <--> Emacs
  ^                                                                       |
  |                                                                       |
  ------- send mail with lieer      (gmi send) <---------------------------
#+end_example

** Setting up lieer and notmuch                                    :external:

~pip install -e .~ or ~setup.py install -e~ installs lieer in editable
mode (setuptools "develop mode").

#+begin_example
$ sudo port install notmuch
$ notmuch
#+end_example

Create the mail directory at =/Users/myusername/Mail=.

Edit =~/.notmuch-config= and modify the ~[new]~ section as follows:

#+begin_example
[new]
tags=
ignore=/.*[.](json|lock|bak)$/
#+end_example

Next create a virtual environment for ~lieer~ and activate it.

#+begin_example
$ conda create -n lieer python=3.7
$ conda activate lieer
#+end_example

Create a build directory (say =~/build=, change as needed).

#+begin_example
$ mkdir -p ~/build
#+end_example

Install the Python bindings for ~notmuch~.
If possible, ~git checkout~ the commit or tag corresponding to the
version of ~notmuch~ installed for maximum compatibility before
installing the binding via ~pip~.

#+begin_example
$ cd ~/build
$ git clone git://git.notmuchmail.org/git/notmuch
$ cd notmuch/bindings/python
$ pip install .
#+end_example

Install ~lieer~.

#+begin_example
$ cd ~/build
$ git clone https://github.com/gauteh/lieer.git
$ cd lieer
$ pip install .
#+end_example

*Note*: After doing the above, lieer can run using a command ~gmi~.
Because ~notmuch~ was installed with MacPorts and lieer within its own
environment, ~gmi~ needs to be called from within the virtual
environment container and with ~DYLD_FALLBACK_LIBRARY_PATH~ set to
MacPort's library directory at =/opt/local/lib= so it can find the
~notmuch~ libraries, for example:

#+begin_example
DYLD_FALLBACK_LIBRARY_PATH=/opt/local/lib ~/miniconda3/envs/lieer/bin/gmi
#+end_example

To avoid having to specify this every time ~gmi~ needs to be called,
a wrapper script can be used (change the paths as appropriate).

#+begin_example
#!/bin/sh

# Wrapper script for running gmi

DYLD_FALLBACK_LIBRARY_PATH=/opt/local/lib ~/miniconda3/envs/lieer/bin/gmi "$@"
#+end_example

Run the following to create such a file in a directory on the system
path (say =~/.local/bin=), after which calling ~gmi~ directly should
work as expected.

#+begin_example
$ cat > ~/.local/bin/gmi <<EOF
#!/bin/sh

# Wrapper script for running gmi

DYLD_FALLBACK_LIBRARY_PATH=/opt/local/lib ~/miniconda3/envs/lieer/bin/gmi "$@"
EOF
$ chmod +x ~/.local/bin/gmi
#+end_example

Deactivate the virtual environment and build the ~notmuch~ database.

#+begin_example
$ conda deactivate lieer
$ cd ~/Mail
$ notmuch new
#+end_example

Initialize the ~notmuch~ database.

#+begin_example
$ cd ~
$ mkdir Mail
$ notmuch new
#+end_example

Create a local mail directory for the account (replace
=username@emailserver.com= with the actual email address).

#+begin_example
$ cd Mail
$ mkdir username@emailserver.com
$ cd username@emailserver.com
#+end_example

Two options for setting up and authenticating the email account in lieer:

1. Run ~gmi init username@emailserver.com~ which uses the developer's
   client ID.
2. Create a Google dev OAuth client ID, download its secrets file, and
   run ~gmi init -c your-client-secrets.json username@emailserver.com~
   to use the client secrets file.

Option 2 is typically better due to the higher likelihood of running
up against potential usage limits on the shared client ID key, so that
is covered here.

- Go to [[https://console.developers.google.com/flows/enableapi?apiid=gmail][this link]] to create a new Gmail project and API credentials for it.
- Agree to the terms and conditions.
- Continue and navigate to the project. Click the *Credentials* entry
  on the sidebar.
- /Which API are you using?/ \to Gmail API
- /Where will you be calling from?/ \to Other UI (e.g. Windows, CLI tool)
- /What data will you be accessing?/ \to User data
- Click *What credentials do I need?*.
- Click *SET UP CONSENT SCREEN* which opens an /OAuth consent screen/
  tab.
- Select *Internal* if a GSuite user, otherwise *External*.
- In the next screen, fill in the application name (like ~my-email-app~)
  and click *Save*.
- Close the tab and return to the previous one on the /Credentials/ page.
- Refresh the page or click again on *Credentials* and the page
  layout should now be a different one.
# - Click *+ CREATE CREDENTIALS* followed by *API KEY*.
# - Note down the API key.
# - Make sure to edit the API key settings to restrict it to only Gmail APIs.
- Click *+ CREATE CREDENTIALS* followed by *OAuth client ID*.
  Set application type to *Desktop app* and name the application
  (like ~my-email-app~).
- After setting it up, click the download button to download that
  OAuth client ID secret as a JSON file.

When done, move the client secret file (replace ~CLIENT_ID~ with the
actual client id) to the local mailbox directory and use it to
authenticate.
If there's a need to change client IDs in the future, it is possible to
use something like ~gmi auth -f -c client_secrets.json~ (untested).

#+begin_example
$ mv ~/Downloads/client_secret_CLIENTID.json
$ gmi init -c client_secret_CLIENTID.json username@emailserver.com
#+end_example

Now retrieve email using ~gmi~.

#+begin_example
$ gmi pull
#+end_example

*Aside*

By default the custom tags in ~new.tags~ in the notmuch configuration
that are populated for all new emails are also pushed to the remote.

As an example, suppose notmuch is configured to add the ~new~ tag to
any new email pulled per the following section in =~/.notmuch-config=
(note that for Gmail, it already has the ~unread~ tag so this is not
really that needed).

#+begin_example
[new]
tags=new
ignore=/.*[.](json|lock|bak)$/
#+end_example

Lieer can be configured to not push the custom tag ~new~ when syncing
for each specific email source (so that the source does not get
populated with extraneous tags) by running the following in the email
source directory.

#+begin_example
$ gmi set --ignore-tags-local new
#+end_example

*Back from aside*

Next, configure a hook that runs ~gmi pull~ when ~notmuch new~ is called.

First, make a ~hooks~ directory in the notmuch database.

#+begin_example
$ mkdir -p ~/Mail/.notmuch/hooks
#+end_example

Next create the file =~/Mail/.notmuch/hooks/pre-new= with the following contents
(change the local mailbox directory name and ~gmi~ path as relevant).

#+begin_example
#/bin/sh
cd ~/Mail/username@emailserver.com
~/.local/bin/gmi sync
#+end_example

Modify its permissions so it is executable.

#+begin_example
$ chmod +x ~/Mail/.notmuch/hook/pre-new
#+end_example

After setting this up, running ~notmuch new~ will sync new mail to the database.

(Adapted from [[http://www.johnborwick.com/2019/02/09/notmuch-gmailieer.html][this blog post]] and the lieer [[https://github.com/gauteh/lieer/blob/master/docs/index.md][documentation]].)

** Lieer and notmuch usage notes

*** Changing local or remote ignored tags in lieer Gmail synchronization

When changing local or remote ignored tags (for example using commands
~gmi set --ignore-tags-remote~ or ~gmi set --ignore-tags-local~), it
is best to do a full push or push (doing a dry run first to make sure
the changes are the ones that are desired).

The reason is that if the ignored tags were changed after the initial
sync, this will not update already synced messages.

Before changing anything, make sure the local and remote respositories
are fully synchronized.

After ~gmi set --ignore-tags-remote~ or editing the JSON config to the
same effect:

#+begin_example
$ gmi pull -f --dry-run
$ gmi pull -f
#+end_example

And after ~gmi set --ignore-tags-local~ or editing the JSON config to
the same effect:

#+begin_example
$ gmi push -f --dry-run
$ gmi push -f
#+end_example

Also, most importantly make sure to *only change one at a time* (don't
change both local and remote ignored tags at the same time).

A full push or pull is also a heavy handed fix when tags end up not
synchronizing in general.

*** Regular background mail syncs using launchd in Mac OS X        :external:

On most Linux and Unix machines, it is easiest to have ~notmuch new~
run in the backend according to a schedule using ~cron~ to sync mail
automatically.

On Mac OS X, it is recommended to use [[https://support.apple.com/guide/terminal/script-management-with-launchd-apdc6c1077b-5d5d-4d35-9c19-60f2397b2369/mac][launchd]] ([[https://developer.apple.com/library/archive/documentation/MacOSX/Conceptual/BPSystemStartup/Chapters/CreatingLaunchdJobs.html][more info]]) instead.
Create the file =~/Library/LaunchAgents/local.notmuch.new.plist= with
the following contents that specifies the ~notmuch new~ launchd job.
This provided configuration makes the next assumptions.
- The ~notmuch~ binary was installed to =/opt/local/bin/notmuch= which
  is the default location using MacPorts. Modify this as needed.
- ~notmuch new~ is to be run every 600 seconds. If changing this run
  interval, modify both ~StartInterval~ and ~ThrottleInterval~ to the
  desired time interval in seconds; note that due to a [[https://gist.github.com/dabrahams/4092951][some]] [[https://alvinalexander.com/mac-os-x/mac-os-x-startinterval-broken-launchctl-throttleinterval/][bugs]], it is
  best to /make sure both values are set/.

#+begin_example
<?xml version="1.0" encoding="UTF-8"?>
<!DOCTYPE plist PUBLIC "-//Apple//DTD PLIST 1.0//EN" "http://www.apple.com/DTDs/PropertyList-1.0.dtd">
<plist version="1.0">
<dict>
  <!-- Name of job -->
  <key>Label</key>
  <string>local.notmuch.new</string>
  <!-- Command to run -->
  <key>ProgramArguments</key>
  <array>
    <string>/opt/local/bin/notmuch</string>
    <string>new</string>
  </array>
  <!-- Run interval in seconds -->
  <key>StartInterval</key>
  <integer>600</integer>
  <!-- Throttle, https://alvinalexander.com/mac-os-x/mac-os-x-startinterval-broken-launchctl-throttleinterval/ -->
  <key>ThrottleInterval</key>
  <integer>600</integer>
</dict>
</plist>
#+end_example

To load the mail sync job, run the following.

#+begin_example
$ launchctl load ~/Library/LaunchAgents/local.notmuch.new.plist
#+end_example

To see if the mail sync job is currently loaded, run the following.

#+begin_example
$ launchctl list | grep local.notmuch.new
#+end_example

To unload the mail sync job, do the following.

#+begin_example
$ launchctl unload ~/Library/LaunchAgents/local.notmuch.new.plist
#+end_example

** Reading email using notmuch.el                 :workaround:external:melpa:

Notmuch has an Emacs [[https://notmuchmail.org/notmuch-emacs/][client]].

*Configuration*:
- Prompt for which account to use when sending email.
- When archiving, remove the ~inbox~ tag.
- Show 10 most recent searches in the hello screen.
- Use ~,~ as the thousands separator character.
- Sort search results by date descending (default is date ascending).
- Don't show notmuch logo.
- Set up replies so text being replied to is properly quoted by Gmail
  (it automatically assumes duplicated text should be quoted).

*Notes*:
- It is best to copy over the contents of the =emacs= folder from the
  Notmuch codebase into the =site-lisp/notmuch= folder in
  ~user-emacs-directory~ (or some directory in ~load-path~) than using
  the MELPA version, so that the Elisp code matches the system binary.

#+name: notmuch
#+begin_src emacs-lisp
;; configure Notmuch email client
(when (executable-find "notmuch")
  (use-package notmuch
    :ensure nil ;; in site-lisp directory
    :bind (("C-c C-M-n" . notmuch)
           :map notmuch-show-mode-map
           ("d" . notmuch-show--toggle-trash-tag)
           :map notmuch-search-mode-map
           ("d" . notmuch-search--toggle-trash-tag)
           :map notmuch-tree-mode-map
           ("d" . notmuch-tree--toggle-trash-tag))
    :init
    (setq notmuch-always-prompt-for-sender t
          notmuch-archive-tags '("-inbox")
          notmuch-hello-recent-searches-max 10
          notmuch-hello-thousands-separator "," ;; US convention
          notmuch-search-oldest-first nil ;; sort date descending
          notmuch-search-result-format `(("date" . "%12s ")
                                         ("count" . "%-7s ")
                                         ("authors" . "%-20s ")
                                         ("tags" . "%s ")
                                         ("subject" . "%s"))
          notmuch-show-logo nil
          ;; workaround for Notmuch using SVG icons when unsupported
          ;; https://emacs.stackexchange.com/questions/14875/notmuch-mode-very-slow-in-emacs-mac-port-railwaycat
          notmuch-tag-formats '(("unread"
                                 (propertize tag 'face 'notmuch-tag-unread))
                                ("flagged"
                                 (propertize tag 'face 'notmuch-tag-flagged)))
          notmuch-tree-result-format `(("date" . "%12s  ")
                                       ("authors" . "%-20s")
                                       ((("tree" . "%s")
                                         ("subject" . "%s"))
                                        . " %-54s ")
                                       ("tags" . "%s")))
    :config
    ;; toggle deletion of message from the Show view
    ;; note that in Gmail, deleted messages are marked with the "trash" label
    (defun notmuch-show--toggle-trash-tag ()
      "Toggle trash tag for message in the Show view."
      (interactive)
      (if (member "trash" (notmuch-show-get-tags))
          (notmuch-show-tag (list "-trash"))
        (notmuch-show-tag (list "+trash" "-inbox"))))
    ;; toggle deletion of thread from the Search view
    ;; note that in Gmail, deleted messages are marked with the "trash" label
    (defun notmuch-search--toggle-trash-tag (&optional beg end)
      "Toggle trash tag for thread(s) in the Search view.
If applying to a selected region, it adds or removes the trash
tag based on the entry at the beginning of the region."
      (interactive (notmuch-interactive-region))
      (if (member "trash" (notmuch-search-get-tags beg))
          (notmuch-search-tag (list "-trash") beg end)
        (notmuch-search-tag (list "+trash" "-inbox") beg end)))
    ;; toggle deletion of thread from the Tree view
    ;; note that in Gmail, deleted messages are marked with the "trash" label
    (defun notmuch-tree--toggle-trash-tag ()
      "Toggle trash tag for message in the Tree view."
      (interactive)
      (if (member "trash" (notmuch-tree-get-tags))
          (notmuch-tree-tag (list "-trash"))
        (notmuch-tree-tag (list "+trash" "-inbox"))))))
#+end_src

** Shortening names in Search view for threads with multiple authors

Advise ~:around~ ~notmuch-search-insert-authors~ to show the first and
last author of a thread, and abbreviate and truncate each of their
names in the ~authors~ param to better fit the author column width.

Regexp name transforms adapted from [[https://scripter.co/narrowing-the-author-column-in-magit/][here]].

#+name: notmuch-shorten-multiple-author-names
#+begin_src emacs-lisp
;; advise `notmuch-search-insert-authors' so that when a thread has
;; multiple authors, only the first and last message authors are
;; displayed and their names are abbreviated to fit the column width
(with-eval-after-load 'notmuch
  (defvar notmuch--abbreviate-person-name-width
    (let* ((format-string (string-trim
                           (cdr
                            (assoc "authors"
                                   notmuch-search-result-format))))
           (authors-width (string-width (format format-string ""))))
      (- (/ authors-width 2) 1))
    "Width of each author in Notmuch Search view when more than one.
Should be N/2-1, N is the width of the Search view author column.")

  (defun notmuch--abbreviate-person-name (name &optional maxlen)
    "Abbreviates a person NAME.
The result will have `notmuch--abbreviate-person-name-width'
characters or less. This is done by using the initial of the
person's first name and shortening the person's last name as
necessary; also handles emails."
    (let* ((maxlen (or maxlen notmuch--abbreviate-person-name-width))
           (split-idx (string-match-p "\[,@\]" name))
           (split-char (if split-idx
                           (substring name split-idx (+ split-idx 1))
                         "")))
      (cond ((string-equal split-char "@") ;; user.name@server.com -> u name
             (let ((name-part (substring name 0 split-idx)))
               (notmuch--abbreviate-person-name name-part)))
            (t
             ;; is-comma-split t? lastname, firstname -> f lastname
             ;; is-comma-split f? firstname lastname -> f lastname
             ;;                   OR firstname -> firstname
             (let* ((is-comma-split (string-equal split-char ","))
                    (regexp (if is-comma-split
                                "\\(.*?\\), *\\(.\\).*"
                              "\\(.\\).*?[. ]+\\(.*\\)"))
                    (replacement (if is-comma-split
                                     "\\2 \\1"
                                   "\\1 \\2"))
                    (abbrev-name (replace-regexp-in-string regexp
                                                           replacement
                                                           name))
                    (further-truncate (> (length abbrev-name)
                                         maxlen)))
               (if further-truncate
                   (concat
                    (substring abbrev-name
                               0
                               (- maxlen 2))
                    "..")
                 abbrev-name))))))

  (defun notmuch-search-insert-authors--around-abbreviate (orig-fun &rest args)
    "Advice for `notmuch-search-insert-authors' to abbreviate names.
Extracts the authors field from ARGS, abbreviates its elements
using `notmuch--abbreviate-person-name' and calls ORIG-FUN
replacing the original authors with their abbreviated names.
Assumes ', ' is used to separate authors and names are not of the
form 'Lastname, Firstname'."
    (seq-let [format-string authors] args
      (save-match-data
        (let ((author-list (mapcar (lambda (s) (replace-regexp-in-string
                                                "'" "" s)) ;; no single quotes
                                   (split-string authors ", "))))
          (if (> (length author-list) 1)
              (let* ((oldest-newest-authors (cons (car author-list)
                                                  (last author-list)))
                     (abbrev-authors
                      (mapconcat 'identity
                                 (mapcar 'notmuch--abbreviate-person-name
                                         oldest-newest-authors)
                                 ", ")))
                (apply orig-fun (list format-string abbrev-authors)))
            (apply orig-fun args))))))

  ;; abbreviate names when there are multiple authors
  (advice-add 'notmuch-search-insert-authors :around
              'notmuch-search-insert-authors--around-abbreviate))
#+end_src

** Toggling visibility of search tags in the notmuch search results list

Add ability to toggle search tags in the notmuch search results.

When turned on, the search results will not show tags that
are part of the search query, the rationale being the user knows
what they searched for and because the search query.

However, tag modifications are always shown. Examples:
- When the search query is ~tag:inbox~ and the ~inbox~ tag removed
  from a message, +~inbox~+ is displayed for that message.
- When the search query is ~not tag:inbox~ and the ~inbox~ tag is
  added to a message, _~inbox~_ is displayed for that message.

This has some similarities to what Gmail does with labels in search
results, hiding labels that are part of the search query.

*Configuration*:
- Turned on by default.
- ~C-t~ in a search results buffer toggles search tag visibility.

#+name: notmuch-toggle-search-tags-in-results
#+begin_src emacs-lisp
;; notmuch extension to toggle search tag visibility in results by
;; advising the search listings field insertion function to remove
;; tags in the search query from the displayed tags except for those
;; modified after the search
(with-eval-after-load 'notmuch

  (defun notmuch--extract-search-tags (query)
    "Extracts out a list of tags from a given notmuch search QUERY.
More concretely, it identifies tokens that begin with the prefix
'is:' or 'tag:' and returns them as a list without the prefix.
Returns nil if there are no tags in the query."
    (seq-filter
     'identity
     (mapcar (lambda (x)
               (if (string-match "^\\(tag\\|is\\):\\([^ ]*\\)" x)
                   (match-string 2 x)
                 nil))
             (split-string query))))

  (defun string-equal-except (except-list s1 s2)
    "Tests if strings S1 are S2 the same, but return nil if
either is in EXCEPT-LIST."
    (if (or (member s1 except-list)
            (member s2 except-list))
        nil
      (string-equal s1 s2)))

  (defun notmuch--filter-common-search-tags (tags orig-tags query)
    "Returns '(TAGS ORIG-TAGS) with search tags in QUERY filtered out.
Only query search tags appearing in both TAGS and ORIG-TAGS are
removed."
    (let ((add-tags (cl-set-difference tags orig-tags :test 'string-equal))
          (rem-tags (cl-set-difference orig-tags tags :test 'string-equal))
          (search-tags (notmuch--extract-search-tags query)))
      (list (cl-set-difference tags
                               search-tags
                               :test (apply-partially
                                      'string-equal-except
                                      add-tags))
            (cl-set-difference orig-tags
                               search-tags
                               :test (apply-partially
                                      'string-equal-except
                                      rem-tags)))))

  (defun notmuch-search-insert-field--filter-search-tags (orig-fun &rest args)
    "Advises the `notmuch-search-insert-field' function
to filter search tags from the displayed tags like in Gmail.
ORIG-FUN should be `notmuch-search-insert-field' and ARGS are the
original arguments passed to it."
    (seq-let [field format-string result] args
      (if (string-equal field "tags")
          (let ((base-tags (plist-get result :tags))
                (base-orig-tags (plist-get result :orig-tags))
                (query (if (boundp 'notmuch-search-query-string)
                           notmuch-search-query-string
                         nil)))
            (seq-let [tags orig-tags] (notmuch--filter-common-search-tags
                                       base-tags base-orig-tags query)
              (insert (format format-string
                              (notmuch-tag-format-tags tags orig-tags)))))
        (apply orig-fun args))))

  (defun notmuch-tree-format-field--filter-search-tags (orig-fun &rest args)
    "Advises the `notmuch-tree-format-field' function
to filter search tags from the displayed tags like in Gmail.
ORIG-FUN should be `notmuch-tree-format-field' and ARGS are the
original arguments passed to it."
    (seq-let [field format-string msg] args
      (cond ((listp field) (apply orig-fun args))
            ((string-equal field "tags")
             (let ((base-tags (plist-get msg :tags))
                   (base-orig-tags (plist-get msg :orig-tags))
                   (face (if (plist-get msg :match)
                             'notmuch-tree-match-tag-face
                           'notmuch-tree-no-match-tag-face))
                   (query (if (boundp 'notmuch-tree-basic-query)
                              notmuch-tree-basic-query
                            nil)))
               (seq-let [tags orig-tags] (notmuch--filter-common-search-tags
                                          base-tags base-orig-tags query)
                 (format format-string
                         (notmuch-tag-format-tags tags orig-tags face)))))
            (t (apply orig-fun args)))))

  ;; using a global variable helps in correcting scenarios where
  ;; individual tag visibility states get misaligned
  (defvar notmuch--search-tags-visible t
    "Indicates if search tags are visible in Notmuch Tree and Search views.")

  (defun notmuch--toggle-search-tag-visibility ()
    "Toggle visibility of search tags in the Search and Tree views.
Assumes "
    (interactive)
    (let ((current-hide-search-tags
           (advice-member-p #'notmuch-search-insert-field--filter-search-tags
                            'notmuch-search-insert-field))
          (current-hide-tree-tags
           (advice-member-p #'notmuch-tree-format-field--filter-search-tags
                            'notmuch-tree-format-field)))
      ;; toggle Search view advice as needed
      (cond
       ((and current-hide-search-tags (not notmuch--search-tags-visible))
        (advice-remove 'notmuch-search-insert-field
                       #'notmuch-search-insert-field--filter-search-tags))
       ((and (not current-hide-search-tags) notmuch--search-tags-visible)
        (advice-add 'notmuch-search-insert-field :around
                    #'notmuch-search-insert-field--filter-search-tags)))
      ;; toggle Tree view advice as needed
      (cond
       ((and current-hide-tree-tags (not notmuch--search-tags-visible))
        (advice-remove 'notmuch-tree-format-field
                       #'notmuch-tree-format-field--filter-search-tags))
       ((and (not current-hide-tree-tags) notmuch--search-tags-visible)
        (advice-add 'notmuch-tree-format-field :around
                    #'notmuch-tree-format-field--filter-search-tags)))
      (setq notmuch--search-tags-visible (not notmuch--search-tags-visible))
      (notmuch-refresh-all-buffers)
      (message (if notmuch--search-tags-visible
                   "Search tags visible."
                 "Search tags hidden."))))

  ;; enable filtering of search tags in the Search and Tree views by default
  (notmuch--toggle-search-tag-visibility)

  ;; bindings to toggle visibility of search tags in the results
  (dolist (map '(notmuch-hello-mode-map
                 notmuch-search-mode-map
                 notmuch-tree-mode-map))
    (define-key map (kbd "C-t")
      #'notmuch--toggle-search-tag-visibility)))
#+end_src

** Sending mail with notmuch.el using Lieer as the sendmail program :external:

Lieer can be used to send outgoing mail through the Gmail account
([[https://github.com/gauteh/lieer/wiki/GNU-Emacs-and-Lieer][instructions]]).

This can be configured by creating a =notmuch-config.el= file in the
~user-emacs-directory~ with the following contents, but changing the
paths to the mailbox =~/Mail/username@emailserver.com= and the ~gmi~
executable path =~/.local/bin/gmi= as appropriate.

In most instances the filepath for the notmuch init file is the
=~/.emacs.d/notmuch-config.el=.

#+begin_example
;;; notmuch-config.el --- Notmuch config file -*- lexical-binding: t -*-

;;; Commentary:

;; Notmuch configuration file, helpful for avoiding clutter in the
;; regular config files due to machine-specific notmuch settings.
;; This file is loaded when notmuch starts up in Emacs.

;;; Code:

;; configure sendmail to use lieer to send email
(setq sendmail-program (expand-file-name "~/.local/bin/gmi"))
(setq message-sendmail-extra-arguments
      `("send" "--quiet" "-t"
        "-C" ,(expand-file-name "~/Mail/username@emailserver.com")))

;; don't save outgoing mail locally, as sent mails are saved in Gmail
;; automatically
(setq notmuch-fcc-dirs nil)

;; kill message composition buffers after sending
(setq message-kill-buffer-on-exit t)

(provide 'notmuch-config)

;;; notmuch-config.el ends here
#+end_example

** OrgMsg for composing HTML emails                                   :melpa:

[[https://github.com/jeremy-compostella/org-msg][OrgMsg]] provides HTML email composition capability.
Calling ~org-msg-mode~ toggles whether to use HTML composition for emails
by (by default, it is off).

#+name: org-msg
#+begin_src emacs-lisp
;; provides HTML email composition using Org-mode
;; for autogreeting, set `org-msg-greeting-fmt' to "\nHi *%s*,\n\n"
(use-package org-msg
  :config
  (setq org-msg-options (concat "html-postamble:nil H:5 num:nil ^:{} "
                                "toc:nil author:nil email:nil \\n:t")
        org-msg-startup "hidestars indent inlineimages"
        org-msg-greeting-fmt nil
        org-msg-greeting-name-limit 3
        org-msg-text-plain-alternative t)
  (with-eval-after-load 'notmuch
    ;; enable HTML email message composition
    (org-msg-mode 1)
    ;; bindings to toggle HTML email message composition
    (dolist (map '(notmuch-hello-mode-map
                   notmuch-search-mode-map
                   notmuch-show-mode-map
                   notmuch-tree-mode-map))
      (define-key map (kbd "M") #'org-msg-mode))))
#+end_src

*** OrgMsg hydra                                                      :hydra:

Major mode-specific hydra for ~org-msg-edit-mode~ for jumping to
various parts of the email and performing email-related actions.

*Note*: ~org-msg-edit-mode~ is in effect when editing email messages
with ~org-msg-mode~ enabled.

#+name: org-msg-edit-mode-hydra
#+begin_src emacs-lisp
;; major mode-specific hydra for OrgMsg edit mode
(with-eval-after-load 'org-msg
  (defhydra my-hydra/org-msg-edit-mode (:color teal :columns 6)
    "
OrgMsg (_q_: quit)"
    ("q" nil nil)
    ("f" message-goto-from "from")
    ("t" message-goto-to "to")
    ("c" message-goto-cc "cc")
    ("B" message-goto-bcc "bcc")
    ("F" message-goto-fcc "fcc")
    ("S" message-goto-subject "subj")
    ("b" org-msg-goto-body "body")
    ("C-a" org-msg-attach "attach")
    ("C-e" org-msg-preview "preview")
    ("C-c" org-ctrl-c-ctrl-c "send")
    ("C-k" org-msg-edit-kill-buffer "kill"))

  ;; binding for org-msg-edit-mode
  (define-key org-msg-edit-mode-map (kbd "C-c C-M-m")
    #'my-hydra/org-msg-edit-mode/body))
#+end_src

** Link to notmuch messages in Org-mode documents                  :external:

[[https://code.orgmode.org/bzg/org-mode/src/master/contrib/lisp/ol-notmuch.el][ol-notmuch]] is an Org-mode contributed package that adds Org-mode
support for notmuch links.

~ol-notmuch~ is installed either by installing the ~org-contrib~
package from the Org package repository, or by downloading the
=ol-notmuch.el= file to a directory on the ~load-path~ like a folder
in the =site-lisp/= subdirectory of ~emacs-user-directory~.

Loading the packages enables support for following notmuch links in
Org-mode buffers and storing links to searches and messages using
~org-store-link~.

*Note*: This requires Org mode 9.2.3 or newer.

#+name: ol-notmuch
#+begin_src emacs-lisp
(condition-case nil
    (require 'ol-notmuch)
  (error (message "ol-notmuch requires Org 9.2.3+")))
#+end_src

* Information

** Display current time in minibuffer

Convenience function for displaying the current time in minibuffer.

#+name: display-current-datetime
#+begin_src emacs-lisp
(defun my-display-current-datetime ()
  "Display the current time in the minibuffer."
  (interactive)
  (message (format-time-string "%Y-%b-%d %l:%M:%S%p %Z %A")))
#+end_src

** Emacs information

Convenience functions for displaying Emacs process and build info.

#+name: display-emacs-info
#+begin_src emacs-lisp
(defun my-display-emacs-pid ()
  "Display the process id of current Emacs process in the minibuffer."
  (interactive)
  (message (format "%d" (emacs-pid))))

(defun my-display-emacs-build-configuration ()
  "Display the Emacs build configuration in the minibuffer."
  (interactive)
  (message (mapconcat 'identity
                      `("Emacs build configuration"
                        ,(format "  Build target:     %s"
                                 system-configuration)
                        ,(format "  Enabled features: %s"
                                 system-configuration-features))
                      "\n")))
#+end_src

** System process management with proced

[[https://github.com/emacs-mirror/emacs/blob/master/lisp/proced.el][Proced]] lists system processes in a buffer and provides
commands to operate to listed processes.
It can be used like [[https://en.wikipedia.org/wiki/Top_(software)][top]] by toggling auto-refresh
using ~M-x proced-toggle-auto-update~.

Note that Mac OS X isn't very well supported by this package,
so configure it only for Linux systems.
For Mac OS X, an alternative is using ~top~ in an ~term-mode~ buffer.

For more usage information, see [[https://www.masteringemacs.org/article/displaying-interacting-processes-proced][here]].

#+name: proced
#+begin_src emacs-lisp
;; manage system processes in Linux
(when (eq system-type 'gnu/linux)
  (setq proced-format 'medium))
#+end_src

** Information hydra

Hydra for random information, like the current datetime or system
resource usage.

#+name: info-hydra
#+begin_src emacs-lisp
(defhydra my-hydra/info (:color amaranth :columns 3)
  "
Info (_q_: quit)"
  ("q" nil nil :exit t)
  ("b" my-display-emacs-build-configuration "emacs-build-config")
  ("i" emacs-init-time "emacs-init-time")
  ("P" my-display-emacs-pid "emacs-pid")
  ("p" proced "proced" :exit t)
  ("t" my-display-current-datetime "datetime")
  ("T" display-time-world "world-time" :exit t)
  ("u" emacs-uptime "emacs-uptime")
  ("v" emacs-version "version"))

(global-set-key (kbd "C-c C-M-i") #'my-hydra/info/body)
#+end_src

* Marks and markers

[[https://www.gnu.org/software/emacs/manual/html_node/emacs/Mark.html][Marks]] and [[https://www.gnu.org/software/emacs/manual/html_node/elisp/Markers.html][markers]] are useful for saving pointer locations and jumping
back to them later.

They can be thought of as temporary bookmarks, and can be
set up so that they are automatically created.

Examples of automatic mark and marker creation configured by default:
- Isearch automatically creates marks where the search originated.
- Calling ~xref-find-definitions~ automatically creates a marker where
  the jump was initiated.

** Marks and markers hydra

Hydra for manipulating and managing marks and markers.

#+name: marks-and-markers-hydra
#+begin_src emacs-lisp
;; backtrack through the entire xref--marker-ring in a single action
(defun xref-pop-marker-stack-all ()
  "Pop back to where \\[xref-find-definitions] was first invoked."
  (interactive)
  (let ((ring xref--marker-ring))
    (when (ring-empty-p ring)
      (user-error "Marker stack is empty"))
    (let ((marker (ring-remove ring nil))) ;; oldest marker
      (switch-to-buffer (or (marker-buffer marker)
                            (user-error "The marked buffer has been deleted")))
      (goto-char (marker-position marker))
      (set-marker marker nil nil)
      (run-hooks 'xref-after-return-hook)
      (xref-clear-marker-stack)))) ;; clear the rest of the marker stack

;; hydra for manipulating and managing marks and markers
(defhydra my-hydra/marks-and-markers (:color amaranth :columns 3)
  "
Marks / Markers (_q_: quit)"
  ("q" nil nil :exit t)
  ("SPC" (lambda ()
           (interactive)
           (push-mark))
   "mark-push" :exit t)
  ("S-SPC" (lambda ()
             (interactive)
             (set-mark-command t))
   "mark-pop")
  (")" mark-sexp "mark-sexp")
  ("}" mark-paragraph "mark-paragraph")
  ("]" mark-defun "mark-defun")
  ("b" mark-whole-buffer "mark-whole-buf")
  ("x" exchange-point-and-mark "exchange-pt-mk")
  ("." (lambda ()
         (interactive)
         (xref-push-marker-stack))
   "xref-push-marker" :exit t)
  ("," xref-pop-marker-stack "xref-pop-marker")
  ("<" xref-pop-marker-stack-all "xref-pop-markers")
  ("c" (lambda ()
         (interactive)
         (xref-clear-marker-stack)
         (message "Cleared xref--marker-ring"))
   "xref-clear-markers"))
(global-set-key (kbd "C-c C-M-.") 'my-hydra/marks-and-markers/body)
#+end_src

** Add helm mark ring entrypoints to marks hydra                      :hydra:

Add entry point to ~helm-mark-ring~ and ~helm-all-mark-rings~ to
the marks and markers hydra.

#+name: add-helm-mark-ring-to-marks-and-markers-hydra
#+begin_src emacs-lisp
(with-eval-after-load 'helm
  (defhydra+ my-hydra/marks-and-markers nil
    ("m" helm-mark-ring "helm-marks-buf" :exit t)
    ("M" helm-all-mark-rings "helm-marks-all" :exit t)))
#+end_src

* Non-programming files

** Configuring the DocView mode                                    :external:

Emacs has a built-in [[https://www.gnu.org/software/emacs/manual/html_node/emacs/Document-View.html][DocView mode]] that utilizes available system tools
to view DVI, PS, PDF, OpenDocument, and Microsoft Office documents.

The following libraries provide document viewing capability:
- [[https://www.ghostscript.com/][Ghostscript]] for viewing DVI, PS, PDF and EPS files.
- [[https://poppler.freedesktop.org/][Poppler]] for viewing PDF files as text.
- [[https://0x2a.at/site/projects/djvu2pdf/][djvu2pdf]] for viewing DJVU files.
- [[https://github.com/unoconv/unoconv][unoconv]] for viewing Office files (requires LibreOffice be
  installed). Note that unoconv is not in MacPorts.

#+begin_example
$ sudo port install ghostscript
$ sudo port install poppler
$ sudo port install djvu2pdf
#+end_example

For more information, see [[https://emacsnotes.wordpress.com/2018/08/09/222/][this blog post]] on configurating and using DocView mode in Emacs.

Configure enhanced menus for DocView mode (from the blog post above).

#+name: doc-view-enhanced-menus
#+begin_src emacs-lisp
(with-eval-after-load 'doc-view
  (easy-menu-define my-doc-view-menu doc-view-mode-map "Menu for Doc-View Mode."
    '("DocView"
      ["Switch to a different mode" doc-view-toggle-display :help "Switch to a different mode"]
      ["Open Text" doc-view-open-text :help "Display the current doc's contents as text"]
      "--"
      ("Navigate Doc"
       ["Goto Page ..." doc-view-goto-page :help "View the page given by PAGE"]
       "--"
       ["Scroll Down" doc-view-scroll-down-or-previous-page :help "Scroll page down ARG lines if possible, else goto previous page"]
       ["Scroll Up" doc-view-scroll-up-or-next-page :help "Scroll page up ARG lines if possible, else goto next page"]
       "--"
       ["Next Line" doc-view-next-line-or-next-page :help "Scroll upward by ARG lines if possible, else goto next page"]
       ["Previous Line" doc-view-previous-line-or-previous-page :help "Scroll downward by ARG lines if possible, else goto previous page"]
       ("Customize"
        ["Continuous Off"
         (setq doc-view-continuous nil)
         :help "Stay put in the current page, when moving past first/last line" :style radio :selected
         (eq doc-view-continuous nil)]
        ["Continuous On"
         (setq doc-view-continuous t)
         :help "Goto to the previous/next page, when moving past first/last line" :style radio :selected
         (eq doc-view-continuous t)]
        "---"
        ["Save as Default"
         (customize-save-variable 'doc-view-continuous doc-view-continuous)
         t])
       "--"
       ["Next Page" doc-view-next-page :help "Browse ARG pages forward"]
       ["Previous Page" doc-view-previous-page :help "Browse ARG pages backward"]
       "--"
       ["First Page" doc-view-first-page :help "View the first page"]
       ["Last Page" doc-view-last-page :help "View the last page"])
      "--"
      ("Adjust Display"
       ["Enlarge" doc-view-enlarge :help "Enlarge the document by FACTOR"]
       ["Shrink" doc-view-shrink :help "Shrink the document"]
       "--"
       ["Fit Width To Window" doc-view-fit-width-to-window :help "Fit the image width to the window width"]
       ["Fit Height To Window" doc-view-fit-height-to-window :help "Fit the image height to the window height"]
       "--"
       ["Fit Page To Window" doc-view-fit-page-to-window :help "Fit the image to the window"]
       "--"
       ["Set Slice From Bounding Box" doc-view-set-slice-from-bounding-box :help "Set the slice from the document's BoundingBox information"]
       ["Set Slice Using Mouse" doc-view-set-slice-using-mouse :help "Set the slice of the images that should be displayed"]
       ["Set Slice" doc-view-set-slice :help "Set the slice of the images that should be displayed"]
       ["Reset Slice" doc-view-reset-slice :help "Reset the current slice"])
      ("Search"
       ["New Search ..."
        (doc-view-search t)
        :help "Jump to the next match or initiate a new search if NEW-QUERY is given"]
       "--"
       ["Search" doc-view-search :help "Jump to the next match or initiate a new search if NEW-QUERY is given"]
       ["Backward" doc-view-search-backward :help "Call `doc-view-search' for backward search"]
       "--"
       ["Show Tooltip" doc-view-show-tooltip :help nil])
      ("Maintain"
       ["Reconvert Doc" doc-view-reconvert-doc :help "Reconvert the current document"]
       "--"
       ["Clear Cache" doc-view-clear-cache :help "Delete the whole cache (`doc-view-cache-directory')"]
       ["Dired Cache" doc-view-dired-cache :help "Open `dired' in `doc-view-cache-directory'"]
       "--"
       ["Revert Buffer" doc-view-revert-buffer :help "Like `revert-buffer', but preserves the buffer's current modes"]
       "--"
       ["Kill Proc" doc-view-kill-proc :help "Kill the current converter process(es)"]
       ["Kill Proc And Buffer" doc-view-kill-proc-and-buffer :help "Kill the current buffer"])
      "--"
      ["Customize"
       (customize-group 'doc-view)]))
  (easy-menu-define my-doc-view-minor-mode-menu doc-view-minor-mode-map "Menu for Doc-View Minor Mode."
    '("DocView*"
      ["Display in DocView Mode" doc-view-toggle-display :help "View"]
      ["Exit DocView Mode" doc-view-minor-mode])))
#+end_src

** csv-mode for comma-delimited and tab-delimited files (CSV, TSV)     :elpa:

[[https://elpa.gnu.org/packages/csv-mode.html][csv-mode]] provides a major mode for editing comma and tab delimited
(CSV and TSV) files.

To extend it to separator characters other the default comma and tab,
add or modify the ~csv-separators~ list.

#+name: csv-mode
#+begin_src emacs-lisp
(use-package csv-mode
  :commands csv-mode
  :bind (:map csv-mode-map
         ("C-c C-M-m" . my-hydra/csv-mode/body)
         ("C-c C-S-a" . csv-align-visible-fields))
  :config
  (setq csv-align-style 'auto) ;; `csv-align-fields' left/right-aligns text/numbers
  (defun csv-align-visible-fields ()
    "Align visible lines in `csv-mode'. Useful for large CSV files where
`csv-align-fields' can take a very long time to run."
    (interactive)
    (csv-align-fields nil (window-start) (window-end))))
#+end_src

*** csv-mode hydra                                                    :hydra:

Hydra for ~csv-mode~.

#+name: csv-mode-hydra
#+begin_src emacs-lisp
;; major mode-specific hydra for csv-mode
(defhydra my-hydra/csv-mode (:color teal :columns 4)
  "
CSV (_q_: quit)"
  ("q" nil nil)
  ("s" csv-sort-fields "sort")
  ("n" csv-sort-numeric-fields "numsort")
  ("r" csv-reverse-region "reverse")
  ("d" csv-toggle-descending "toggle-desc-sort" :exit nil)
  ("t" csv-transpose "transpose")
  ("k" csv-kill-fields "cut")
  ("y" csv-yank-fields "paste")
  ("z" csv-yank-as-new-table "paste-as-new-tab")
  ("A" csv-align-visible-fields "align-visible" :exit nil)
  ("a" csv-align-fields "align" :exit nil)
  ("u" csv-unalign-fields "unalign" :exit nil)
  ("h" csv-header-line "toggle-header" :exit nil)
  ("v" csv-toggle-invisibility "toggle-invis-sep" :exit nil))

;; binding for csv-mode hydra
(with-eval-after-load 'csv-mode
  (define-key csv-mode-map (kbd "C-c C-M-m") #'my-hydra/csv-mode/body))
#+end_src

** dockerfile-mode for Dockerfiles                                    :melpa:

[[https://github.com/spotify/dockerfile-mode][dockerfile-mode]] provides a major mode for editing Docker files.

#+name: dockerfile-mode
#+begin_src emacs-lisp
(use-package dockerfile-mode
  :commands dockerfile-mode
  :config (add-to-list 'auto-mode-alist '("Dockerfile\\'" . dockerfile-mode)))
#+end_src

** nov.el for EPUB reading

[[https://depp.brause.cc/nov.el/][nov.el]] is a major mode for reading EPUB files.

#+name: nov
#+begin_src emacs-lisp
;; major mode for reading EPUBs
(use-package nov
  :init (add-to-list 'auto-mode-alist
                     '("\\.epub\\'" . nov-mode)))
#+end_src

** json-mode for JSON files                                           :melpa:

[[https://github.com/joshwnj/json-mode][json-mode]] provides a major mode for editing [[https://www.json.org/json-en.html][JSON]] files.

#+name: json-mode
#+begin_src emacs-lisp
;; provides a major mode for editing JSON files
(use-package json-mode
  :defer t)
#+end_src

** markdown-mode for Markdown files                                   :melpa:

[[https://jblevins.org/projects/markdown-mode/][markdown-mode]] provdes a major mode for editing Markdown files.

*Configuration*:
- Enable ~markdown-mode~ automatically for the common suffixes, except
  for =README.md= where ~gfm-mode~ is enabled instead for editing
  Github-flavored Markdown.
- Load the [[https://www.mathjax.org/][MathJax]] Javascript library in HTML output to render [[http://tug.org/][TeX]],
  [[https://www.w3.org/Math/][MathML]] and [[http://asciimath.org/][AsciiMath]] expressions, target HTML+CSS (the loaded
  configuration can be modified if targeting SVG or MathML instead).

#+name: markdown-mode
#+begin_src emacs-lisp
;; major mode for editing Markdown files
(use-package markdown-mode
  :commands (markdown-mode gfm-mode)
  :mode (("README\\.md\\'" . gfm-mode)
         ("\\.md\\'" . markdown-mode)
         ("\\.markdown\\'" . markdown-mode))
  :init
  ;; place header markup only at the start of a line
  ;; syntax highlighting in fenced code blocks
  ;; use underscores for italics instead of asterisks
  (setq markdown-asymmetric-header t
        markdown-fontify-code-blocks-natively t
        markdown-italic-underscore t)
  ;; if available, use pandoc for converting markdown files
  (when (executable-find "pandoc")
    (setq markdown-command "pandoc --from markdown --to html"
          markdown-command-needs-filename t))
  ;; render mathematical expressions in HTML previews
  (setq markdown-xhtml-header-content
        (concat "<script type=\"text/x-mathjax-config\">"
                "MathJax.Hub.Config({"
                "  tex2jax: {"
                "    inlineMath: [ ['$','$'], [\"\\\\(\",\"\\\\)\"] ],"
                "    processEscapes: true"
                "  }"
                "});"
                "</script>"
                "<script type=\"text/javascript\" async"
                "        src=\"https://cdnjs.cloudflare.com/ajax/libs/"
                "mathjax/2.7.7/MathJax.js?config=TeX-MML-AM_CHTML\">"
                "</script>")))
#+end_src

*** markdown-mode hydra                                               :hydra:

Major mode-specific hydra for ~markdown-mode~.

#+name: markdown-mode-hydra
#+begin_src emacs-lisp
;; major mode-specific hydra for markdown-mode
(defhydra my-hydra/markdown-mode (:color teal :hint nil)
  "
Markdown mode (_q_: quit)
Keymaps     _c_ : commands  _s_ : styles
Outline     _n_ : next      _p_ : prev      _f_ : fwd-level _b_ : bwd-level
            _←_ : promote   _→_ : demote    _↓_ : move-down _↑_ : move-up
Shift-Rgn   _<_ : left      _>_ : right
Toggle      _E_ : math      _F_ : code-font _I_ : images    _L_ : url
            _M_ : markup
Other       _d_ : do        _o_ : follow    _'_ : edit code block
"
  ("q" nil nil)
  ;; keymaps
  ("c" (lambda () (interactive) (setq unread-command-events (listify-key-sequence "\C-c\C-c"))))
  ("s" (lambda () (interactive) (setq unread-command-events (listify-key-sequence "\C-c\C-s"))))
  ;; outline
  ("n" markdown-outline-next :color red)
  ("p" markdown-outline-previous :color red)
  ("f" markdown-outline-next-same-level :color red)
  ("b" markdown-outline-previous-same-level :color red)
  ("<left>" markdown-promote :color red)
  ("<right>" markdown-demote :color red)
  ("<down>" markdown-move-down :color red)
  ("<up>" markdown-move-up :color red)
  ;; shift region
  ("<" markdown-outdent-region :color red)
  (">" markdown-indent-region :color red)
  ;; user interface
  ("E" markdown-toggle-math)
  ("F" markdown-toggle-fontify-code-blocks-natively)
  ("I" markdown-toggle-inline-images)
  ("L" markdown-toggle-url-hiding)
  ("M" markdown-toggle-markup-hiding)
  ;; other
  ("d" markdown-do)
  ("o" markdown-follow-thing-at-point)
  ("'" markdown-edit-code-block))

;; bindings for markdown-mode hydra
(with-eval-after-load 'markdown-mode
  (define-key gfm-mode-map (kbd "C-c C-M-m") #'my-hydra/markdown-mode/body)
  (define-key markdown-mode-map (kbd "C-c C-M-m") #'my-hydra/markdown-mode/body))
#+end_src

*** markdown-toc for creating tables of content in Markdown buffers   :melpa:

#+name: markdown-toc
#+begin_src emacs-lisp
(use-package markdown-toc
  :after markdown-mode)
#+end_src

**** Add heads to create or remove Markdown TOCs to markdown-mode hydra :hydra:

#+name: add-markdown-toc-to-markdown-mode-hydra
#+begin_src emacs-lisp
;; add heads to create, update and delete tables of contents in
;; markdown-mode buffers
(with-eval-after-load 'markdown-toc
  (defhydra+ my-hydra/markdown-mode nil
    ("t" markdown-toc-generate-or-refresh-toc "insert-or-refresh-toc")
    ("C-t" markdown-toc-delete-toc "delete-toc")))
#+end_src

** YAML                                                               :melpa:

[[https://github.com/yoshiki/yaml-mode][yaml-mode]] provides a major mode for editing [[https://yaml.org/][YAML]] files.

#+name: yaml-mode
#+begin_src emacs-lisp
;; provides a major mode for editing YAML files
(use-package yaml-mode
  :commands yaml-mode
  :mode ("\\.ya?ml\\'" . yaml-mode))
#+end_src

** neuron-mode                                               :external:melpa:

[[https://github.com/felko/neuron-mode][neuron-mode]] is a mode for editing [[https://neuron.zettel.page/][neuron]] zettelkasten notes.

Assumes that neuron Zettelkasten files are stored in the
=~/zettelkasten= directory.

Requires that neuron be installed, which currently requires [[https://nixos.org/][nixpkg]].

#+name: neuron-mode
#+begin_src emacs-lisp
;; neuron-mode, settings adapted from
;; https://gist.github.com/felko/cdb3fc19b3a60db27eb3c5bd319fc479
(use-package neuron-mode
  :init
  (defface neuron-stub-face
    '((((class color) (min-colors 88) (background dark)) :foreground "#C16069" :underline "#C16069")
      (((class color) (min-colors 88) (background light)) :foreground "#C16069" :underline "#C16069")
      (((class color) :foreground "Red" :underline "Red"))
      (t :inherit neuron-title-overlay-face))
    "Face for stub links."
    :group 'neuron-faces)
  (setq neuron-default-zettelkasten-directory (expand-file-name "~/zettelkasten")
        neuron-default-tags '("stub")
        neuron-id-format 'hash
        neuron-tag-specific-title-faces '(("stub" neuron-stub-face)))
  :config
  ;; push location on to marker stack before following neuron link
  ;; so backtracking is possible via `xref-pop-marker-stack' or "M-,"
  (advice-add #'neuron-follow-thing-at-point :before #'xref-push-marker-stack))
#+end_src

*** Installing neuron

Some ways to install neuron:
- Download a static Linux bundle that can run as is (Linux-only).
- Install using [[https://nixos.org/][Nix]] (Linux, Mac, Windows via WSL).

**** Linux bundle

Nightly builds of static Linux bundles are available from [[https://github.com/srid/neuron/releases/tag/nightly][here]],
generated using [[https://github.com/matthewbauer/nix-bundle][nix-bundle]]. These can simply be run directly.

Download ~neuron-linux-bundle~ to some location on disk, say to the
=/path/to/= directory.

#+begin_example
$ chmod +x neuron-linux-bundle
$ ./neuron-linux-bundle --help
#+end_example

Symlink the bundle file to ~neuron~ a directory in ~$PATH~ (change
=./local/bin= and =/path/to/neuron-linux-bundle= as necessary).

#+begin_example
$ cd .local/bin
$ ln -s /path/to/neuron-linux-bundle neuron
#+end_example

Test that neuron is installed correctly.

#+begin_example
$ neuron --help
#+end_example

**** Nix installation

Install Nix. The following instructions show how to install Nix
in single-user mode; see the [[https://nixos.org/nix/manual/][official documentation]] for a multi-user
install.

#+begin_example
$ sudo mkdir /nix
$ sudo chown $USER /nix
$ curl -O https://nixos.org/nix/install
$ bash install --no-daemon
#+end_example

Note that the Nix =install= script can be safely removed after this.

Install neuron following [[https://neuron.zettel.page/2011501.html][instructions]] on the official website.

Install [[https://cachix.org/][Cachix]] if haven't already done so.

#+begin_example
$ nix-env -iA cachix -f https://cachix.org/api/v1/install
#+end_example

Use the cache to fetch binaries instead of compiling most packages.

#+begin_example
$ cachix use srid
#+end_example

Install Neuron.

#+begin_example
$ nix-env -if https://github.com/srid/neuron/archive/master.tar.gz
#+end_example

*** Upgrading neuron

Neuron upgrades can be done using the same command for installing it.

#+begin_example
$ nix-env -if https://github.com/srid/neuron/archive/master.tar.gz
#+end_example

*** neuron-mode hydras                                                :hydra:

Hydras for interacting with the configured neuron Zettelkasten, and
when editing neuron zettels.

#+name: neuron-hydra
#+begin_src emacs-lisp
;; entrypoint hydra into neuron Zettelkasten
(defhydra my-hydra/neuron (:color teal :columns 4)
  "
Neuron Zettelkasten (_q_: quit)"
  ("q" nil nil)
  ("z" neuron-new-zettel "new")
  ("e" neuron-edit-zettel "edit")
  ("o" neuron-open-zettel "open")
  ("O" neuron-open-index "index")
  ("j" neuron-open-daily-notes "daily")
  ("t" neuron-query-tags "query-tags")
  ("g" neuron-refresh "refresh")
  ("c" neuron-edit-zettelkasten-configuration "config")
  ("rw" neuron-rib-watch "rib-watch")
  ("rg" neuron-rib-generate "rib-gen")
  ("rs" neuron-rib-serve "rib-serve")
  ("ro" neuron-rib-open-zettel "rib-open")
  ("rz" neuron-rib-open-z-index "rib-z-idx")
  ("rk" neuron-rib-kill "rib-kill"))

;; binding for neuron entrypoint hydra
(with-eval-after-load 'neuron-mode
  (global-set-key (kbd "C-c C-M-z") #'my-hydra/neuron/body))
#+end_src

#+name: neuron-mode-hydra
#+begin_src emacs-lisp
;; mode-specific hydra for neuron-mode
(defhydra my-hydra/neuron-mode (:color teal :columns 3)
  "
Neuron mode (_q_: quit)"
  ("q" nil nil)
  ("u" neuron-edit-uplink "edit-uplink")
  ("t" neuron-add-tag "add-tag")
  ("T" neuron-add-tags "add-tags")
  ("l" neuron-create-and-insert-zettel-link "create-link")
  ("L" neuron-create-zettel-from-selected-title "create-selected")
  ("s" neuron-insert-static-link "insert-static-link")
  ("c" neuron-toggle-connection-type "toggle-conn-type")
  ("r" neuron-open-current-zettel "open-current")
  ("o" neuron-follow-thing-at-point "follow"))

;; wrapper hydra for accessing markdown and neuron-mode hydras from
;; mode-specific entrypoint
(defhydra my-hydra/markdown-neuron-mode-wrapper (:color teal)
  "
Markdown mode hydra / Neuron mode hydra (_q_: quit)
"
  ("q" nil nil)
  ("m" my-hydra/markdown-mode/body "markdown-mode")
  ("z" my-hydra/neuron-mode/body "neuron-mode"))

;; binding for mode-specific wrapper hydra for markdown/neuron modes
(with-eval-after-load 'neuron-mode
  (add-hook 'neuron-mode-hook
            (lambda ()
              (use-local-map (copy-keymap markdown-mode-map))
              (local-set-key (kbd "C-c C-M-m")
                             #'my-hydra/markdown-neuron-mode-wrapper/body))))
#+end_src

* Org-mode

[[https://orgmode.org/][Org-mode]] is a major mode for document editing, formatting and
organizing, designed to help with taking notes, planning and authoring
in Emacs.
Org files typically have filenames with the ~.org~ suffix.

*Note*: The version of Org packaged with a given Emacs version is
typically adequate, but it is often better to install the newest
version of ~org~ from ELPA or the Org repository through
~list-packages~.

Examples of good configurations include [[http://doc.norang.ca/org-mode.html][this]].

*Customizations*:
- Use =~/org/= as the main Org directory.
- Set =inbox.org= in ~org-directory~ as the default filing location to
  be utilized later.

#+name: org
#+begin_src emacs-lisp
;; set Org directory and inbox file
(setq org-directory (file-name-as-directory (expand-file-name "~/org"))
      my-org-agenda-inbox (concat org-directory "inbox.org"))

;; basic Org-mode settings
(setq org-adapt-indentation nil ;; don't auto-indent when promoting/demoting
      org-attach-dir-relative t ;; use relative directories when setting DIR property using `org-attach-set-directory'
      ;; org-blank-before-new-entry '((heading . nil) ;; don't auto-add new lines
      ;;                              (plain-list-item . nil)) ;; same as above
      org-catch-invisible-edits 'error
      org-confirm-babel-evaluate nil ;; don't confirm before evaluating code blocks in Org documents
      org-cycle-separator-lines 2 ;; collapse single item separator lines when cycling
      org-edit-src-content-indentation 2
      org-fontify-done-headline t
      org-fontify-quote-and-verse-blocks t
      org-fontify-whole-heading-line t
      org-hide-emphasis-markers nil
      org-hide-leading-stars nil
      org-highlight-latex-and-related '(latex script entities) ;; highlight LaTeX fragments with the `org-highlight-latex-and-related' face
      org-image-actual-width (list (/ (display-pixel-width) 3)) ;; auto-resize displayed images to one-third of display width
      org-link-file-path-type 'adaptive ;; use relative paths for links to files in Org file dir or subdirs, absolute otherwise
      org-log-done 'time ;; log time that task was marked DONE
      org-log-into-drawer t
      org-outline-path-complete-in-steps nil
      org-pretty-entities t
      org-pretty-entities-include-sub-superscripts nil ;; don't render sub/superscripts in-buffer
      org-return-follows-link t
      org-src-fontify-natively nil ;; don't syntax color org source blocks
      org-src-preserve-indentation t ;; preserve src code block indentation on export and when switching btw org buffer and edit buffer
      org-src-strip-leading-and-trailing-blank-lines t
      org-src-tab-acts-natively t
      org-src-window-setup 'current-window ;; reuse Org file window for editing source blocks when using "C-c '"
      org-startup-folded t
      org-startup-indented nil
      org-treat-S-cursor-todo-selection-as-state-change nil
      org-use-fast-todo-selection t
      org-use-speed-commands nil)

;; make sure UUIDs generated for Org usage are alway upcased, which
;; solves issues with synced directories, for example Linux generates
;; lower case UUIDs while Mac generates upper case UUIDs.
(with-eval-after-load 'org-id
  (defun org-id-uuid--around-upcase (orig-fun &rest args)
    "Advice for `org-id-uuid' to upcase the uuids it outputs.
ORIG-FUN is the original function.
ARGS are the arguments provided to ORIG-FUN."
    (let ((uuid (apply orig-fun args)))
      (upcase uuid)))
  (advice-add 'org-id-uuid :around
              'org-id-uuid--around-upcase))
#+end_src

** Bind over org-open-line with version calling my-org-open-line-below

Bind over ~org-open-line~ with a variant that calls
~my-open-line-below~ instead.

#+name: my-org-open-line-below
#+begin_src emacs-lisp
(defun my-org-open-line-below (n)
  "Insert a new row in tables, call `my-open-line-below' elsewhere.
If `org-special-ctrl-o' is nil, call `my-open-line-below' everywhere.
As a special case, when a document starts with a table, allow to
call `open-line' on the very first character."
  (interactive "*p")
  (if (and org-special-ctrl-o (/= (point) 1) (org-at-table-p))
      (org-table-insert-row)
    (my-open-line-below n)))

;; bind over `org-open-line' to call `my-org-open-line-below' instead
;; making it consistent with customized global-mode-map "C-o"
(with-eval-after-load 'org-keys
  (define-key org-mode-map (kbd "C-o") #'my-org-open-line-below))
#+end_src

** Org TODO keywords and task states

Possible Org task states:
- *TODO*: Pending inactive task.
- *NEXT*: Active task.
- *DONE*: Completed task.
- *HOLD*: Paused inactive task.
- *WAIT*: Paused active task, waiting for external action before continuation.
- *CANX*: Canceled task.

#+name: org-todo-keywords
#+begin_src emacs-lisp
;; Set possible Org task states
;; Diagram of possible task state transitions
;;     -------------------------
;;     |                       |
;;     |                       v
;; -> TODO....... -> NEXT -> DONE ----->
;;    | ^  |  | ^    | ^      ^     |
;;    v |  |  v |    v |      |     |
;;   HOLD  |  WAIT...... ------     |
;;     |   |  | (note records what  |
;;     v   v  v  it is waiting for) |
;;     CANX.... ---------------------
;;     (note records why it was cancelled)
(setq org-todo-keywords '((sequence "NEXT(n)" "TODO(t)" "|" "DONE(d!)")
                          (sequence "WAIT(w@/!)" "HOLD(h@/!)" "|" "CANX(c@/!)")))
#+end_src

** Org basic capture templates

#+name: org-capture-templates
#+begin_src emacs-lisp
;; Org capture templates
(setq org-capture-templates '(("t" "Todo" entry (file my-org-agenda-inbox)
                               "* TODO %i%?\n%U")
                              ("r" "Respond" entry (file my-org-agenda-inbox)
                               "* NEXT Respond to %i%?\n%U")
                              ("i" "Interrupt Task" entry (file my-org-agenda-inbox)
                               "* NEXT %i%?\n%U"
                               :jump-to-captured t :clock-in t :clock-resume t)
                              ("n" "Note" entry (file my-org-agenda-inbox)
                               "* %i%? :note:\n%U")
                              ("s" "Someday" entry (file my-org-agenda-inbox)
                               "* %i%? :someday:\n%U")
                              ("l" "Link" entry (file my-org-agenda-inbox)
                               "* %a%?\n%U")
                              ("y" "Paste" entry (file my-org-agenda-inbox)
                               "* %?\n%U\n%c")))
#+end_src

** Maximize Org capture buffers

Have Org capture buffers always be maximized.
Restore the window configuration after capturing the task.

#+name: org-maximize-capture-buffers
#+begin_src emacs-lisp
(with-eval-after-load 'org
  ;; maximize org-capture buffer
  (defun my-org-capture-setup (&rest args)
    "Save window configuration prior to `org-capture'."
    (set-frame-parameter
     nil
     'my-org-capture-prior-config
     (current-window-configuration)))
  (defun my-org-capture-teardown ()
    "Restore window configuration prior to `org-capture'."
    (let ((prior-window-configuration (frame-parameter
                                       nil
                                       'my-org-capture-prior-config)))
      (when prior-window-configuration
        (set-window-configuration prior-window-configuration))))
  (advice-add 'org-capture :before 'my-org-capture-setup)
  (add-hook 'org-capture-mode-hook 'delete-other-windows)
  (add-hook 'org-capture-after-finalize-hook 'my-org-capture-teardown))
#+end_src

** Org tags

#+name: org-tags
#+begin_src emacs-lisp
;; tags (note that tags within the same group are mutually exclusive)
(setq org-tag-alist '((:startgroup) ;; importance
                      ("important" . ?1)
                      ("unimportant" . ?2)
                      (:endgroup)
                      (:startgroup) ;; time-sensitivity
                      ("urgent" . ?3)
                      ("nonurgent" . ?4)
                      (:endgroup)
                      (:startgroup) ;; location
                      ("@home" . ?7)
                      ("@office" . ?8)
                      ("@travel" . ?9)
                      ("@errands" . ?0)
                      (:endgroup)
                      (:startgroup) ;; export
                      ("export" . ?e)
                      ("noexport" . ?E)
                      (:endgroup)
                      ;; ungrouped
                      ("meeting" . ?m)
                      ("note" . ?n)
                      ;; work-related relationship category
                      ("hiring" . ?H)
                      ("managing" . ?M)
                      ("vendor" . ?V)
                      ("partner" . ?P)
                      ("client" . ?C)
                      ;; work-related meeting type
                      ("internal" . ?I)
                      ("external" . ?X)
                      ;; work-related project category
                      ("healthcare" . ?\^h) ;; C-h
                      ("retail" . ?\^r))) ;; C-r
#+end_src

** Org export global macros

Useful Org export macros that are enabled globally.

To color text (works with LaTeX and HTML exports):
#+begin_example
{{{color(colorname, text)}}}
#+end_example

To insert [[https://en.wikipedia.org/wiki/Lorem_ipsum][filler]] text:
#+begin_example
{{{loremipsum}}}
#+end_example

To export different text for LaTeX and for other formats:
#+begin_example
{{{if-latex-else(latex-specific text,other text)}}}
#+end_example

#+name: org-export-global-macros
#+begin_src emacs-lisp
;; `org-export' macros
(with-eval-after-load 'ox
  ;; color macro, {{{color(colorname, text)}}} to use
  (push `("color"
          .
          ,(concat "@@latex:\\textcolor{$1}{$2}@@"
                   "@@html:<span style=\"color:$1\">$2</span>@@"))
        org-export-global-macros)
  ;; placeholder text, {{{loremipsum}}} to use
  (push `("loremipsum"
          .
          ,(mapconcat 'identity
                      '("Lorem ipsum dolor sit amet, consectetur"
                        "adipisicing elit, sed do eiusmod tempor"
                        "incididunt ut labore et dolore magna"
                        "aliqua. Ut enim ad minim veniam, quis"
                        "nostrud exercitation ullamco laboris nisi"
                        "ut aliquip ex ea commodo consequat. Duis"
                        "aute irure dolor in reprehenderit in"
                        "voluptate velit esse cillum dolore eu"
                        "fugiat nulla pariatur. Excepteur sint"
                        "occaecat cupidatat non proident, sunt in"
                        "culpa qui officia deserunt mollit anim id"
                        "est laborum."
                        "\n\n"
                        "Curabitur pretium tincidunt lacus. Nulla"
                        "gravida orci a odio. Nullam varius, turpis"
                        "et commodo pharetra, est eros bibendum elit,"
                        "nec luctus magna felis sollicitudin mauris."
                        "Integer in mauris eu nibh euismod gravida."
                        "Duis ac tellus et risus vulputate vehicula."
                        "Donec lobortis risus a elit. Etiam tempor."
                        "Ut ullamcorper, ligula eu tempor congue,"
                        "eros est euismod turpis, id tincidunt sapien"
                        "risus a quam. Maecenas fermentum consequat"
                        "mi. Donec fermentum. Pellentesque malesuada"
                        "nulla a mi. Duis sapien sem, aliquet nec,"
                        "commodo eget, consequat quis, neque. Aliquam"
                        "faucibus, elit ut dictum aliquet, felis nisl"
                        "adipiscing sapien, sed malesuada diam lacus"
                        "eget erat. Cras mollis scelerisque nunc."
                        "Nullam arcu. Aliquam consequat. Curabitur"
                        "augue lorem, dapibus quis, laoreet et,"
                        "pretium ac, nisi. Aenean magna nisl, mollis"
                        "quis, molestie eu, feugiat in, orci. In hac"
                        "habitasse platea dictumst.")
                      " "))
        org-export-global-macros)
  ;; flow control for latex-specific text and otherwise
  ;; {{{if-latex-else(latex text, other text)}}} to use
  (push '("if-latex-else"
          .
          "(eval (if (org-export-derived-backend-p
                     org-export-current-backend
                     'latex)
                    $1
                  $2))")
        org-export-global-macros))
#+end_src

** Org-mode hydra                                                     :hydra:

Major mode-specific hydra for ~org-mode~.

#+name: org-mode-hydra
#+begin_src emacs-lisp
;; hydra for org-mode
(defhydra my-hydra/org-mode (:color amaranth :columns 3)
  "
Org-mode (_q_: quit)"
  ("q" nil nil :exit t)
  ("M-s" org-narrow-to-subtree "narrow-subtree")
  ("M-b" org-narrow-to-block "narrow-block")
  ("M-w" widen "widen")
  ("M-l" org-toggle-link-display "toggle-link-disp")
  ("M-i" (lambda ()
           (interactive)
           (if org-image-actual-width
               (setq org-image-actual-width nil)
             (setq org-image-actual-width (list (/ (display-pixel-width) 3))))
           (org-redisplay-inline-images)) "toggle-img-width")
  ("i" org-toggle-inline-images "toggle-images")
  ("I" org-indent-mode "toggle-indent")
  ("P" org-toggle-pretty-entities "toggle-prettify")
  ("<tab>" org-cycle "cycle")
  ("<S-tab>" org-global-cycle "global-cycle")
  ("/" org-sparse-tree "sparse-tree")
  ("c" org-remove-occur-highlights "occur-clear")
  ("p" (lambda (n)
         (interactive "p")
         (if org-occur-highlights
             (previous-error n)
           (org-previous-visible-heading n)))
   "previous")
  ("n" (lambda (n)
         (interactive "p")
         (if org-occur-highlights
             (next-error n)
           (org-next-visible-heading n)))
   "next")
  ("g" org-goto "goto" :exit t)
  ("s" org-sort "sort" :exit t)
  ("o" org-occur "occur" :exit t)
  ("r" org-refile "refile" :exit t)
  ("t" org-todo "state" :exit t)
  (":" org-set-tags-command "tags" :exit t)
  ("," org-priority "priority" :exit t)
  ("D" org-insert-drawer "drawer" :exit t)
  ("P" org-set-property "property" :exit t)
  ("N" org-add-note "note" :exit t)
  ("F" org-footnote-action "footnote" :exit t)
  ("a" org-archive-subtree-default "archive" :exit t)
  ("<" org-insert-structure-template "structure" :exit t)
  ("'" org-edit-special "edit-special" :exit t)
  ("e" my-hydra/org-mode/emphasize/body "→ Emphasize" :exit t))

;; hydra for org-mode text formatting
(defhydra my-hydra/org-mode/emphasize (:color teal :columns 4)
  "
Org-mode → Emphasize (_q_: ←)"
  ("q" my-hydra/org-mode/body nil)
  ("b" (org-emphasize ?*) "bold")
  ("i" (org-emphasize ?/) "italic")
  ("u" (org-emphasize ?_) "underline")
  ("s" (org-emphasize ?+) "strike-through")
  ("c" (org-emphasize ?~) "code")
  ("v" (org-emphasize ?=) "verbatim"))

;; binding for org-mode hydra
(with-eval-after-load 'org
  (define-key org-mode-map (kbd "C-c C-M-m") #'my-hydra/org-mode/body))
#+end_src

** Org Agenda

Org-mode provides [[https://orgmode.org/manual/Agenda-Views.html][agenda views]] that give an overview of open action
items or events with specific scheduled or deadline dates in Org files
specified by ~org-agenda-files~.

A number of customizations are done here, including defining
a view that displays 3-day agenda and undated ~TODO~ entries.

*Note*: Archiving ~DONE~ and ~CANX~ items every so often helps to keep
Org agenda parsing and operations speedy ([[https://www.gnu.org/software/emacs/manual/html_node/org/Speeding-up-your-agendas.html][link]]):
- ~M-x org-agenda~ to bring up the agenda menu.
- ~t~ to list ~TODO~ items.
- ~N r~ (where ~N~ is the number corresponding to a task state) to
  select the ~DONE~ (~N=3~ in this configuration) or the ~CANX~ (~N=6~
  in this configuration) task state.
- ~*~ to select all listed items.
- ~B $~ to bulk archive the selected items to their respective
  =<filename>.org_archive= files.

#+name: org-agenda
#+begin_src emacs-lisp
;; org-agenda settings:
;; - narrow to subtree in org-agenda-follow-mode ("F" in agenda)
;; - full-frame Agenda view
;; - use ~/ORG-DIRECTORY/*.org files as Org agenda files
(setq org-agenda-follow-indirect t
      org-agenda-restore-windows-after-quit t
      org-agenda-start-on-weekday nil
      org-agenda-window-setup 'only-window
      org-agenda-files (file-expand-wildcards (concat org-directory "*.org")))

;; add separator between each day in agenda view
(setq org-agenda-format-date
      (lambda (date)
        (let* ((datestr (org-agenda-format-date-aligned date))
               (separator-width (- (window-width)
                                   (string-width datestr)
                                   1)))
          (concat "\n" datestr " " (make-string separator-width ?_)))))

(with-eval-after-load 'org-agenda
  ;; add custom agenda commands that only show undated tasks in list view
  (dolist (my-custom-cmd
           '(("N" "Three-day agenda and undated TODO entries"
              ((agenda "" ((org-agenda-span 3)))
               (alltodo "" ((org-agenda-todo-ignore-with-date t)
                            (org-agenda-sorting-strategy '(todo-state-up priority-down effort-up category-keep alpha-up))))))
             ("u" "Undated TODO entries"
              (alltodo "" ((org-agenda-todo-ignore-with-date t)
                           (org-agenda-sorting-strategy '(todo-state-up priority-down effort-up category-keep alpha-up)))))))
    (add-to-list 'org-agenda-custom-commands my-custom-cmd)))
#+end_src

** Org Agenda hydra                                                   :hydra:

Major mode-specific hydra for ~org-agenda-mode~.

#+name: org-agenda-hydra
#+begin_src emacs-lisp
;; mode-specific hydra for org-agenda-mode
(defhydra my-hydra/org-agenda-mode (:color amaranth :hint nil)
  "
Org agenda (_q_: quit)
Headline    _ht_  : set status   _hk_  : kill         _hr_  : refile
            _hA_  : archive      _h:_  : set tags     _hp_  : set priority
Visit Entry _SPC_ : other window _TAB_ : & go to loc  _RET_ : & del other wins
            _o_   : link
Date        _ds_  : schedule     _dd_  : set deadline _dt_  : timestamp
View        _vd_  : day          _vw_  : week         _vm_  : month
            _vn_  : next span    _vp_  : prev span    _vr_  : reset
Filter      _ft_  : by tag       _fc_  : by category  _fh_  : by top headline
            _fx_  : by regex     _fd_  : reset
Clock       _ci_  : in           _co_  : out          _cq_  : cancel
            _cg_  : goto
Other       _gr_  : reload       _gd_  : go to date   _._   : go to today
            _sd_  : hide done
"
  ("q" nil nil :exit t)
  ("ht" org-agenda-todo)
  ("hk" org-agenda-kill)
  ("hr" org-agenda-refile)
  ("hA" org-agenda-archive-default)
  ("h:" org-agenda-set-tags)
  ("hp" org-agenda-priority)
  ("SPC" org-agenda-show-and-scroll-up)
  ("TAB" org-agenda-goto :exit t)
  ("RET" org-agenda-switch-to :exit t)
  ("o" link-hint-open-link :exit t)
  ("ds" org-agenda-schedule)
  ("dd" org-agenda-deadline)
  ("dt" org-agenda-date-prompt)
  ("vd" org-agenda-day-view)
  ("vw" org-agenda-week-view)
  ("vm" org-agenda-month-view)
  ("vn" org-agenda-later)
  ("vp" org-agenda-earlier)
  ("vr" org-agenda-reset-view)
  ("ft" org-agenda-filter-by-tag)
  ("fc" org-agenda-filter-by-category)
  ("fh" org-agenda-filter-by-top-headline)
  ("fx" org-agenda-filter-by-regexp)
  ("fd" org-agenda-filter-remove-all)
  ("ci" org-agenda-clock-in :exit t)
  ("co" org-agenda-clock-out)
  ("cq" org-agenda-clock-cancel)
  ("cg" org-agenda-clock-goto :exit t)
  ("gr" org-agenda-redo)
  ("gd" org-agenda-goto-date)
  ("." org-agenda-goto-today)
  ("sd" (lambda () (interactive)
          (progn (setq org-agenda-skip-scheduled-if-done
                       (if org-agenda-skip-scheduled-if-done nil t))
                 (org-agenda-redo-all t)))))

;; bind org-agenda-mode hydra
(with-eval-after-load 'org-agenda
  (define-key org-agenda-mode-map (kbd "C-c C-M-m") #'my-hydra/org-agenda-mode/body))
#+end_src

** Org refiling of tasks and subtrees

Set up Org refile targets:
- Current buffer up to a max depth of 9 levels.
- Org Agenda files corresponding to non-directory entries of
  ~org-agenda-files~ at the top-level of each file.

#+name: org-refile
#+begin_src emacs-lisp
;; allow refiling up to 9 levels deep in the current buffer
;; and 3 levels deep in Org agenda files
;; allow refiling to the top level
(setq org-refile-targets `((nil . (:maxlevel . 9)) ;; current buffer
                           ;; top-level of regular `org-agenda-files' files
                           (,(seq-filter
                              'file-regular-p
                              org-agenda-files) . (:level . 0)))
      org-refile-use-outline-path 'file
      org-refile-allow-creating-parent-nodes 'confirm)
#+end_src

** Org entrypoint hydra                                               :hydra:

Hydra providing entrypoints to multiple Org-mode functions.

#+name: org-entrypoints-hydra
#+begin_src emacs-lisp
;; hydra for Org entrypoints
(defhydra my-hydra/org-entrypoints (:color teal :columns 4)
  "
Org (_q_: quit)"
  ("q" nil nil)
  ("a" org-agenda "agenda")
  ("c" org-capture "capture")
  ("b" org-switchb "switch buffer")
  ("l" org-store-link "store link"))

;; bind Org entrypoints hydra
(global-set-key (kbd "C-c C-M-o") #'my-hydra/org-entrypoints/body)
#+end_src

** Automatic text wrapping in Org-mode documents

#+name: org-visual-line-mode
#+begin_src emacs-lisp
(add-hook 'org-mode-hook #'visual-line-mode)
#+end_src

** Compile Org documents to PDF with LaTeX                         :external:

Org-mode supports an Org \to LaTeX \to PDF build chain for compiling Org
documents to PDF files.

By default, this build process utilizes the base ~latex~ compiler and
does not handle [[http://tug.org/bibtex/][BibTeX]] bibliography database files (~.bib~ files).

It is better change to this to a more modern compiler like [[http://www.luatex.org/][LuaTeX]] for
better font and unicode support, and add a BibTeX compiler like [[http://biblatex-biber.sourceforge.net/][Biber]]
to the build chain.

#+name: org-latex-pdf-process
#+begin_src emacs-lisp
;; compile Org documents to PDF with LuaTeX and Biber
(when (executable-find "lualatex")
  (with-eval-after-load 'org
    (setq org-latex-pdf-process
          '("lualatex -interaction nonstopmode -output-directory %o %f"
            "lualatex -interaction nonstopmode -output-directory %o %f"))
    (if (executable-find "biber")
        (push "biber %b" org-latex-pdf-process))
    (push "lualatex -interaction nonstopmode -output-directory %o %f"
          org-latex-pdf-process)))
#+end_src

** Use LuaTeX to generate LaTeX fragments previews in Org-mode     :external:

Use [[http://www.luatex.org/][LuaTeX]] to generate previews of [[https://orgmode.org/manual/LaTeX-fragments.html][LaTeX fragments]] as images in
~org-mode~.

The build chain converts LaTeX \to DVI \to PNG, so ~lualatex~ (LuaTeX
binary) and ~dvipng~ (which should come installed with most TeX
distributions) are needed.

For more information on LaTeX fragment previews, see [[https://orgmode.org/manual/Previewing-LaTeX-fragments.html#Previewing-LaTeX-fragments][here]].

#+name: org-preview-latex-process
#+begin_src emacs-lisp
;; use LuaTeX for previewing LaTeX math formula as images
(when (and (executable-find "lualatex")
           (executable-find "dvipng"))
  (with-eval-after-load 'org
    (add-to-list 'org-preview-latex-process-alist
                 '(dvipng :programs ("lualatex" "dvipng")
                          :description "dvi > png"
                          :message "you need to install lualatex and dvipng."
                          :image-input-type "dvi"
                          :image-output-type "png"
                          :image-size-adjust (1.0 . 1.0)
                          :latex-compiler
                          (concat "lualatex -output-format dvi"
                                  " -interaction nonstopmode"
                                  " -output-directory %o %f")
                          :image-converter ("dvipng -D %D -T tight -o %O %f")))))
#+end_src

** Automatically preview LaTeX fragments and resize them with text scaling :external:

Automatically preview LaTeX fragments and also scale them according
the current text scale (font size modifier).

#+name: org-preview-and-scale-latex-fragments
#+begin_src emacs-lisp
;; preview LaTeX fragments scaled to font size, requires dvipng from TexLive
(when (and (display-graphic-p)
           (executable-find "dvipng"))
  (with-eval-after-load 'org
    (defvar my-org-latex-scale-base (plist-get org-format-latex-options :scale)
      "Base LaTeX fragment scale.")
    (defun my-org-display-latex-fragments ()
      "Previews LaTeX fragments in the buffer scaled to match font size."
      (interactive)
      (let* ((curr-text-scale (condition-case nil
                                  text-scale-mode-amount
                                (error 0)))
             (new-latex-scale (+ my-org-latex-scale-base curr-text-scale)))
        (when (eq major-mode 'org-mode)
          ;; modify LaTeX scale in a local copy of `org-format-latex-options'
          (if (not (assoc 'org-format-latex-options (buffer-local-variables)))
              (setq-local org-format-latex-options
                          (copy-tree org-format-latex-options)))
          (setq-local org-format-latex-options
                      (plist-put org-format-latex-options :scale new-latex-scale))
          ;; preview LaTeX fragments
          (org--latex-preview-region (point-min) (point-max)))))
    ;; preview LaTeX fragments when opening Org documents ...
    (add-hook 'org-mode-hook (lambda (&optional arg)
                               (my-org-display-latex-fragments)))
    ;; ... and regenerate after changing font size
    (advice-add 'text-scale-mode :after (lambda (&optional arg)
                                          (my-org-display-latex-fragments)))))
#+end_src

** Mouse support and variable pitch fonts in graphical Emacs Org-mode

When using graphical emacs, enable mouse support (like clicking on the
header stars to cycle visibility of the subtree) and use variable
pitch fonts with a little extra line spacing except for certain parts of
Org documents like code and tables.

#+name: org-graphical-customizations
#+begin_src emacs-lisp
;; add mouse support and use variable pitch fonts in graphical Emacs org-mode
(when (display-graphic-p)
  (with-eval-after-load 'org
    (require 'org-mouse) ;; mouse support
    ;; use variable pitch fonts ...
    (add-hook 'org-mode-hook #'variable-pitch-mode)
    (add-hook 'org-mode-hook (lambda () (setq line-spacing 0.1)))
    ;; ... but keep some faces fixed-pitch
    (require 'org-indent) ;; ensure `org-indent' face is defined
    (let ((fixed-pitch-family (face-attribute 'fixed-pitch :family nil 'default)))
      (dolist (curr-face '(org-block
                           org-block-begin-line
                           org-block-end-line
                           org-code
                           org-date
                           org-document-info-keyword
                           org-done
                           org-indent ;; properly align indentation
                           org-latex-and-related
                           org-meta-line
                           org-property-value
                           org-special-keyword
                           org-table
                           org-todo
                           org-verbatim))
        (set-face-attribute curr-face nil :family fixed-pitch-family)))))
#+end_src

** org-cliplink for inserting URLs from the clipboard as Org-mode links :melpa:

[[https://github.com/rexim/org-cliplink][org-cliplink]] provides support for inserting URLs from the clipboard as
links in Org-mode documents with the URL page titles as link
descriptions.

#+name: org-cliplink
#+begin_src emacs-lisp
;; insert urls from clipboard as links with title of page
(when (display-graphic-p)
  (use-package org-cliplink
    :after org
    :bind (:map org-mode-map
           ("C-c C-S-l" . org-cliplink))))
#+end_src

** org-download for dragging and dropping images into Org-mode        :melpa:

[[https://github.com/abo-abo/org-download][org-download]] enables support for downloading images to Org-mode
documents, either by running an external command to capture a
screenshot or by dragging and dropping an image onto the document.

#+name: org-download
#+begin_src emacs-lisp
;; drag and drop images into Org buffers
(when (display-graphic-p)
  (use-package org-download
    :after org
    :config
    ;; Mac screenshot command
    (if (memq window-system '(mac ns))
        (setq org-download-screenshot-method "screencapture -i %s"))
    ;; adapted from https://coldnew.github.io/hexo-org-example/2018/05/22/use-org-download-to-drag-image-to-emacs/
    ;; save drag-and-drop images into folder of the same name as Org file
    ;; with filename prefixed by a timestamp of format `org-download-timestamp'
    ;; e.g. dragging test.png to abc.org saves it to abc/20180522183050-test.png
    (defun my-org-download-method (link)
      """Returns download save path for LINK, for use with `org-download'"""
      (let ((filename (format "%s%s"
                              (format-time-string org-download-timestamp)
                              (file-name-nondirectory
                               (car (url-path-and-query
                                     (url-generic-parse-url link))))))
            (dirname (file-name-sans-extension (buffer-name))))
        ;; create dir if it does not exist
        (unless (file-exists-p dirname)
          (make-directory dirname))
        ;; save path
        (expand-file-name filename dirname)))
    (setq org-download-method 'my-org-download-method
          org-download-timestamp "%Y%m%d%H%M%S-")))
#+end_src

*** org-download hydra                                                :hydra:

Hydra for ~org-download~.
Add an entrypoint for the hydra to the org-mode hydra.

#+name: org-download-hydra
#+begin_src emacs-lisp
(when (display-graphic-p)
  ;; hydra for org-download
  (defhydra my-hydra/org-mode/download (:color teal :columns 3)
    "
Org-mode → Download (_q_: ←)"
    ("q" my-hydra/org-mode/body nil)
    ("s" org-download-screenshot "screenshot")
    ("y" org-download-yank "yank"))

  ;; add entrypoint to download hydra to the org-mode hydra
  (defhydra+ my-hydra/org-mode nil
    ("d" my-hydra/org-mode/download/body "→ Download" :exit t)))
#+end_src

** org-journal for journaling using Org-mode               :workaround:melpa:

[[https://github.com/bastibe/org-journal][org-journal]] extends journaling capabilities to Org-mode.

*Note*: Journal files are not added to the Org agenda by default in
the config due to potential slowdown as the number of files increases.
Instead it is better to use ~org-store-link~ to store an Org link to
the heading and then creating a new todo using ~org-capture~ and
pasting/yanking the Org link in the todo using ~C-c C-l~.

#+name: org-journal
#+begin_src emacs-lisp
;; journaling using Org documents
(use-package org-journal
  :after org
  :init
  ;; org-capture helper function from https://github.com/bastibe/org-journal
  (defun my-org-journal-find-location ()
    "Find location of today's Org journal, for use with `org-capture'."
    ;; Open today's journal but specify a non-nil prefix argument in order to
    ;; inhibit inserting the heading; org-capture will insert the heading.
    (org-journal-new-entry t)
    ;; Position point on the journal's top-level heading so that org-capture
    ;; will add the new entry as a child entry.
    (goto-char (point-min)))
  ;; add org-capture-template for new journal entries
  (push '("j" "Journal" entry (function my-org-journal-find-location)
          "* %(format-time-string org-journal-time-format)%?\n%i")
        org-capture-templates)
  (setq org-journal-date-prefix "#+TITLE: Daily Journal "
        ;; separate journal files into folders by year
        org-journal-file-format "%Y/%Y%m%d.org"
        org-journal-file-type 'daily
        ;; don't carry over TODO items from a previous days
        org-journal-carryover-items nil
        ;; use ORG-DIRECTORY/journal/ as the default journal directory
        org-journal-dir (concat org-directory "journal/"))
  ;; add journal files to Org agenda
  ;; may cause the Org agenda parsing to slow down as the number as
  ;; the number of files grows, so make sure to prune or archive the
  ;; files elsewhere every so often if this is enabled.
  ;; (push org-journal-dir org-agenda-files)
  :config
  ;; workaround on `org-journal-is-journal' `string-match' error when
  ;; exporting to HTML due to `buffer-file-name' func returning nil
  (defun org-journal-is-journal--around-workaround (orig-fun &rest args)
    "Drop-in replacement advice function for `org-journal-is-journal'."
    (let ((buf-file-name (or (buffer-file-name) "")))
      (string-match (org-journal-dir-and-file-format->pattern)
                    buf-file-name)))
  (advice-add 'org-journal-is-journal :around
              #'org-journal-is-journal--around-workaround))
#+end_src

** org-present for minimalist presentations within Emacs              :melpa:

[[https://github.com/rlister/org-present][org-present]] extends org-mode so that it can be used for minimalist
presentations within Emacs.

Each top-level heading will correspond to a presentation slide.
=LEFT= and =RIGHT= will move forward and backward through the
slides, and =C-c C-q= will quit the presentation.

*Customizations*:
- Hide header and mode lines when presentation is active.
- Additional keybindings during presentation
  - =UP=/=DOWN=: Scroll up/down.
  - =SHIFT-UP=/=SHIFT-DOWN=: Go to top/bottom of slide.
  - =SHIFT-LEFT=/=SHIFT-RIGHT=: Go to first/last slide.
  - =f=: Toggle fullscreen.
  - =q=: Quit presentation (like =C-c C-q=).
  - =-=: Decrease text scale.
  - =+=: Increase text scale.

#+name: org-present
#+begin_src emacs-lisp
;; in-editor presentations using Org documents
(use-package org-present
  :after org
  :hook ((org-present-mode . (lambda ()
                               (org-present-big)
                               (org-display-inline-images)
                               (org-present-read-only)
                               (my-hide-header-and-mode-lines)))
         (org-present-mode-quit . (lambda ()
                                    (org-present-small)
                                    (org-remove-inline-images)
                                    (org-present-read-write)
                                    (my-unhide-header-and-mode-lines))))
  :config
  ;; regenerate LaTeX fragment preview images on slide transition
  (when (and (display-graphic-p)
             (executable-find "dvipng"))
    (add-hook 'org-present-after-navigate-functions
              (lambda (&optional name header)
                (my-org-display-latex-fragments))))
  ;; functions for hiding header and mode lines when in a presentation
  (defvar-local my-orig-mode-line-format nil
    "Temporary variable to store original `mode-line-format'.")
  (defvar-local my-orig-header-line-format nil
    "Temporary variable to store original `header-line-format'.")
  (defun my-hide-header-and-mode-lines ()
    "Hide header and mode lines, and store originals in temporary variables."
    (interactive)
    (when mode-line-format
      (setq-local my-orig-mode-line-format mode-line-format)
      (setq-local mode-line-format nil))
    (when header-line-format
      (setq-local my-orig-header-line-format header-line-format)
      (setq-local header-line-format nil)))
  (defun my-unhide-header-and-mode-lines ()
    "Reset header and mode lines using originals in temporary variables."
    (interactive)
    (when (not mode-line-format)
      (setq-local mode-line-format my-orig-mode-line-format)
      (setq-local my-orig-mode-line-format nil))
    (when (not header-line-format)
      (setq-local header-line-format my-orig-header-line-format)
      (setq-local my-orig-header-line-format nil)))
  ;; easier nav keys for read-only presentations
  (define-minor-mode my-org-present-extra-mode
    "Overlay minor mode on top of org-present-mode with easier nav keys."
    :keymap (let ((map (make-sparse-keymap)))
              ;; <left>/<right> = previous/next slide
              (define-key map (kbd "<up>") 'scroll-down-line)
              (define-key map (kbd "<down>") 'scroll-up-line)
              (define-key map (kbd "s-<up>") 'beginning-of-buffer)
              (define-key map (kbd "s-<down>") 'end-of-buffer)
              (define-key map (kbd "s-<left>") 'org-present-beginning)
              (define-key map (kbd "s-<right>") 'org-present-end)
              (define-key map (kbd "f") 'toggle-frame-fullscreen)
              (define-key map (kbd "q") 'org-present-quit)
              (define-key map (kbd "-") 'text-scale-decrease)
              (define-key map (kbd "+") 'text-scale-increase)
              map))
  ;; toggle minor mode after the relevant org-present funcalls
  (advice-add 'org-present-read-only
              :after (lambda () (my-org-present-extra-mode 1)))
  (advice-add 'org-present-read-write
              :after (lambda () (my-org-present-extra-mode 0))))
#+end_src

*** Add Org-mode presentation head to org-mode hydra                  :hydra:

Add head for initializing an ~org-present~ presentation to org-mode
hydra.

#+name: add-org-present-to-org-mode-hydra
#+begin_src emacs-lisp
;; add org-present present head to org-mode hydra
(defhydra+ my-hydra/org-mode nil
  ("C-p" (lambda ()
           (interactive)
           (let ((in-present-mode (condition-case nil
                                      org-present-mode
                                    (error nil))))
             (if in-present-mode (org-present-quit) (org-present))))
   "org-present" :exit t))
#+end_src

** ox-md for Org export backend to Markdown

Load the [[https://github.com/emacsmirror/org/blob/master/lisp/ox-md.el][built-in Org backend]] for exporting Org documents to Markdown.

#+name: ox-md
#+begin_src emacs-lisp
;; load Org backend for exporting to Markdown
(with-eval-after-load 'org
  (require 'ox-md))
#+end_src

** org-superstar for nicer headings and plain lists

[[https://github.com/integral-dw/org-superstar-mode][org-superstar]] provides a minor mode that replaces header stars and
plain lists in Org-mode with UTF-8 characters.

Replace plain list bullets can be slow when lists are long, so only
header star replacements are configured.

#+name: org-superstar
#+begin_src emacs-lisp
(use-package org-superstar
  :hook (org-mode . org-superstar-mode)
  :init (setq org-superstar-headline-bullets-list '("◉" "◇" "○" "▷")
              ;; don't prettify plain lists, which can be slow
              org-superstar-prettify-item-bullets nil))
#+end_src

** org-noter for document annotation                                  :melpa:

[[https://github.com/weirdNox/org-noter][Org-noter]] allows the creation of external synchronized notes for
documents viewed using [[https://www.gnu.org/software/emacs/manual/html_node/emacs/Document-View.html][DocView]], [[https://github.com/politza/pdf-tools][PDF Tools]] or [[https://depp.brause.cc/nov.el/][Nov.el]].

Use ~C-c C-M-S-n~ or ~M-x org-noter~ while in an Org document or when
viewing a document in the above tools to annotate.

When annotating in the split-screen mode:
- ~i~ inserts a note approximately (document buffer-only).
- ~M-i~ inserts a precise note (document buffer-only).
- ~q~ exits the annotation session (document buffer-only).
- ~M-n~ goes to the next page.
- ~M-p~ goes to the previous page.
- ~C-M-n~ goes to the next note.
- ~C-M-p~ goes to the previous note.

#+name: org-noter
#+begin_src emacs-lisp
;; create sychronized external notes in DocView and Nov.el
(use-package org-noter
  :bind ("C-c C-M-S-n" . org-noter)
  :init (setq org-noter-always-create-frame nil))
#+end_src

** org-super-agenda                                                   :melpa:

[[https://github.com/alphapapa/org-super-agenda][org-super-agenda]] groups agenda items into sections.

#+name: org-super-agenda
#+begin_src emacs-lisp
(use-package org-super-agenda
  :config
  (setq org-super-agenda-groups '((:name "Today"
                                   :time-grid t
                                   :scheduled today)
                                  (:name "Due today"
                                   :deadline today)
                                  (:name "Important"
                                   :priority "A")
                                  (:name "Overdue"
                                   :deadline past)
                                  (:name "Due soon"
                                   :deadline future)
                                  (:name "Backlog"
                                   :scheduled past)
                                  (:name "Upcoming"
                                   :scheduled future)
                                  (:priority<= "B")
                                  (:name "Waiting"
                                   :todo "WAIT")
                                  (:name "On hold"
                                   :todo "HOLD")))
  (org-super-agenda-mode 1))
#+end_src

** org-protocol

Enable [[https://orgmode.org/worg/org-contrib/org-protocol.html][org-protocol]] to allow intercepting calls to emacsclient and
triggering custom actions.

By default, Emacs Mac port for Mac OS registers itself as the
handler for ~org-protocol://~ URLs.

For other versions of Emacs, refer to [[https://github.com/alphapapa/org-protocol-capture-html#org-protocol-instructions][here]] or [[https://org-roam.readthedocs.io/en/master/roam_protocol/][here]] for instructions on
setting up the protocol handler.

#+name: org-protocol
#+begin_src emacs-lisp
;; start server and load org-protocol
(server-mode 1)
(require 'org-protocol)
#+end_src

*** org-protocol-capture

The org-protocol-capture API can accessed by calling the URL starting
with ~org-protocol://capture~ and passing information through query
parameters.

org-protocol-capture supports a few URL query parameters:
- ~url~ corresponding to ~%:link~ in the capture template.
- ~title~ corresponding to ~%:description~ in the capture template.
- ~body~ corresponding to ~%:initial~ in the capture template. Note
  that in the bookmarklet below this is the selected text in the page.
  For the entire page's text, use ~document.body.innerText~ instead of
  ~window.getSelection()~.
- ~template~ which automatically loads the template associated with
  the parameter character, for example
  ~javascript:location.href='org-protocol://capture?template=b'+ ...~
  uses the template ?b automatically. Note that the template character
  must be unquoted.
For more info, see the docstring and code for ~org-protocol-capture~.

As an example, the following bookmarklet can be created in any web
browser and when clicked will send details about the current page to
the org-protocol capture API. This will bring into focus the Emacs
window and call `org-capture' with the query parameters.

#+begin_example
javascript:location.href='org-protocol://capture?url='+encodeURIComponent(location.href)+'&title='+encodeURIComponent(document.title)+'&body='+encodeURIComponent(window.getSelection())
#+end_example

*** Capturing web snippets

One useful org-protocol use is to capture snippets from the web.

Add the following bookmarklet to a web browser.

#+begin_example
javascript:location.href='org-protocol://capture?template=W&url='+encodeURIComponent(location.href)+'&title='+encodeURIComponent(document.title)+'&body='+encodeURIComponent(window.getSelection())
#+end_example

Next configure the capture template used by the org-protocol bookmarklet.

#+name: org-capture-websnippet
#+begin_src emacs-lisp
;; add capture template for web snippets
(setq org-websnippet-capture-file "scratch/websnippets.org")
(push `("W" "Capture web snippet using org-protocol" entry
        (file+headline ,org-websnippet-capture-file "Unsorted")
        "* %?%:description\n:PROPERTIES:\n:URL: %:link\n:ADDED: %U\n:END:\n%:initial\n")
      org-capture-templates)
#+end_src

Clicking the bookmarklet will capture the selected text to the
websnippets Org file.

* Programming

** Buffer reformatter macro                                           :melpa:

[[https://github.com/purcell/reformatter.el][reformatter.el]] defines a macro to allow an easy way to create Elisp
commands that reformat the current buffer or a given region using a
command-line program, along with an optional minor mode that applies
the reformatting command automatically on save.

The following example from the official repository defines the following:
- An interactive command ~yaml-format-buffer~ which reformats the
  buffer.
- An interactive command ~yaml-format-region~ which reformats a
  selected region.
- A buffer-local minor mode ~yaml-format-on-save-mode~ that reformats
  the buffer on save when it is enabled.

#+begin_example
(reformatter-define yaml-format
  :program "prettier"
  :group 'yaml
  :lighter 'YAMLFmt)
#+end_example

#+name: reformatter
#+begin_src emacs-lisp
;; defines the `reformatter-define' macro that allows definition of
;; commands that run reformatters on the current buffer
(use-package reformatter)
#+end_src

** Flycheck syntax checker                                            :melpa:

[[https://www.flycheck.org/en/latest/][FlyCheck]] ([[https://github.com/flycheck/flycheck][Github]]) is an on-the-fly syntax checker for Emacs that is a
popular alternative to the built-in [[https://elpa.gnu.org/packages/flymake.html][Flymake]] package.

If using this, it is typical to not enable Flymake.

#+name: flycheck
#+begin_src emacs-lisp
;; linting support, used in place of FlyMake
(use-package flycheck
  :init
  ;; customizations:
  ;; - remove newlines from events triggering linting checks
  ;; - don't mark error lines in fringe or margin
  (setq flycheck-check-syntax-automatically '(save
                                              idle-change
                                              mode-line)
        flycheck-indication-mode nil)
  :config
  ;; automatically adjust idle delay before automatically checking the
  ;; buffer depending on whether there are outstanding syntax errors;
  ;; check less frequently if there were no errors, and check more
  ;; frequently if there were errors; have this behavior be per-buffer
  (make-variable-buffer-local 'flycheck-idle-change-delay)
  (defun flycheck--adjust-flycheck-idle-change-delay ()
    "Adjust `flycheck-idle-change-delay' to check less frequently
when buffer is clean, and more frequently when it has errors."
    (setq flycheck-idle-change-delay (if flycheck-current-errors
                                         0.5
                                       3.0)))
  (add-hook 'flycheck-after-syntax-check-hook
            #'flycheck--adjust-flycheck-idle-change-delay)
  ;; default modes within which to use FlyCheck
  (add-hook 'emacs-lisp-mode-hook #'flycheck-mode))
#+end_src

*** Flycheck hydra                                                    :hydra:

Hydra for FlyCheck.

#+name: flycheck-hydra
#+begin_src emacs-lisp
;; flycheck hydra
(defhydra my-hydra/flycheck (:color amaranth :columns 3)
  "
FlyCheck [checker=%s(if flycheck-checker (symbol-name flycheck-checker) \"default\")] (_q_: quit)"
  ("q" nil nil :exit t)
  ("c" flycheck-buffer "run")
  ("C" flycheck-clear "clear")
  ("C-c" flycheck-compile "compile")
  ("n" flycheck-next-error "next")
  ("p" flycheck-previous-error "previous")
  ("l" (condition-case nil (quit-windows-on "*Flycheck errors*" t)
         (error (flycheck-list-errors))) "list")
  ("H" display-local-help "local-help")
  ("h" flycheck-display-error-at-point "display-at-pt")
  ("e" flycheck-explain-error-at-point "explain-at-pt")
  ("s" flycheck-select-checker "select-checker")
  ("?" flycheck-describe-checker "describe-checker" :exit t)
  ("v" flycheck-verify-setup "verify-setup" :exit t)
  ("C-w" flycheck-copy-errors-as-kill "copy-errors")
  ("i" flycheck-manual "web-manual" :exit t))

;; binding for flycheck hydra
(with-eval-after-load 'flycheck
  (define-key flycheck-mode-map (kbd "C-c C-M-!") #'my-hydra/flycheck/body))
#+end_src

** Security linting using DevSkim and FlyCheck                     :external:

[[https://github.com/Microsoft/DevSkim/][DevSkim]] is a collection of static analyzers that does code security
analysis.

If it is installed on the system, there is a FlyCheck [[https://github.com/microsoft/DevSkim/tree/main/flycheck][backend]]
distributed with the official codebase (add to a directory in
~load-path~ and load it when initializing Emacs). It does not run by
default for the supported modes (C, C++ and Python), and has to be
chained to the checker that is run for those modes. For example,
~(flycheck-add-next-checker 'lsp 'devskim)~ if using ~lsp-mode~ is
used as the main checker for the mode.

#+name: flycheck-devskim
#+begin_src emacs-lisp
(when (executable-find "devskim")
  (use-package flycheck-devskim
    :ensure nil ;; in site-lisp subfolder within user emacs directory
    :config
    (setq flycheck-devskim-executable "devskim")
    (with-eval-after-load 'lsp-mode
      (flycheck-add-next-checker 'lsp 'devskim))))
#+end_src

** Conda package and environment manager                              :melpa:

[[https://docs.conda.io/][Conda]] is a cross-platform package and environment manager, enabling
easy creation, saving, loading and switching of environments.

It was initially created to target Python development, but has
expanded beyond just that and now includes R and other software
packages.

*Customizations*:
- Set the conda home directory to =~/miniconda3/=.
- Support Eshell and term-mode buffers.
- Show current conda environment in the mode line.

#+name: conda
#+begin_src emacs-lisp
(when (executable-find "conda")
  (use-package conda
    :init (setq conda-anaconda-home (expand-file-name "~/miniconda3/"))
    :config
    (conda-env-initialize-interactive-shells)
    (conda-env-initialize-eshell)
    ;; display current conda env in the mode line
    (add-to-list 'mode-line-misc-info
                 '(:eval (if conda-env-current-name
                             (format " «%s»"
                                     (truncate-string-to-width
                                      conda-env-current-name
                                      15 nil nil "…"))
                           ""))
                 t)))
#+end_src

*** Conda hydra                                                       :hydra:

Hydra for conda.

#+name: conda-hydra
#+begin_src emacs-lisp
(defhydra my-hydra/conda (:color teal :columns 4)
  "
conda (_q_: quit)"
  ("q" nil nil)
  ("a" conda-env-activate "activate")
  ("d" conda-env-deactivate "deactivate")
  ("l" conda-env-list "list"))
(with-eval-after-load 'conda
  (global-set-key (kbd "C-c C-M-S-v") 'my-hydra/conda/body))
#+end_src

** lsp-mode Language Server Protocol client                           :melpa:

[[https://github.com/emacs-lsp/lsp-mode][lsp-mode]] is an Language Server Protocol client for Emacs.
It is heavier than [[https://github.com/joaotavora/eglot][Emacs Polyglot]] (eglot) but also has more features.

#+name: lsp-mode
#+begin_src emacs-lisp
;; lsp-mode Language Server Protocol client
;; auto-signature-help activation is not enabled by default, but to
;; show it activate it using `lsp-toggle-signature-auto-activate' or
;; C-S-SPC to peek, https://github.com/emacs-lsp/lsp-mode/issues/1223
(use-package lsp-mode
  :init (setq lsp-print-io nil ;; disable logging packets between Emacs and LS
              lsp-print-performance nil ;; disable performance logging
              lsp-eldoc-enable-hover nil ;; don't have eldoc display hover info
              lsp-eldoc-render-all nil ;; don't show all returned from document/onHover, only symbol info
              lsp-enable-on-type-formatting nil ;; don't have the LS automatically format the document when typing
              lsp-diagnostic-package :flycheck ;; use FlyCheck for syntax checking
              lsp-signature-auto-activate nil)) ;; don't automatically show signature
#+end_src

*** company-mode support for lsp-mode                                 :melpa:

Add company-mode support for lsp-mode.

#+name: company-lsp
#+begin_src emacs-lisp
;; company backend for LSP-driven completion
(use-package company-lsp
  :after lsp-mode
  :init (setq company-lsp-cache-candidates t))
#+end_src

*** lsp-mode hydra                                                    :hydra:

Hydra for lsp-mode.

#+name: lsp-mode-hydra
#+begin_src emacs-lisp
;; hydras adapted from lsp-mode's default command map, see lsp-command-map in
;; https://github.com/emacs-lsp/lsp-mode/blob/master/lsp-mode.el
(defhydra my-hydra/lsp (:color teal :columns 3)
  "
Language Server (_q_: quit)"
  ("q" nil nil)
  ("s" my-hydra/lsp-session/body "→ Session")
  ("=" my-hydra/lsp-format/body "→ Format")
  ("F" my-hydra/lsp-folder/body "→ Folder")
  ("T" my-hydra/lsp-toggle/body "→ Toggle")
  ("g" my-hydra/lsp-goto/body "→ Goto")
  ("h" my-hydra/lsp-help/body "→ Help")
  ("r" my-hydra/lsp-refactor/body "→ Refactor")
  ("a" my-hydra/lsp-actions/body "→ Actions"))
(defhydra my-hydra/lsp-session (:color teal :columns 3)
  "
Language Server → Session (_q_: ←)"
  ("q" my-hydra/lsp/body nil)
  ("r" (condition-case nil (lsp-restart-workspace) (error (lsp))) "(re-)start")
  ("s" lsp-workspace-shutdown "shutdown")
  ("d" lsp-describe-session "describe")
  ("D" lsp-disconnect "disconnect"))
(defhydra my-hydra/lsp-format (:color teal :columns 3)
  "
Language Server → Format (_q_: ←)"
  ("q" my-hydra/lsp/body nil)
  ("=" lsp-format-buffer "buffer")
  ("r" lsp-format-region "range"))
(defhydra my-hydra/lsp-folder (:color teal :columns 3)
  "
Language Server → Folder (_q_: ←)"
  ("q" my-hydra/lsp/body nil)
  ("a" lsp-workspace-folders-add "add")
  ("r" lsp-workspace-folders-remove "remove")
  ("b" lsp-workspace-blacklist-remove "un-blacklist"))
(defhydra my-hydra/lsp-toggle (:color amaranth :columns 3)
  "
Language Server → Toggle (_q_: ←)"
  ("q" my-hydra/lsp/body nil :exit t)
  ("l" lsp-lens-mode "lens-mode")
  ("L" lsp-toggle-trace-io "trace-io")
  ("h" lsp-toggle-symbol-highlight "symbol-highlight")
  ("b" lsp-headerline-breadcrumb-mode "headerline-breadcrumb")
  ("s" lsp-toggle-signature-auto-activate "signature-help")
  ("f" lsp-toggle-on-type-formatting "on-type-formatting"))
(defhydra my-hydra/lsp-goto (:color teal :columns 3)
  "
Language Server → Goto (_q_: ←)"
  ("q" my-hydra/lsp/body nil)
  ("j" (lambda ()
         (interactive)
         (if (fboundp 'imenu-list-smart-toggle)
             (imenu-list-smart-toggle)
           (message "Requires imenu-list"))) "imenu")
  ("g" lsp-find-definition "definition")
  ("r" lsp-find-references "references")
  ("i" lsp-find-implementation "implementation")
  ("t" lsp-find-type-definition "type-implementation")
  ("d" lsp-find-declaration "declaration")
  ("a" xref-find-apropos "workspace-symbol"))
(defhydra my-hydra/lsp-help (:color teal :columns 3)
  "
Language Server → Help (_q_: ←)"
  ("q" my-hydra/lsp/body nil)
  ("h" lsp-describe-thing-at-point "describe")
  ("s" lsp-signature-activate "signature-activate"))
(defhydra my-hydra/lsp-refactor (:color teal :columns 3)
  "
Language Server → Refactor (_q_: ←)"
  ("q" my-hydra/lsp/body nil)
  ("r" lsp-rename "rename")
  ("o" lsp-organize-imports "organize-imports"))
(defhydra my-hydra/lsp-actions (:color teal :columns 3)
  "
Language Server → Actions (_q_: ←)"
  ("q" my-hydra/lsp/body nil)
  ("a" lsp-execute-code-action "execute-code-action")
  ("l" lsp-avy-lens "avy-lens")
  ("h" lsp-document-highlight "document-highlight"))

;; bind lsp-mode hydra
(with-eval-after-load 'lsp-mode
  (define-key lsp-mode-map (kbd "C-c C-M-l") #'my-hydra/lsp/body))
#+end_src

** dap-mode Debug Adaptor Protocol client                             :melpa:

[[https://github.com/emacs-lsp/dap-mode][dap-mode]] is a Debug Adaptor Protocol client.

#+name: dap-mode
#+begin_src emacs-lisp
;; client for Debug Adaptor Protocol servers
(use-package dap-mode
  :after lsp-mode
  :config (add-hook 'dap-stopped-hook
                    (lambda (arg) (call-interactively #'dap-hydra))))
#+end_src

*** Add entry points for dap-mode functions to lsp hydra              :hydra:

Add ~dap-debug~ and ~dap-debug-edit-template~ to main lsp hydra.

#+name: add-dap-mode-funs-to-lsp-mode-hydra
#+begin_src emacs-lisp
;; add dap-mode debugging function entry points to lsp-mode hydra
(with-eval-after-load 'lsp-mode
  (defhydra+ my-hydra/lsp nil
    ("d" dap-debug "dap-debug" :exit t)
    ("D" dap-debug-edit-template "dap-template" :exit t)))
#+end_src

** Clojure

*** clojure-mode for basic Clojure support                            :melpa:

[[https://github.com/clojure-emacs/clojure-mode][clojure-mode]] provides a major mode for editing Clojure buffers.

#+name: clojure-mode
#+begin_src emacs-lisp
;; basic support
(use-package clojure-mode
  :defer t
  :hook ((clojure-mode . paredit-mode)
         (clojure-mode . subword-mode)))
#+end_src

*** CIDER for a more complete Clojure development environment         :melpa:

[[https://docs.cider.mx/cider/index.html][CIDER]] (Clojure(Script) Interactive Development Environment that
Rocks!) complements ~clojure-mode~, providing additional functionality
for Clojure development including compiling, debugging, running tests,
definition and documentation lookup, and so on.

#+name: cider
#+begin_src emacs-lisp
;; Clojure IDE
(use-package cider
  :after clojure-mode
  :hook ((cider-mode . eldoc-mode)
         (cider-repl-mode . eldoc-mode)
         (cider-repl-mode . paredit-mode))
  :config (setq nrepl-log-messages t))
#+end_src

**** CIDER hydra                                                      :hydra:

Mode-specific Hydra for accessing CIDER in ~clojure-mode~.

#+name: cider-hydra
#+begin_src emacs-lisp
;; hydras, adapted from https://github.com/clojure-emacs/cider-hydra
(defhydra my-hydra/cider (:color teal :columns 3)
  "
CIDER (_q_: quit)"
  ("q" nil nil)
  ;; start a REPL and connect to it
  ("j" cider-jack-in-clj "jack-in-clj")
  ("s" cider-jack-in-cljs "jack-in-cljs")
  ("b" cider-jack-in-clj&cljs "jack-in-clj&cljs")
  ;; sub-hydras
  ("d" my-hydra/cider-doc/body "→ Documentation")
  ("e" my-hydra/cider-eval/body "→ Evaluation")
  ("T" my-hydra/cider-test/body "→ Test")
  ("D" my-hydra/cider-debug/body "→ Debug")
  ("r" my-hydra/cider-repl/body "→ REPL"))
(defhydra my-hydra/cider-doc (:color teal :columns 4)
  "
CIDER → Documentation (_q_: ←)"
  ("q" my-hydra/cider/body nil)
  ;; CiderDoc
  ("d" cider-doc "cider-docs")
  ;; ClojureDocs
  ("r" cider-clojuredocs "clojure-docs")
  ("h" cider-clojuredocs-web "clojure-docs-web")
  ;; JavaDoc
  ("j" cider-javadoc "java-docs-web")
  ;; apropos
  ("a" cider-apropos "search-symbols")
  ("s" cider-apropos-select "select-symbols")
  ("A" cider-apropos-documentation "search-docs")
  ("e" cider-apropos-documentation-select "select-docs"))
(defhydra my-hydra/cider-eval (:color teal :columns 3)
  "
CIDER → Eval (_q_: ←)"
  ("q" my-hydra/cider/body nil)
  ;; load
  ("k" cider-load-buffer "load-buffer")
  ("l" cider-load-file "load-file")
  ("p" cider-load-all-project-ns "load-all-proj-ns")
  ;; eval
  ("r" cider-eval-region "eval-region")
  ("n" cider-eval-ns-form "eval-ns-form")
  ("e" cider-eval-last-sexp "eval-last-sexp")
  ("P" cider-pprint-eval-last-sexp "eval-last-sexp-pp")
  ("w" cider-eval-last-sexp-and-replace "eval-last-sexp-replace")
  ("E" cider-eval-last-sexp-to-repl "eval-last-sexp-to-repl")
  ("d" cider-eval-defun-at-point "eval-defun-at-point")
  ("f" cider-pprint-eval-defun-at-point "eval-defun-at-point-pp")
  (":" cider-read-and-eval "read-and-eval")
  ;; inspect
  ("i" cider-inspect "inspect")
  ;; macro expansion
  ("m" cider-macroexpand-1 "macroexpand-1")
  ("M" cider-macroexpand-all "macroexpand-all"))
(defhydra my-hydra/cider-test (:color teal :columns 4)
  "
CIDER → Test (_q_: ←)"
  ("q" my-hydra/cider/body nil)
  ("t" cider-test-run-test "run")
  ("l" cider-test-run-loaded-tests "run-loaded")
  ("p" cider-test-run-project-tests "run-project")
  ("n" cider-test-run-ns-tests "run-ns")
  ("r" cider-test-rerun-failed-tests "rerun-failed")
  ("s" cider-test-show-report "show-report"))
(defhydra my-hydra/cider-debug (:color teal :columns 3)
  "
CIDER → Debug (_q_: ←)"
  ("q" my-hydra/cider/body nil)
  ("x" (lambda () (interactive) (cider-eval-defun-at-point t)) "eval-defun-at-pt")
  ("v" cider-toggle-trace-var "toggle-var-trace")
  ("n" cider-toggle-trace-ns "toggle-ns-trace"))
(defhydra my-hydra/cider-repl (:color teal :columns 3)
  "
CIDER → REPL (_q_: ←)"
  ("q" my-hydra/cider/body nil)
  ;; connection
  ("d" cider-display-connection-info "disp-conn-info")
  ("r" cider-rotate-default-connection "rot-default-conn")
  ;; input
  ("z" cider-switch-to-repl-buffer "switch-to-repl")
  ("n" cider-repl-set-ns "set-repl-ns")
  ("p" cider-insert-last-sexp-in-repl "ins-last-sexp-in-repl")
  ("x" cider-refresh "refresh")
  ;; output
  ("o" cider-find-and-clear-repl-output "clear-repl-output")
  ("O" (lambda () (interactive) (cider-find-and-clear-repl-output t)) "clear-repl-all")
  ;; interrupt or quit connected REPL
  ("b" cider-interrupt "interrupt")
  ("Q" cider-quit "quit-cider"))

;; binding for main CIDER hydra
(with-eval-after-load 'clojure-mode
  (define-key clojure-mode-map (kbd "C-c C-M-m") #'my-hydra/cider/body))
#+end_src

*** flycheck-clj-kondo for Clojure linting using clj-kondo   :melpa:external:

[[https://github.com/borkdude/flycheck-clj-kondo][flycheck-clj-kondo]] provides [[https://github.com/borkdude/clj-kondo][clj-kondo]] (a linter for Clojure code)
integration with FlyCheck.

#+name: flycheck-clj-kondo
#+begin_src emacs-lisp
;; clojure linting, requires clj-kondo be installed on the system
(when (executable-find "clj-kondo")
  (use-package flycheck-clj-kondo
    :after (flycheck clojure-mode)
    :config
    (require 'flycheck-clj-kondo)
    ;; start flycheck-mode
    (add-hook 'clojure-mode-hook (lambda () (flycheck-mode 1)) t)))
#+end_src

** Emacs Lisp

*** Emacs Lisp debugger hydra                                         :hydra:

Hydras for [[https://www.gnu.org/software/emacs/manual/html_node/elisp/Debugging.html][debugging]] Emacs Lisp.
One is for changing when the debugger is invoked.
The other is a mode-specific hydra.

#+name: debugger-hydra
#+begin_src emacs-lisp
;; hydra for built-in Emacs Lisp debugger
(defhydra my-hydra/debugger (:color teal :hint nil
                             :pre (require 'debug))
  "
Emacs debugger settings (_q_: quit)
Toggle    _1_ : debug-on-error (currently: %`debug-on-error)
          _2_ : debug-on-quit  (currently: %`debug-on-quit)
Functions _fl_ : list functions to invoke debugger on entry
          _fa_ : add debugger invocation to function
          _fc_ : cancel debugger invocation from function
Variables _vl_ : list variables to invoke debugger on change
          _va_ : add debugger invocation to variable on change
          _vc_ : cancel debugger invocation from variable on change
"
  ("q" nil nil)
  ("1" toggle-debug-on-error :exit nil)
  ("2" toggle-debug-on-quit :exit nil)
  ("fl" debugger-list-functions)
  ("fa" debug-on-entry)
  ("fc" cancel-debug-on-entry)
  ("vl" (lambda () (interactive) (prin1 (debug--variable-list))))
  ("va" debug-on-variable-change)
  ("vc" cancel-debug-on-variable-change))

;; binding for debugger-settings hydra
(global-set-key (kbd "C-c C-M-S-d") 'my-hydra/debugger/body)
#+end_src

#+name: debugger-mode-hydra
#+begin_src emacs-lisp
;; mode-specific hydra for debugger
(defhydra my-hydra/debugger-mode (:color teal :columns 4)
  "
Emacs debugger (_q_: quit)"
  ("q" nil nil)
  ("c" debugger-continue "continue")
  ("d" debugger-step-through "step")
  ("b" debugger-frame "frame")
  ("u" debugger-frame-clear "no-frame")
  ("j" debugger-jump "jump")
  ("e" debugger-eval-expression "eval-expr")
  ("R" debugger-record-expression "record-expr")
  ("Q" top-level "quit-to-top")
  ("r" debugger-return-value "return-val")
  ("l" debugger-list-functions "list-funs")
  ("v" debugger-toggle-locals "list-vars")
  ("h" describe-mode "help"))

;; binding for debugger hydra
(with-eval-after-load 'debug
  (define-key debugger-mode-map (kbd "C-c C-M-m") #'my-hydra/debugger-mode/body))
#+end_src

*** Emacs Lisp profiler hydra                                         :hydra:

Hydra for built in Emacs [[https://www.gnu.org/software/emacs/manual/html_node/elisp/Profiling.html][profiler]].

#+name: profiler-hydra
#+begin_src emacs-lisp
;; hydra for built-in Emacs Lisp profiler
(defhydra my-hydra/profiler (:color teal :columns 3
                             :pre (require 'profiler))
  "
Emacs profiler [CPU=%(profiler-running-p) MEM=%(profiler-memory-running-p)] (_q_: quit)"
  ("q" nil nil)
  ("s" profiler-start "start/reset" :exit nil)
  ("p" profiler-report "report")
  ("e" profiler-stop "stop" :exit nil))

;; binding for Emacs profiler hydra
(global-set-key (kbd "C-c C-M-S-e") 'my-hydra/profiler/body)
#+end_src

*** el-patch                                                          :melpa:

[[https://github.com/raxod502/el-patch][el-patch]] provides a way to customize Emacs Lisp functions.

#+name: el-patch
#+begin_src emacs-lisp
(use-package el-patch
  :demand t)
#+end_src

**** Modify lisp-indent-function to handle property list indentation

Modify ~lisp-indent-function~ so that it indents property lists in
Emacs Lisp (and other Lisps) in the expected manner (like in Clojure).

Uses functions from the ~el-patch~ package, so make sure that package
is loaded before this code block.

#+name: modify-lisp-indent-function
#+begin_src emacs-lisp
;; Modifies lisp indentation to handle property lists used as
;; data structures
;; --- DEFAULT BEHAVIOR
;;  `(:token ,token
;;           :token-quality ,quality)
;; ---
;; --- DESIRED BEHAVIOR
;;  `(:token ,token
;;    :token-quality ,quality)
;; ---
;; Copied from https://emacs.stackexchange.com/questions/10230/
(with-eval-after-load 'lisp-mode
  (el-patch-defun lisp-indent-function (indent-point state)
    "This function is the normal value of the variable `lisp-indent-function'.
The function `calculate-lisp-indent' calls this to determine
if the arguments of a Lisp function call should be indented specially.
INDENT-POINT is the position at which the line being indented begins.
Point is located at the point to indent under (for default indentation);
STATE is the `parse-partial-sexp' state for that position.
If the current line is in a call to a Lisp function that has a non-nil
property `lisp-indent-function' (or the deprecated `lisp-indent-hook'),
it specifies how to indent.  The property value can be:
,* `defun', meaning indent `defun'-style
  (this is also the case if there is no property and the function
  has a name that begins with \"def\", and three or more arguments);
,* an integer N, meaning indent the first N arguments specially
  (like ordinary function arguments), and then indent any further
  arguments like a body;
,* a function to call that returns the indentation (or nil).
  `lisp-indent-function' calls this function with the same two arguments
  that it itself received.
This function returns either the indentation to use, or nil if the
Lisp function does not specify a special indentation."
    (el-patch-let (($cond (and (elt state 2)
                               (el-patch-wrap 1 1
                                 (or (not (looking-at "\\sw\\|\\s_"))
                                     (looking-at ":")))))
                   ($then (progn
                            (if (not (> (save-excursion (forward-line 1) (point))
                                        calculate-lisp-indent-last-sexp))
                                (progn (goto-char calculate-lisp-indent-last-sexp)
                                       (beginning-of-line)
                                       (parse-partial-sexp (point)
                                                           calculate-lisp-indent-last-sexp 0 t)))
                            ;; Indent under the list or under the first sexp on the same
                            ;; line as calculate-lisp-indent-last-sexp.  Note that first
                            ;; thing on that line has to be complete sexp since we are
                            ;; inside the innermost containing sexp.
                            (backward-prefix-chars)
                            (current-column)))
                   ($else (let ((function (buffer-substring (point)
                                                            (progn (forward-sexp 1) (point))))
                                method)
                            (setq method (or (function-get (intern-soft function)
                                                           'lisp-indent-function)
                                             (get (intern-soft function) 'lisp-indent-hook)))
                            (cond ((or (eq method 'defun)
                                       (and (null method)
                                            (> (length function) 3)
                                            (string-match "\\`def" function)))
                                   (lisp-indent-defform state indent-point))
                                  ((integerp method)
                                   (lisp-indent-specform method state
                                                         indent-point normal-indent))
                                  (method
                                   (funcall method indent-point state))))))
      (let ((normal-indent (current-column))
            (el-patch-add
              (orig-point (point))))
        (goto-char (1+ (elt state 1)))
        (parse-partial-sexp (point) calculate-lisp-indent-last-sexp 0 t)
        (el-patch-swap
          (if $cond
              ;; car of form doesn't seem to be a symbol
              $then
            $else)
          (cond
           ;; car of form doesn't seem to be a symbol, or is a keyword
           ($cond $then)
           ((and (save-excursion
                   (goto-char indent-point)
                   (skip-syntax-forward " ")
                   (not (looking-at ":")))
                 (save-excursion
                   (goto-char orig-point)
                   (looking-at ":")))
            (save-excursion
              (goto-char (+ 2 (elt state 1)))
              (current-column)))
           (t $else)))))))
#+end_src

** fish shell scripts

*** fish-mode                                                         :melpa:

[[https://github.com/wwwjfy/emacs-fish][fish-mode]] is a major-mode for working with [[https://fishshell.com/][fish shell]] scripts.

#+name: fish-mode
#+begin_src emacs-lisp
(use-package fish-mode
  :init (setq fish-enable-auto-indent t
              fish-indent-offset 4))
#+end_src

** Python

*** Use jupyter or ipython as the Python interpreter when available :external:

Create a POSIX shell script =py= with the following contents somewhere
on the system path, like =~/.local/bin/py= for example.

#+begin_example
#!/bin/sh

# Wrapper script to use jupyter or ipython, if available, over python

PY_CMD="python"
PY_EXTRA_ARGS=""

if command -v jupyter-console
then
  PY_CMD="jupyter-console"
  if echo "$INSIDE_EMACS" | grep -q -e ",comint$"
  then
    PY_EXTRA_ARGS="$PY_EXTRA_ARGS --simple-prompt"
  fi
elif command -v ipython
then
  PY_CMD="ipython"
  if echo "$INSIDE_EMACS" | grep -q -e ",comint$"
  then
    PY_EXTRA_ARGS="$PY_EXTRA_ARGS --simple-prompt -i"
  fi
else  # python
  if echo "$INSIDE_EMACS" | grep -q -e ",comint$"
  then
    PY_EXTRA_ARGS="$PY_EXTRA_ARGS -i"
  fi
fi

$PY_CMD $PY_EXTRA_ARGS $@
#+end_example

Make sure it is set to executable.

#+begin_example
$ chmod +x ~/.local/bin/py
#+end_example

Set the Emacs Python interpreter command and options to the script.

#+name: python-shell-interpreter
#+begin_src emacs-lisp
(setq python-shell-interpreter "py"
      python-shell-interpreter-args ""
      python-shell-prompt-detect-failure-warning nil)

(with-eval-after-load 'python
  (add-to-list 'python-shell-completion-native-disabled-interpreters "py"))
#+end_src

*Limitations*:
- Completion in the inferior shell does not work on object attributes
  (see [[https://github.com/jorgenschaefer/elpy/issues/1545][link]]).

*** Developing

#+begin_example
$ pip install -e .
#+end_example

or

#+begin_example
$ python setup.py --editable
#+end_example

*** Debugging

The intrusive way to debug Python programs is to create a debugging
breakpoint by adding a ~import pdb; pdb.set_trace()~ statement to the
desired breakpoint location.
Running the Python program normally will stop the program at the
breakpoint and bring up the ~pdb~ interface.

The non-intrusive way to debug is to run ~pdb~ as a module using
~python -m pdb some_script.py~ which starts debugging from the
beginning of the code.

Emacs provides a way to start the debugging session from the buffer:
run ~M-x pdb~ which opens ~pdb~ using Emacs' [[https://www.gnu.org/software/emacs/manual/html_node/emacs/Debuggers.html][Grand Unified Debugger]]
(GUD) which provides some additional user interface around ~pdb~.

*Note*: For autocompletion in ~pdb~, create a =~/.pdbrc= file with the
following contents.

#+begin_example
import rlcompleter
import pdb

pdb.Pdb.complete=rlcompleter.Completer(locals()).complete
#+end_example

*** Python syntax table

By default, Emacs uses ~_~ to demarcate word boundaries.

However, using snake case for variable names (for example,
~some_variable~) is idiomatic style in Python code, so modify the
Python syntax table to keep underscores within a word boundary.

#+name: python-mode-syntax-table
#+begin_src emacs-lisp
;; modify Python syntax table to keep underscores within a word
;; boundary when editing Python buffers
(with-eval-after-load 'python
  (modify-syntax-entry ?_ "w" python-mode-syntax-table))
#+end_src

*** Python hydra                                                      :hydra:

Mode-specific Hydra for Python mode.

#+name: python-mode-hydra
#+begin_src emacs-lisp
;; mode-specific hydra for Python mode
(defhydra my-hydra/python-mode (:color teal :columns 4)
  "
Python (_q_: quit)"
  ("q" nil nil)
  ;; python repl
  ("p" run-python "run-python")
  ("s" python-shell-send-string "send-str")
  ("e" python-shell-send-statement "send-stmt")
  ("r" python-shell-send-region "send-rgn")
  ("x" python-shell-send-defun "send-def")
  ("c" python-shell-send-buffer "send-buf")
  ("l" python-shell-send-file "send-file")
  ("z" python-shell-switch-to-shell "switch-to-sh")
  ;; indentation
  ("<" python-indent-shift-left "indent-l" :exit nil)
  (">" python-indent-shift-right "indent-r" :exit nil)
  ;; utilities
  ("v" python-check "check-err")
  ("f" python-eldoc-at-point "eldoc-at-pt")
  ("d" python-describe-at-point "descr-at-pt")
  ;; other
  ("j" imenu "imenu")
  ("D" pdb "pdb"))

;; binding for Python hydra
(with-eval-after-load 'python
  (define-key python-mode-map (kbd "C-c C-M-m") #'my-hydra/python-mode/body))
#+end_src

*** Python buffer reformatter using black                          :external:

Use the ~reformatter-define~ macro from the ~reformatter~ package to
define two interactive commands ~python-black-format-buffer~ and
~python-black-format-region~ (only works on top-level objects) along
with the local minor mode ~python-black-format-on-save-mode~ that uses
[[https://github.com/psf/black][black]] to format the code in the buffer.

#+name: python-black-format
#+begin_src emacs-lisp
(with-eval-after-load 'reformatter
  ;; define `python-black-format-buffer', `python-black-format-region'
  ;; and `python-black-format-on-save-mode'
  (reformatter-define python-black-format
    :program "black"
    :args '("-")
    :group 'python
    :lighter 'PyBlFmt)
  ;; dwim function that calls `python-black-format-region' if a region
  ;; is selected, or `python-black-format-buffer' otherwise
  (defun python-black-format-buffer-or-region ()
       "Format the current Python buffer or a region if selected.
Formatting a selected region only works on top-level objects."
       (interactive)
       (cond
        ((use-region-p) (python-black-format-region (region-beginning)
                                                    (region-end)))
        (t (python-black-format-buffer)))))
#+end_src

**** Add Python reformatter commands to python-mode hydra             :hydra:

Add entry point to ~python-black-format-buffer~ and toggle for
~python-black-format-on-save-mode~ to python-mode hydra.

#+name: add-python-black-format-to-python-mode-hydra
#+begin_src emacs-lisp
;; add entry point to yapfify in python-mode hydra
(defhydra+ my-hydra/python-mode nil
  ("y" (lambda ()
         (interactive)
         (python-black-format-buffer-or-region))
   "format" :exit t)
  ("Y" python-black-format-on-save-mode "format-on-save"))
#+end_src

*** Live Coding in Python                                             :melpa:

[[https://donkirkby.github.io/live-py-plugin/][Live Coding in Python]] ([[https://github.com/donkirkby/live-py-plugin][Github]]) provides a minor mode ~live-py-mode~
that supports live coding, similar to the coding environment described
in [[http://worrydream.com/][Bret Victor]]'s [[http://worrydream.com/#!/InventingOnPrinciple][Inventing on Principle]] talk ([[https://github.com/ezyang/cusec2012-victor/blob/master/transcript.md][transcript]]).

When enabled, Python code will be run as it is being typed and a side
window with a live coding buffer opened that visualizes the results.

#+name: live-py-mode
#+begin_src emacs-lisp
;; live coding in python
(use-package live-py-mode)
#+end_src

**** Add toggle for live-py-mode to python-mode hydra                 :hydra:

Add an entry point to ~live-py-mode~ in the python-mode hydra,
that toggles python live coding.

#+name: add-live-py-mode-to-python-mode-hydra
#+begin_src emacs-lisp
;; add live-py-mode toggle to python-mode hydra
(defhydra+ my-hydra/python-mode nil
  ("L" live-py-mode "live-py-mode" :exit t))
#+end_src

*** Microsoft Pyright Language Server support with lsp-pyright        :melpa:

[[https://github.com/emacs-lsp/lsp-pyright][lsp-pyright]] enables the use of ~lsp-mode~ as a client for the
Microsoft [[https://github.com/microsoft/pyright][Pyright]] Python Language Server.

#+name: lsp-pyright
#+begin_src emacs-lisp
(use-package lsp-pyright
  :defer t
  :ensure nil ;; being added to MELPA, currently in site-lisp
  :init
  (defun lsp-pyright--setup ()
    "Convenience function for setting up lsp-pyright."
    ;; load packages if deferred
    (require 'lsp-mode)
    (require 'lsp-pyright)
    ;; start LSP client
    (lsp-mode))
  (add-hook 'python-mode-hook #'lsp-pyright--setup t))
#+end_src

**** Installing pyright

If [[https://www.npmjs.com/][npm]] is installed and in ~$PATH~, pyright should be installed
automatically the first the client is started.

However, pyright can also be installed and managed manually.

***** Managing Node.js versions (optional)                         :external:

Installing a [[https://nodejs.org/][Node.js]] version manager will make managing Node.js
versions much less painful.

The standard Node.js version manager is [[https://github.com/nvm-sh/nvm][nvm]].

****** Installing nvm when Bash is the login shell

Either install nvm using the [[https://github.com/nvm-sh/nvm#installing-and-updating][install script]] or using MacPorts. The
following installs it using MacPorts.

#+begin_example
$ sudo port install nvm
#+end_example

Make sure NVM is initialized during Bash startup by adding the
following to the =~/.bash_profile= or =~/.bashrc= file.

#+begin_example
# Initialize NVM
if [ -e /opt/local/share/nvm/init-nvm.sh ]; then
  . /opt/local/share/nvm/init-nvm.sh
fi
#+end_example

Install the latest LTS version of Node.js using nvm.

#+begin_example
$ nvm install --lts
#+end_example

****** Installing nvm when fish is the login shell

If fish is the login shell, use [[https://github.com/jorgebucaran/fish-nvm][fish-nvm]] instead as nvm requires a
POSIX shell. The following installs fish-nvm using [[https://github.com/jorgebucaran/fisher][fisher]].

#+begin_example
$ fisher add jorgebucaran/fish-nvm
#+end_example

Install the latest LTS version of Node.js using nvm.

#+begin_example
$ nvm use lts
#+end_example

***** Installing and updating pyright

Make sure Node.js is installed and its binaries are in ~$PATH~.

Install pyright using [[https://www.npmjs.com/][npm]] which is bundled with Node.js.

The following installs it globally.

#+begin_example
$ npm install -g pyright
#+end_example

The following updates an existing install of pyright.

#+begin_example
$ npm update -g pyright
#+end_example

**** Installing Python stubs

Pyright depends on [[https://www.python.org/dev/peps/pep-0561/][type stubs]] for resolving imports (it can also scan
the library code for that if ~python.analysis.useLibraryCodeForTypes~
is enabled or ~lsp-pyright-use-library-code-for-types~ is set to ~t~
when using ~lsp-pyright~; however, this can have significant
overhead).

Pyright includes a copy of stdlib type stubs from [[https://github.com/python/typeshed][Typeshed]], and some
third-party packages also come with stubs defined.

In other cases it may be helpful to install third-party type stubs.
For example, the following shows the installation of a third-party
[[https://pypi.org/][PyPI]] package [[https://github.com/predictive-analytics-lab/data-science-types][data-science-types]] providing stubs for ~numpy~, ~scipy~
and ~pandas~ in a specific conda environment.

#+begin_example
$ conda activate some-dev-env
$ pip install data-science-types
#+end_example

*** Microsoft Python DAP Server support with dap-mode

#+name: dap-python
#+begin_src emacs-lisp
(add-hook 'python-mode-hook
          (lambda ()
            (require 'dap-python))
          t)
#+end_src

**** Installing the Microsoft Python debugger for Visual Studio Code

Microsoft's Python debugger for Visual Studio Code supports the Debug
Adaptor Protocol, and is available from [[https://pypi.org/][PyPI]] as the ~ptvsd~ package.

#+begin_example
$ pip install "ptvsd>=4.2"
#+end_example

If using conda, ~ptvsd~ is available from the ~conda-forge~ channel.
Make sure to install it within the environment where code to be
debugged will be run.

#+begin_example
$ conda install -c conda-forge ptvsd
#+end_example

** R

*** Emacs Speaks Statistics                                           :melpa:

R support is provided by [[https://ess.r-project.org/][Emacs Speaks Statistics]], more commonly known
by its abbreviation ESS ([[https://github.com/emacs-ess/ESS][Github]]).

If linting using FlyCheck or Flymake, it might be necessary to install
the r-lintr CRAN package and also create the =~/.R/lintr_cache~=
directory (see [[https://emacs.stackexchange.com/questions/53018/flycheck-r-lintr-doesnt-find-anything][StackOverflow issue]]).

#+begin_example
# if using conda, replace my-renv with the proper environment name
# if not using conda, install via install.packages() instead
$ conda activate my-renv
$ conda install r-lintr
# ensure lintr cache directory exists
$ mkdir ~/.R/lintr_cache
#+end_example

#+name: ess
#+begin_src emacs-lisp
;; support for R language using Emacs Speaks Statistics
;; linting is configured here to use FlyCheck
(use-package ess
  :mode ("\\.R$" . R-mode)
  :commands (R-mode ess-switch-to-ESS)
  :init (setq ess-eval-visibly 'nowait
              ess-default-style 'RStudio
              ;; disable ESS auto-use of Flymake since using FlyCheck
              ess-use-flymake nil)
  :config (add-hook 'ess-r-mode-hook (lambda () (flycheck-mode 1)) t))
#+end_src

*** Shortcuts for commonly used R operators

The assignment operator ~<-~ is used a lot, so it makes sense to create a shortcut for that operator that is quicker to input.

Besides that, another good candidate is the forward pipe operator
~%>%~.
This operator is from the [[https://cran.r-project.org/web/packages/magrittr/vignettes/magrittr.html][magrittr]] package (one of the [[https://www.tidyverse.org/][tidyverse]]
packages), which introduces the forward pipe operator ~%>%~, then
exposition pipe operator ~%$%~, the tee pipe operator ~%T>%~ and the
compound assignment pipe operator ~%<>%~.
Of these, the forward pipe operator ~%>%~ is most common in usage.

*Configuration*:
- ~M--~ inserts ~<-~ in ~ess-mode~ and ~inferior-ess-mode~.
- ~C-S-m~ inserts ~%>%~ followed by a new line in ~ess-mode~ and
  ~inferior-ess-mode~.

#+name: ess-pipe-operators
#+begin_src emacs-lisp
;; forward pipe and assignment R operator shortcuts, adapted from
;; https://emacs.stackexchange.com/questions/8041/how-to-implement-the-piping-operator-in-ess-mode
(defun my-insert-R-forward-pipe-operator ()
  "Insert R magrittr forward pipe operator '%>%'."
  (interactive)
  (just-one-space 1)
  (insert "%>%")
  (reindent-then-newline-and-indent))
(defun my-insert-R-assignment-operator ()
  "Insert R assigment operator '<-'."
  (interactive)
  (just-one-space 1)
  (insert "<- "))

;; bindings for the above R operator shortcuts
(with-eval-after-load 'ess-mode
  (define-key ess-mode-map (kbd "M--") #'my-insert-R-assignment-operator)
  (define-key ess-mode-map (kbd "C-S-m") #'my-insert-R-forward-pipe-operator))
(with-eval-after-load 'ess-inf
  (define-key inferior-ess-mode-map (kbd "M--") #'my-insert-R-assignment-operator)
  (define-key inferior-ess-mode-map (kbd "C-S-m") #'my-insert-R-forward-pipe-operator))
#+end_src

*** ess-mode hydra                                                    :hydra:

Hydra for ~ess-mode~.

#+name: ess-mode-hydra
#+begin_src emacs-lisp
;; major mode-specific hydra for ess-mode
(defhydra my-hydra/ess-mode (:color teal :hint nil)
  "
Emacs Speaks Statistics (_q_: quit)
Session       _N_ : new       _R_ : request   _s_ : switch    _C-q_ : quit
Eval          _l_ : line      _f_ : func      _r_ : region    _b_   : buffer
Workspace     _D_ : chdir     _d_ : R dired
Help          _h_ : object    _H_ : browser   _A_ : apropos
"
  ("q" nil nil)
  ;; session
  ("N" (lambda () (interactive)
         (cond ((string= ess-dialect "R") (R))
               ((string= ess-dialect "julia") (julia))
               (t (message "Unsupported dialect")))))
  ("R" ess-request-a-process)
  ("s" ess-switch-to-ESS)
  ("C-q" ess-quit)
  ;; eval
  ("l" ess-eval-line)
  ("f" ess-eval-function)
  ("r" ess-eval-region)
  ("b" ess-eval-buffer)
  ;; workspace
  ("D" ess-change-directory)
  ("d" ess-rdired)
  ;; help
  ("h" ess-display-help-on-object)
  ("H" ess-display-help-in-browser)
  ("A" ess-display-help-apropos))

;; binding for ess-mode hydra
(with-eval-after-load 'ess-mode
  (define-key ess-mode-map (kbd "C-c C-M-m") #'my-hydra/ess-mode/body))
#+end_src

*** Better support for R Markdown and bookdown files                  :melpa:

[[https://github.com/polymode/poly-R/][poly-R]] provides better support for R Markdown and bookdown files, as
well as other formats interleaved with R code.

Leverages support for multiple major modes using [[https://github.com/polymode/polymode][polymode]] to have
different major modes be active for different buffer regions.

#+name: poly-R
#+begin_src emacs-lisp
(use-package poly-R)
#+end_src

** Racket                                                             :melpa:

[[https://github.com/greghendershott/racket-mode][racket-mode]] provides a major mode for editing Racket buffers.

#+name: racket-mode
#+begin_src emacs-lisp
(use-package racket-mode
  :defer t)
#+end_src

*** racket-mode hydra                                                 :hydra:

Mode-specific hydra for ~racket-mode~.

#+name: racket-mode-hydra
#+begin_src emacs-lisp
;; major mode-specific hydra for racket-mode
(defhydra my-hydra/racket-mode (:color teal :columns 4)
  "
Racket Mode (_q_: quit)"
  ("q" nil nil)
  ;; refactoring requires
  ("Rt" racket-tidy-requires "tidy-req")
  ("RT" racket-trim-requires "trim-req")
  ("Rb" racket-base-requires "base-req")
  ;; compile Racket Mode's .rkt files for faster startup
  ("S" racket-mode-start-faster "mode-compile")
  ;; racket modes
  ("x" racket-xp-mode "xp-mode" :exit nil)
  ;; repl
  ("rr" racket-run "run")
  ("rm" racket-run-module-at-point "run-module")
  ("rR" racket-racket "racket")
  ;; profiling and logging
  ("rp" racket-profile "profile")
  ("rl" racket-logger "logger")
  ;; testing
  ("t" racket-test "test")
  ("T" racket-raco-test "raco-test")
  ;; misc
  ("f" racket-find-collection "find-coll")
  ;; help
  ("." racket-xp-visit-definition "visit-defn")
  ("C-." racket-visit-module "visit-modl")
  ("," racket-unvisit "unvisit")
  ("h" racket-xp-describe "desc")
  ("H" racket-xp-documentation "docs")
  ;; editing
  ("a" racket-align "align")
  ("A" racket-unalign "unalign"))

;; bindings for racket-mode hydra
(with-eval-after-load 'racket-mode
  (define-key racket-mode-map (kbd "C-c C-M-m") #'my-hydra/racket-mode/body))
#+end_src

* Project interaction

** Projectile                                                         :melpa:

[[https://github.com/bbatsov/projectile][Projectile]] is a project interaction library for Emacs, providing many
convenience functions to navigate, search and build projects.

*Customizations*:
- Create test file if missing when toggling to it using ~projectile-toggle-between-implementation-and-test~.
- Run ~projectile-commander~ instead of ~projectile-find-file~ when switching projects using ~projectile-switch-project~.
- Use ~git grep~ when searching within the project.
- Enable globally.

#+name: projectile
#+begin_src emacs-lisp
;; project interaction library
(use-package projectile
  :demand t
  :config
  (setq projectile-completion-system (if (featurep 'helm) 'helm 'default)
        projectile-create-missing-test-files t ;; create test file if none is found when toggling
        projectile-switch-project-action 'projectile-commander
        projectile-use-git-grep t) ;; use git grep to skip backup, object, and untracked files when in a Git project
  (projectile-mode)) ;; enable mode globally
#+end_src

*** Projectile hydra                                                  :hydra:

Major mode-specific hydra for interfacing with Projectile commands.

#+name: projectile-hydra
#+begin_src emacs-lisp
;; hydra for Projectile
(defhydra my-hydra/projectile-mode (:color teal :hint nil)
  "
Projectile: %s(projectile-project-name) (_q_: quit)
Buffer _←_ : previous proj buf  _→_ : next proj buf      _b_ : switch
       _I_ : ibuffer            _S_ : save proj bufs     _k_ : kill proj bufs
File   _f_ : find (curr proj)   _F_ : find (known projs) _g_ : find (context)
       _t_ : goto impl/test     _e_ : recent             _E_ : dir-locals-file
Dir    _d_ : find dir           _D_ : dired
Search _o_ : multi-occur        _s_ : grep               _r_ : replace string
Tags   _j_ : find tag           _R_ : regenerate tags
Shell  _x_ : eshell             _!_ : run command        _&_ : run command async
Other  _C_ : configure proj     _c_ : compile proj       _u_ : run proj
       _P_ : test proj          _z_ : cache curr file    _i_ : clear cache
"
  ("q" nil nil)
  ;; buffer
  ("b" projectile-switch-to-buffer)
  ("<left>" projectile-previous-project-buffer :exit nil)
  ("<right>" projectile-next-project-buffer :exit nil)
  ("I" projectile-ibuffer)
  ("S" projectile-save-project-buffers)
  ("k" projectile-kill-buffers)
  ;; file
  ("f" projectile-find-file)
  ("F" projectile-find-file-in-known-projects)
  ("g" projectile-find-file-dwim)
  ("t" projectile-toggle-between-implementation-and-test)
  ("e" projectile-recentf)
  ("E" projectile-edit-dir-locals)
  ;; dir
  ("d" projectile-find-dir)
  ("D" projectile-dired)
  ;; search
  ("o" projectile-multi-occur)
  ("s" projectile-grep)
  ("r" projectile-replace)
  ;; tags
  ("j" projectile-find-tag)
  ("R" projectile-regenerate-tags)
  ;; other
  ("C" projectile-configure-project)
  ("c" projectile-compile-project)
  ("u" projectile-run-project)
  ("P" projectile-test-project)
  ("z" projectile-cache-current-file)
  ("i" projectile-invalidate-cache)
  ("x" projectile-run-eshell)
  ("!" projectile-run-shell-command-in-root)
  ("&" projectile-run-async-shell-command-in-root)
  ;; misc
  ("m" projectile-commander "commander")
  ("p" projectile-switch-project "switch project"))

;; binding for projectile hydra
(with-eval-after-load 'projectile
  (define-key projectile-mode-map (kbd "C-c C-M-p") #'my-hydra/projectile-mode/body))
#+end_src

*** org-projectile for Projectile project Org TODOs                   :melpa:

[[https://github.com/IvanMalison/org-projectile][org-projectile]] provides functions for creating Org TODOs for [[Projectile][Projectile]] projects.
It can be set up to use one file for all projects (the default), or one file per
project.
Configure it to use one file per project, specifically a ~TODO.org~ file at the
root directory of the project.
Also add a corresponding entry type to ~org-capture-templates~.

#+name: org-projectile
#+begin_src emacs-lisp
;; Org TODOs for projectile projects
;; use `org-capture' to capture and store TODOs for the current project
;; in `org-projectile-per-project-filepath' at the project's root directory
(use-package org-projectile
  :after (org projectile)
  :config
  (org-projectile-per-project)
  (setq org-projectile-per-project-filepath "TODO.org")
  (push (org-projectile-project-todo-entry) org-capture-templates))
#+end_src

** Magit                                                     :external:melpa:

[[https://github.com/magit/magit][Magit]] is a [[https://git-scm.com/][Git]] porcelain.
In other words, it is an high-level interface to the ~git~
command-line program on the system.

#+name: magit
#+begin_src emacs-lisp
;; binding for calling Magit
(use-package magit
  :commands magit-status
  :bind ("C-c C-M-g" . magit-status))

;; Uncomment to check VC info on file auto-revert (increases I/O load)
;; https://magit.vc/manual/magit/The-mode_002dline-information-isn_0027t-always-up_002dto_002ddate.html
;; (setq auto-revert-check-vc-info t)
#+end_src

*** Magit integration with Ibuffer

Integrate Magit with Ibuffer, so that ~magit-status~ can be called on
the file at point in Ibuffer.

This uses the ~ibuffer-vc~ package, which is used to retrieve the
root directory of the file's project.

Adapted from [[https://www.manueluberti.eu/emacs/2019/08/06/ibuffer-magit/][here]].

#+name: magit-ibuffer-integration
#+begin_src emacs-lisp
;; call `magit-status' for file at point in Ibuffer, uses `ibuffer-vc'
;; adapted from https://www.manueluberti.eu/emacs/2019/08/06/ibuffer-magit/
(defun my-ibuffer-magit-status-at-pt ()
  "Call `magit-status' for the buffer at point while in Ibuffer."
  (interactive)
  (condition-case nil
      (progn
        (require 'ibuffer-vc)
        (let ((buf (ibuffer-current-buffer t)))
          (magit-status (cdr (ibuffer-vc-root buf)))))
    (message "requires `ibuffer-vc' package be installed.")))

;; bind the above function to the "G" key in Ibuffer
(with-eval-after-load 'ibuffer
  (define-key ibuffer-mode-map (kbd "G") #'my-ibuffer-magit-status-at-pt))
#+end_src

** Viewing historical versions of Git-controlled files

[[https://gitlab.com/pidu/git-timemachine][git-timemachine]] provides an easy way to view and step through
historical versions of Git-controlled files.

#+name: git-timemachine
#+begin_src emacs-lisp
;; Browse older versions of Git-controlled files
(use-package git-timemachine
  :commands git-timemachine
  :bind ("C-c C-M-S-g" . git-timemachine))
#+end_src

** Treemacs                                                           :melpa:

[[https://github.com/Alexander-Miller/treemacs][Treemacs]] is a per-project file tree explorer for Emacs.

#+name: treemacs
#+begin_src emacs-lisp
;; per-project file trees
(use-package treemacs
  :demand t
  :bind ("C-c C-M-S-t" . treemacs)
  :init
  ;; resize treemacs icon sizes to 75% of line-height
  (add-hook 'after-init-hook
            (lambda ()
              (when (and (display-graphic-p)
                         (eq system-type 'darwin))
                (treemacs-resize-icons
                 (truncate (* (line-pixel-height) 0.75)))))))
#+end_src

*** Treemacs Projectile integration

Allows quick addition of Projectile projects to Treemacs workspaces.

#+name: treemacs-projectile
#+begin_src emacs-lisp
;; treemacs projectile integration
(use-package treemacs-projectile
  :after (treemacs projectile))
#+end_src

*** Treemacs Magit integration

Have Magit operations be reflected automatically in Treemacs.

#+name: treemacs-magit
#+begin_src emacs-lisp
;; treemacs magit integration
(use-package treemacs-magit
  :after (treemacs magit))
#+end_src

*** Treemacs all-the-icons integration

Use icons from all-the-icons in Treemacs.

#+name: treemacs-all-the-icons
#+begin_src emacs-lisp
;; use icons from all-the-icons in Treemacs
(use-package treemacs-all-the-icons
  :after (treemacs all-the-icons)
  :config
  (treemacs-load-theme "all-the-icons")
  ;; reduce tab-width to fix spacing between icons and text
  (add-hook 'treemacs-mode-hook
            (lambda () (setq-local tab-width 2)))
  ;; do the same if `helm-icons' is being used
  (with-eval-after-load 'helm-icons
    (add-hook 'helm-after-initialize-hook
              (lambda ()
                (with-helm-buffer
                  (setq tab-width 2))))))
#+end_src

* Search

** Search hydra                                                       :hydra:

Hydra for easier access to Emacs [[https://www.gnu.org/software/emacs/manual/html_node/emacs/Search.html][search]] functionality.

#+name: search-hydra
#+begin_src emacs-lisp
(defhydra my-hydra/search (:color teal :columns 3)
  "
Search (_q_: quit)"
  ("q" nil nil)
  ("gg" grep "grep")
  ("gr" rgrep "rgrep")
  ("gl" lgrep "lgrep")
  ("gf" grep-find "grep-find")
  ("gz" rzgrep "rzgrep")
  ("oo" occur "occur")
  ("om" multi-occur "multi-occur")
  ("ob" multi-occur-in-matching-buffers "multi-occur-match-buf")
  ("rs" query-replace "replace string")
  ("rr" query-replace-regexp "replace regexp")
  ("kg" kill-grep "kill-grep")
  ("." (lambda ()
         (interactive)
         (call-interactively #'xref-find-definitions))
   "xref-find-def"))
(global-set-key (kbd "C-c C-M-/") 'my-hydra/search/body)
#+end_src

** Helm occur bindings in search hydra                                :hydra:

Bind over the ~occur~ entry in the search hydra. Since the search
hydra is only defined later, the binding action is added to the
post-initialization hook.

#+name: add-helm-occur-to-search-hydra
#+begin_src emacs-lisp
;; bind over occur entry with helm-occur in search hydra
(with-eval-after-load 'helm
  (defhydra+ my-hydra/search nil
    ("oo" helm-occur "occur")
    ("ov" helm-occur-visible-buffers "occur-visible")))
#+end_src

** wgrep for writable grep buffers with changes pushed to files       :melpa:

[[https://github.com/mhayashi1120/Emacs-wgrep][wgrep.el]] makes grep buffers writable with changes pushed to files.
Enable with ~C-c C-p~ when in a ~*grep*~ buffer.

Automatically installed by ~rg.el~.

#+name: wgrep
#+begin_src emacs-lisp
;; "C-c C-p" in grep bufs allow writing with changes pushed to files
(use-package wgrep
  :config (setq wgrep-auto-save-buffer nil
                wgrep-too-many-file-length 10))
#+end_src

** Ripgrep support with rg.el                                :external:melpa:

[[https://github.com/dajva/rg.el][rg.el]] provides support for [[https://github.com/BurntSushi/ripgrep][ripgrep]], an alternative for the grep search tool that is
extremely fast.

It supports inline editing of search results via [[https://github.com/mhayashi1120/Emacs-wgrep][wgrep]], accessed
through ~e~ while on the search results page.

#+name: ripgrep
#+begin_src emacs-lisp
(when (executable-find "rg")
  (use-package rg
    :bind ("<f6>" . rg-menu)))
#+end_src

*** rg.el entrypoint in search hydra                                  :hydra:

Add rg.el entrypoint to the search hydra.

#+name: search-hydra-rg
#+begin_src emacs-lisp
(when (executable-find "rg")
  (defhydra+ my-hydra/search nil
    ("R" rg-menu "ripgrep" :exit t)))
#+end_src

** Show match counts with Anzu                                        :melpa:

[[https://github.com/emacsorphanage/anzu][anzu.el]] provides a minor mode that shows the total number of matches
when searching and the current match number.

*Warning*:
This can be pretty slow in large buffers.
Consider turning off this mode for the buffer in those cases.

#+name: anzu
#+begin_src emacs-lisp
;; show current and total search matches, and preview query replace results
(use-package anzu
  :bind (([remap query-replace] . anzu-query-replace)
         ([remap query-replace-regexp] . anzu-query-replace-regexp))
  :init (global-anzu-mode))
#+end_src

** dumb-jump                                                          :melpa:

[[https://github.com/jacktasia/dumb-jump][dumb-jump]] provides jump to definition capability based on fast
searching using [[https://github.com/ggreer/the_silver_searcher][ag]], [[https://github.com/BurntSushi/ripgrep][rg]] or grep to find potential definitions of the
function or variable under point and filtering based on heuristics.

It is configured here as a fallback backend for
~xref-find-definitions~ when no better options are available.

#+name: dumb-jump
#+begin_src emacs-lisp
;; jump to definition using ag or rg and applying heuristics
(use-package dumb-jump
  :init
  (setq dumb-jump-aggressive nil
        dumb-jump-default-project "./"
        dumb-jump-prefer-searcher 'rg
        dumb-jump-selector (if (fboundp 'helm)
                               'helm
                             'completing-read))
  :config
  ;; add dumb-jump to the end of the list of backends for
  ;; `xref-find-definitions' so it is used as a fallback option
  ;; when no better finders are available
  (add-hook 'xref-backend-functions #'dumb-jump-xref-activate t))
#+end_src

** avy                                                                :melpa:

[[https://github.com/abo-abo/avy][Avy]] provides support for jumping to visible text by traversing a
character-based decision tree.

*Configuration*:
- Remap ~goto-line~ bindings to call ~avy-goto-line~ instead.
  ~goto-line~ can be invoked by entering numbers for ~avy-goto-line~
  input instead of characters in the decision tree.

#+name: avy
#+begin_src emacs-lisp
;; jump to visible text using char-based decision tree
(use-package avy
  :config
  ;; bind over `goto-line' since it can be invoked by entering numbers
  ;; for `avy-goto-line' input instead characters in the decision tree
  (global-set-key [remap goto-line] #'avy-goto-line)
  ;; jump to location in frame using "C-:"
  (global-set-key (kbd "C-:") #'avy-goto-char-timer))
#+end_src

** ace-link                                                           :melpa:

[[https://github.com/abo-abo/ace-link][ace-link]] provides support for displaying, selecting and jumping to links
in a number of major modes using [[https://github.com/abo-abo/avy][avy]] as the backend.

*Configuration*:
- Call default setup code, which binds ~o~ in ~compilation-mode~,
  ~Custom-mode~, ~eww-mode~, ~help-mode~, ~Info-mode~ and ~woman-mode~
  to the corresponding ace-link command.
- ~M-O~ in ~org-mode-map~ calls the corresponding ace-link function.

#+name: ace-link
#+begin_src emacs-lisp
;; display, select and jump to links in various major modes
(use-package ace-link
  :config
  ;; bind "o" to calling ace-link in compilation-mode, Custom-mode,
  ;; eww-mode, help-mode, Info-mode and woman-mode
  (ace-link-setup-default)
  ;; bind "M-O" (Meta-CapitalOh) to jump to link in Org mode
  (with-eval-after-load 'org
    (define-key org-mode-map (kbd "M-O") #'ace-link-org))
  (with-eval-after-load 'org-agenda
    (define-key org-agenda-mode-map (kbd "M-O") #'ace-link-org-agenda)))
#+end_src

** Notdeft for searching notes and other text files

[[https://github.com/hasu/notdeft][Notdeft]] is an Emacs mode for quickly searching and managing
directories of files, utilizing [[https://xapian.org/][Xapian]] for indexing.

*** Installing Notdeft                                             :external:

Clone ~notdeft~ into a build directory (change =~/build= as needed).
This can be a new directory in one of the ~load-path~ directories,
for example the =site-lisp= subdirectory in ~user-emacs-directory~.

#+begin_example
$ cd ~/.emacs.d/site-lisp
$ git clone https://github.com/hasu/notdeft.git
#+end_example

Install build dependencies and compile the ~notdeft-xapian~ backend.
The following shows installing of build dependencies using MacPorts
(modify the dependency installation step as needed if using another
package manager).

#+begin_example
$ sudo port install tclap xapian
$ cd notdeft
$ make
$ cd xapian
$ make
#+end_example

If notdeft was not cloned and built to a subdirectory in ~load-path~
(like the =site-lisp= folder in ~user-emacs-directory~), make a
shallow copy (no ~.git~ directory) or create a softlink to a
~load-path~ directory.

The following shows the softlink approach
(replace =~/.emacs.d/site-lisp= as appropriate).

#+begin_example
$ ln -s ~/build/notdeft ~/.emacs.d/site-lisp
#+end_example

*** Configuring Notdeft

*Configuration*:
- Use the =journal= and =scratch= subdirectories in the Org
  directory (specified by the ~org-directory~ variable).
- Use Org documents as primary file format, supporting text and
  Markdown as secondary file formats.
- Set the path to the =notdeft-xapian= compiled binary.

#+name: notdeft
#+begin_src emacs-lisp
;; load notdeft, make sure this comes after org-directory is set
(require 'notdeft-autoloads)
(setq notdeft-directories `(,(concat org-directory "journal/")
                            ,(concat org-directory "scratch/"))
      notdeft-extension "org"
      notdeft-secondary-extensions '("md" "txt")
      notdeft-directory (concat org-directory "scratch/")
      notdeft-xapian-program (concat (file-name-directory
                                      (locate-library "notdeft"))
                                     "xapian/notdeft-xapian"))

;; binding to access Notdeft
(global-set-key (kbd "C-c C-M-s") #'notdeft)
#+end_src

** Imenu

[[https://www.gnu.org/software/emacs/manual/html_node/emacs/Imenu.html][Imenu]] allows jumping to major definitions in a file by name.
Have Imenu automatically rescan whenever the content changes.

#+name: imenu-auto-rescan
#+begin_src emacs-lisp
(setq imenu-auto-rescan t)
#+end_src

*** Imenu extension to all open buffers using imenu-anywhere          :melpa:

[[https://github.com/vspinu/imenu-anywhere][imenu-anywhere]] provides an Imenu extension that lists major
definitions across all open buffers.

#+name: imenu-anywhere
#+begin_src emacs-lisp
;; menu list of major definitions across several buffers
(use-package imenu-anywhere
  :defer t
  :after imenu
  :bind ("C-c C-M-;" . imenu-anywhere))
#+end_src

*** Imenu extension to list headings in a side buffer                 :melpa:

[[https://github.com/bmag/imenu-list][imenu-list]] is an Imenu extension that opens up a side buffer
listing the major definitions and headings in the current buffer.
This configuration customizes it such that on jumping to a
definition or heading, it pulses the destination line once
and automatically closes the imenu-list.

#+name: imenu-list
#+begin_src emacs-lisp
;; show imenu as a list in a side buffer
(use-package imenu-list
  :defer t
  :after imenu
  :bind ("C-c C-M-'" . imenu-list-smart-toggle)
  :config
  (setq imenu-list-focus-after-activation t)
  ;; pulse target after selecting
  (add-hook 'imenu-list-after-jump-hook
            (lambda () (pulse-momentary-highlight-one-line (point))))
  ;; close imenu list after going to entry
  (advice-add 'imenu-list-goto-entry :after 'imenu-list-quit-window))
#+end_src

* Visual

** Change default cursor type                                         :early:

Change cursor type to a non-blinking bar.

#+name: cursor
#+begin_src emacs-lisp
;; set cursor after initialization
(setq-default cursor-type 'bar)
(blink-cursor-mode 0)
#+end_src

** Decorations                                                        :early:

Remove toolbar and menu to maximize space usage.

#+name: remove-decorations
#+begin_src emacs-lisp
;; remove unused UI elements
(if (fboundp 'scroll-bar-mode)
    (scroll-bar-mode -1))
(if (fboundp 'tool-bar-mode)
    (tool-bar-mode -1))
(if (and (not (display-graphic-p))
         (fboundp 'menu-bar-mode))
    (menu-bar-mode -1))
#+end_src

** Custom color theme                                        :external:early:

Use custom ~ereader~ theme in ~lisp/ereader-theme.el~.

#+name: load-custom-theme
#+begin_src emacs-lisp
;; use local eReader theme from ~/.emacs.d/themes/ereader-theme.el
(add-to-list 'custom-theme-load-path
             (file-name-as-directory
              (expand-file-name "themes" user-emacs-directory)))
(load-theme 'ereader t)
#+end_src

*Note*: Emacs 27+ added a new face attribute ~:extend~ to control extension of the face from the EOL until the edge of the window. By default it is only turned on for the ~hl-line~ and ~region~ faces. When modifying the custom theme, make sure to set this attribute to ~t~ for the faces that need to be extended.

** Display face fonts                                                 :early:

Set the fonts used for display faces.

There are a few main display faces many other faces inherit from:
- ~default~: The main display face, everything inherits from this directly or
  indirectly unless otherwise specified.
- ~fixed-pitch~: For specific cases where a fixed-pitch
  (i.e. monospace) font is expected.
- ~variable-pitch~: For specific cases where a variable-pitch
  (i.e. proportional) font is expected.
- ~mode-line~: Mode line face for active buffers.
- ~mode-line-inactive~: Mode line face for inactive buffers.

Also set a fallback font for filling gaps in character coverage in the
display face fonts using ~set-fontset-font~,
which has the function signature
~(set-fontset-font NAME TARGET FONT-SPEC &optional FRAME ADD)~.
The key observations here are that:
- When ~NAME~ is ~t~, this is applied to the default fontset.
- When ~TARGET~ is ~nil~, ~FONT-SPEC~ is used for missing characters
  (if it has that character).
- When ~ADD~ is ~append~, then this font is added to the end of the
  fontset which means it will be searched for a matching character
  glyph after all other fonts in the set.

For a list of good free or open-source fonts, see this [[https://github.com/matheuristic/dotfiles/blob/master/font_notes.org][link]].
For more information about fonts and fontsets in Emacs, see this [[https://idiocy.org/emacs-fonts-and-fontsets.html][link]].

#+name: set-display-face-fonts
#+begin_src emacs-lisp
;; set typefaces for graphical Emacs
;; main fonts
(set-face-attribute 'default nil :family "Iosevka Term SS08"
                    :height (if (eq system-type 'darwin) 150 110)
                    :weight 'normal :width 'normal)
(set-face-attribute 'fixed-pitch nil :family "Iosevka Term SS08"
                    :height (if (eq system-type 'darwin) 150 110)
                    :weight 'normal :width 'normal)
(set-face-attribute 'variable-pitch nil :family "Iosevka Aile"
                    :height (if (eq system-type 'darwin) 150 110)
                    :weight 'normal :width 'normal)
(set-face-attribute 'mode-line nil :family "Iosevka Term SS08"
                    :height (if (eq system-type 'darwin) 120 90)
                    :weight 'normal :width 'normal)
(set-face-attribute 'mode-line-inactive nil :family "Iosevka Term SS08"
                    :height (if (eq system-type 'darwin) 120 90)
                    :weight 'normal :width 'normal)
;; fallback font
(set-fontset-font t nil "Symbola" nil 'append)
;; increase min underline offset for more readable underlined words
(setq underline-minimum-offset 5)
#+end_src

For some fonts, an alternative character may be preferable to the
standard character show for some symbol code.
In those instances, the [[https://www.gnu.org/software/emacs/manual/html_node/elisp/Active-Display-Table.html#Active-Display-Table][standard display table]] can be modified to
display replace characters of a given code with characters of a
different one.
An example is shown below.

#+begin_example
;; the following displays '0' using '\ue007' (private slashed zero in B612 font)
(setq standard-display-table (make-display-table))
(aset standard-display-table ?0 [?])
#+end_example

** Icon fonts using all-the-icons       :workaround:semiearly:external:melpa:

Icon fonts can be enabled using the [[https://github.com/domtronn/all-the-icons.el][all-the-icons]] package.
These are useful for a few scenarios:
- Fitting more information in the mode line by converting descriptor words into icons.
- Making the interface more visually information and/or appealing (subjective).

Package functionality requires that specific icon fonts be installed on the system.
This can be done automatically by running ~M-x all-the-icons-install-fonts~,
or manually by installing the TTF fonts from [[https://github.com/domtronn/all-the-icons.el/tree/master/fonts][here]] using the system font manager (for example the Font Book app in Mac OS X).

#+name: all-the-icons
#+begin_src emacs-lisp
;; font icons
(when (display-graphic-p)
  (use-package all-the-icons
    :config (setq all-the-icons-color-icons nil
                  ;; workaround for doom-modeline getting truncated in certain conditions
                  ;; https://github.com/hlissner/doom-emacs/issues/2967#issuecomment-619319082
                  all-the-icons-scale-factor 1.1)))
#+end_src

** Fancy mode line using doom-modeline           :workaround:semiearly:melpa:

Use [[https://github.com/seagle0128/doom-modeline][doom-modeline]] when not using via a TTY.
Requires the ~all-the-icons~ package.

#+name: doom-modeline
#+begin_src emacs-lisp
;; set custom mode line in graphical Emacs
(when (display-graphic-p)
  ;; fast and fancy minimalist mode line, requires all-the-icons be installed
  (use-package doom-modeline
    :after all-the-icons
    :config
    (setq doom-modeline-buffer-file-name-style 'buffer-name
          doom-modeline-env-version nil
          doom-modeline-height 23 ;; change this based on mode-line face height
          doom-modeline-icon (display-graphic-p)
          doom-modeline-irc nil
          doom-modeline-minor-modes t
          doom-modeline-persp-name nil
          doom-modeline-unicode-fallback t)
    ;; workaround for modeline getting truncated in certain conditions
    ;; https://github.com/hlissner/doom-emacs/issues/2967#issuecomment-619319082
    (doom-modeline-def-modeline 'main
      '("  " workspace-name window-number modals matches buffer-info remote-host buffer-position word-count parrot selection-info)
      '(objed-state misc-info grip debug repl lsp minor-modes input-method indent-info buffer-encoding major-mode process vcs checker "  "))
    ;; hide left margin indicator bar
    (set-face-background 'doom-modeline-bar
                         (face-background 'mode-line))
    (set-face-background 'doom-modeline-bar-inactive
                         (face-background 'mode-line-inactive))
    ;; enable mode line
    (doom-modeline-mode 1)))
#+end_src

** Hiding minor modes                                       :semiearly:melpa:

Hide minor modes by default using the [[https://github.com/tarsius/minions][minions]] package behind a pop-up menu, only displaying the most important ones.

#+name: minions
#+begin_src emacs-lisp
(if (display-graphic-p)
    ;; hide minor modes in a menu, access with mouse or `minions-minor-mode-menu'
    (use-package minions
      :init
      ;; modes in minions-direct are always shown
      ;; use UTF-8 mode line lighter
      (setq minions-direct '(overwrite-mode view-mode)
            minions-mode-line-lighter "☰")
      (minions-mode 1)))
#+end_src

** Visual hydra                                                       :hydra:

Hydra for manipulating various visual settings.

#+name: visual-hydra
#+begin_src emacs-lisp
;; hydra for visual settings
(defhydra my-hydra/visual (:color amaranth :hint nil
                           :pre (progn
                                  (require 'follow)
                                  (require 'hilit-chg)
                                  (require 'hl-line)
                                  (require 'display-line-numbers)
                                  (require 'face-remap)
                                  (require 'whitespace)))
  "
Visual (_q_: quit)
_b_ : blink-cursor [% 5`blink-cursor-mode]^^^^^   _F_ : follow       [% 5`follow-mode]^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^   _f_ : font-lock    [% 5`font-lock-mode]
_H_ : hl-changes   [% 5`highlight-changes-mode]   _h_ : hl-line      [% 5`hl-line-mode]^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^   _l_ : line-nums    [% 5`display-line-numbers-mode]
_p_ : show-paren   [% 5`show-paren-mode]^^^^^^^   _s_ : scroll-bar   [% 5(frame-parameter nil 'vertical-scroll-bars)]   _S_ : hscroll-bar  [% 5(frame-parameter nil 'horizontal-scroll-bars)]
_T_ : transient-mk [% 5`transient-mark-mode]^^^   _t_ : truncate-lns [% 5`truncate-lines]^^^^^^^^^^^^^^^^^^^^^^^^^^^^   _v_ : visual-line  [% 5`visual-line-mode]
_W_ : whitespace   [% 5`whitespace-mode]^^^^^^^   _w_ : trailing-ws  [% 5`show-trailing-whitespace]
_nr_ / _np_ / _nd_ / _nw_ : narrow to-region / to-page / to-defun / widen      ^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^[% 5(buffer-narrowed-p)]
_+_  / _-_  / _0_    ^  ^ : zoom   in        / out     / reset                 ^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^[% 5(if text-scale-mode text-scale-mode-amount nil)]
"
  ("q" nil :exit t)
  ("b" blink-cursor-mode)
  ("F" follow-mode)
  ("f" font-lock-mode)
  ("H" highlight-changes-mode)
  ("h" hl-line-mode)
  ("l" display-line-numbers-mode)
  ("p" show-paren-mode)
  ("s" toggle-scroll-bar)
  ("S" toggle-horizontal-scroll-bar)
  ("T" transient-mark-mode)
  ("t" toggle-truncate-lines)
  ("v" visual-line-mode)
  ("W" whitespace-mode)
  ("w" (lambda ()
         (interactive)
         (setq-local show-trailing-whitespace
                     (not show-trailing-whitespace))
         (message "show-trailing-whitespace: %s"
                  (if show-trailing-whitespace "yes" "no"))))
  ("nr" narrow-to-region)
  ("np" narrow-to-page)
  ("nd" narrow-to-defun)
  ("nw" widen)
  ("+" text-scale-increase)
  ("-" text-scale-decrease)
  ("0" (text-scale-adjust 0))
  ("r" (lambda ()
         (interactive)
         ;; refocus doom-modeline just in case
         (when (and (boundp 'doom-modeline-mode)
                    doom-modeline-mode)
           (doom-modeline-focus))
         ;; redraw display
         (redraw-display))
   "redraw"))

(defvar-local my-hydra/visual/emphasis--face-remap-cookies '()
  "Alist storing cookies for `face-remap-add-relative' calls.")

(defun my-hydra/visual/emphasis--toggle-lighten-face (face)
  "Toggle lightening of FACE color for emphasis or emphasis."
  (let ((face-remap-cookie-old (alist-get face my-hydra/visual/emphasis--face-remap-cookies)))
    (if face-remap-cookie-old
        (progn
          (face-remap-remove-relative face-remap-cookie-old)
          (setq my-hydra/visual/emphasis--face-remap-cookies
                (assq-delete-all
                 face
                 my-hydra/visual/emphasis--face-remap-cookies)))
      (let* ((light-color (color-lighten-name
                           (face-attribute face :foreground)
                           50)) ;; lighten color by 50 percent
             (face-remap-cookie-new (face-remap-add-relative
                                     face
                                     :foreground light-color)))
        (push `(,face . ,face-remap-cookie-new)
              my-hydra/visual/emphasis--face-remap-cookies)))))

(defhydra my-hydra/visual/emphasis (:color amaranth :hint nil)
  "
Visual → Emphasis (_q_: quit)
_c_ : comments      [% 3(null (assq 'font-lock-comment-face my-hydra/visual/emphasis--face-remap-cookies))]   _C_ : comment-delim  [% 3(null (assq 'font-lock-comment-delimiter-face my-hydra/visual/emphasis--face-remap-cookies))]   _d_ : doc            [% 3(null (assq 'font-lock-doc-face my-hydra/visual/emphasis--face-remap-cookies))]
"
  ("q" my-hydra/visual/body :exit t)
  ("c" (lambda ()
         (interactive)
         (my-hydra/visual/emphasis--toggle-lighten-face
          'font-lock-comment-face)))
  ("C" (lambda ()
         (interactive)
         (my-hydra/visual/emphasis--toggle-lighten-face
          'font-lock-comment-delimiter-face)))
  ("d" (lambda ()
         (interactive)
         (my-hydra/visual/emphasis--toggle-lighten-face
          'font-lock-doc-face))))

;; bind visual hydra
(global-set-key (kbd "C-c C-M-v") 'my-hydra/visual/body)

;; add entrypoint to visual emphasis hydra to visual hydra
(defhydra+ my-hydra/visual nil
  ("e" my-hydra/visual/emphasis/body "→ Emphasis" :exit t))
#+end_src

** darkroom for workspaces without visual distractions                 :elpa:

[[http://elpa.gnu.org/packages/darkroom.html][darkroom]] provides two minor modes ~darkroom-mode~ and
~darkroom-tentative-mode~ that remove visual distractions.

Turning on any of the modes removes the mode line, increases the text
scale and increases the text margins.

The difference between ~darkroom-tentative-mode~ and ~darkroom-mode~
is that ~darkroom-tentative-mode~ only removes visual distractions if
the Emacs frame contains only one window.

#+name: darkroom
#+begin_src emacs-lisp
;; provides toggleable modes that remove visual distractions
(use-package darkroom
  :config (setq darkroom-text-increase-scale 2))
#+end_src

*** darkroom hydra                                                    :hydra:

Extend visual hydra to support ~darkroom-mode~ and
~darkroom-tentative-mode~.

#+name: darkroom-hydra
#+begin_src emacs-lisp
;; extend visual hydra to support darkroom-mode and darkroom-tentative-mode
(eval
 `(defhydra+ my-hydra/visual
    ,(append my-hydra/visual/params '(:pre (require 'darkroom)))
    ,(concat my-hydra/visual/docstring
             "_dm_ : darkroom-mode            [% 3`darkroom-mode]   _dt_ : darkroom-tentative-mode  [% 3`darkroom-tentative-mode]
")
    ("dm" darkroom-mode :exit nil)
    ("dt" darkroom-tentative-mode :exit nil)))
#+end_src

** Color coding by depth using prism                                  :melpa:

[[https://github.com/alphapapa/prism.el][prism.el]] disperses Lisp and other forms in different colors by depth.
There are two modes:
- ~prism-mode~ for Lisp forms.
- ~prism-whitespace-mode~ for languages where whitespace has semantic meaning.

#+name: prism
#+begin_src emacs-lisp
;; color code by depth
(use-package prism
  :config
  (prism-set-colors :num 16
    :desaturations (cl-loop for i from 0 below 16
                            collect (* i 2.5))
    :lightens (cl-loop for i from 0 below 16
                       collect (* i 2.5))
    :colors (list "saddle brown"
                  "midnight blue"
                  "dark green")
    :comments-fn
    (lambda (color)
      (prism-blend color
                   (face-attribute 'font-lock-comment-face
                                   :foreground)
                   0.25))
    :strings-fn
    (lambda (color)
      (prism-blend color "white" 0.5))))
#+end_src

*** Prism hydra                                                       :hydra:

Extend visual hydra to support ~prism-mode~ and ~prism-whitespace-mode~.

#+name: prism-hydra
#+begin_src emacs-lisp
;; extend visual hydra to support prism-mode and prism-whitespace-mode
(eval
 `(defhydra+ my-hydra/visual
    ,(append my-hydra/visual/params '(:pre (require 'prism)))
    ,(concat my-hydra/visual/docstring
             "_Pm_ : prism-mode               [% 3`prism-mode]   _Pw_ : prism-whitespace-mode    [% 3`prism-whitespace-mode]
")
    ("Pm" prism-mode :exit nil)
    ("Pw" prism-whitespace-mode :exit nil)))
#+end_src

** Semantic highlighting using color-identifiers-mode                 :melpa:

[[https://github.com/ankurdave/color-identifiers-mode][Color identifiers mode]] provides semantic highlighting, coloring each
source code identifier based on its name (specifically, a symbol or
variable name has the same color wherever it appears in the text).

The configuration below is the default, but two of the main parameters
users can modify are ~color-identifiers-coloring-method~ which
controls how words are mapped to colors and
~color-identifiers:num-colors~ which controls the cardinality of the
colorspace words are mapped to (more is not better since many of the
colors could end up very similar).

#+name: color-identifiers-mode
#+begin_src emacs-lisp
;; provides semantic coloring where same keywords are also colored the same
(use-package color-identifiers-mode
  :config (setq color-identifiers-coloring-method 'sequential
                color-identifiers:num-colors 10))
#+end_src

*** color-identifiers-mode hydra                                      :hydra:

Extend visual hydra to allow toggling of ~color-identifiers-mode~.

#+name: color-identifiers-mode-hydra
#+begin_src emacs-lisp
;; extend visual hydra to support color-identifiers-mode
(eval
 `(defhydra+ my-hydra/visual
    ,(append my-hydra/visual/params '(:pre (require 'color-identifiers-mode)))
    ,(concat my-hydra/visual/docstring
             "_c_  : color-identifiers-mode   [% 3`color-identifiers-mode]
")
    ("c" color-identifiers-mode :exit nil)))
#+end_src

** Outline minor mode hydra                                           :hydra:

[[https://www.gnu.org/software/emacs/manual/html_node/emacs/Outline-Mode.html][Outline mode]] is a mode for visualizing and editing the outline structure
of documents.
It provides commands to navigate the outline structure and to hide
text that are not heading lines (i.e. folding code).
The navigation commands are:
- ~C-c C-n~ and ~C-c C-p~ navigate to the next and previous visible
  headings.
- ~C-c C-f~ and ~C-c C-b~ navigate to the next and previous visible
  headings of the same level.
- ~C-c C-u~ navigates to the parent heading of the current one.

Outline mode can be enabled as a major mode or a minor mode.
The minor mode is typically the more useful of the two, so define a
hydra to access its visualization commands in a user friendly manner.

#+name: outline-hydra
#+begin_src emacs-lisp
;; hydra for toggling outline-minor-mode and running its commands
(defhydra my-hydra/visual/outline (:color amaranth :hint nil
                                   :pre (require 'outline))
  "
Visual → Outline [minor-mode-enabled=%`outline-minor-mode] (_q_: ←)
Mode    _m_ : toggle
Hide    _c_ : entry     _l_ : leaves    _d_ : subtree   _o_ : other
        _t_ : body
Show    _e_ : entry     _i_ : children  _k_ : branches  _s_ : subtree
        _a_ : all
"
  ("q" my-hydra/visual/body nil :exit t)
  ("c" outline-hide-entry)
  ("l" outline-hide-leaves)
  ("d" outline-hide-subtree)
  ("t" outline-hide-body)
  ("o" outline-hide-other)
  ("e" outline-show-entry)
  ("i" outline-show-children)
  ("k" outline-show-branches)
  ("s" outline-show-subtree)
  ("a" outline-show-all)
  ("m" outline-minor-mode))

;; add entry-point into outline hydra from visual hydra
(defhydra+ my-hydra/visual nil
  ("o" my-hydra/visual/outline/body "→ Outline" :exit t))
#+end_src

** Tab-cycling of folds in outline-minor-mode with outline-magic :melpa:

[[https://github.com/tj64/outline-magic][outline-magic]] extends outline-mode and outline-minor-mode with
visibility cycling and structural editing.

Emulate the node visibility cycling of Org mode, where pressing ~TAB~
while on an outline heading with cycle its subtree visibility.

#+name: outline-magic
#+begin_src emacs-lisp
;; <tab> cycles the folding of the current node in outline-minor-mode
;; https://www.reddit.com/r/emacs/comments/a6tu8y/outlineminormode_for_emacs_maybe_useful/eby6c2r/
(use-package outline-magic
  :config
  (add-hook 'outline-minor-mode-hook
            (lambda ()
              (define-key outline-minor-mode-map (kbd "TAB")
                '(menu-item "" nil :filter
                            (lambda (&optional _)
                              (when (outline-on-heading-p)
                                #'outline-cycle)))))))
#+end_src

** Display line numbers when editing code

Display line numbers by default when editing code.

#+name: display-line-numbers-when-editing-code
#+begin_src emacs-lisp
;; display line numbers by default when editing code
(add-hook 'prog-mode-hook
          (lambda ()
            (display-line-numbers-mode 1)))
#+end_src

** Show pointer location column number in the mode line

Show the column number of the pointer location in the mode line.

#+name: show-column-number
#+begin_src emacs-lisp
;; show pointer location column number in mode line
(setq column-number-mode t)
#+end_src

** Show matching parentheses immediately

Show matching parentheses while editing without delay.

#+name: show-matching-parentheses
#+begin_src emacs-lisp
;; show matching parentheses with no delay
(setq show-paren-delay 0)
(show-paren-mode 1)
#+end_src

** Temporarily highlight yanked text                                  :melpa:

[[https://github.com/k-talo/volatile-highlights.el][volatile-highlights.el]] provides a minor mode that temporarily
highlights yanked text.

#+name: volatile-highlights
#+begin_src emacs-lisp
(use-package volatile-highlights
  :hook (after-init . volatile-highlights-mode))
#+end_src

** Highlight indentation levels                                       :melpa:

[[https://github.com/DarthFennec/highlight-indent-guides][highlight-indent-guides]] adds dynamic indentation guides which are
useful for languages where whitespace is important like Python.

Use when editing Python files by default.

#+name: highlight-indent-guides
#+begin_src emacs-lisp
;; add visual indentation guides
(use-package highlight-indent-guides
  :init (setq highlight-indent-guides-method 'character
              highlight-indent-guides-responsive 'top
              highlight-indent-guides-character ?\x2502)
  :config
  (add-hook 'python-mode-hook (lambda ()
                                (highlight-indent-guides-mode 1))))
#+end_src

*** Add toggle for highlight-indent-guides-mode to visual hydra       :hydra:

Add ~highlight-indent-guides-mode~ toggle entry point to visual hydra.

#+name: add-highlight-indent-guides-toggle-to-visual-hydra
#+begin_src emacs-lisp
;; add `highlight-indent-guides-mode' toggle to visual hydra
(eval
 `(defhydra+ my-hydra/visual
    ,(append my-hydra/visual/params '(:pre (require 'highlight-indent-guides)))
    ,(concat my-hydra/visual/docstring
             "_i_  : highlight-indent-guides  [% 3`highlight-indent-guides-mode]
")
    ("i" highlight-indent-guides-mode :exit nil)))
#+end_src

** Add frame padding

Adding a default frame padding creates nicer typography at the cost of
a few characters.

#+name: frame-internal-border
#+begin_src emacs-lisp
;; add internal frame border
(add-to-list 'default-frame-alist
             `(internal-border-width . 12))
(defun my-default-frame-border-teardown ()
  "Removes internal-border-width entries from `default-frame-alist'."
  (setq default-frame-alist
        (assq-delete-all 'internal-border-width default-frame-alist)))
;; add teardown function to be run before closing Emacs, which needs
;; to run early when closing so add it to the end of `after-init-hook'
(add-hook 'after-init-hook
          (lambda ()
            (add-hook 'kill-emacs-hook #'my-default-frame-border-teardown))
          t)
#+end_src

** Add non-visible bottom window dividers for easier mouse resizing

By default for GUI Emacs (at least for the Emacs Mac port), right
window dividers are rendered and can be dragged with the mouse to
resize windows horizontally.

Add non-visible bottom window dividers to also allow vertical window
resizing by dragging the space between two up-down adjacent windows.

#+name: add-bottom-window-dividers
#+begin_src emacs-lisp
;; add non-visisible bottom window dividers for mouse-based vertical resizing
(setq window-divider-default-bottom-width (if (eq system-type 'darwin)
                                              6
                                            3)
      window-divider-default-places 'bottom-only)
(let ((fg-color (face-attribute 'default :foreground))
      (bg-color (face-attribute 'default :background)))
  (set-face-attribute 'window-divider nil :foreground bg-color)
  (set-face-attribute 'window-divider-first-pixel nil :foreground bg-color)
  (set-face-attribute 'window-divider-last-pixel nil :foreground bg-color))
(window-divider-mode 1)
#+end_src

* Web

** Browsing

*** Emacs Web Wowser

[[https://www.gnu.org/software/emacs/manual/html_mono/eww.html][Emacs Web Wowser]] (~eww~) is a built-in Emacs web browser.
It supports text and images, but not webfonts or Javascript.

*Customizations*:
- Use the lightweight version of [[https://duckduckgo.com/][DuckDuckGo]] for web searches by default.
- Don't render images by default.

#+name: eww
#+begin_src emacs-lisp
;; built-in Emacs text web browser
(use-package eww
  :ensure nil ;; built-in
  :commands (eww eww-follow-link)
  :bind (:map eww-mode-map
         ("I" . my-eww-toggle-images))
  :init (setq eww-search-prefix "https://duckduckgo.com/lite?q=")
  ;; don't render images in HTML pages by default
  :config (setq-default shr-inhibit-images t))
#+end_src

*** Add eww entry point to search hydra                               :hydra:

Add entry point into eww from search hydra.

#+name: search-hydra-eww
#+begin_src emacs-lisp
;; add eww entry point to search hydra
(defhydra+ my-hydra/search nil
  ("w" eww "eww" :exit t))
#+end_src

*** Emacs Web Wowser hydra                                            :hydra:

Major mode-specific hydra for Emacs Web Wowser (~eww~).

#+name: eww-hydra
#+begin_src emacs-lisp
;; hydra for Emacs Web Wowser
(defhydra my-hydra/eww-mode (:color teal :columns 3)
  "
Emacs Web Wowser (_q_: quit)"
  ("q" nil nil)
  ("d" eww-download "download-link")
  ("G" eww "search")
  ("o" eww-open-file "open-file")
  ("l" eww-back-url "back")
  ("r" eww-forward-url "forward")
  ("g" eww-reload "reload")
  ("v" eww-view-source "view-source")
  ("w" eww-copy-url "copy-url")
  ("&" eww-browse-with-external-browser "browse-ext")
  ("b" eww-add-bookmark "bookmark-page")
  ("B" eww-list-bookmarks "bookmark-list")
  ("R" eww-readable "readable-only")
  ("F" eww-toggle-fonts "toggle-fonts")
  ("I" my-eww-toggle-images "toggle-images")
  ("M-C" eww-toggle-colors "toggle-colors")
  ("D" eww-toggle-paragraph-direction "toggle-text-dir")
  ("s" eww-switch-to-buffer "eww-switch-buf")
  ("S" eww-list-buffers "eww-list-buf")
  ("H" eww-list-histories "history")
  ("C" url-cookie-list "cookie-list"))

;; binding
(with-eval-after-load 'eww
  (define-key eww-mode-map (kbd "C-c C-M-m") #'my-hydra/eww-mode/body))
#+end_src

**** Helper functions for Emacs Web Wowser hydra

Helper functions for ~eww~:
- Toggle images in ~eww-mode~.

#+name: eww-hydra-helper-functions
#+begin_src emacs-lisp
;; helper function for toggling images in Emacs Web Wowser
(defun my-eww-toggle-images ()
  "Toggle displaying of images when rendering HTML."
  (interactive)
  (setq-local shr-inhibit-images (not shr-inhibit-images))
  (eww-reload)
  (message "Images are now %s" (if shr-inhibit-images "off" "on")))
#+end_src

** REST development tools

*** restclient.el                                                     :melpa:

[[https://github.com/pashky/restclient.el][restclient.el]] provides a major mode for interacting with HTTP queries
stored in text files, in particular editing and sending them.

*Customizations*:
- Load the major mode automatically when opening files with a ~.http~ suffix.
- When the response to a sent HTTP query is received, the request is
  pulsed once in the buffer to give visual feedback on which query the
  response is for (adapted from [[https://github.com/jordonbiondo/.emacs.d/blob/master/init.el][here]]).

#+name: restclient
#+begin_src emacs-lisp
(use-package restclient
  :defer t
  ;; assume request source files have ".http" suffix
  :mode ("\\.http\\'" . restclient-mode)
  :config
  ;; pulse *HTTP Response* buffer after receiving request response
  ;; adapted from https://github.com/jordonbiondo/.emacs.d/blob/master/init.el
  (defun my-restclient-pulse-buffer ()
    "Pulses the current buffer."
    (save-excursion
      (goto-char (point-min))
      (pulse-momentary-highlight-region (point-min) (point-max))))
  (add-hook 'restclient-response-loaded-hook #'my-restclient-pulse-buffer))
#+end_src

*** restclient.el hydra                                               :hydra:

Major mode-specific hydra for ~restclient-mode~.
The head for pretty printing the JSON response requires the
~json-mode~ package be installed.

#+name: restclient-hydra
#+begin_src emacs-lisp
;; hydra for restclient
(defhydra my-hydra/restclient-mode (:color teal :columns 3)
  "
REST client (_q_: quit)"
  ("q" nil nil)
  ("c" restclient-http-send-current "send")
  ("r" restclient-http-send-current-raw "send-raw")
  ("v" restclient-http-send-current-stay-in-window "send-bg")
  ("n" restclient-jump-next "next" :exit nil)
  ("p" restclient-jump-prev "prev" :exit nil)
  ("." restclient-mark-current "mark")
  ("u" restclient-copy-curl-command "copy-curl")
  ("N" (lambda ()
         (interactive)
         (if (buffer-narrowed-p)
             (widen)
           (restclient-narrow-to-current)))
   "narrow" :exit nil)
  ("f" (lambda ()
         (interactive)
         (require 'json-mode nil t)
         (if (fboundp 'json-mode-pretty-print-dwim)
             (call-interactively 'json-mode-pretty-print-dwim)
           (message "Requires the `json-mode' package be installed.")))
   "fmt-json-rgn"))

;; binding for restclient hydra
(with-eval-after-load 'restclient
  (define-key restclient-mode-map (kbd "C-c C-M-m") #'my-hydra/restclient-mode/body))
#+end_src

** Network security

Increased network security settings.

#+name: network-security
#+begin_src emacs-lisp
;; increase network security settings
(setq gnutls-verify-error t)
(setq gnutls-min-prime-bits 1024)
(setq network-security-level 'high)
(setq nsm-save-host-names t)
#+end_src

** HTTP requests privacy

More private HTTP requests.

#+name: http-requests-privacy
#+begin_src emacs-lisp
;; HTTP requests privacy settings
(setq url-cookie-untrusted-urls '(".*")) ;; no cookies
(setq url-privacy-level 'paranoid) ;; more private HTTP requests
(url-setup-privacy-info) ;; apply `url-privacy-level'
#+end_src

** Add read-it-later functionality to Org mode

Add read-it-later functionality (bookmarking and archiving a webpage
for later offline reading) to Org mode, with keybindings bound to the
prefix ~C-c o~ (use ~C-c o ?~ for the full set of available bindings).

By default this is configured to use [[https://github.com/Y2Z/monolith][monolith]] which can bundle a web
page into single HTML file.

This custom package is forked from [[https://github.com/scallywag/org-board][org-board]] and modified as follows:
1. Simplify the user interaction model---only one URL per header.
2. Support ~monolith~ or another tool by having an API that is less
   specific to ~wget~. This also means less ~wget~ specific magic like
   option completions when editing the entry's properties in Org mode.

#+name: org-readitlater
#+begin_src emacs-lisp
;; read-it-later functionality in Org mode
;; custom package in the lisp subfolder of the user emacs directory
(require 'org-readitlater)
(define-key org-mode-map (kbd "C-c o") org-readitlater-keymap)
#+end_src

*** Monolith                                                       :external:

[[https://github.com/Y2Z/monolith][Monolith]] is a CLI tool for saving complete webpages into a single HTML
file with embedded images, CSS and JS.

**** Installing monolith

***** Pre-built binaries

[[https://github.com/Y2Z/monolith/releases][Pre-built binaries]] are available for Linux (~x86_64~) and Windows.

***** Building from source

Install dependencies. The following example instructions are for
MacPorts.

#+begin_example
$ sudo port install openssl
$ sudo port install cargo
$ sudo port install pkgconfig
#+end_example

Make sure =~/.cargo/bin= is in the shell ~$PATH~ ([[https://unix.stackexchange.com/questions/26047/how-to-correctly-add-a-path-to-path][bash]], [[https://fishshell.com/docs/current/tutorial.html#path][fish]]).

Clone the monolith repo and build it.

#+begin_example
$ cd /path/to/build
$ git clone https://github.com/Y2Z/monolith.git
$ cd monolith
$ make install
#+end_example

Test the built binary.

#+begin_example
$ monolith --help
#+end_example

*** Configure org-capture template for org-readitlater

Configure an [[https://orgmode.org/manual/Capture.html][org-capture]] template for capturing org-readitlater
entries to the =readitlater/readitlater.org= file in ~org-directory~,
filing it under the "Unsorted" heading and automatically downloading
the article.

If org-protocol is enabled, the following bookmarklet can be added to
a web browser to send the current page details (and any selected text)
to the org-readitlater capture template associated with key ~a~ (the
~template='a'~ query parameter triggers the automatic dispatch).

#+begin_example
javascript:location.href='org-protocol://capture?template=a&url='+encodeURIComponent(location.href)+'&title='+encodeURIComponent(document.title)+'&body='+encodeURIComponent(window.getSelection())
#+end_example

#+name: org-capture-readitlater
#+begin_src emacs-lisp
;; add capture template for org-readitlater
(setq org-readitlater-capture-file "readitlater/readitlater.org")
(push `("a" "Archive page to read-it-later list" entry
        (file+headline ,org-readitlater-capture-file "Unsorted")
        "* %?%:description\n:PROPERTIES:\n:URL: %:link\n:ADDED: %U\n:END:\n%:initial\n")
      org-capture-templates)
;; auto-download page after capturing with org-readitlater template
(defun do-org-readitlater-dl-hook ()
  (when (equal (buffer-name)
               (concat "CAPTURE-"
                       (file-name-nondirectory org-readitlater-capture-file)))
    (org-readitlater-archive)))
(add-hook 'org-capture-before-finalize-hook #'do-org-readitlater-dl-hook)
#+end_src

* Writing

** Writing hydra                                                      :hydra:

Hydra for writing functions.

#+name: writing-hydra
#+begin_src emacs-lisp
;; hydra for writing functions
(defhydra my-hydra/writing (:color amaranth :hint nil
                            :pre (require 'flyspell))
  "
Writing (_q_: quit)
Flyspell   [% 4(if flyspell-mode (if (eq flyspell-generic-check-word-predicate #'flyspell-generic-progmode-verify) 'prog t) nil)]   _f_ : toggle  _F_ : prog
"
  ("q" nil :exit t)
  ("f" flyspell-mode)
  ("F" flyspell-prog-mode))

;; bindings for writing hydra
(global-set-key (kbd "C-c C-M-S-w") #'my-hydra/writing/body)
#+end_src

** Spell checking

*** Flyspell for spell checking in Emacs

[[https://www.gnu.org/software/emacs/manual/html_node/emacs/Spelling.html][Flyspell]] is a built-in spell checker that comes in two flavors:
- ~flyspell-mode~ checks spelling anywhere in the buffer.
- ~flyspell-prog-mode~ checks spelling only in comments and strings.

When one of the modes is active, unrecognized words are highlighted.
To maintain performance, it only checks words that are newly typed or
that the pointer moves across.

Activating the mode also enables a few bindings that act on
unrecognized words:
- ~C-c $~ pops up menu of corrections for the word before the pointer.
- ~C-.~ corrects the current word. Pressing it repeatedly will propose
  successive correction candidates.
- ~C-;~ corrects the previous word. Pressing it repeated will propose
  successively correction candidates.
- ~C-,~ jumps to the next unrecognized word.

*Note*: Flyspell uses as its backend a spell checker like Aspell, so
one needs to be installed on system. An appropriate dictionary or
dictionaries for the spell checker should also be installed.

*** Aspell command-line spell checker                              :external:

[[http://aspell.net/][Aspell]] is a spell checker. There are a number of alternatives but
they are either deprecated ([[https://www.cs.hmc.edu/~geoff/ispell.html][Ispell]]), similar ([[https://en.wikipedia.org/wiki/Hunspell][Hunspell]]), or
language-specific ([[https://voikko.puimula.org/][Voikko]] for Finnish).

This or another supported spell checker has to be installed in order
for Flyspell to work. Additionally, a dictionary for the spell checker
should also be intalled.

If using MacPorts, installation instructions follow for Aspell and its
English dictionary.

#+begin_example
$ sudo port install aspell
$ sudo port install aspell-dict-en
#+end_example

** dictionary.el for looking up word definitions                      :melpa:

[[https://github.com/myrkr/dictionary-el][dictionary.el]] provides a frontend for accessing a local or online
dictionary server that implements [[https://en.wikipedia.org/wiki/DICT][RFC 2229]].

#+name: dictionary
#+begin_src emacs-lisp
;; provides word lookups from a dictionary server
;; `dictionary-server' can be set to "localhost" to use a local
;; dictionary server like dictd or GNU Dico that implements RFC 2229
(use-package dictionary
  :init (setq dictionary-server "dict.org"
              dictionary-default-dictionary "*"))
#+end_src

*** dictionary.el hydra                                               :hydra:

#+name: dictionary-hydra
#+begin_src emacs-lisp
;; add dictionary entrypoints to writing hydra
(eval
 `(defhydra+ my-hydra/writing
    ,(append my-hydra/writing/params '(:pre (require 'dictionary)))
    ,(concat my-hydra/writing/docstring "Dictionary          _s_ : search  _m_ : match
")
    ("s" dictionary-search :exit t)
    ("m" dictionary-match-words :exit t)))
#+end_src

*** GNU Collaborative International Dictionary of English

The [[https://gcide.gnu.org.ua/][GNU Collaborative International Dictionary of English]] (GCIDE) is a free dictionary derived from the Webster's 1913 dictionary (which provides definitions that are fuller and more descriptive than more modern dictionaries) supplemented with newer definitions from Wordnet and other sources.

It is available as a source on [[http://www.dict.org/][dict.org]] which can be accessed by
~dictionary.el~ functions.

Downloads are available for use with a local dictionary server.
One local dictionary server that has good GCIDE support is [[https://www.gnu.org.ua/software/dico/][GNU Dico]],
which has a bundled [[http://puszcza.gnu.org.ua/software/dico/manual/html_section/gcide.html][interface module]] for the dictionary.

** Synosaurus in-editor thesaurus                                     :melpa:

[[https://github.com/hpdeifel/synosaurus][Synosaurus]] provides a thesaurus frontend with pluggable backends.
Supported backends are limited to:
- [[https://www.openthesaurus.de/][OpenThesaurus]] (German), which the package can access via HTTP
  requests.
- [[https://wordnet.princeton.edu/][WordNet]] (English), which requires the command-line tool ~wn~ be
  installed locally.

To install WordNet on Mac OS X using MacPorts, do the following.

#+begin_example
$ sudo port install wordnet
#+end_example

#+name: synosaurus
#+begin_src emacs-lisp
;; thesaurus functions using Synosaurus
(use-package synosaurus
  :init (setq synosaurus-choose-method 'default
              synosaurus-backend 'synosaurus-backend-wordnet))
#+end_src

*** Synosaurus hydra                                                  :hydra:

Hydra for Synosaurus.

#+name: synosaurus-hydra
#+begin_src emacs-lisp
;; add synosaurus entrypoints to writing hydra
(eval
 `(defhydra+ my-hydra/writing
    ,(append my-hydra/writing/params '(:pre (require 'synosaurus)))
    ,(concat my-hydra/writing/docstring "Synosaurus [% 4`synosaurus-mode]   _S_ : toggle  _L_ : lookup  _R_ : replace _I_ : insert
")
    ("S" synosaurus-mode :exit nil)
    ("L" synosaurus-lookup :exit t)
    ("R" synosaurus-choose-and-replace :exit t)
    ("I" synosaurus-choose-and-insert :exit t)))
#+end_src

** LanguageTool for grammar checking

[[https://languagetool.org/][LanguageTool]] ([[https://github.com/languagetool-org/languagetool][Github]]) provides a standalone open-source grammar checking tool that can be used on the command line or run as a HTTP server.

*** Installing LanguageTool                                        :external:

Make sure a working Java distribution is installed on the system.
The following shows the installing of OpenJDK 8 using MacPorts.

#+begin_example
$ sudo port install openjdk8
#+end_example

[[https://languagetool.org/#standalone][Download]] the standalone desktop version of LanguageTool for offline use
which comes in a zip file and extract it to some directory.
The following assumes the zip file was downloaded to =~/Downloads= and
extracts its contents to =~/build= (change paths as appropriate as well
as the zip filename).

#+begin_example
$ unzip ~/Downloads/LanguageTool-4.9.1.zip -d ~/build
#+end_example

Optionally, softlink the JAR files to be used to a common storage folder like =~/jars= for easier access (change paths as appropriate).

#+begin_example
$ cd ~/jars
$ ln -s ~/build/LanguageTool-4.9.1/languagetool-commandline.jar
#+end_example

*** LanguageTool basic Emacs configuration                            :melpa:

[[https://github.com/mhayashi1120/Emacs-langtool][langtool]] provides support for using LanguageTool in Emacs as a grammar checking backend.

The configuration here is for using the command-line version of LanguageTool.
See the website for setting up the HTTP server version.

#+name: langtool
#+begin_src emacs-lisp
;; grammar checking functions using LanguageTool
(use-package langtool
  :init (setq langtool-default-language "en-US"
              langtool-language-tool-jar (expand-file-name "~/jars/languagetool-commandline.jar")))
#+end_src

**** LanguageTool hydra                                               :hydra:

Hydra for ~langtool~.

#+name: langtool-hydra
#+begin_src emacs-lisp
;; add langtool functions to writing hydra
(eval
 `(defhydra+ my-hydra/writing
    ,(append my-hydra/writing/params '(:pre (require 'langtool)))
    ,(concat my-hydra/writing/docstring "LangTool            _w_ : check   _W_ : done    _l_ : lang    _c_ : correct-buf
")
    ("w" langtool-check nil :exit nil)
    ("W" langtool-check-done nil :exit nil)
    ("l" langtool-switch-default-language nil :exit nil)
    ("c" langtool-correct-buffer nil :exit nil)))
#+end_src

** typo.el for typographical editing

[[https://github.com/jorgenschaefer/typoel][typo.el]] is a minor mode that makes insertion of typographically useful
unicode characters easier.

Enabling the buffer-local ~typo-mode~ does the following:
- Automatically convert ~'~ to ~‘~ and ~’~, and ~"~ to ~“~ and ~”~.
- Pressing ~'~, ~"~ and ~-~ cycles among their different variants.
  For example, repeatedly pressing ~-~ cycles between en-dash,
  em-dash, and other dash-like glyphs.
- Three periods in a row get automatically replaced by an ellipsis.

Enabling ~typo-global-mode~ does the following:
- ~C-c 8~ brings up a unicode insertion dispatcher that is like the
  standard ~C-x 8~ unicode insertion dispatcher but focused on a
  different selection of commonly used typographical characters.

#+name: typo
#+begin_src emacs-lisp
(use-package typo)
#+end_src

*** Add toggle for typo-mode to writing hydra

Add a toggle for ~typo-mode~ to writing hydra.

#+name: add-typo-mode-toggle-to-writing-hydra
#+begin_src emacs-lisp
;; add toggle for typo-mode to writing hydra
(eval
 `(defhydra+ my-hydra/writing
    ,(append my-hydra/writing/params '(:pre (require 'typo)))
    ,(concat my-hydra/writing/docstring
             "Typography [% 4`typo-mode]   _t_ : toggle
")
    ("t" typo-mode :exit nil)))
#+end_src

* Other

** real-auto-save for auto-saving files at regular intervals          :melpa:

The [[https://github.com/ChillarAnand/real-auto-save][real-auto-save]] package supports per-file auto-saving at regular
time intervals, i.e. a buffer-local ~auto-save-visited-mode~.

When the buffer-local minor mode ~real-auto-save-mode~ is active, the
buffer is automatically saved every ~real-auto-save-interval~ seconds.

#+name: real-auto-save
#+begin_src emacs-lisp
;; buffer-local `auto-save-visited-mode'
(use-package real-auto-save
  :defer t
  :config (setq real-auto-save-interval 10)) ;; save interval, in seconds
#+end_src

*Example use case*:
If some directory (say where files in ~org-agenda-files~ are
stored) is a cloud-enabled shared folder and external changes should be
reflected with files auto-saved on changes so that the buffer, local and
remote files are kept in sync, create a ~.dir-locals.el~ file in the folder
with the following contents.

#+begin_example
;; Directory-local settings
((nil . ((eval . (auto-revert-mode 1)) ;; auto-revert files
         (eval . (real-auto-save-mode 1))))) ;; buffer-local auto-save
#+end_example

** Configure mouse settings to be trackpad-friendly

Modify mouse scroll settings to be friendlier for trackpads:
- Regular scrolling is one line at a time.
- Hold Shift while scrolling for five lines at a time.
- Hold Control while scrolling for a screen at a time.
- Natural scrolling (scrolling right moves the screen left and the
  converse is true as well).

Only apply these settings after initialization is complete
to avoid it being clobbered by settings applied by the
Emacs implementation (looking at Emacs Mac Port here).

#+name: mouse-settings
#+begin_src emacs-lisp
;; mouse settings
(when (display-graphic-p)
  (add-hook
   'after-init-hook
   (lambda ()
     ;; use super-left-click as middle-click (trackpad workaround)
     ;; (define-key key-translation-map (kbd "<s-mouse-1>") (kbd "<mouse-2>"))
     ;; smooth scrolling, hold SHIFT/CONTROL for 5 line/full window increments
     (setq mouse-wheel-scroll-amount '(1
                                       ((shift) . 5)
                                       ((control) . nil)))
     ;; enable horizontal scrolling
     (setq mouse-wheel-flip-direction t ;; t/nil for trackpad/mouse
           mouse-wheel-tilt-scroll t))
   t))
#+end_src

** Help hydra                                                         :hydra:

Hydra for summoning Emacs [[https://www.gnu.org/software/emacs/manual/html_node/emacs/Help.html][help]].

#+name: help-hydra
#+begin_src emacs-lisp
;; hydra for help entrypoints
(defhydra my-hydra/help (:color teal :columns 4)
  "
Help (_q_: quit)"
  ("q" nil nil)
  ("a" apropos-command "apropos-cmd")
  ("d" apropos-documentation "apropos-doc")
  ("f" describe-function "desc-fun")
  ("v" describe-variable "desc-var")
  ("c" describe-key-briefly "desc-key-brief")
  ("k" describe-key "desc-key")
  ("b" describe-bindings "desc-bind")
  ("m" describe-mode "desc-mode")
  ("p" describe-package "desc-pkg")
  ("y" describe-syntax "desc-syntax")
  ("e" view-echo-area-messages "messages")
  ("l" view-lossage "lossage")
  ("i" info "info")
  ("s" info-lookup-symbol "info-symbol")
  ("w" where-is "where-is"))

;; bind help hydra
(global-set-key (kbd "C-c C-M-S-h") 'my-hydra/help/body)
#+end_src

** Scratch buffer default mode set to fundamental-mode

Set the initial mode of the scratch buffer to ~fundamental-mode~,
as it can used for anything and not just Emacs Lisp interaction.

S-expressions in the buffer can still be evaluated as Emacs Lisp by
using ~C-x C-e~ if desired.

#+name: scratch-buffer-initial-mode
#+begin_src emacs-lisp
;; set *scratch* buffer major-mode to fundamental-mode
(setq initial-major-mode 'fundamental-mode)
#+end_src

** Scroll conservatively at window edge

The default behavior in Emacs when scrolling past the window edge is
to recenter the viewport on the pointer.
Modify this to scroll conservatively, moving the viewport one column
or line at a time instead of recentering.

#+name: scroll-conservatively
#+begin_src emacs-lisp
;; scroll a line at a time at window edge
(setq scroll-conservatively 101)
#+end_src

** Silence audio and visual bells

Supress audio and visual bells.
These are super distracting.

#+name: silence-audio-and-visual-bells
#+begin_src emacs-lisp
;; turn off audio and visual bells
(setq ring-bell-function 'ignore)
#+end_src

** Suppress auto-revert messages

Suppress the minibuffer messages that appear whenever a file is
auto-reverted.

#+name: suppress-auto-revert-messages
#+begin_src emacs-lisp
;; suppress auto-revert minibuffer messages
(setq auto-revert-verbose nil)
#+end_src

** Suppress startup splash screen

Suppress the startup splash screen.
Don't need it.

#+name: suppress-startup-splash
#+begin_src emacs-lisp
;; suppress splash screen that appears on startup by default
(setq inhibit-startup-message t)
#+end_src

** Very large file support                                            :melpa:

[[https://github.com/m00natic/vlfi][Very Large Files]] provides a minor mode ~vlf-mode~ that allows loading
of large file in chunks, trading increased processing time for reduced
memory usage.

When ~vlf-mode~ is active, it exposes a number of commands that
are prefixed by ~C-c C-v~:
- ~C-c C-v n~ and ~C-c C-v p~ moves forward and back through the file
  by chunk.
- ~C-c C-v SPC~ shows the chunk starting from the current point.
- ~C-c C-v [~ and ~C-c C-v ]~ goes to the front and end of the file.
- ~C-c C-v l~ and ~C-c C-v j~ jumps to a given line in the file and a given chunk number respectively.
- ~C-c C-v s~, ~C-c C-v r~, ~C-c C-v o~ and ~C-c C-v %~ are the forward search,
  backward search, occur and replace equivalents that act across all the file
  chunks. Note the ~C-c C-v %~ auto-saves the file when each chunk is
  processed.
- ~C-c C-v f~ toggles continuous chunk around the point.
- ~C-c C-v +~ and ~C-c C-v -~ control chunk size.

The package also provides two commands ~vlf-ediff-files~ and
~vlf-ediff-buffers~ that compare files and buffers in chunks.

#+name: vlf
#+begin_src emacs-lisp
;; visit large files without loading it entirely
(use-package vlf
  :config (require 'vlf-setup))
#+end_src

** Auto-disable modes that slow down Emacs in files with long lines

Many Emacs modes cause slowdown when working with buffers containing
extremely long lines.

Emacs 27+ includes a package ~so-long~ that if enabled will
automatically disable major and minor modes that can cause extreme
slowdown when visiting files with excessively long lines.

~C-c C-c~ will revert ~so-long-mode~ after it is activated back to the
original major mode.

See ~M-x so-long-commentary~ for more information.

#+name: so-long
#+begin_src emacs-lisp
;; automatically disable major and minor modes that can slow down
;; Emacs when visiting files with long lines, Emacs 27+ only
(when (require 'so-long nil :noerror)
  (global-so-long-mode 1)
  ;; leave major modes alone, only disable minor modes
  ;; increase threshold before so-long action is invoked
  (setq so-long-action 'so-long-minor-mode
        so-long-max-lines 10
        so-long-threshold 500))
#+end_src

*** Extend buffer hydra with so-long                                  :hydra:

Extend buffer hydra with toggles for ~so-long-mode~ and
~so-long-minor-mode~.

#+name: so-long-hydra
#+begin_src emacs-lisp
(defhydra+ my-hydra/buffer nil
  ("l" so-long-mode "so-long")
  ("L" so-long-minor-mode "so-long-mm"))
#+end_src

** Uniquify file buffer names

Disambiguate open file buffers with the same name but from different
directories.

#+name: uniquify
#+begin_src emacs-lisp
(require 'uniquify)
(setq uniquify-buffer-name-style 'post-forward-angle-brackets)
#+end_src

** Calc built-in calculator

[[https://www.gnu.org/software/emacs/manual/html_node/calc/index.html][Calc]] is a built-in calculator that uses [[https://en.wikipedia.org/wiki/Reverse_Polish_notation][Reverse Polish notation]] (RPN).
Use ~C-x *~ to start a Calc dispatch on the expression under the
point, for example:
- ~C-x * c~ to call ~calc~ and open a Calc instance.
- ~C-x * e~ to enter Embedded mode when the point is on a formula.
- ~C-x * q~ to run a quick calculation.

See the help buffer surfaced by ~C-h f calc-dispatch-help~ for more
information.

#+name: calc
#+begin_src emacs-lisp
(setq calc-multiplication-has-precedence nil
      calc-ensure-consistent-units t
      calc-context-sensitive-enter t
      calc-undo-length 100
      calc-highlight-selections-with-faces nil)
#+end_src

** Enable commands that are disabled by default

Some commands are disabled by default and show a warning when they are
called for the first time.

Enable some of them:
- Horizontal scrolling, ~C-x <~ and ~C-x >~
- Narrow to region and page, ~C-x n n~ and ~C-x n p~
- Downcasing and upcasing a text region, ~C-x C-l~ and ~C-x C-u~

#+name: enable-default-disabled-functions
#+begin_src emacs-lisp
;; Enable some functions disabled by default.
(put 'scroll-left 'disabled nil)
(put 'scroll-right 'disabled nil)
(put 'narrow-to-region 'disabled nil)
(put 'narrow-to-page 'disabled nil)
(put 'downcase-region 'disabled nil)
(put 'upcase-region 'disabled nil)
#+end_src

** Revert buffer using F5

Buffers can be reverted using ~s-u~ but since the Super key is not on
all keyboards it is a good idea to bind it to a function key, which is
~<f5>~ here.

#+name: revert-buffer
#+begin_src emacs-lisp
(global-set-key (kbd "<f5>") #'revert-buffer)
#+end_src

** Hacky workaround for GnuTLS timing issues                     :workaround:

The Emacs usage of GnuTLS has some timing-related issues, especially
with ~url-retrieve-synchronously~ and on GUI Emacs. This appears to be
a sort of race condition, and a bug reporter thinks that GnuTLS is not
properly waiting for the server to write the initial greeting.

See [[https://bugs.debian.org/cgi-bin/bugreport.cgi?bug=930573][link]] for more information and a [[https://bugs.debian.org/cgi-bin/bugreport.cgi?bug=930573#10][workaround]].

#+name: open-gnutls-stream-workaround
#+begin_src emacs-lisp
(defun open-gnutls-stream--after-sleep-250ms (&rest args)
  "Workaround for race condition bug in `open-gnutls-stream'.

Adds 250ms to the opening of GnuTLS connections.

ARGS is a list of the original arguments passed to
`open-gnutls-stream' and is ignored.

See https://bugs.debian.org/cgi-bin/bugreport.cgi?bug=930573#10
for more information."
  (sleep-for 0 250))

;; add advice to `open-gnutls-stream' to have it sleep for 250ms
;; as a workaround for GnuTLS race condition bug
(advice-add #'open-gnutls-stream :after #'open-gnutls-stream--after-sleep-250ms)
#+end_src

** Password management and storage

The [[https://www.passwordstore.org/][pass]] password store is a Bash script for CLI password management
using GnuPG and Git.

It has a number of interfaces, including [[https://github.com/mssun/passforios][Pass for iOS]], [[https://github.com/android-password-store/Android-Password-Store][Password Store]] for Android and [[https://github.com/browserpass/browserpass-extension][browserpass]] for web browsers, as well as for Emacs.

There is also a Go rewrite of pass called [[https://github.com/gopasspw/gopass][gopass]] that adds a number of
useful features, but is also more complicated and less battle-tested.

References:
- [[https://jherrlin.github.io/posts/emacs-gnupg-and-pass/][Emacs, GnuPG and Pass | jherrlin]]
- [[https://medium.com/@chasinglogic/the-definitive-guide-to-password-store-c337a8f023a1][The Definitive Guide to password-store | by Mathew Robinson | Medium]]

*** auth-source-pass                                                  :melpa:

[[https://github.com/DamienCassou/auth-password-store][auth-password-store]] provides integration between the Emacs built-in
auth-source and [[https://www.passwordstore.org/][pass]].

#+name: auth-source-pass
#+begin_src emacs-lisp
;; enable auth-source integration with pass
(when (executable-find "pass")
  (use-package auth-source-pass
    :demand t
    :config (auth-source-pass-enable)))
#+end_src

*** password-store.el                                                 :melpa:

[[https://git.zx2c4.com/password-store/tree/contrib/emacs][password-store.el]] provides integration with the [[https://www.passwordstore.org/][pass]] password-store
package.

#+name: password-store
#+begin_src emacs-lisp
;; emacs integration with pass password-store
(when (executable-find "pass")
  (use-package password-store))
#+end_src

**** password-store hydra                                             :hydra:

Hydra for password-store.el commands.

#+name: password-store-hydra
#+begin_src emacs-lisp
(when (executable-find "pass")
  ;; hydra for password-store
  (defhydra my-hydra/password-store (:color teal :columns 5
                                     :pre (require 'password-store))
    "
Password store (_q_: quit)"
    ("q" nil nil :exit t)
    ("C" password-store-clear "clear")
    ("c" password-store-copy "copy-pw")
    ("f" password-store-copy-field "copy-field")
    ("e" password-store-edit "edit")
    ("I" password-store-init "init")
    ("i" password-store-insert "insert")
    ("g" password-store-generate "generate")
    ("R" password-store-remove "remove")
    ("r" password-store-rename "rename")
    ("u" password-store-url "url")
    ("v" password-store-version "version"))

  ;; binding for password-store hydra
  (global-set-key (kbd "C-c C-M-S-p") 'my-hydra/password-store/body))
#+end_src

* OS-specific

** Mac OS X

There are two main Emacs ports for Mac OS X. Both work pretty well.
- [[https://emacsformacosx.com/][Emacs for Mac OS X]] is a vanilla build.
- The [[https://bitbucket.org/mituharu/emacs-mac/][Emacs Mac Port]] ([[https://github.com/railwaycat/homebrew-emacsmacport/releases][binaries]]) by Mitsuharu Yamamoto has a bunch of
  Mac-specific features patched in.

Mac OS X does not support file notifications, so use file polling
instead for ~auto-revert-mode~.

#+name: mac
#+begin_src emacs-lisp
;; on Mac OS X, use Option keys as Meta and file polling for auto-revert
(when (eq system-type 'darwin)
  (setq auto-revert-use-notify nil ;; OS X does not support file notifications
        mac-option-modifier 'meta ;; use Option key as Meta
        mac-right-option-modifier 'left ;; right Option uses left's mapping
        mac-command-modifier 'super)) ;; keep Super key as is
#+end_src

*** Keyboard settings in Emacs for OS X

Emacs for OS X keyboard settings:
- Use the Option key as Meta.
  For accented characters, use ~C-x 8~ (for example ~C-x 8 , c~).
- Keep Super key as is.
  Super key combinations are first intercepted by the system and
  only passed on if they don't correspond to an existing system
  shortcut (say ~s-TAB~ or ~s-`~), which can limit the bindings
  available for use.

#+name: mac-emacs-for-os-x-keys
#+begin_src emacs-lisp
(when (eq window-system 'ns)
  (setq mac-option-modifier 'meta ;; use Option key as Meta
        mac-right-option-modifier 'left ;; right Option uses left's mapping
        mac-command-modifier 'super)) ;; keep Super key as is
#+end_src

*** Revert Command bindings in Emacs Mac Port

Emacs Mac Port keyboard settings:
- Use the Option key as Meta.
  For accented characters, use ~C-x 8~ (for example ~C-x 8 , c~).
- Modify Super key combinations to match Emacs for OS X as possible.
  Super key combinations are first intercepted by the system and
  only passed on if they don't correspond to an existing system
  shortcut (say ~s-TAB~ or ~s-`~), which can limit the bindings
  available for use.

Adapted from [[https://news.ycombinator.com/item?id=19647365][here]].

#+name: mac-emacs-mac-port-keys
#+begin_src emacs-lisp
;; revert Command keys in Emacs Mac Port to match Emacs for Mac OS X bindings
(when (eq window-system 'mac)
  (setq mac-option-modifier 'meta
        mac-right-option-modifier 'left
        mac-command-modifier 'super)
  (global-set-key (kbd "s-'") 'next-multiframe-window)
  (global-set-key (kbd "s-,") 'customize)
  (global-set-key (kbd "s-`") 'other-frame)
  (global-set-key (kbd "s-a") 'mark-whole-buffer)
  (global-set-key (kbd "s-c") 'kill-ring-save) ;; ns-copy-including-secondary
  (global-set-key (kbd "s-d") 'isearch-repeat-backward)
  (global-set-key (kbd "s-f") 'isearch-forward)
  (global-set-key (kbd "s-g") 'isearch-repeat-forward)
  ;; (global-set-key (kbd "s-h") 'ns-do-hide-emacs) ;; done by default
  (global-set-key (kbd "s-j") 'exchange-point-and-mark)
  (global-set-key (kbd "s-k") 'kill-this-buffer)
  (global-set-key (kbd "s-l") 'goto-line)
  (global-set-key (kbd "s-m") 'iconify-frame)
  (global-set-key (kbd "s-n") 'make-frame)
  ;; (global-set-key (kbd "s-o") 'ns-open-file-using-panel) ;; no equivalent
  ;; (global-set-key (kbd "s-p") 'ns-print-buffer) ;; no equivalent
  (global-set-key (kbd "s-q") 'save-buffers-kill-emacs)
  (global-set-key (kbd "s-s") 'save-buffer)
  (global-set-key (kbd "s-u") 'revert-buffer)
  (global-set-key (kbd "s-v") 'yank)
  (global-set-key (kbd "s-w") 'delete-frame)
  (global-set-key (kbd "s-x") 'kill-region)
  (global-set-key (kbd "s-y") 'yank) ;; ns-paste-secondary
  (global-set-key (kbd "s-z") 'undo))
#+end_src

*** Case-insensitive sorting in Dired

Configure case-insensitive sorting in Dired to match OS X behavior.
Copied from [[http://pragmaticemacs.com/emacs/case-insensitive-sorting-in-dired-on-os-x/][here]].

#+name: mac-case-insensitive-sorting-in-dired
#+begin_src emacs-lisp
;; case-insensitive sorting in Dired
;; http://pragmaticemacs.com/emacs/case-insensitive-sorting-in-dired-on-os-x/
(when (eq system-type 'darwin)
  (require 'ls-lisp)
  (setq ls-lisp-use-insert-directory-program nil)
  (setq ls-lisp-ignore-case t)
  (setq ls-lisp-use-string-collate nil)
  ;; customise the appearance of the listing
  (setq ls-lisp-verbosity '(links uid))
  ;; (setq ls-lisp-format-time-list '("%b %e %H:%M" "%b %e %Y"))
  (setq ls-lisp-use-localized-time-format t))
#+end_src

*** Dired pipe to Mac OS X default open

On Mac OS X, in Dired mode add shortcut for calling the system ~open~
command to open the file under point using the default application.

#+name: mac-dired-open
#+begin_src emacs-lisp
;; open files in dired mode using 'open' when using Mac OS X,
;; bindings are 'z' in Dired which is also in the Dired mode hydra
(with-eval-after-load 'dired
  (defun dired--mac-open-file-at-pt ()
    "Opens file at point in Dired using Mac OS X 'open' command."
    (interactive)
    (let ((filename (dired-get-file-for-visit)))
      (start-process "default-app" nil "open" filename)))
  (define-key dired-mode-map (kbd "z") #'dired--mac-open-file-at-pt)
  (defhydra+ my-hydra/dired-mode nil
    ("z" dired--mac-open-file-at-pt "mac-open")))
#+end_src

*** Exclude Emacs source files from being tracked by recentf

Exclude Emacs source files from ~recentf~ history on Mac OS X.

#+name: mac-recentf-exclude
#+begin_src emacs-lisp
;; exclude Emacs source files from recentf history on Mac OS X
(add-to-list 'recentf-exclude "^/Applications/Emacs.app/")
#+end_src

*** Open Finder at directory of current buffer                        :hydra:

Add a hydra head to the [[Buffer manipulation hydra]] that opens
Finder at the directory of the current buffer.

#+name: mac-open-finder-at-buffer-directory
#+begin_src emacs-lisp
;; add my-hydra/buffer head to open Finder at current buffer directory in OS X
(when (eq system-type 'darwin)
  (defun my-open-finder (&optional path)
    "Opens a new Finder window to PATH if provided,
or the current buffer file or directory if not (Mac OS X)."
    (interactive)
    (let* ((my-path (cl-some 'identity (list path
                                             (buffer-file-name)
                                             default-directory)))
           (my-full-path (expand-file-name my-path))
           (my-process-args (list "my-open-finder" nil
                                  "open" "-R" my-full-path)))
      (if (eq system-type 'darwin)
          (apply 'start-process my-process-args)
        (message "my-open-finder is Mac OS X-only"))))
  (defhydra+ my-hydra/buffer nil
    ("e" my-open-finder "open-finder" :exit t)))
#+end_src

*** Scale up Org-mode LaTeX fragment previews on Mac OS X

#+name: mac-scale-org-latex-fragment-previews
#+begin_src emacs-lisp
;; scale up LaTeX fragment preview images on OS X
(if (and (display-graphic-p)
         (eq system-type 'darwin)
         (executable-find "dvipng"))
    (setq org-format-latex-options (plist-put org-format-latex-options
                                              :scale 1.5)))
#+end_src

*** Ligatures in Emacs Mac Port                                       :hydra:

The [[https://bitbucket.org/mituharu/emacs-mac/][Emacs Mac Port]] ([[https://github.com/railwaycat/homebrew-emacsmacport/releases][binaries]]) by Mitsuharu Yamamoto has built-in support for
font ligatures with its ~mac-auto-operator-composition-mode~.
Add a head to the visual settings hydra for toggling this built-in support.

#+name: mac-emacs-mac-port-hydra-toggle-ligatures
#+begin_src emacs-lisp
;; enable toggling of ligatures in visual hydra when using emacs-mac port
(when (fboundp 'mac-auto-operator-composition-mode)
  (defhydra+ my-hydra/visual nil
    ("L" mac-auto-operator-composition-mode "toggle-ligature" :exit nil)))
#+end_src

If using [[https://emacsformacosx.com/][Emacs for Mac OS X]], use the built-in [[https://github.com/emacs-mirror/emacs/blob/master/lisp/progmodes/prog-mode.el#L199-L238][Prettify Symbols mode]] and modify ~prettify-symbols-alist~ to configure the desired character compositions.

*** DocView resolution increase to accommodate Retina screen DPI

The default DocView DPI resolution is 100, which is very low for
Retina screens which can lead to blurry PDF rendering.

One solution is to increase the DPI resolution and let Emacs scale the
resulting higher DPI imagine downward, but this only works on the
Emacs Mac Port (which leverages Mac systems' Image I/O framework to
scale images).

#+name: mac-emacs-mac-doc-view-resolution
#+begin_src emacs-lisp
;; increment DocView DPI resolution for Mac Retina screens
;; when using emacs-mac port
(when (eq window-system 'mac)
  (setq doc-view-resolution 300))
#+end_src

*** load-history-filename-element workaround                     :workaround:

For whatever reason, the ~load-history~ list gets polluted with
symbols in Emacs 27+ on some systems, Mac OS X included (at least for
the Emacs for Mac OS X binary releases), which is probably some issue
around the newly introduced portable dumper.

This causes issues whenever ~load-history-filename-element~ is called,
which can happen pretty often.

One workaround is to redefine ~load-history-filename-element~ to
ignore these problematic entries, although it is unclear if the
problematic entries in ~load-history~ create other issues elsewhere.

See these links:
- [[https://debbugs.gnu.org/db/34/34094.html][GNU bug report logs - #34094]]
- [[https://emacs.stackexchange.com/questions/5552/emacs-on-android-org-mode-error-wrong-type-argument-stringp-require-t][Emacs on Android - org-mode - error - `(wrong-type-argument stringp (require ...) ...)`]]

#+name: mac-load-history-filename-element-workaround
#+begin_src emacs-lisp
;; workaround for problematic entries in `load-history' which affects
;; Emacs 27+ on some systems, probably to do with the portable dumper
;; mainly affecting Emacs for OS X
(when (and (not (version< emacs-version "27"))
           (eq window-system 'ns))
  (defun load-history-filename-element (file-regexp)
    "Get the first elt of `load-history' whose car matches FILE-REGEXP.
Return nil if there isn't one."
    (let* ((loads load-history)
           (load-elt (and loads (car loads))))
      (save-match-data
        (while (and loads
                    (or (null (car load-elt))
                        (not (and (stringp (car load-elt)) ;; skip non-strings
                                  (string-match file-regexp (car load-elt))))))
          (setq loads (cdr loads)
                load-elt (and loads (car loads)))))
      load-elt)))
#+end_src

* Back matter

** Footer

#+name: generate-footer
#+begin_src emacs-lisp
(concat "(provide '" feature ")\n"
        ";;; " feature ".el ends here")
#+end_src

* Tangled files

** early-init.el

In Emacs 27+, this is loaded before ~package.el~ and UI rendering.
Static UI customizations should go here to optimize startup time.

#+name: early-init.el
#+begin_src emacs-lisp :noweb no-export :tangle emacs/.emacs.d/early-init.el
<<generate-header(feature="early-init", summary="Emacs early init file")>>

<<author-info>>
<<generate-timestamp()>>

;;; Commentary:

<<file-commentary-early-init>>

;;; Code:

;; Optimizations

<<startup-optimizations>>

<<io-optimizations>>

;; Package management

<<load-prefer-newer>>

<<add-dirs-to-load-path>>

;; Visual user interface components

<<cursor>>

<<remove-decorations>>

<<load-custom-theme>>

<<set-display-face-fonts>>

<<generate-footer(feature="early-init")>>
#+end_src

** init.el

The main configuration file.

#+name: init.el
#+begin_src emacs-lisp :noweb no-export :tangle emacs/.emacs.d/init.el
<<generate-header(feature="init", summary="Emacs init file")>>

<<author-info>>
<<generate-timestamp()>>

;;; Commentary:

<<file-commentary-init>>

;;; Code:

;; Backward compatibility

<<early-init-pre-27>>

;; Customize file

<<custom-file>>

;; Package management

<<elpa-repositories>>

<<package-init>>

<<use-package>>

;; Environment variables

<<environment-variables>>

;; Backend and frontend frameworks for building user interfaces

<<flex-minibuffer-completion-style>>

<<helm>>

<<helm-icons>>

<<hydra>>

<<company>>

<<edit-indirect>>

;; Visual (part 1)

<<all-the-icons>>

<<doom-modeline>>

<<minions>>

;; Backups

<<backup-files-directory>>

;; Bookmarks and history

<<bookmarks-hydra>>

<<recentf>>

<<helm-recentf>>

<<saveplace>>

<<savehist>>

;; Buffers, windows, frames, workspaces / Buffer management

<<protect-buffers>>

<<buffer-hydra>>

<<ibuffer>>

<<ibuffer-filter-groups>>

<<ibuffer-vc>>

<<ibuffer-hydra>>

<<all-the-icons-ibuffer>>

;; Buffers, windows, frames, workspaces / Window management

<<nswbuff>>

<<my-kill-other-buffers>>

<<add-my-kill-other-buffers-to-buffer-hydra>>

<<buffer-cleanup-hydra>>

<<winner-mode>>

<<window-hydra>>

<<window-hydra-helper-functions>>

<<rotate-window-buffers-hydra>>

<<popwin>>

<<ace-window>>

<<add-ace-window-to-window-hydra>>

;; Buffers, windows, frames, workspaces / Frame management

<<frame-hydra>>

<<frame-hydra-helper-functions>>

<<transpose-frame>>

<<transpose-frame-hydra>>

;; Buffers, windows, frames, workspaces / Workspace management

<<desktop>>

<<workspace-hydra>>

;; Command-line interaction

<<eshell>>

<<eshell-visual-commands>>

<<eshell-disable-git-pager>>

<<eshell-named-buffers>>

<<esh-autosuggest>>

<<helm-eshell-completion>>

<<helm-fish-completion>>

<<eshell-z>>

<<comint-prompt-read-only>>

<<kill-term-buffers-with-q-after-end>>

<<term-mode-hydra>>

<<vterm>>

<<vterm-switchb>>

<<tmux-send>>

<<shell-hydra>>

;; Comparison tools

<<ediff>>

<<ediff-copy-a-and-b-to-c>>

<<ediff-hydra>>

<<smerge-hydra>>

<<ztree>>

<<ztree-dir-hydra>>

<<ztree-diff-hydra>>

;; DevOps

<<docker>>

;; Dired

<<dired>>

<<dired-hydra>>

<<dired-hydra-helper-functions>>

<<recentf-track-dired-buffers>>

<<dired-filter>>

<<dired-filter-hydra>>

<<all-the-icons-dired>>

;; Editing text

<<indent-with-soft-tabs>>

<<completing-yank>>

<<delsel>>

<<sentence-end-single-space>>

<<epa-file>>

<<kmacros-hydra>>

<<registers-hydra>>

<<which-key>>

<<expand-region>>

<<iedit>>

<<multiple-cursors>>

<<multiple-cursors-hydra>>

<<yasnippet>>

<<yasnippet-hydra>>

<<paredit>>

<<undo-tree>>

<<zap-up-to-char>>

<<cycle-spacing>>

<<my-join-next-line>>

<<my-open-line-below-and-above>>

;; Emacs as an edit server

<<sigusr1-restart-emacs-server>>

<<emacs-client-server-hydra>>

;; Email

<<notmuch>>

<<notmuch-shorten-multiple-author-names>>

<<notmuch-toggle-search-tags-in-results>>

<<org-msg>>

<<org-msg-edit-mode-hydra>>

<<ol-notmuch>>

;; Information

<<display-current-datetime>>

<<display-emacs-info>>

<<proced>>

<<info-hydra>>

;; Marks and markers

<<marks-and-markers-hydra>>

<<add-helm-mark-ring-to-marks-and-markers-hydra>>

;; Non-programming files

<<doc-view-enhanced-menus>>

<<csv-mode>>

<<csv-mode-hydra>>

<<dockerfile-mode>>

<<nov>>

<<json-mode>>

<<markdown-mode>>

<<markdown-mode-hydra>>

<<markdown-toc>>

<<add-markdown-toc-to-markdown-mode-hydra>>

<<yaml-mode>>

<<neuron-mode>>

<<neuron-hydra>>

<<neuron-mode-hydra>>

;; Org-mode

<<org>>

<<my-org-open-line-below>>

<<org-todo-keywords>>

<<org-capture-templates>>

<<org-maximize-capture-buffers>>

<<org-tags>>

<<org-export-global-macros>>

<<org-mode-hydra>>

<<org-agenda>>

<<org-agenda-hydra>>

<<org-refile>>

<<org-entrypoints-hydra>>

<<org-visual-line-mode>>

<<org-latex-pdf-process>>

<<org-preview-latex-process>>

<<org-preview-and-scale-latex-fragments>>

<<org-graphical-customizations>>

<<org-cliplink>>

<<org-download>>

<<org-download-hydra>>

<<org-journal>>

<<org-present>>

<<add-org-present-to-org-mode-hydra>>

<<ox-md>>

<<org-superstar>>

<<org-noter>>

<<org-super-agenda>>

<<org-protocol>>

<<org-capture-websnippet>>

;; Programming / Buffer reformatter macro

<<reformatter>>

;; Programming / FlyCheck syntax checker

<<flycheck>>

<<flycheck-hydra>>

;; Programming / DevSkim and FlyCheck

<<flycheck-devskim>>

;; Programming / Conda package and environment manager

<<conda>>

<<conda-hydra>>

;; Programming / lsp-mode Language Server Protocol client

<<lsp-mode>>

<<company-lsp>>

<<lsp-mode-hydra>>

;; Programming / dap-mode Debug Adaptor Protocol client

<<dap-mode>>

<<add-dap-mode-funs-to-lsp-mode-hydra>>

;; Programming / Emacs Lisp

<<debugger-hydra>>

<<debugger-mode-hydra>>

<<profiler-hydra>>

<<el-patch>>

<<modify-lisp-indent-function>>

;; Programming / Clojure

<<clojure-mode>>

<<cider>>

<<cider-hydra>>

<<flycheck-clj-kondo>>

;; Programming / fish shell scripts

<<fish-mode>>

;; Programming / Python

<<python-shell-interpreter>>

<<python-mode-syntax-table>>

<<python-mode-hydra>>

<<python-black-format>>

<<add-python-black-format-to-python-mode-hydra>>

<<live-py-mode>>

<<add-live-py-mode-to-python-mode-hydra>>

<<lsp-pyright>>

<<dap-python>>

;; Programming / R

<<ess>>

<<ess-pipe-operators>>

<<ess-mode-hydra>>

<<poly-R>>

;; Programming / Racket

<<racket-mode>>

<<racket-mode-hydra>>

;; Project interaction

<<projectile>>

<<projectile-hydra>>

<<org-projectile>>

<<magit>>

<<magit-ibuffer-integration>>

<<git-timemachine>>

<<treemacs>>

<<treemacs-projectile>>

<<treemacs-magit>>

<<treemacs-all-the-icons>>

;; Search

<<search-hydra>>

<<add-helm-occur-to-search-hydra>>

<<wgrep>>

<<ripgrep>>

<<search-hydra-rg>>

<<anzu>>

<<dumb-jump>>

<<avy>>

<<ace-link>>

<<notdeft>>

<<imenu-auto-rescan>>

<<imenu-anywhere>>

<<imenu-list>>

;; Visual (part 2)

<<visual-hydra>>

<<darkroom>>

<<darkroom-hydra>>

<<prism>>

<<prism-hydra>>

<<color-identifiers-mode>>

<<color-identifiers-mode-hydra>>

<<outline-hydra>>

<<outline-magic>>

<<display-line-numbers-when-editing-code>>

<<show-column-number>>

<<show-matching-parentheses>>

<<volatile-highlights>>

<<highlight-indent-guides>>

<<add-highlight-indent-guides-toggle-to-visual-hydra>>

<<frame-internal-border>>

<<add-bottom-window-dividers>>

;; Web

<<eww>>

<<search-hydra-eww>>

<<eww-hydra>>

<<eww-hydra-helper-functions>>

<<restclient>>

<<restclient-hydra>>

<<network-security>>

<<http-requests-privacy>>

<<org-readitlater>>

<<org-capture-readitlater>>

;; Writing

<<writing-hydra>>

<<dictionary>>

<<dictionary-hydra>>

<<synosaurus>>

<<synosaurus-hydra>>

<<langtool>>

<<langtool-hydra>>

<<typo>>

<<add-typo-mode-toggle-to-writing-hydra>>

;; Other

<<real-auto-save>>

<<mouse-settings>>

<<help-hydra>>

<<scratch-buffer-initial-mode>>

<<scroll-conservatively>>

<<silence-audio-and-visual-bells>>

<<suppress-auto-revert-messages>>

<<suppress-startup-splash>>

<<vlf>>

<<so-long>>

<<so-long-hydra>>

<<uniquify>>

<<calc>>

<<enable-default-disabled-functions>>

<<revert-buffer>>

<<open-gnutls-stream-workaround>>

<<auth-source-pass>>

<<password-store>>

<<password-store-hydra>>

;; OS-specific / Mac OS X

<<mac>>

<<mac-emacs-for-os-x-keys>>

<<mac-emacs-mac-port-keys>>

<<mac-case-insensitive-sorting-in-dired>>

<<mac-dired-open>>

<<mac-recentf-exclude>>

<<mac-open-finder-at-buffer-directory>>

<<mac-scale-org-latex-fragment-previews>>

<<mac-emacs-mac-port-hydra-toggle-ligatures>>

<<mac-emacs-mac-doc-view-resolution>>

<<mac-load-history-filename-element-workaround>>

<<generate-footer(feature="init")>>
#+end_src

* TODO Backlog

** Backend and frontend frameworks for building user interfaces

*** [#C] Transient                                                    :melpa:

[[https://github.com/magit/transient][Transient]] provides transient keymaps, like Hydra.

Consider this package against the current usage of ~hydra~ and
evaluate if it allows for easier maintenance of the hydras.

References:
- [[https://www.reddit.com/r/emacs/comments/f3o0v8/anyone_have_good_examples_for_transient/][Anyone have good examples for Transient? : emacs]]

** Email

*** [#B] Multiple mailboxes

*Planning notes*:
- Not sure how to configure sending using multiple accounts, although it could probably go something like this:
  - Configuring multiple gmail mailboxes using Lieer linked to separate notmuch subdirectories (see [[https://github.com/gauteh/lieer/issues/56#issuecomment-546015279][here]]).
  - ~(setq send-mail-function #'sendmail-send-it)~
  - Advise ~sendmail-send-it~ to change its ~message-sendmail-extra-arguments~ based on the ~From~ header in the email message using the ~:before~ combinator (see [[https://www.emacswiki.org/emacs/MultipleSMTPAccounts][here]]).
- notmuch indexing does seem not handle multiple Gmail mailboxes very well (see [[https://github.com/gauteh/lieer/issues/56][Github issue]]).

*** [#B] Auto-completion of email address

*Planning notes*:
- [[https://github.com/redguardtoo/gmail2bbdb][gmail2bbdb]] for converting Google contacts to BBDB.
- [[https://elpa.gnu.org/packages/bbdb.html][bbdb]] for managing addresses in Emacs.
- See [[https://github.com/alhassy/emacs.d/blob/master/init.org#auto-completing-mail-addresses][here]] for an example configuration.
- See [[https://emacs.stackexchange.com/questions/22724/bbdb-searching-for-a-name-by-regex-with-tab-auto-completion/23842][here]] for an alternative method of completion using
  ~completing-read~ instead of ~company~.

** Non-programming files

*** Nix configuration files

**** [#C] nix-mode for editing Nix configuration files                :melpa:

[[https://github.com/NixOS/nix-mode][GitHub - NixOS/nix-mode: An Emacs major mode for editing Nix expressions.]]

**** [#C] nix-update-el for updating the rev/sha of a fetchgit declaration :melpa:

[[https://github.com/jwiegley/nix-update-el][GitHub - jwiegley/nix-update-el]] ([[https://melpa.org/#/nix-update][MELPA]])

*** [#B] PlantUML                                                     :melpa:

*** [#C] Ledger                                                       :melpa:

Ledger is a plain text double-entry accounting system for the
command-line.

[[https://github.com/ledger/ledger-mode][ledger-mode]] provides a major mode for editing [[https://github.com/ledger/ledger][ledger]] files.

** Org-mode

*** [#C] Org export backend to Tufte book and handout style PDFs   :external:

*TODO*: fork ~ox-tufte-latex.el~ and incorporate modifications from
~org-tufte-latex.el~ into it, especially making sure variable
modifications are buffer-local and reversible, and changing the
export shortcut keys so they align with the default LaTeX export ones.

Additionally, use the ~ETbb~ package from CTAN for the font instead
of the look-alike in the old code.

*** [#B] Org-ref                                                      :melpa:

[[https://github.com/jkitchin/org-ref][Org-ref]] is a collection of modules for managing citations, references
and BibTeX bibliographies in Org-mode.

*** [#C] org-reveal or org-re-reveal                                  :melpa:

[[https://gitlab.com/oer/org-re-reveal][org-re-reveal]] and [[https://github.com/yjwen/org-reveal][org-reveal]] are comparable packages support
exporting Org documents to [[https://revealjs.com/#/][reveal.js]] presentations.

One rationale for going with ~org-re-reveal~ is for its support for
audio and video, improved support for [[https://github.com/viebel/klipse][klipse]], bilbliography
support via the supporting ~org-re-reveal-ref~ and a number of quality of life
improvements for authoring [[https://en.wikipedia.org/wiki/Open_educational_resources][Open Educational Resources]] via the
supporting ~oer-reveal~ package.

One rationale for going with ~org-reveal~ is that it is more widely
used, and could be earlier to have esoteric bugs fixed and
cutting-edge features implemented.

References:
- [[http://www.zhangjiee.com/blog/2019/emacs-slide.html][Emacs 基于 org-reveal 做幻灯片]]

*** [#B] ox-hugo                                             :external:melpa:

[[https://github.com/kaushalmodi/ox-hugo][GitHub - kaushalmodi/ox-hugo: A carefully crafted Org exporter back-end for Hugo]]

[[https://www.kengrimes.com/ox-hugo-tutorial/][Ken Grimes :: Using ox-hugo To Build Websites with Emacs]]

**** [#C] Using Github Actions to build and deploy websites

[[https://www.aaron-powell.com/posts/2019-12-17-implementing-github-actions-for-my-blog/][Implementing GitHub Actions for My Blog]]

[[https://dev.to/bnb/an-unintentionally-comprehensive-introduction-to-github-actions-ci-blm][An Unintentionally Comprehensive Introduction to GitHub Actions CI - DEV]]

**** [#C] hugo.el                                                  :external:

[[https://github.com/aaronbieber/hugo.el][GitHub - aaronbieber/hugo.el: An interactive wrapper around the Hugo site gen...]]

**** [#C] Javascript (optional)

[[https://zihao.me/post/building-a-blog-with-hugo-and-webpack/][The Perfect Blog Setup with Hugo, Webpack 4 and Babel 7 in 2019 · Zihao Zhang]]

[[https://simonclayson.co.uk/notebook/hugo-pipes-mounts-optimisation/][Minify CSS and JS with Hugo Pipes and Module Mounts | Simon Clayson]]

*** [#C] org-static-blog                                              :melpa:

[[https://github.com/bastibe/org-static-blog][org-static-blog]] is a static website generator.

It integrates well with Github pages or GitLab pages.

It is possible to set up auto-rendering using CI tooling a new blog
post is pushed, see [[https://gitlab.com/_zngguvnf/org-static-blog-example][here]].

To enable local development, may want to use a =.dir-locals= file in
the repositort that sets up a switcher between local development and
remote deployment for:
- ~org-static-blog-publish-url~
- ~org-static-blog-publish-directory~
- ~org-static-blog-posts-directory~
- ~org-static-blog-drafts-directory~

References:
- [[https://jao.io/blog/2020-02-11-simplicity.html][simplicity]]
- [[https://favicon.io/emoji-favicons/floppy-disk/][Floppy Disk | Favicon.io]] (for favicon)

*** [#C] Convert data to Org-mode representation using Orger       :external:

Built-in modules support conversion from a variety of sources,
alternative is Memacs.

[[https://github.com/karlicoss/orger][GitHub - karlicoss/orger]]

** Programming

*** Bash

**** [#C] Static analysis using ShellCheck                         :external:

Install via MacPorts.

[[https://github.com/koalaman/shellcheck][GitHub - koalaman/shellcheck]]

FlyCheck has built-in support.

MELPA has a package for Flymake support.
Or can define using ~flymake-quickdef~.

[[https://github.com/federicotdn/flymake-shellcheck][GitHub - federicotdn/flymake-shellcheck]]

*** SQL

**** WAIT [#B] ejc-sql database client using Clojure JDBC connections :external:melpa:
:LOGBOOK:
- State "WAIT"       from              [2020-06-07 Sun 16:24] \\
  Currently uses ~auto-complete-mode~ for completion.
  Wait until ~company-mode~ is supported.
  See https://github.com/kostafey/ejc-sql/issues/67
  For now, can use something like [[http://squirrel-sql.sourceforge.net/][SQuirreL SQL]] or [[https://www.knime.com/][KNIME]].
:END:

[[https://github.com/kostafey/ejc-sql][ejc-sql]] is a SQL client that uses Clojure JDBC connections to connect
to databases.

It uses Clojure and so requires [[https://leiningen.org/][Leiningen]] be installed.

[[https://github.com/kostafey/ejc-sql][GitHub - kostafey/ejc-sql: Emacs SQL client uses Clojure JDBC.]]

*Note*: Uses ~auto-complete~ which may conflict with ~company-mode~.

** Project interaction

*** [#B] Forge for working with Git forges from Magit                 :melpa:

[[https://github.com/magit/forge][Forge]] extends Magit to integrate more tightly with Git forges like
Github and Gitlab, exposing functionality to work on issues and pull
requests as well as to perform other forge-related tasks like forking.

Quick overview of using Forge in a typical open-source contribution workflow.

1. ~M-x magit-status~ on Git clone of project to contribute to.
2. Add GitHub user id to Forge custom option ~forge-owned-accounts~.
3. ~magit-status~ buffer \to ~' c f~ to create a fork (defaults ok).
4. Create feature branch, add contribution and push to feature branch.
5. ~magit-status~ buffer \to ~' c p~ to create the pull-request.

*** [#C] nix-buffer for directory/file-specific specific environments :external:melpa:

[[https://github.com/shlevy/nix-buffer][nix-buffer]] allows for directory-specific or file-specific (see below)
environments using Nix.

- Using nix-buffer for file-local environments ([[https://blog.jethro.dev/posts/nix_buffer_emacs/][link]])

*** [#B] Generic session manager                                      :melpa:

[[https://github.com/vspinu/sesman][GitHub - vspinu/sesman: Session manager for Emacs based IDEs.]]

*** [#C] EditorConfig                                                 :melpa:

[[https://editorconfig.org/][EditorConfig]] [[https://github.com/editorconfig/editorconfig-emacs][plugin]] for Emacs.

EditorConfig helps to maintain consistent coding styles when multiple
developers work on a project. A code style file can be defined for a
project and the editor is automatically configured (built-in for some
editors, using a plugin for others) to adhere to that code style when
working on that project.

** Reference management

*** [#C] Biblio                                                       :melpa:

[[https://github.com/cpitclaudel/biblio.el][Biblio]] provides Emacs functions to browse bibliographic references
from a number of sources and import them as BibTeX records.

See [[https://juanjose.garciaripoll.com/blog/ebib-biblio-interface/index.html][link]] for one reference configuration, but it's probaly better to
start with a simpler workflow like copy DOI link \to Bilbio \to import
into Ebib (~M-x ebib-import~ on region or with entries in a buffer).
See the /Merging and Importing/ section of the Ebib [[http://joostkremers.github.io/ebib/ebib-manual.html][manual]].

*** [#B] Helm-bibtex                                                  :melpa:

Leverages Helm to search and manage BibTeX bilbiographies.
Potentially useful complement to Ebib.

** Web

*** [#A] Elfeed                                                       :melpa:

[[https://github.com/skeeto/elfeed][Elfeed]] is an Atom and RSS feed reader. Also look at supporting
packages [[https://github.com/remyhonig/elfeed-org][elfeed-org]] and [[https://github.com/sp1ff/elfeed-score][elfeed-score]].

** Writing

*** [#C] fountain-mode for screenwriting in Fountain markup           :melpa:

[[https://github.com/rnkn/fountain-mode][Fountain Mode]] provides a major mode for editing [[https://fountain.io/][Fountain]] plain-text
markup.

** Other
*** [#C] literate-calc-mode

[[https://github.com/sulami/literate-calc-mode.el][GitHub - sulami/literate-calc-mode.el]]

This package supports literate programming for calculations,
automatically picking up calculations and inserting the results as
overlays.

*** [#C] org-trello for syncing between Org buffers and Trello boards

[[https://github.com/org-trello/org-trello][GitHub - org-trello/org-trello: Org minor mode - 2-way sync org & trello]]

*** [#B] org-kungfu for writing Confluence pages

[[https://github.com/baohaojun/org-kungfu][GitHub - baohaojun/org-kungfu: Using Org-mode to write Confluence pages]]

* DONE Deprecated

** Backend and frontend frameworks for building user interfaces

*** Minibuffer completion with Icomplete

Use [[https://github.com/emacs-mirror/emacs/blob/master/lisp/icomplete.el][Icomplete]] as the completion backend, emulating [[https://www.gnu.org/software/emacs/manual/html_mono/ido.html][Ido]] where possible.
In Emacs 27+, there is a ~fido-mode~ that very close emulates Ido.
In prior versions, we configure Icomplete behavior directly (though
not as thoroughly as ~fido-mode~).

In most instances, use only this or Icomplete but not both.

#+name: icomplete
#+begin_src emacs-lisp
;; use Icomplete as the completion backend
;; emulate ido behavior where possible
(if (version< emacs-version "27")
    ;; no `fido-mode' on older Emacs versions
    (progn
      (setq completion-category-defaults nil
            icomplete-compute-delay 0
            icomplete-hide-common-prefix nil
            icomplete-prospects-height 2
            icomplete-show-matches-on-no-input t
            icomplete-tidy-shadowed-file-names t)
      (icomplete-mode)
      ;; C-s and C-r cycles through completion candidates like isearch
      (define-key icomplete-minibuffer-map (kbd "C-s")
        #'icomplete-forward-completions)
      (define-key icomplete-minibuffer-map (kbd "C-r")
        #'icomplete-backward-completions)
      ;; RET selects current completion candidate like ido
      ;; M-j uses input as is, e.g. to create new files or new dirs
      (define-key icomplete-minibuffer-map (kbd "RET")
        #'icomplete-force-complete-and-exit)
      (define-key icomplete-minibuffer-map (kbd "M-j")
        #'exit-minibuffer))
  ;; enable `fido-mode'
  (fido-mode))
#+end_src

*** Company front-end with icons                                      :melpa:

[[https://github.com/sebastiencs/company-box][company-box]] provides a fancy front-end for Company with icons.

#+name: company-box
#+begin_src emacs-lisp
(use-package company-box
  :hook (company-mode . company-box-mode))
#+end_src

** Bookmarks and history

*** Minibuffer commands                                               :melpa:

Use [[https://github.com/DarwinAwardWinner/amx][amx]] in place of the standard ~M-x~.

It is compatible with most completion systems and provides the
following enhancements:
- Prioritizing most-used commands.
- Showing keyboard shortcuts.

*Note*: When Helm is enabled, use ~helm-M-x~ instead.

#+name: amx
#+begin_src emacs-lisp
;; alternative interface for M-x
(when (not (featurep 'helm))
  (use-package amx
    :bind ("M-X" . amx-major-mode-commands)
    :init (amx-mode)))
#+end_src

** Buffers, windows, frames, workspaces
*** Buffer management

**** Bury scratch and message buffer instead of killing them

Make ~*scratch*~ and ~*Message*~ buffers unkillable, burying the
buffers instead when the user tries to delete them.

#+name: bury-not-kill-buffers
#+begin_src emacs-lisp
;; bury these buffers on kill command instead of killing them
(setq my-unkillable-buffers '("*scratch*"
                              "*Messages*"))
(defun my-bury-unkillable-buffers ()
  "Buries the current buffer if it is unkillable, otherwise return t."
  (if (member (buffer-name) my-unkillable-buffers)
      (progn
        (bury-buffer)
        nil)
    t))
(add-hook 'kill-buffer-query-functions #'my-bury-unkillable-buffers)
#+end_src

**** better-jumper to maintain and traverse jump-lists                :melpa:

[[https://github.com/gilbertw1/better-jumper][better-jumper]] is a jump-list implementation useful for automatically
building file jump lists and moving backwards and forwards through it.

#+name: better-jumper
#+begin_src emacs-lisp
;; jump list implementation
(use-package better-jumper
  :config
  (better-jumper-mode 1)
  ;; manage marker stack with better-jumper instead of the default
  (advice-add #'xref-push-marker-stack :around #'better-jumper-set-jump)
  (global-set-key [remap xref-pop-marker-stack]
                  #'better-jumper-jump-backward))
#+end_src

***** Add better-jumper calls to buffer hydra

#+name: add-better-jumper-to-buffer-hydra
#+begin_src emacs-lisp
;; add better-jumper calls to buffer hydra
(defhydra+ my-hydra/buffer nil
  ("," better-jumper-jump-backward "jump-bwd")
  ("." better-jumper-jump-forward "jump-fwd"))
#+end_src

** Command line interaction

*** Eshell

**** fish-completion of CLI options in Eshell                :external:melpa:

The [[https://gitlab.com/ambrevar/emacs-fish-completion][fish-completion]] package extends [[https://github.com/emacs-mirror/emacs/blob/master/lisp/pcomplete.el][pcomplete]] to power Eshell CLI
option completions using [[https://fishshell.com/][fish]].

This package requires that ~fish~ be installed on the system and
be on the system path.

If using ~helm-fish-completion~, ~fish-completion-mode~ should be disabled.

#+name: fish-completion
#+begin_src emacs-lisp
;; extend pcomplete with fish shell
(when (executable-find "fish")
  (use-package fish-completion
    :after eshell
    :config (when (not (featurep 'helm))
              (add-hook 'eshell-mode-hook #'fish-completion-mode))))
#+end_src

** Dired

*** Dired icons using treemacs-icons-dired                            :melpa:

Add icons from [[https://github.com/Alexander-Miller/treemacs][Treemacs]] to Dired.

Requires the ~treemacs~ package be installed.

#+name: treemacs-icons-dired
#+begin_src emacs-lisp
;; use Treemacs icons in Dired
(when (display-graphic-p)
  (use-package treemacs-icons-dired
    :after dired
    :hook (dired-mode . treemacs-icons-dired-mode)))
#+end_src

** Email

*** OrgMsg for composing HTML emails

**** Modifying the org-msg setup function to not remove quoted text

Redefine ~org-msg-post-setup~ ([[https://github.com/jeremy-compostella/org-msg/blob/master/org-msg.el#L872-L908][link to source]]) so that quoted text is
not removed when replying to emails, much like Gmail and other modern
mail programs.

#+name: org-msg-post-setup-no-remove-quote-text
#+begin_src emacs-lisp
;; redefine `org-msg-post-setup' so quoted text is not removed when
;; replying to emails like in Gmail and other modern mail programs
;; https://github.com/jeremy-compostella/org-msg/blob/master/org-msg.el#L872-L908
(with-eval-after-load 'org-msg
  (defun org-msg-post-setup (&rest _args)
    "Transform the current `message' buffer into a OrgMsg buffer.
If the current `message' buffer is a reply, the
`org-msg-separator' string is inserted at the end of the editing
area."
    (unless (eq major-mode 'org-msg-edit-mode)
      (message-goto-body)
      (let ((new (not (org-msg-message-fetch-field "subject")))
            (with-original (not (= (point) (point-max))))
            (reply-to))
        (when (or new (org-msg-mua-call 'article-htmlp))
          (unless new
            (setq reply-to (org-msg-mua-call 'save-article-for-reply)))
          (insert (org-msg-header reply-to))
          (when org-msg-greeting-fmt
            (insert (format org-msg-greeting-fmt
                            (if new
                                ""
                              (org-msg-get-to-first-name)))))
          (save-excursion
            (insert "\n\n")
            (when with-original
              (org-escape-code-in-region (point) (point-max)))
            (save-excursion
              (end-of-buffer)
              (when org-msg-signature
                (insert org-msg-signature)))
            (org-msg-edit-mode))
          (set-buffer-modified-p nil))
        (if (org-msg-message-fetch-field "to")
            (recenter)
          (message-goto-to))))))
#+end_src

**** Use Gmail blockquotes for quoted text in OrgMsg replies

Try to replicate Gmail-style blockquotes for quoted text.

#+name: org-msg-use-gmail-blockquotes
#+begin_src emacs-lisp
;; set up Gamil-style citations in replies
(with-eval-after-load 'notmuch
  (with-eval-after-load 'org-msg
    ;; configure message-mode citation settings to be more Gmail-like
    ;; https://emacs.stackexchange.com/questions/14625/how-to-control-quoting-of-original-message-when-replying
    (setq notmuch-mua-cite-function 'message-cite-original
          message-fill-column nil ;; don't break paragraphs
          message-citation-line-function 'message-insert-formatted-citation-line
          message-cite-reply-position 'above
          ;; message-yank-prefix "    "
          ;; message-yank-cited-prefix "    "
          ;; message-yank-empty-prefix "    "
          message-citation-line-format "On %a, %b %e, %Y at %l:%M %p %f wrote:
")
    ;; modify HTML text to use Gmail blockquotes in place of '>'
    (defun my-gmail-quote-html (htmltext)
      "Modify HTMLTEXT to use Gmail blockquotes."
      (let ((blockquote-begin
             (concat "<blockquote type=\"gmail_quote\" "
                     "style=\"margin:0px 0px 0px 0.8ex;"
                     "border-left:1px solid rgb(204,204,204);"
                     "padding-left:1ex\""
                     ">\n"))
            (blockquote-end "</blockquote>\n"))
        (with-temp-buffer
          ;; change message citation line HTML from a <p> element to a
          ;; <div> element with class 'gmail_quote'
          (insert (replace-regexp-in-string
                   (concat "<p[^\n\r]*>[\n\r]"
                           "\\([^\r\n]*\\)"
                           "</p>"
                           "\\([\n\r]*<p[^\n\r]*>[\n\r]*&gt;\\)")
                   "<br/><div class=\"gmail_quote\">\\1</div>\\2"
                   htmltext))
          ;; process one line at a time to traverse blockquote levels
          ;; using the number of leading '>' characters in each line
          (goto-char (point-min))
          (let ((blockquote-level 0))
            (while (not (eobp))
              (let ((line-level 0))
                (while (looking-at "&gt;[[:blank:]]*")
                  (replace-match "")
                  (cl-incf line-level))
                (while (< blockquote-level line-level)
                  (insert blockquote-begin)
                  (cl-incf blockquote-level))
                (while (> blockquote-level line-level)
                  (insert blockquote-end)
                  (cl-decf blockquote-level)))
              (forward-line)))
          (buffer-string))))
    (defun org-msg-preview (arg)
      "Create a temporary mail and open it with `browse-url'.
With the prefix argument ARG set, it calls
`xwidget-webkit-browse-url' instead of `browse-url'.
Modified copy of original using Gmail blockquotes."
      (interactive "P")
      (save-window-excursion
        (let ((browse-url-browser-function (if arg
                                               'xwidget-webkit-browse-url
                                             browse-url-browser-function))
              (tmp-file (make-temp-file "org-msg" nil ".html"))
              (mail (org-msg-build)))
          (with-temp-buffer
            ;; following line modified from original
            (insert (my-gmail-quote-html (org-msg-xml-to-str mail)))
            (write-file tmp-file))
          (browse-url (concat "file://" tmp-file)))))
    (defun org-msg-prepare-to-send ()
      "Convert the current OrgMsg buffer into `mml' content.
This function is a hook for `message-send-hook'.
Modified copy of original using Gmail blockquotes."
      (save-window-excursion
        (when (eq major-mode 'org-msg-edit-mode)
          (let ((mail (org-msg-build))
                (attachments (org-msg-get-prop "attachment")))
            (dolist (file attachments)
              (unless (file-exists-p file)
                (error "File '%s' does not exist" file)))
            (setq org-msg-attachment attachments)
            (when org-msg-text-plain-alternative
              (setq org-msg-text-plain (org-msg-org-to-text-plain)))
            (goto-char (org-msg-start))
            (delete-region (org-msg-start) (point-max))
            (when (org-msg-mml-recursive-support)
              (when attachments
                (mml-insert-multipart "mixed")
                (dolist (file attachments)
                  (mml-insert-tag 'part 'type (org-msg-file-mime-type file)
                                  'filename file 'disposition "attachment")))
              (when org-msg-text-plain-alternative
                (mml-insert-multipart "alternative")
                (mml-insert-part "text/plain")
                (insert org-msg-text-plain)
                (forward-line)))
            (mml-insert-part "text/html")
            ;; following line modified from original
            (insert (my-gmail-quote-html (org-msg-xml-to-str mail)))))))))
#+end_src

** Org-mode

*** org-bullets for nicer header bullets in Org-mode                  :melpa:

[[https://github.com/integral-dw/org-bullets][org-bullets]] mode replaces header stars in Org-mode with UTF-8
characters that can be customized to differ by header leve.

#+name: org-bullets
#+begin_src emacs-lisp
;; UTF-8 bullets in Org buffers
(use-package org-bullets
  :after org
  :hook (org-mode . org-bullets-mode)
  :config (setq org-bullets-bullet-list '("■" "◆" "▲" "▶")))
#+end_src

*** Display the outline path at point in Org-mode using which-func

Use ~which-func~ to display the outline path where the point is within
the Org-mode document.

This depends on some of the ~which-func~ customization set up in
\S[[Display function or outline node at point using which-func]]
so make sure the code there is loaded first.

#+name: org-which-func
#+begin_src emacs-lisp
(with-eval-after-load 'org
  ;; display the outline path at point using which-func
  (with-eval-after-load 'which-func
    (add-to-list 'which-func-modes 'org-mode)
    (defun my-org-which-function-string-shortener (str &optional maxlen)
      "Shortens STR if it is longer than MAXLEN chars."
      (let* ((len (length str))
             (maxlen (or maxlen 40)) ;; default maxlen of 40
             (num-left-chars (/ maxlen 2))
             (num-right-chars (- maxlen num-left-chars 3)))
        (if (> len maxlen)
            (concat (substring str 0 num-left-chars)
                    "..."
                    (substring str (- len num-right-chars) len))
          str)))
    (defun my-org-which-function ()
      "Returns current outline path."
      (if (eq major-mode 'org-mode)
          (condition-case nil
              (mapconcat #'my-org-which-function-string-shortener
                         (org-get-outline-path t)
                         " > ")
            (error nil))))
    (add-to-list 'which-func-functions #'my-org-which-function)
    ;; Org-specific which-func header
    (defun my-org-narrow-to-subtree-toggle ()
      "Toggle org-narrow-to-subtree."
      (interactive)
      (if (buffer-narrowed-p)
          (widen)
        (org-narrow-to-subtree)))
    (defvar my-which-func-header-keymap-org
      (let ((map (make-sparse-keymap)))
        (define-key map [header-line mouse-1] 'my-org-narrow-to-subtree-toggle)
        ;; work around mouse-1 mapping to mouse-2 when cursor is on org bullet
        (define-key map [header-line mouse-2] 'my-org-narrow-to-subtree-toggle)
        (define-key map [header-line mouse-3] 'outline-up-heading)
        (define-key map [header-line wheel-up] 'org-backward-heading-same-level)
        (define-key map [header-line wheel-down] 'org-forward-heading-same-level)
        map)
      "Keymap for header line which-func.")
    (defvar my-which-func-header-keymap-help-text-org
      "mouse-1 : toggle rest visibility\n\
mouse-3 : go up one heading\n\
wheel-u : next same-level heading\n\
wheel-d : prev same-level heading"
      "Help text for `my-which-fun-header-keymap-org'.")
    (defvar my-which-func-header-format-org
      `(:propertize which-func-current
        local-map ,my-which-func-header-keymap-org
        face which-func
        mouse-face mode-line-highlight
        help-echo my-which-func-header-keymap-help-text-org))
    ;; add Org-mode which-func header to lookup assoc list, see init-ui.el
    (add-to-list 'my-which-func-header-formats `(org-mode . ,my-which-func-header-format-org))))
#+end_src

** Programming

*** Flymake syntax checker

[[https://elpa.gnu.org/packages/flymake.html][Flymake]] is a built-in on-the-fly syntax checker, with updated versions
available from the GNU ELPA repository.

To display the error message at point in the minibuffer, do ~C-h .~
while the point is over an error.

To define new Flymake backend, refer to the docstring of
~flymake-diagnostic-functions~, the [[https://www.gnu.org/software/emacs/manual/html_node/flymake/index.html][Flymake manual]] or the code of
existing backends.

If using this, it is typical not enable FlyCheck.

#+name: flymake
#+begin_src emacs-lisp
;; basic Flymake customizations
(setq flymake-no-changes-timeout 0.5 ;; auto check buffer change wait time
      flymake-start-on-save-buffer nil) ;; don't run checks when saving

;; deferred Flymake customizations
(with-eval-after-load 'flymake
  ;; don't use legacy Flymake checker
  (remove-hook 'flymake-diagnostic-functions #'flymake-proc-legacy-flymake)
  ;; function for toggling Flymake diagnostics window
  (defun my-toggle-flymake-diagnostics ()
    "Toggles flymake diagnostics window for current buffer."
    (interactive)
    (if flymake-mode
        (let* ((buf-name (buffer-name (current-buffer)))
               (flymake-winds (condition-case nil
                                  (get-buffer-window-list
                                   (concat "*Flymake diagnostics for " buf-name "*"))
                                (error nil))))
          (if flymake-winds
              (dolist (wind flymake-winds) (quit-window nil wind))
            (flymake-show-diagnostics-buffer)))))
  ;; shorten mode line symbol in when running Emacs in the TTY
  (when (not (display-graphic-p))
    ;; Truncate Flymake mode-line symbol
    (defun my-flymake-modeline-filter (ret)
      "Filter function for `flymake--mode-line-format`."
      (setf (seq-elt (car ret) 1) " FlyM")
      ret)
    (advice-add #'flymake--mode-line-format
                :filter-return #'my-flymake-modeline-filter))
  ;; convenience bindings
  (define-key flymake-mode-map (kbd "C-c ! n") #'flymake-goto-next-error)
  (define-key flymake-mode-map (kbd "C-c ! p") #'flymake-goto-prev-error)
  (define-key flymake-mode-map (kbd "C-c ! l") #'my-toggle-flymake-diagnostics))

;; enable Flymake when editing Emacs Lisp buffers
(add-hook 'emacs-lisp-mode-hook #'flymake-mode)
#+end_src

**** Flymake hydra                                                    :hydra:

Hydra for Flymake.

#+name: flymake-hydra
#+begin_src emacs-lisp
;; hydra for Flymake
(defhydra my-hydra/flymake (:color amaranth :columns 4)
  "
Flymake (_q_: quit)"
  ("q" nil nil :exit t)
  ("p" flymake-goto-prev-error "prev-err")
  ("n" flymake-goto-next-error "next-err")
  ("l" my-toggle-flymake-diagnostics "list")
  ("s" flymake-start "start-check"))

;; binding for Flymake hydra
(with-eval-after-load 'flymake
  (define-key flymake-mode-map (kbd "C-c C-M-!") #'my-hydra/flymake/body))
#+end_src

**** flymake-quickdef for defining Flymake backends                   :melpa:

[[https://github.com/karlotness/flymake-quickdef][Flymake-Quickdef]] provides a macro for quickly defining Flymake backends.
See the [[https://github.com/karlotness/flymake-quickdef/blob/master/flymake-quickdef.el#L56-L158][documentation]] for how to use this macro.

#+name: flymake-quickdef
#+begin_src emacs-lisp
(use-package flymake-quickdef
  :demand t)
#+end_src

*** Security linting using DevSkim and Flymake                     :external:

[[https://github.com/Microsoft/DevSkim/][DevSkim]] is a collection of static analyzers that does code security analysis.

If it is installed on the system, Flymake backend can be defined to
use it to provide security linting.

Flymake-Quickdef (see above) can be used for defining this backend.

To use this backend in for specific programming language, call the
custom setup function ~flymake-devskim-setup~ before ~flymake-mode~
is enabled.

One way to ensure the order is to add ~flymake-mode~ to a programming
mode's hook by appending instead of pushing, for example by doing
~(add-hook 'python-mode-hook #'flymake-mode t)~.

#+name: devskim-flymake
#+begin_src emacs-lisp
;; Code security analysis using devskim, https://github.com/microsoft/DevSkim
;; A Flymake backend for it is defined here, and can be used by calling
;; `flymake-devskim-setup' before `flymake-mode' in a given mode's hook, e.g.
;;   (add-hook 'python-mode-hook 'flymake-devskim-setup)
;;   (add-hook 'python-mode-hook 'flymake-mode t)
;; For more info on the different severity types, see
;; https://github.com/microsoft/DevSkim/wiki/Rule-Object-Schema
(with-eval-after-load 'flymake-quickdef
  (flymake-quickdef-backend flymake-devskim-backend
    :pre-let ((devskim-exec (executable-find "devskim")))
    :pre-check (unless devskim-exec (error "Cannot find devskim executable"))
    :write-type 'file
    :proc-form (list devskim-exec
                     "analyze"
                     "-f" "text"
                     "-o" "%L:%C: %S : [%R] %N"
                     fmqd-temp-file)
    :search-regexp
    "\\([[:digit:]]+\\):\\([[:digit:]]+\\): \\([[:alpha:]]+\\) : \\(.+\\)$"
    :prep-diagnostic (let* ((lnum (string-to-number (match-string 1)))
                            (lcol (string-to-number (match-string 2)))
                            (severity (downcase (match-string 3)))
                            (msg (match-string 4))
                            (pos (flymake-diag-region fmqd-source lnum lcol))
                            (beg (car pos))
                            (end (cdr pos))
                            (type (cond
                                    ((string= severity "critical") :error)
                                    ((string= severity "important") :error)
                                    ((string= severity "moderate") :warning)
                                    ((string= severity "best-practice") :note)
                                    ((string= severity "manual-review") :note)
                                    (t :note))))
                       (list fmqd-source beg end type msg)))
  ;; define function for enabling the Flymake backend
  (defun flymake-devskim-setup ()
    "Enable devskim backend for Flymake."
    (add-hook 'flymake-diagnostic-functions #'flymake-devskim-backend nil t)))
#+end_src

*** lsp-mode Language Server Protocol Client                          :melpa:

**** Workaround for stale Flymake error reports                  :workaround:

In certain scenarios, the language server diagnostics before Flymake changes the reporting function and new diagnostics are ignored while old errors continue being reported, leading to stale error reports. See [[https://github.com/emacs-lsp/lsp-mode/pull/1423][Github issue]].

#+name: lsp-mode-flymake-stale-errors-workaround
#+begin_src emacs-lisp
;; redefine lsp--flymake to work around scenarios where the LS sends
;; diagnostics before Flymake changes the reporting function and new
;; diagnostics are ignored while old errors continue being reported
;; https://github.com/emacs-lsp/lsp-mode/pull/1423
(with-eval-after-load 'lsp-mode
  (defun lsp--flymake-backend (report-fn &rest _args)
    "Flymake backend."
    (let ((first-run (null lsp--flymake-report-fn)))
      (setq lsp--flymake-report-fn report-fn)
      (lsp--flymake-update-diagnostics))))
#+end_src

*** Clojure

**** Clojure linting using clj-kondo with Flymake                  :external:

Add a Flymake backend for [[https://github.com/borkdude/clj-kondo][clj-kondo]], a linter for Clojure code.
Enable this backend automatically when editing Clojure buffers.

#+name: flymake-clj-kondo
#+begin_src emacs-lisp
;; linting, requires clj-kondo be installed on the system
;; see https://github.com/borkdude/clj-kondo for install instructions
(when (executable-find "clj-kondo")
  ;; Flymake config, adapted from https://github.com/turbo-cafe/flymake-kondor
  (with-eval-after-load 'flymake-quickdef
    (flymake-quickdef-backend flymake-clj-kondo-backend
      :pre-let ((clj-kondo-exec (executable-find "clj-kondo")))
      :pre-check (unless clj-kondo-exec (error "Cannot find clj-kondo executable"))
      :write-type 'pipe
      :proc-form (list clj-kondo-exec "--lint" "-")
      :search-regexp "^.+:\\([[:digit:]]+\\):\\([[:digit:]]+\\): \\([[:alpha:]]+\\): \\(.+\\)$"
      :prep-diagnostic (let* ((lnum (string-to-number (match-string 1)))
                              (lcol (string-to-number (match-string 2)))
                              (severity (match-string 3))
                              (msg (match-string 4))
                              (pos (flymake-diag-region fmqd-source lnum lcol))
                              (beg (car pos))
                              (end (cdr pos))
                              (type (cond
                                     ((string= severity "error") :error)
                                     ((string= severity "warning") :warning)
                                     ((string= severity "info") :note)
                                     (t :note))))
                         (list fmqd-source beg end type msg)))
    (defun flymake-clj-kondo-setup ()
      "Enable clj-kondo backend for Flymake."
      (add-hook 'flymake-diagnostic-functions #'flymake-clj-kondo-backend nil t))
    ;; enable Flymake with clj-kondo backend when editing Clojure
    (with-eval-after-load 'clojure-mode
      (add-hook 'clojure-mode-hook 'flymake-clj-kondo-setup)
      (add-hook 'clojure-mode-hook 'flymake-mode))))
#+end_src

*** Python

**** Enable FlyCheck mode when editing Python files

Enable FlyCheck mode as the last function in the Python mode hook.
When using ~lsp-mode~ in Python buffers, this is unnecessary as
~lsp-mode~ starts ~flycheck-mode~ automatically.

#+name: python-flycheck
#+begin_src emacs-lisp
(add-hook 'python-mode-hook #'flycheck-mode t)
#+end_src

**** Enable Flymake mode when editing Python backends

Enable Flymake mode as the last function in the Python mode hook.

#+name: python-flymake
#+begin_src emacs-lisp
(add-hook 'python-mode-hook #'flymake-mode t)
#+end_src

**** Enable Flymake DevSkim backend when editing Python buffers    :external:

Enable the Flymake DevSkim back when editing Python buffers (see
\S[[Security linting using DevSkim and Flymake]]).

#+name: python-devskim-flymake
#+begin_src emacs-lisp
(when (executable-find "devskim")
  (with-eval-after-load 'flymake-quickdef
    (add-hook 'python-mode-hook #'flymake-devskim-setup)))
#+end_src

**** Enable which-func and Imenu menubar entry in Python buffers

When editing Python buffers, enable which-func and add an Imenu
entry to the menubar.

#+name: python-which-func-imenu
#+begin_src emacs-lisp
;; show function at point
(with-eval-after-load 'which-func
  (add-to-list 'which-func-modes 'python-mode))

;; add Imenu index to menubar
(with-eval-after-load 'imenu
  (add-hook 'python-mode-hook 'imenu-add-menubar-index))
#+end_src

**** Microsoft Python Language Server support with lsp-mode           :melpa:

Use ~lsp-mode~ as a client for the Microsoft Python Language Server.

#+name: lsp-python-ms
#+begin_src emacs-lisp
;; lsp-mode client for MS Python LS, https://github.com/emacs-lsp/lsp-python-ms
(use-package lsp-python-ms
  ;; append lsp-mode hooks
  :config (add-hook 'python-mode-hook
                    (lambda ()
                      ;; load packages if deferred
                      (require 'lsp-mode)
                      (require 'lsp-python-ms)
                      ;; start LSP client
                      (lsp-mode))
                    t))
#+end_src

***** Installing the Microsoft Python Language Server              :external:

- Using ~M-x lsp-python-ms-update-server~ to download it into the
  =.cache/lsp= subdirectory within ~user-emacs-directory~, which can
  be automated by setting ~lsp-python-auto-install-server~ to ~t~.
- Building from [[https://github.com/Microsoft/python-language-server][source]] and setting ~lsp-python-ms-executable~ to the
  path of the compiled binary during Emacs initialization.

**** yapfify for formatting Python buffers with yapf                  :melpa:

[[https://github.com/JorisE/yapfify][yapfify]] creates a convenience functions ~yapfify-buffer~,
~yapfify-region~ and ~yapfify-region-or-buffer~ for using [[https://github.com/google/yapf][yapf]] to
format a Python buffer or region.

It also provides a minor mode ~yapf-mode~ that automically runs ~yapf~
prior to saving a file.

#+name: yapfify
#+begin_src emacs-lisp
;; convenience functions for running yapf on Python buffers/regions
(use-package yapfify)
#+end_src

***** Add yapfify commands to python-mode hydra                       :hydra:

Add entry point to ~yapfify-buffer-or-region~ to python-mode hydra.

#+name: add-yapfify-to-python-mode-hydra
#+begin_src emacs-lisp
;; add entry point to yapfify in python-mode hydra
(defhydra+ my-hydra/python-mode nil
  ("y" yapfify-region-or-buffer "yapf" :exit t))
#+end_src

** Project interaction management

*** Git identity management                                           :melpa:

[[https://github.com/akirak/git-identity.el][git-identity.el]] is a frontend in Emacs for managing Git identities.
This is useful when multiple Git identities need to handled on the
same machine.

It has integration with Magit.

#+name: git-identity
#+begin_src emacs-lisp
;; the "I" key in Magit opens a Git identity management interface
(use-package git-identity
  :after magit
  :bind (:map magit-status-mode-map
         ("I" . git-identity-info))
  :config
  (require 'git-identity-magit)
  (git-identity-magit-mode 1))
#+end_src

** Reference management

*** ebib for managing BibTeX databases                                :melpa:

[[http://joostkremers.github.io/ebib/][ebib]] provides support for managing BibTeX bibliographic databases
(=.bib= files).

*Configuration*:
- ~C-c C-M-S-b e~ calls ~ebib~ globally.

#+name: ebib
#+begin_src emacs-lisp
;; manager for BibTeX bibliographic databases
(use-package ebib
  :bind (("C-c C-M-S-b e" . ebib)))
#+end_src

**** Ebib Org-mode support

Configure Org-mode support for Ebib.

*Configuration*:
- Add new Org link type supporting ebib key lookup and preamble,
  pre-note and post-note text for the reference, with the format being
  =[[ebib:key][Preamble text::Pre-note::Post-note]]= where
  ~key~ should be a valid key in the =.bib= file currently open in ebib.
- ~C-c C-M-S-b i~ calls ~ebib-insert-citation~ in Org-mode buffers.

#+name: ebib-org-mode-config
#+begin_src emacs-lisp
;; configure Ebib Org-mode support
;;
;; this setup supports exporting Org to PDF with BibTeX bibliographies via
;; lualatex and biber, so they will need to be installed on the system
;;
;; org-mode documents should include the LaTeX headers for
;; bibliographies via "#+LATEX_HEADER:" structural markup elements,
;; and "\printbibliography" should be added at the desired location
;; for the bibliography (usually at the end of an article or book
;; chapter or before the index)
;;
;; references to bibliography entries in org-mode can be inserted by
;; pressing `i' when on an entry in ebib or by calling
;; `ebib-insert-citation'
;;
;; to export references from Org to LaTeX, ebib needs to be opened with the
;; bibliographies for the references that appear in the document
;;
;; use "::" in the Org link description to separate the preamble text,
;; pre-note, and post-note elements (all optional) for export to LaTeX,
;; i.e. "[[ebib:key][Preamble text::Pre-note::Post-note]]"
;; will export to "Preamble text\cite[Pre-note][Post-note]{key}"
;;
;; example:
;; ---
;; ...
;; #+LATEX_HEADER: \usepackage[backend=biber]{biblatex}
;; #+LATEX_HEADER: \addbibresource{path/to/bibtex_file.bib}
;; ...
;; [[ebib:some_ebib_entry_key]]
;; [[ebib:some_ebib_entry_key][Preamble]
;; [[ebib:some_ebib_entry_key][Preamble::::Post-note]
;; [[ebib:some_ebib_entry_key][Preamble::Pre-note::Post-note]]
;; [[ebib:incognito_1970][::see::pg. 99]]
;; ...
;; \printbibliography
;; ...
;; ---
;;
(with-eval-after-load 'org
  ;; ebib configuration for org-mode
  (with-eval-after-load 'ebib
    (require 'org-ebib)
    (defun my-org-ebib-export (path desc format)
      "Export an ebib link. See `org-link-parameters' for details about PATH, DESC and FORMAT."
      (let* ((my-desc (or desc ""))
             (desc-parts (split-string my-desc "::"))
             (desc-name (car desc-parts))
             (desc-pre-note (or (nth 1 desc-parts) ""))
             (desc-post-note (mapconcat 'identity (nthcdr 2 desc-parts) "::")))
        (cond
         ((eq format 'html)
          (if desc
              (format "(%s<cite>%s</cite>%s)"
                      (if (string= "" desc-pre-note) "" (concat desc-pre-note " "))
                      (if (string= "" desc-name) path desc-name)
                      (if (string= "" desc-post-note) "" (concat ", " desc-post-note)))
            (format "(<cite>%s</cite>)" path)))
         ((eq format 'latex)
          (if desc
              (format "%s\\cite%s%s{%s}"
                      (concat desc-name " ")
                      (if (string= "" desc-pre-note) "" (format "[%s]" desc-pre-note))
                      (if (string= "" desc-post-note) "" (format "[%s]" desc-post-note))
                      path)
            (format "\\cite{%s}" path))))))
    (org-link-set-parameters "ebib" :export 'my-org-ebib-export))
  ;; binding for `ebib-insert-citation'
  (define-key org-mode-map (kbd "C-c C-M-S-b i") #'ebib-insert-citation))
#+end_src

** Search and navigation

*** Ripgrep support                                          :hydra:external:

Support for [[https://github.com/BurntSushi/ripgrep][ripgrep]], an alternative for the grep search tool that is
extremely fast.
Besides having its own binding, also add a hydra head for calling it
to the [[Search hydra]].

It has an editing mode ~deadgrep-edit-mode~ (still not mature) that
can can be enabled within deadgrep buffers somewhat like [[https://github.com/mhayashi1120/Emacs-wgrep][wgrep.el]].
This allows the direct editing of the deadgrep buffer, after which
running ~M-x deadgrep-mode~ propogates the changes back to the
files and the buffer is switched back to ~deadgrep-mode~.

#+name: ripgrep-support
#+begin_src emacs-lisp
;; support for ripgrep if installed on the system
(when (executable-find "rg")
  (use-package deadgrep
    :defer t
    :bind ("<f5>" . deadgrep))
  (defhydra+ my-hydra/search nil
    ("gR" deadgrep "ripgrep" :exit t)))
#+end_src

*** dumb-jump hydra                                                   :hydra:

Hydra for ~dumb-jump~.

~dumb-jump-mode~ is a minor mode that enables three bindings:
- ~C-M-g~ to jump to the definition of the thing under point.
- ~C-M-p~ to jump backwards.
- ~C-M-q~ to show definition of the thing under point in a tooltip.

#+name: dumb-jump-hydra
#+begin_src emacs-lisp
;; hydra for dumb-jump
;; adapted from https://github.com/jacktasia/dumb-jump/blob/master/README.md
(defhydra my-hydra/search/dumb-jump (:color teal :columns 3
                                     :pre (require 'dumb-jump))
  "
Dumb Jump [mode-enabled=% 3`dumb-jump-mode] (_q_: ←)"
  ("q" my-hydra/search/body nil)
  ("j" dumb-jump-go "go")
  ("o" dumb-jump-go-other-window "go-other")
  ("e" dumb-jump-go-prefer-external "go-ext")
  ("x" dumb-jump-go-prefer-external-other-window "go-ext-other")
  ("i" dumb-jump-go-prompt "prompt")
  ("l" dumb-jump-quick-look "peek")
  ("b" dumb-jump-back "back" :exit nil)
  ("m" dumb-jump-mode "toggle-mode" :exit nil))

;; add entrypoint for dumb-jump hydra in my-hydra/search
(defhydra+ my-hydra/search nil
  ("j" my-hydra/search/dumb-jump/body "dumb-jump"))
#+end_src

** Visual

*** Display function or outline node at point using which-func    :semiearly:

Display current function or outline node at point using [[https://www.gnu.org/software/emacs/manual/html_node/emacs/Which-Function.html][Which Function mode]].
The mode is customized from its defaults as follows:
- The current function or outline node is displayed in the header line
  instead of the mode line.
- Mouse mappings for the which func part in the header line are
  modified to better support trackpads and support narrowing to the
  current definition or outline node.

#+name: which-func
#+begin_src emacs-lisp
;; display function or outline node at point
(setq which-func-modes '() ;; use `which-func-mode' only for given modes
      which-func-unknown "n/a")

;; enable minor mode
(which-function-mode)

;; modify to show current function in header instead of in mode line

(defun my-narrow-to-defun-toggle ()
  "Toggle narrow to defun."
  (interactive)
  (if (buffer-narrowed-p)
      (widen)
    (narrow-to-defun)))

(defvar my-which-func-header-keymap-default
  (let ((map (make-sparse-keymap)))
    (define-key map [header-line s-mouse-1] 'my-narrow-to-defun-toggle) ;; trackpad workaround
    (define-key map [header-line mouse-2] 'my-narrow-to-defun-toggle)
    (define-key map [header-line wheel-up] 'beginning-of-defun)
    (define-key map [header-line wheel-down] 'end-of-defun)
    map)
  "Keymap for header line which-func.")

(defvar my-which-func-header-keymap-help-text-default
  "mouse-2 : toggle rest visibility\n\
wheel-u : go to beginning\n\
wheel-d : go to end"
  "Help text for `my-which-fun-header-keymap-default'.")

(defvar my-which-func-header-format-default
  `(:propertize which-func-current
                local-map ,my-which-func-header-keymap-default
                face which-func
                mouse-face mode-line-highlight
                help-echo my-which-func-header-keymap-help-text-default)
  "Default header format for which-func part.")

;; remove which-func part from the mode line
(setq mode-line-misc-info (assq-delete-all 'which-function-mode mode-line-misc-info))

;; see Org mode section for a mode-specific example for Org-mode
(defvar my-which-func-header-formats
  `((nil . ,my-which-func-header-format-default))
  "Association list for looking up mode-specific which-func header-lines.
Keys should be major mode symbols and values should unevaluated
mode-line constructs, see
https://www.gnu.org/software/emacs/manual/html_node/elisp/Mode-Line-Data.html
for more info.")

(defun my-which-func-get-header-format ()
  "Gets `header-line-format' associated with the current major mode in `my-which-func-header-formats'."
  (cdr (or (assoc major-mode my-which-func-header-formats) ;; mode-specific
           (assoc nil my-which-func-header-formats)))) ;; default

(defun which-func-ff-hook--add-which-func-to-header-line ()
  "Add which-func part to header line for major modes in `which-func-modes'."
  (when (memq major-mode which-func-modes)
    (add-to-list 'header-line-format
                 '(which-function-mode
                   (which-func-mode
                    ("[ " (:eval (my-which-func-get-header-format)) " ]"))))))

;; run `which-func-ff-hook--add-which-func-to-header-line' after `which-func-ff-hook'
(advice-add 'which-func-ff-hook
            :after #'which-func-ff-hook--add-which-func-to-header-line)
#+end_src

** Other

*** Auth sources

The auth-source package provides a API for secure storage and access
of user secrets.
A number of Emacs packages come with support for this, most notably
[[https://www.gnu.org/software/emacs/manual/html_node/gnus/][GNUS]] and [[https://www.gnu.org/software/tramp/][TRAMP]].

#+name: auth-sources
#+begin_src emacs-lisp
;; Auth Sources, https://www.gnu.org/software/emacs/manual/auth.html
(if (file-exists-p "~/.authinfo.gpg")
    (setq auth-sources '((:source "~/.authinfo.gpg" :host t :protocol t)))
  (setq auth-sources '((:source "~/.authinfo" :host t :protocol t))))
#+end_src

*** CRUX --- A Collection of Ridiculously Useful eXtensions           :melpa:

[[https://github.com/bbatsov/crux][A Collection of Ridiculously Useful eXtensions for Emacs]] (CRUX)
provides a number of useful interactive commands that can
improve quality-of-life while using Emacs.

~C-S-j~ is bound to ~crux-top-join-line~ globally, which joins the
current line with the one below it like ~J~ in Vim.

#+name:crux
#+begin_src emacs-lisp
;; useful extensions
(use-package crux)
#+end_src

**** CRUX hydra                                                       :hydra:

Access most of CRUX functionality through a hydra, to reduce
conflicts with global or mode-specific bindings.

#+name: crux-hydra
#+begin_src emacs-lisp
;; hydra for CRUX commands
(defhydra my-hydra/crux (:color teal :columns 3)
  "
CRUX (_q_: quit)"
  ("q" nil nil)
  ("M-o" crux-smart-open-line "newline" :exit nil)
  ("C-M-o" crux-smart-open-line-above "newline-above" :exit nil)
  ("J" crux-top-join-line "join-line" :exit nil)
  ("C-y" crux-duplicate-current-line-or-region "duplicate")
  ("C-;" crux-duplicate-and-comment-current-line-or-region "duplicate+comment")
  ("C" crux-cleanup-buffer-or-region "cleanup-buf/rgn")
  ("R" crux-rename-file-and-buffer "rename-file+buf")
  ("D" crux-delete-file-and-buffer "delete-file+buf")
  ("K" crux-kill-other-buffers "kill-other-bufs")
  ("S" crux-reopen-as-root "sudo-edit")
  ("V" crux-view-url "view-url")
  ("o" crux-open-with "open-external"))
(global-set-key (kbd "C-c C-M-x") #'my-hydra/crux/body)
#+end_src

*** Hyperspace launcher for Emacs                                     :melpa:

[[https://github.com/ieure/hyperspace-el][Hyperspace]] is a launcher for Emacs, much like Alfred or Quicksilver
for Mac OS X.

On calling ~hyperspace~, a ~HS:~ prompt will appear in the minibuffer
and the user is expected to provide an input.
The input should be composed of a /keyword/ and an /query/.
The first part of the input (typically the first word) is assumed to be
the keyword, and the remainder of the input is used as the query.

Keywords are mapped to defined actions in ~hyperspace-actions~, which
are either Elisp function symbols or strings.
If the mapped action is an Elisp function symbol, the function is
called with the query as the first argument.
If the mapped action is a string, ~browse-url~ is called with ~(format
ACTION QUERY)~ as the parameter (which typically opens the the
parameter as a URL in the default web browser).

As example, ~ac stuff~ could run the action ~apropos-command~ mapped
to ~ac~ with ~"stuff"~ as the parameter, evaluating the Elisp
expression ~(apropos-command "stuff")~.

As another example, ~g stuff~ could run the action
="https://www.google.com/search?q=%s"= with ~"stuff"~ as the
parameter, opening to the URL
="https://www.google.com/search?q=stuff"= with the default web
browser.

#+name: hyperspace
#+begin_src emacs-lisp
;; launcher for Emacs like Alfred or Quicksilver
;; for example, calling `hyperspace' then "ac stuff"
;; does an apropos command search for "stuff"
(use-package hyperspace
  :demand t
  :init
  (setq hyperspace-actions
        `(("ac" . apropos-command)
          ("af" . (lambda (query) (apropos-command query t)))
          ("av" . apropos-variable)
          ("az" . "https://www.amazon.com/s?k=%s")
          ("bb" . bbdb-search-name)
          ;; TODO : change to https once slow HTTPS queries are fixed in Emacs
          ("e"  . (lambda (query)
                    (eww (format "http://duckduckgo.com/lite?q=%s" query))))
          ("el" . (apply-partially #'hyperspace-action->info "(elisp)Top"))
          ("c"  . "https://anaconda.org/search?q=%s")
          ("d"  . "https://duckduckgo.com/?q=%s")
          ("di" . "https://duckduckgo.com/?q=%s&iax=images&ia=images")
          ("g"  . "https://www.google.com/search?q=%s")
          ("gd" . "https://datasetsearch.research.google.com/search?query=%s")
          ("gm" . "https://maps.google.com/maps?q=%s")
          ("gi" . "https://www.google.com/search?tbm=isch&q=%s")
          ("gt" . "https://trends.google.com/trends/explore?q=%s")
          ("m"  . "https://melpa.org/#/?q=%s")
          ("py" . "https://pypi.org/search/?q=%s")
          ("r"  . "https://www.reddit.com/search.compact?q=%s")
          ("w"  . ,(concat "https://en.wikipedia.org/w/index.php?search=%s"
                           "&title=Special:Search&go=Go"))
          ("y"  . "https://yandex.com/search/?text=%s")
          ("yi" . "https://yandex.com/images/search?text=%s")))
  ;; default action if the keyword of the input is not an action
  ;; comment to use the default setting (first entry of `hyperspace-actions')
  (setq hyperspace-default-action "e")
  :bind (:map hyperspace-minor-mode-map
         ("C-M-S-SPC" . hyperspace))
  :config
  ;; unbind default keys
  (unbind-key "H-SPC" hyperspace-minor-mode-map)
  (unbind-key "<H-return>" hyperspace-minor-mode-map)
  (hyperspace-minor-mode))
#+end_src

*** god-mode                                                          :melpa:

[[https://github.com/emacsorphanage/god-mode][god-mode]] is a minor-mode for entering Control and Meta key sequences
without modifier keys.

#+name: god-mode
#+begin_src emacs-lisp
;; minor-mode to input ctrl/meta key sequences without modifier keys
(use-package god-mode
  :bind (("<escape>" . god-mode-all)
         ;; easier window manipulation in god-mode
         ("C-x C-1" . delete-other-windows)
         ("C-x C-2" . split-window-below)
         ("C-x C-3" . split-window-right)
         ("C-x C-0" . delete-window)
         :map god-local-mode-map
         ("." . repeat)
         :map god-mode-isearch-map
         ("<escape>" . god-mode-isearch-disable)
         :map isearch-mode-map
         ("<escape>" . god-mode-isearch-activate))
  :config
  ;; enable in isearch
  (require 'god-mode-isearch)
  ;; use cursor to indicate if in god-mode
  (defun god-mode--update-cursor ()
    "Sets cursor to a box in `god-mode' or read-only buf, else a bar."
    (setq cursor-type (if (or god-local-mode buffer-read-only)
                          'box
                        'bar)))
  (add-hook 'god-mode-enabled-hook #'god-mode--update-cursor)
  (add-hook 'god-mode-disabled-hook #'god-mode--update-cursor)
  ;; enable which-key integration
  (with-eval-after-load 'which-key
    (which-key-enable-god-mode-support)))
#+end_src

* Local Variables                                                  :noexport:

Autocaption source blocks with their names when exporting so that
[[https://www.gnu.org/software/emacs/manual/html_node/org/noweb.html][noweb]] entries in the [[Tangled files]] section can be referenced against
their source blocks.

#+begin_src org
Local Variables:
eval: (setq-local org-babel-exp-code-template (concat "\n#+caption:=%name=\n" org-babel-exp-code-template))
End:
#+end_src
